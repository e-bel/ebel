##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Authors = "Bruce Schultz"
SET DOCUMENT ContactInfo = "bruce.schultz@scai.fraunhofer.de"
SET DOCUMENT Copyright = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Description = "This knowledge assembly is for the Phago project."

SET DOCUMENT Licenses = "Generic License"
SET DOCUMENT Name = "Phago_BS.bel"
SET DOCUMENT Version = "2.4.2"

###################################################################################
# Definitions Section


# NAMESPACES

DEFINE NAMESPACE HP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE MESHCS AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-cellular-structures/mesh-cellular-structures-20190128.belns"
DEFINE NAMESPACE PFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/pfam/pfam-names-20181024.belns"

DEFINE NAMESPACE CHEBI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/chebi/chebi-20180106.belns"
DEFINE NAMESPACE DO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE GOBP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-biological-process/go-biological-process-20170725.belns"
DEFINE NAMESPACE GOCC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-cellular-component/go-cellular-component-20170511.belns"
DEFINE NAMESPACE HGNC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc-human-genes/hgnc-human-genes-20170511.belns"
DEFINE NAMESPACE MESHC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-chemicals/mesh-chemicals-20170725.belns"
DEFINE NAMESPACE MESHD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-diseases/mesh-diseases-20170725.belns"
DEFINE NAMESPACE MESHPP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-processes/mesh-processes-20170511.belns"
DEFINE NAMESPACE MESHA AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-anatomy/mesh-anatomy-20171030.belns"
DEFINE NAMESPACE MGI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20170725.belns"
DEFINE NAMESPACE RGD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20170511.belns"
DEFINE NAMESPACE SCOMP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170511.belns"
DEFINE NAMESPACE SFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170511.belns"
DEFINE NAMESPACE GFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc-gene-families/hgnc-gene-families-20170710.belns"

DEFINE NAMESPACE ADO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/alzheimer-disease-ontology/alzheimer-disease-ontology-1.0.2.belns"
DEFINE NAMESPACE BRCO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/brain-region-ontology/brain-region-ontology-1.0.0.belns"
DEFINE NAMESPACE NIFT AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/imaging-ontology/imaging-ontology-1.0.0.belns"

DEFINE NAMESPACE dbSNP AS PATTERN "rs[0-9]+"

DEFINE NAMESPACE CC AS LIST {"GW3965", "GM6001", "GI254023X"}

DEFINE ANNOTATION Anatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170725.belanno"
DEFINE ANNOTATION CellLine AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION Cell AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Disease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170725.belanno"
DEFINE ANNOTATION MeSHAnatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170725.belanno"
DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"

DEFINE ANNOTATION PMIDSource AS LIST {"26337043", "19230638"}

DEFINE ANNOTATION Assay AS LIST {"transwell", "Sanger sequencing", "Pooled sequencing", "in situ", "whole-genome sequencing", "phagocytosis assay", "liposome sedimentation", "phosphorylated-ERK blotting", "immunohistochemistry", "metabolomics", "RNA-seq", "qRT-PCR", "ELISA", "qPCR", "voxel-based morphometry", "organotypic brain slice assay", "brain slice assay", "in vivo", "ex vivo", "in vitro", "immunostaining", "immunofluorescence microscopy", "flow cytometry", "RT-PCR", "Western Blot"}
DEFINE ANNOTATION ChallengeType AS LIST {"lipopolysaccharide", "amyloid-beta peptide", "amyloid-β1–42", "alpha-2,8-linked polymer of sialic acid", "apoptotic neurons", "GM-CSF", "CSF1 deprivation", "neural debris", "poly(I:C) stimulation", "IFN-γ stimulation", "lipopolysaccharide stimulation", "lipopolysaccharide treatment", "lipopolysaccharide/IFN-γ stimulation"}
DEFINE ANNOTATION PublicationType AS LIST {"Review", "Research"}
DEFINE ANNOTATION Section AS LIST {"Results", "Results and Discussion", "MICROGLIAL ITIM‐SIGNALING RECEPTORS", "MICROGLIAL ITAM‐SIGNALING RECEPTORS", "ITAM SIGNALING FOR PHAGOCYTOSIS", "An environment-dependent transcriptional network", "Alterations of the human microglia transcriptome in vitro", "Human microglia transcriptomes", "6. TREM2 in Alzheimer's Disease", "5. Triggering Receptor Expressed on Myeloid Cells 2 (TREM2)", "4. Inflammation in AD", "3. Mechanisms of Neurodegeneration in AD", "2. Pathology Associated with AD", "1. Defining Alzheimer's Disease", "4. Microglial DAP12 Associated Receptors", "3. Microglial Fc Receptors", "2. Microglial ITAM-ITIM Signaling", "1. Microglia and Alzheimer's Disease", "Findings", "Abstract", "Background", "Results and discussion", "Introduction", "Expression, Metabolism, and Signaling of TREMs", "Homozygous and Compound Mutations of TREM2 or DAP12 in Nasu-Hakola Disease and Frontotemporal Dementia", "Potential Role of TREM2 in the Pathogenesis of AD", "Molecular Genetics of Alzheimer Disease", "How Could Mutations in TREM2 Contribute to the Risk of AD?", "The effects of microglia, detrimental or beneficial?", "Dysregulation of pro-inflammatory cytokine in AD", "Pro-inflammatory cytokine signaling in AD", "TRAFs in signaling by the TNF-R superfamily", "TRAFs in TLR signaling", "TRAFs in NLR signaling", "TRAFs in RLR signaling", "TRAFs in cytokine receptor signaling", "TRAFs in human diseases", "CX3CL1/CX3CR1 and Microglia", "CX3CL1/CX3CR1 and Neuroinflammation", "CX3CL1/CX3CR1 Regulates Synaptic Plasticity", "Function of NF-κB in innate immune cells", "NF-κB function in T cells", "NF-κB in inflammasome regulation", "NF-κB in inflammatory diseases"}
DEFINE ANNOTATION StudyType AS LIST {"Cell-based functional study", "GWAS", "in vitro", "in vivo", "SKAT"}
DEFINE ANNOTATION TimePoint AS LIST {"2 days", "1 day", "14 days post-transduction", "2 days post-transduction", "10-11 days cell culture", "4 days post-immunization", "6 hours tissue culture", "24 hours tissue culture", "168 hours tissue culture", "5 hours post-stimulation", "5 days post-treatment", "14 days post-treatment", "7 days post-treatment", "72 hours post-stimulation", "48 hours post-stimulation", "24 hours post-stimulation", "30 minutes post-stimulation", "1 hour post-stimulation", "4 hours post-stimulation", "8 hours post-stimulation", "3 hours post-stimulation", "2 hours post-stimulation", "12 hours reperfusion", "28 days reperfusion", "18 hours post-stimulation", "7 days reperfusion", "4 months", "8 months", "12 months", "overnight", "3 months", "5 months"}
DEFINE ANNOTATION DataType AS LIST {"whole-exome sequencing data", "cerebrospinal fluid biomarker data"}

DEFINE ANNOTATION AssayLocation AS LIST {"Washington University School of Medicine in St. Louis", "Mayo Clinic Florida"}
DEFINE ANNOTATION BRCO AS LIST {"Superior Temporal Gyrus", "Frontal Gyrus", "Entorhinal Cortex", "Inferior Parietal Cortex", "Orbital Frontal Cortex", "Parietal Cortex", "Hippocampus","Parietal_Lobe","Cerebellum","Temporal_Lobe","Frontal_cortex","Striatum","Occipital_cortex", "Temporal Cortex"}
DEFINE ANNOTATION Condition AS LIST {"Normal Healthy State"}
DEFINE ANNOTATION DiseaseState AS LIST {"Early-onset AD","Late-onset AD","Mild AD","Moderate AD","Pre-dementia"}
DEFINE ANNOTATION KnockoutMice AS LIST {"hTau-CX3CR1−/−", "hAPP-CX3CR1−/−", "APP-PS1/CX3CR1−/−", "CRND8/CX3CR1−/−", "APP23/TNFR1−/−", "App transgenic","GLP1 KO mice","IDE KO mice","IGF1 KO mice","INSR knockout mice","IRS1 KO mice","IRS2 KO mice","KO Mapk8ip1","LID mice","NIRKO mice"}
DEFINE ANNOTATION MouseStrain AS LIST {"APP/PS1", "3xTg-AD", "APP", "C57BL/6", "APP/PS1dE9", "APPPS1", "5XFAD", "SOD1G93A", "P301S", "5XFAD", "TR APOE3/3", "TR APOE4/4", "APPswe/PS1dE9", "PS2APP"}
DEFINE ANNOTATION NIFT AS LIST {"Volumetric MRI","Positron Emission Tomography"}
DEFINE ANNOTATION ApoEɛ4Status AS LIST {"positive","negative"}
DEFINE ANNOTATION Ethnicity AS LIST {"United States", "Netherlands", "German", "Norway", "Spanish", "East Asian", "East Chinese", "Brazilian", "Chinese Han", "African-American", "Caucasian with European origin", "Caucasian with North American origin", "Caucasian with French origin", "Caucasian", "silhouettes women", "Chinese", "Italian", "Swedish", "US women", "Colombian", "Han Chinese in Taiwan","Han Chinese in Singapore", "non-Hispanic white", "non-Hispanic black", "Hispanic", "Black", "White", "Japanese", "Taiwanese", "Icelanders"}
DEFINE ANNOTATION CustomCellLine AS LIST {"BV-2", "N9 cell"}
DEFINE ANNOTATION NumberOfResidues AS LIST {"20", "6"}
DEFINE ANNOTATION TechnologyUsed AS LIST {"FACS", "Illumina", "Luminex", "multiphoton microscopy", "Live Microscopy", "Confocal Microscopy", "Positron Emission Tomography", "Direct RNA Sequencing", "mass spectrometry", "pyrosequencing", "Illumina Infinium 450K methylation beadchip"}
DEFINE ANNOTATION AgeRange AS LIST {"7-10 weeks", "8 months", "4 months", "12 months", "80-100 years old", "15-30 years", "15 months"}
DEFINE ANNOTATION LPSDosage AS LIST {"50 μg/ml", "500 ng/ml", "1 μg/ml"}
DEFINE ANNOTATION ChemicalDosage AS LIST {"1.5 μM", "0.14 μM"}
DEFINE ANNOTATION MicroglialActivationState AS LIST {"M1", "M2"}
DEFINE ANNOTATION MacrophageActivationState AS LIST {"M1", "M2"}
DEFINE ANNOTATION SignalingPathway AS LIST {"RIG-I", "TLR", "RLR", "TNF-R", "TLR3", "TNF-R2", "TNFα", "CD40", "TCR"}
DEFINE ANNOTATION ProteinExpressionMethod AS LIST {"adeno-associated viral vectors"}

##################################################################################

# Statements Section


SET Citation = {"PubMed", "J Exp Med. 2017 Mar 6;214(3):597-607. doi: 10.1084/jem.20160844. Epub 2017 Feb 16.", "28209725"}

SET PublicationType = "Research"
SET Section = "Abstract"

SET Support = "Soluble TREM2 induces inflammatory responses and enhances microglial \
survival"

# microglial survival is simply represented as a(HP:"microglial cell")
# Soluble TREM2 is simply represented as p(HGNC:TREM2, loc(MESHA:"Extracellular Space"))

p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) -> bp(GOBP:"inflammatory response")
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) -> a(MESHA:Microglia)

SET Support = "A soluble form of TREM2 (sTREM2) derived from proteolytic cleavage of \
the cell surface receptor is increased in the preclinical stages of AD \
and positively correlates with the amounts of total and phosphorylated \
tau in the cerebrospinal fluid."

path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:TREM2, loc(MESHA:"Extracellular Space"))
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation p(HGNC:MAPT, loc(MESHA:"Cerebrospinal Fluid"))
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation p(HGNC:MAPT, pmod(Ph), loc(MESHA:"Cerebrospinal Fluid"))

SET Support = "Here, we show that sTREM2 promotes microglial survival in a PI3K/Akt- \
dependent manner and stimulates the production of inflammatory \
cytokines depending on NF-κB."

# p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) -> a(HP:"microglial cell")
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) -> a(MESHA:Microglia)
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) -> bp(GOBP:"cytokine production involved in inflammatory response")

SET Support = "Variants of sTREM2 carrying AD risk-associated mutations were less \
potent in both suppressing apoptosis and triggering inflammatory \
responses."

p(HGNC:TREM2, var("?")) negativeCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
# var("?") = Unspecified variant
p(HGNC:TREM2, var("?")) positiveCorrelation bp(GOBP:"apoptotic process")
p(HGNC:TREM2, var("?")) -- path(MESHD:"Alzheimer Disease")

SET Support = "Importantly, sTREM2 delivered to the hippocampi of both wild-type and \
Trem2-knockout mice elevated the expression of inflammatory cytokines \
and induced morphological changes of microglia."

#ID for Mus musculus
SET Species = "10090"
p(MGI:Trem2, loc(MESHA:Hippocampus)) -> bp(GOBP:"cytokine production involved in inflammatory response")
UNSET Species
bp(GOBP:"cytokine production involved in inflammatory response") => p(MESHC:Cytokines)

SET Support = "Collectively, these data indicate that sTREM2 triggers microglial \
activation inducing inflammatory responses and promoting survival."

p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) -> bp(GOBP:"microglial cell activation")
bp(GOBP:"microglial cell activation") -> bp(GOBP:"inflammatory response")
bp(GOBP:"microglial cell activation") positiveCorrelation a(MESHA:Microglia)

SET Section = "Introduction"

SET Support = "In the central nervous system, TREM2 is mainly expressed in microglia \
and regulates the production of inflammatory cytokines, phagocytosis \
of apoptotic neuron, and cell survival (Painter et al., 2015)."

SET MeSHAnatomy = "Central Nervous System"

p(HGNC:TREM2, loc(MESHA:Microglia)) -- bp(GOBP:"regulation of cytokine production involved in inflammatory response")
p(HGNC:TREM2, loc(MESHA:Microglia)) -- bp(GOBP:"phagocytosis")
p(HGNC:TREM2, loc(MESHA:Microglia)) -- bp(MESHPP:"Cell Survival")

UNSET MeSHAnatomy

SET Support = "Mutations in the TREM2 gene have been reported in a spectrum of \
neurodegenerative disorders, including Nasu-Hakola disease (also known \
as polycystic lipomembranous osteodysplasia with sclerosing \
leukoencephalopathy; Paloneva et al., 2002; Klünemann et al., 2005; \
Numasawa et al., 2011), frontotem- poral dementia (Guerreiro et al., \
2013b; Borroni et al., 2014; Le Ber et al., 2014), and Alzheimer’s \
disease (AD; Guerreiro et al., 2013a; Jonsson et al., 2013; Jin et \
al., 2014; Korvatska et al., 2015)."

#Polycystic lipomembranous osteodysplasia with sclerosing leukoencephalopathy(PLOSL) is Nasu-Hakola disease

g(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "The arginine 47 to histidine (R47H) substitution in the extracellular \
immunoglobulin domain signicantly increases the risk for AD"

p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "How the R47H mutation modulates disease risk remains to be elucidated; \
however, existing evidences suggest that it impairs the binding \
ability of TREM2 to ligands, including phospholipids and apoE (Atagi \
et al., 2015; Bailey et al., 2015; Wang et al., 2015)."

p(HGNC:TREM2, var("p.Arg47His")) -| complex(p(HGNC:APOE), p(HGNC:TREM2))
p(HGNC:TREM2, var("p.Arg47His")) -| complex(a(CHEBI:phospholipid), p(HGNC:TREM2))

SET Support = "The R62H mutation has also been identifed to significantly increase \
the risk of AD (Jin et al., 2014, 2015), whereas the T66M andY38C \
mutations of TREM2 have been linked to frontotemporal dementia–like \
syndrome without bone pathology (Guerreiro et al., 2013b)."

p(HGNC:TREM2, var("p.Arg62His")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Interestingly, these mutations impair the transport and processing of \
TREM2, as well as TREM2-regulated phagocytic function (Kleinberger et \
al., 2014)."

p(HGNC:TREM2, var("p.Arg47His")) -| bp(GOBP:"phagocytosis")
p(HGNC:TREM2, var("p.Arg62His")) -| bp(GOBP:"phagocytosis")

SET Support = "Importantly, sTREM2 is abundantly detected in human plasma and \
cerebrospinal fluid (CSF); and its CSF levels are elevated in AD, \
multiple sclerosis and other neurological inflammatory diseases \
(Piccio et al., 2008, 2016; Heslegrave et al., 2016)."

p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation a(MESHA:"Cerebrospinal Fluid")
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation a(MESHA:Plasma)
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(MESHD:"Multiple Sclerosis")

SET Support = "More interestingly, the amounts of sTREM2 peak in the early \
symptomatic phase of AD and are positively associated with the amounts \
of total and phosphorylated tau in the CSF (Piccio et al., 2016; \
Suárez-Calvet et al., 2016)."

p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation p(HGNC:MAPT, loc(MESHA:"Cerebrospinal Fluid"))
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation p(HGNC:MAPT, pmod(Ph), loc(MESHA:"Cerebrospinal Fluid"))

UNSET Section

SET Section = "Results and discussion"

SET Support = "Granulocyte macrophage colony-stimulating factor (GM- CSF) is a known \
stimulator that promotes the proliferation and cell viability of \
microglia in primary cultures (Lee et al., 1994;Tomozawa et al., \
1996)."

p(HGNC:CSF2) positiveCorrelation bp(GOBP:"microglial cell proliferation")
p(HGNC:CSF2) positiveCorrelation a(MESHA:Microglia)
p(HGNC:CSF2) positiveCorrelation bp(GOBP:"microglia development")

SET Support = "Given that TREM2 is an innate immunoreceptor that inhibits \
neuroinflammation (Zhong et al., 2015)"

SET MeSHAnatomy = "Brain"
p(HGNC:TREM2) -| path(MESHD:Inflammation)
UNSET MeSHAnatomy

SET Support = "As shown in Fig. 3 A, sTREM2 treatment increased the phosphorylation \
of Akt on serine 473 and the phosphorylation of GSK3β on serine 9."

p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) -> p(HGNC:AKT1, pmod(Ph, Ser, 473))
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) -> p(HGNC:GSK3B, pmod(Ph, Ser, 9))

SET Support = "Supporting a role of the NF-κB pathway, sTREM2 increased the \
phosphorylation of NF-κB p65 at serine 536, which is essential for its \
transcriptional activity (Fig. 3, G and H)."

p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) -> p(HGNC:RELA, pmod(Ph, Ser, 536))
p(HGNC:RELA, pmod(Ph, Ser, 536)) -- act(p(HGNC:RELA), ma(tscript))

SET Support = "Arginine residue at aa 47 or 62 substituted with histidine (R47H or \
R62H) is strongly associated with AD (Guerreiro et al., 2013a; Jonsson \
et al., 2013; Jin et al., 2014, 2015)."

p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, var("p.Arg62His")) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Nat Genet. 2017 Sep;49(9):1373-1384. doi: 10.1038/ng.3916. Epub 2017 Jul 17.", "28714976"}

SET Support = "We observed three new genome-wide significant nonsynonymous variants \
associated with Alzheimer's disease: a protective variant in PLCG2 \
(rs72824905: p.Pro522Arg, P = 5.38 × 10-10, odds ratio (OR) = 0.68, \
minor allele frequency (MAF)cases = 0.0059, MAFcontrols = 0.0093), a \
risk variant in ABI3 (rs616338: p.Ser209Phe, P = 4.56 × 10-10, OR = \
1.43, MAFcases = 0.011, MAFcontrols = 0.008), and a new genome-wide \
significant variant in TREM2 (rs143332484: p.Arg62His, P = 1.55 × \
10-14, OR = 1.67, MAFcases = 0.0143, MAFcontrols = 0.0089), a known \
susceptibility gene for Alzheimer's disease."

g(HGNC:PLCG2) hasVariant g(dbSNP:rs72824905)
g(dbSNP:rs72824905) -> p(HGNC:PLCG2, var("p.Pro522Arg"))
g(dbSNP:rs72824905) negativeCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:ABI3) hasVariant g(dbSNP:rs616338)
g(dbSNP:rs616338) -> p(HGNC:ABI3, var("p.Ser209Phe"))
g(dbSNP:rs616338) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2) hasVariant g(dbSNP:rs143332484)
g(dbSNP:rs143332484) -> p(HGNC:TREM2, var("p.Arg62His"))
g(dbSNP:rs143332484) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "These protein-altering changes are in genes highly expressed in \
microglia and highlight an immune-related protein-protein interaction \
network enriched for previously identified risk genes in Alzheimer's \
disease."

a(MESHA:Microglia) -> p(HGNC:PLCG2, var("p.Pro522Arg"))
a(MESHA:Microglia) -> p(HGNC:ABI3, var("p.Ser209Phe"))
a(MESHA:Microglia) -> p(HGNC:TREM2, var("p.Arg62His"))

SET Support = "These genetic findings provide additional evidence that the microglia- \
mediated innate immune response contributes directly to the \
development of Alzheimer's disease."

bp(GOBP:"microglial cell activation involved in immune response") positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

SET Support = "We identified four rare coding variants with genome-wide significant \
association signals with LOAD (P < 5 × 10−8) (Table 2 and \
Supplementary Tables 7 and 8). The first is a missense variant \
p.Pro522Arg (P = 5.38 × 10−10, OR = 0.68) in PLCG2 (phospholipase C \
γ2). This variant is associated with decreased risk of LOAD, showing a \
MAF of 0.0059 in cases and 0.0093 in controls."

#LOAD:Late-onset Alzheimer’s disease
p(HGNC:PLCG2, var("p.Pro522Arg")) negativeCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "The second new association is a missense change p.Ser209Phe (P = 4.56 \
× 10−10, OR = 1.43) in ABI3 (B3-domain-containing transcription factor \
ABI3). The Phe209 allele showed consistent evidence for increasing \
LOAD risk across all stages, with a MAF of 0.011 in cases and 0.008 in \
controls"

p(HGNC:ABI3, var("p.Ser209Phe")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "Following reports of suggestive association with LOAD 34,35, we report \
the first evidence for genome-wide significant association at TREM2 \
coding variant p.Arg62His (P = 1.55 × 10−14, OR = 1.67), with a MAF of \
0.0143 in cases and 0.0089 in controls"

p(HGNC:TREM2, var("p.Arg62His")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "We also observed evidence of association for the previously \
reported9,11 TREM2 rare variant p.Arg47His (Table 2)."

p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "These variants are not in linkage disequilibrium (Supplementary Table \
14), and conditional analyses confirmed that p.Arg62His and p.Arg47His \
are independent risk variants (Supplementary Fig. 11)."

p(HGNC:TREM2, var("p.Arg62His")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "PLCG2 (Supplementary Fig. 13) encodes a transmembrane signaling enzyme \
(PLCγ2) that hydrolyzes the membrane phospholipid PIP2 \
(1-phosphatidyl-1d-myoinositol 4,5-bisphosphate) to secondary \
messengers IP3 (myoinositol 1,4,5-trisphosphate) and DAG (diacyl- \
glycerol)."

p(HGNC:PLCG2) => deg(a(CHEBI:"1-phosphatidyl-1D-myo-inositol 4,5-bisphosphate"))
p(HGNC:PLCG2) => a(CHEBI:"1D-myo-inositol 1,4,5-trisphosphate")
p(HGNC:PLCG2) => a(CHEBI:"diglyceride")

SET Support = "PLCG2 variants also cause antibody deficiency and immune dysregulation \
(PLAID) and autoinflammation and PLAID (APLAID)"

p(HGNC:PLCG2, var("?")) -> path(HP:"Decreased antibody level in blood")
p(HGNC:PLCG2, var("?")) -> path(HP:"Immune dysregulation")
p(HGNC:PLCG2, var("?")) -> path(MESHD:"Hereditary Autoinflammatory Diseases")

SET Support = "Functional annotation (Supplementary Table 21) suggests that ABI3 \
(Supplementary Fig. 14) has a role in the innate immune response via \
interferon-mediated signaling."

p(HGNC:ABI3) -- bp(GOBP:"innate immune response")

SET Support = "In microglia, TREM2–DAP12 induces M2-like activation 44 and \
participates in recognition of membrane debris and amyloid deposits, \
resulting in microglial activation and proliferation"

complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> bp(GOBP:"microglial cell activation")
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> bp(GOBP:"microglial cell proliferation")

SET Support = "SPI1 is a central transcription factor in microglial activation state \
that has a significant gene-wide association with Alzheimer’s disease"

p(HGNC:SPI1) -> bp(GOBP:"microglial cell activation")
p(HGNC:SPI1) positiveCorrelation path(MESHD:"Alzheimer Disease")

##################################################################################
SET Citation = {"PubMed", "Mol Neurodegener. 2017 Aug 2;12(1):56. doi: 10.1186/s13024-017-0197-5.", "28768545"}

SET Support = "TREM2 variants have been identified as risk factors for Alzheimer's \
disease (AD) and other neurodegenerative diseases (NDDs)."

p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")

SET Support = "Because TREM2 encodes a receptor exclusively expressed on immune \
cells, identification of these variants conclusively demonstrates that \
the immune response can play an active role in the pathogenesis of \
NDDs."

bp(GOBP:"immune response") positiveCorrelation path(MESHD:"Neurodegenerative Diseases")

SET Support = "These TREM2 variants also confer the highest risk for developing \
Alzheimer's disease of any risk factor identified in nearly two \
decades, suggesting that understanding more about TREM2 function could \
provide key insights into NDD pathology and provide avenues for novel \
immune-related NDD biomarkers and therapeutics"

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "While it is still often cited that TREM2 expression is decreased by \
pro-inflammatory stimuli, it is now clear that this is true in vitro, \
but inflammatory stimuli in vivo almost universally increase TREM2 \
expression."

SET StudyType = "in vitro"
p(HGNC:TREM2) negativeCorrelation bp(GOBP:"inflammatory response to antigenic stimulus")
UNSET StudyType

SET StudyType = "in vivo"
p(HGNC:TREM2) positiveCorrelation bp(GOBP:"inflammatory response to antigenic stimulus")
UNSET StudyType

UNSET Section

SET Section = "Background"

SET Support = "Many identified genetic linkages are implicated in modulating immune \
function [4], demonstrating an important role for the immune response \
in AD."

bp(GOBP:"immune response") -- path(MESHD:"Alzheimer Disease")

SET Support = "The first NDD-associated TREM2 variants identified were W78X and W44X, \
which result in premature truncation of the protein, a variant at the \
consensus splice site which results in exclusion of exon 3, and the \
K186N mutation, which disrupts association of TREM2 with its obligate \
intracellular signaling adaptor, DAP12 [18]."

p(HGNC:TREM2, var("p.Trp78X")) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")
p(HGNC:TREM2, var("p.Trp44X")) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")
g(HGNC:TREM2, var("p.Lys186Asn")) -| complex(p(HGNC:TREM2), p(HGNC:TYROBP, loc(MESHA:"Intracellular Space")))

SET Support = "TREM2 was first identified as a genetic cause of PLOSL, also commonly \
known as Nasu-Hakola disease [20, 21], which is characterized \
clinically by bone cysts and fractures, neuropsychiatric symptoms and \
dementia [22]."

g(HGNC:TREM2) positiveCorrelation path(DO:"Nasu-Hakola disease")

SET Support = "Studies in families with frontotemporal dementia (FTD) [34] or \
frontotemporal lobar dementia (FTLD) found that PLOSL-associated TREM2 \
variants T66 M [35, 36], W198X [37], Q33X and Y38C [36] in either \
homozygosity or heterozygosity could also cause FTD [35‚Äì37] (Fig. \
1)."

p(HGNC:TREM2, var("p.Thr66Met")) positiveCorrelation path(DO:"Nasu-Hakola disease")
p(HGNC:TREM2, var("p.Trp198X")) positiveCorrelation path(DO:"Nasu-Hakola disease")
p(HGNC:TREM2, var("p.Gln33X")) positiveCorrelation path(DO:"Nasu-Hakola disease")
p(HGNC:TREM2, var("p.Tyr38Cys")) positiveCorrelation path(DO:"Nasu-Hakola disease")
p(HGNC:TREM2, var("p.Thr66Met")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
p(HGNC:TREM2, var("p.Trp198X")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
p(HGNC:TREM2, var("p.Gln33X")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
p(HGNC:TREM2, var("p.Tyr38Cys")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")

SET Support = "Initial studies suggested that there was a positive association \
between TREM2 variants as a whole and risk of FTD [16, 38] with a \
significant association found between FTD risk and individual TREM2 \
variants including T96K and L211P [39] and R47H [40]."

p(HGNC:TREM2, var("p.Thr96Lys")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
p(HGNC:TREM2, var("p.Leu211Pro")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")

SET Support = "However, larger studies in 2013 found that heterozygous expression of \
the TREM2 R47H [5, 6] and D87N variants [5] were significantly \
associated with AD risk."

p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, var("p.Asp87Asn")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Other variants have also been consistently shown to confer AD risk, \
including D87N [5, 15], R62H [51, 56], L211P and T96K, and H157Y [51, \
58] (Fig. 1)."

p(HGNC:TREM2, var("p.Asp87Asn")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, var("p.Arg62His")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, var("p.Leu211Pro")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, var("p.Thr96Lys")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, var("p.His157Tyr")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Variants in Siglec-3 (CD33) are significantly associated with AD risk \
and CD33 levels on human blood monocytes were found to inversely \
correlate with surface TREM2 levels [85]."

p(HGNC:CD33, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2) negativeCorrelation p(HGNC:CD33, loc(MESHA:Monocytes))

SET Support = "In addition, ApoE, a putative TREM2 ligand [94–96], is clearly \
established as an AD risk factor [97]."

p(HGNC:APOE) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Components of the TREM2 signaling pathway have also been associated \
with NDDs, including its intracellular signaling adaptor DNAX \
activation protein of 12 kDa (DAP12, also termed TYROBP)."

p(HGNC:TYROBP) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")

SET Support = "and DAP12 was found to play a central role in AD-related molecular \
networks [89]."

p(HGNC:TYROBP) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Variants in additional proteins associated with the TREM2 signaling \
pathway, SHIP1 and colony stimulating factor 1 receptor (CSF1R), have \
been associated with AD risk and leukoencephalopathy with spheroids \
[92, 93]."

p(HGNC:INPP5D) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:CSF1R) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Variants in the TREML2 [66] and TREML4 [108] gene have been shown to \
increase brain TREM2 and TREML1 expression levels."

g(HGNC:TREML2, var("?")) -> p(HGNC:TREM2, loc(MESHA:Brain))
g(HGNC:TREML4, var("?")) -> p(HGNC:TREML1, loc(MESHA:Brain))

SET Support = "Additionally, variants in TREML2 increase PD risk [62], and other \
disease-related SNPs that alter TREML2 levels [66] associate with AD \
in GWAS analyses [93]."

g(HGNC:TREML2, var("?")) positiveCorrelation path(MESHD:"Parkinson Disease")

SET Support = "One group has suggested that NFκB may instead regulate TREM2 \
expression by increasing levels of microRNA34a which, in reporter \
assays, decreased TREM2 expression"

p(HGNC:NFKB1) -> m(HGNC:MIR34A)
m(HGNC:MIR34A) -| p(HGNC:TREM2)

SET Support = "In humans, hippocampal enrichment of 5-hydroxymethylcytosine (5hmc), a \
marker of active demethylation, at the TREM2 transcription start site \
and in exon 2 were found to positively correlate with TREM2 mRNA \
levels"

SET Species = "9606"
a(CHEBI:"5-(hydroxymethyl)cytosine", loc(MESHA:Hippocampus)) positiveCorrelation r(HGNC:TREM2)
UNSET Species

SET Support = "The nonsense mutations E14X [18] and Q33X [129] were both found to \
eliminate TREM2 protein expression."

p(HGNC:TREM2, var("p.Glu14X")) -| p(HGNC:TREM2)
p(HGNC:TREM2, var("p.Gln33X")) -| p(HGNC:TREM2)

SET Support = "In vitro, application of classically pro-inflammatory molecules \
(TNFalpha [117, 136], IL1beta [107, 136, 166], ROS [166, 167], \
IFNGamma[168], TLR agonists, including lipopolysaccharide LPS) [106, \
114, 135, 136, 167‚Äì170], CpGs [171] and other TLR ligands [114, 127, \
138, 150], mitochondrial lysates [172] and bacteria [173]) decreased \
TREM2 expression, while anti-inflammatory molecules (vasoactive \
intestinal peptide [174] and IL4 [168]) increased TREM2 expression."

p(HGNC:TNF) -> bp(GOBP:"inflammatory response")
p(HGNC:IL1B) -> bp(GOBP:"inflammatory response")
p(HGNC:ROS1) -> bp(GOBP:"inflammatory response")
p(HGNC:IFNG) -> bp(GOBP:"inflammatory response")
a(CHEBI:lipopolysaccharide) -> bp(GOBP:"inflammatory response")
p(GFAM:"Toll like receptors") -> bp(GOBP:"inflammatory response")
p(HGNC:TNF) decreases p(HGNC:TREM2)
p(HGNC:IL1B) decreases p(HGNC:TREM2)
p(HGNC:ROS1) decreases p(HGNC:TREM2)
p(HGNC:IFNG) decreases p(HGNC:TREM2)
a(CHEBI:lipopolysaccharide) decreases p(HGNC:TREM2)
p(GFAM:"Toll like receptors") decreases p(HGNC:TREM2)
p(HGNC:VIP) increases p(HGNC:TREM2)
p(HGNC:IL4) increases p(HGNC:TREM2)

SET Support = "TREM2 is also upregulated in numerous other inflammation-related \
contexts, including sepsis [180], rheumatoid arthritis [181], corneal \
infection [182], macular degeneration [117], glioma [183], oral [184], \
esophaegeal [185], and liver [186] cancers, following prosthetic joint \
implants [187], osteoporosis [188], colonic mucosal injury [189], \
colitis [190], gastrointestinal mucositis [191] and muscular \
sarcoidosis [192]."

p(HGNC:TREM2) positiveCorrelation path(MESHD:Sepsis)
p(HGNC:TREM2) positiveCorrelation path(MESHD:"Arthritis, Rheumatoid")
p(HGNC:TREM2) positiveCorrelation path(MESHD:"Corneal Diseases")
p(HGNC:TREM2) positiveCorrelation path(MESHD:"Macular Degeneration")
p(HGNC:TREM2) positiveCorrelation path(MESHD:Glioma)
p(HGNC:TREM2) positiveCorrelation path(MESHD:"Esophageal Neoplasms")
p(HGNC:TREM2) positiveCorrelation path(MESHD:"Liver Neoplasms")
p(HGNC:TREM2) positiveCorrelation path(MESHD:"Mouth Neoplasms")
p(HGNC:TREM2) positiveCorrelation path(MESHD:Osteoporosis)
p(HGNC:TREM2) positiveCorrelation path(MESHD:"Colonic Diseases")
p(HGNC:TREM2) positiveCorrelation path(MESHD:Mucositis)
p(HGNC:TREM2) positiveCorrelation path(MESHD:Sarcoidosis)

SET Support = "In the CNS, TREM2 expression is increased in the context of traumatic \
brain injury [98, 99], stroke [160, 193], spinal nerve transection \
[194], ALS [64], PD [66], prion dis- ease [155, 195], models of \
demyelination [151, 159, 196–198] and following beta-amyloid (Aβ) \
vaccination"

p(HGNC:TREM2, loc(MESHA:"Central Nervous System")) positiveCorrelation path(MESHD:"Brain Injuries, Traumatic")
p(HGNC:TREM2, loc(MESHA:"Central Nervous System")) positiveCorrelation path(MESHD:Stroke)
p(HGNC:TREM2, loc(MESHA:"Central Nervous System")) positiveCorrelation path(MESHD:"Spinal Cord Injuries")
p(HGNC:TREM2, loc(MESHA:"Central Nervous System")) positiveCorrelation path(MESHD:"Prion Diseases")
p(HGNC:TREM2, loc(MESHA:"Central Nervous System")) positiveCorrelation path(MESHD:"Demyelinating Diseases")
p(HGNC:TREM2, loc(MESHA:"Central Nervous System")) positiveCorrelation p(HGNC:APP)

SET Support = "TREM2 has also been shown in almost all cases to positively correlate \
with aging, both in mouse models [200, 201] and in humans [101]."

SET Species = {"9606","10090"}
p(HGNC:TREM2) positiveCorrelation bp(GOBP:aging)
p(HGNC:TREM2) positiveCorrelation bp(GOBP:aging)
UNSET Species

SET Support = "IL13 and IL4 were also shown to enhance sTREM2 production in bone \
marrow derived macrophages [149]."

p(HGNC:IL13) -> p(HGNC:TREM2, loc(MESHA:Macrophages))
p(HGNC:IL4) -> p(HGNC:TREM2, loc(MESHA:Macrophages))

SET Support = "These data illustrate that TREM2, and its adaptor protein, DAP12, are \
required for myeloid cell accumulation around amyloid plaques."

p(HGNC:TREM2) -> a(MESHA:"Myeloid Cells", loc(CHEBI:"amyloid-beta"))
p(HGNC:TYROBP) -> a(MESHA:"Myeloid Cells", loc(CHEBI:"amyloid-beta"))

SET Support = "TREM2 has been shown in multiple contexts to be important for cell \
survival"

p(HGNC:TREM2) positiveCorrelation bp(MESHPP:"Cell Survival")

SET Support = "Osteoclasts [261] and bone marrow derived macrophages [149] from TREM2 \
deficient mice, and liver cancer [186] and glioma cell lines [183] in \
which TREM2 was knocked down had increased levels of caspase 3, \
Bcl-2-associated X protein (bax), Annexin V and TUNEL positivity, all \
suggesting that TREM2 deficiency enhanced apoptosis."

SET Species = "10090"
p(MGI:Trem2) negativeCorrelation bp(GOBP:"apoptotic process")
p(MGI:Trem2) negativeCorrelation p(MGI:Casp3)
p(MGI:Trem2) negativeCorrelation p(MGI:Bax)
p(MGI:Trem2) negativeCorrelation p(MGI:Anxa5)
UNSET Species

SET Support = "A well-characterized function of TREM2 is to enhance phagocytosis."

p(HGNC:TREM2) -> bp(GOBP:phagocytosis)

SET Support = "Knocking down TREM2 in cell lines increases levels of proinflammatory \
mediators such as iNOS, TNFα, IL1β and IL6 [240] in response to \
apoptotic neuronal membrane components [139], TLR ligands [168], \
including LPS [159, 169, 170, 280] and Aβ42 [157]"

SET Species = "10090"
p(MGI:Trem2) negativeCorrelation p(MGI:Tnf)
p(MGI:Trem2) negativeCorrelation p(MGI:Il1b)
p(MGI:Trem2) negativeCorrelation p(MGI:Il6)

SET Support = "However, many other studies also support that TREM2 can mediate or \
amplify inflammatory responses. For instance, TREM2 knockdown impaired \
ROS production [246, 263]."

p(MGI:Trem2) positiveCorrelation a(CHEBI:"reactive oxygen species")
UNSET Species

SET Support = "R47H patients had higher levels of CSF p-tau [53, 61], and a variant \
located upstream of TREM2 was associated with increased tau pathology \
in the brain [109]."

p(HGNC:TREM2, var("p.Arg47His")) -> p(HGNC:MAPT, pmod(Ph), loc(MESHA:"Cerebrospinal Fluid"))
p(HGNC:TREM2, var("?")) -> p(HGNC:MAPT, pmod(Ph), loc(MESHA:Brain))

SET Support = "TREM2 protein levels in the temporal cortex of AD patients were \
positively correlated with cleaved caspase 3 and negatively correlated \
with presynaptic marker SNAP25 [164], suggestive that loss of TREM2 \
could impact synapse pathology"

SET MeSHDisease ="Alzheimer Disease"
p(HGNC:TREM2, loc(MESHA:"Temporal Lobe")) positiveCorrelation p(HGNC:CASP3)
p(HGNC:TREM2, loc(MESHA:"Temporal Lobe")) negativeCorrelation p(HGNC:SNAP25)
UNSET MeSHDisease

SET Support = "R47H variant carriers with AD also had reduced gray matter volume [63] \
in the temporal cortex and hippocampus [61]. There was also a trend \
toward a reduction in hippocampal volume [306] and significant \
decreases in other brain regions [63] in R47H carriers even in the \
absence of clinical AD."

p(HGNC:TREM2, var("p.Arg47His")) negativeCorrelation a(MESHA:"Gray Matter", loc(MESHA:Hippocampus))
p(HGNC:TREM2, var("p.Arg47His")) negativeCorrelation a(MESHA:"Gray Matter", loc(MESHA:"Temporal Lobe"))

SET Support = "Elevated levels of sTREM2 were first detected in the CSF of patients \
with multiple sclerosis (MS) and other inflammatory neurologic \
diseases [312] and were found to be significantly elevated in MS \
patients [313]."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(MESHD:"Multiple Sclerosis")

SET Support = "Together, these studies suggest a specific elevation in CSF sTREM2 \
levels in the early symptomatic stages of AD."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Evidence of CSF sTREM2 positively correlating with glial protein \
YKL-40 in CSF [314, 316], another proposed AD immune biomarker, in \
addition to immunosuppressive agents caus- ing a reduction in CSF \
sTREM2 levels [313] are consistent with this theory."

p(HGNC:CHI3L1) biomarkerFor path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation p(HGNC:CHI3L1)

UNSET Section

##################################################################################
SET Citation = {"PubMed", "Mult Scler. 2016 Oct;22(12):1587-1595. Epub 2016 Jan 11.", "26754805"}

SET Section = "Abstract"

SET Support = "Loss-of-function mutations in TREM2 or in the gene encoding its \
adaptor protein cause the rare Nasu-Hakola disease (NHD)."

p(HGNC:TREM2, var("?")) -- path(DO:"Nasu-Hakola disease")

SET Support = "Multiple sclerosis (MS) is an autoimmune disease that in common with \
NHD is characterized by demyelination and microglial activation."

path(DO:"multiple sclerosis") positiveCorrelation bp(GOBP:"microglial cell activation")
path(DO:"multiple sclerosis") negativeCorrelation bp(GOBP:myelination)
path(DO:"Nasu-Hakola disease") positiveCorrelation bp(GOBP:"microglial cell activation")
path(DO:"Nasu-Hakola disease") negativeCorrelation bp(GOBP:myelination)

SET Support = "CSF levels of sTREM-2 were significantly increased in patients with \
RRMS, SPMS, and PPMS compared with controls."

# relapsing-remitting MS = RRMS, secondary progressive MS = SPMS, and primary progressive MS = PPMS
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(DO:"relapsing-remitting multiple sclerosis")
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(DO:"secondary progressive multiple sclerosis")
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(DO:"primary progressive multiple sclerosis")

SET Support = "After natalizumab treatment, the levels of sTREM-2 were normalized to \
control levels."

a(MESHC:Natalizumab) -| p(HGNC:TREM2, loc(MESHA:"Extracellular Space"))

SET Support = "The levels of sTREM-2 were also reduced after mitoxantrone treatment."

a(MESHC:Mitoxantrone) -| p(HGNC:TREM2, loc(MESHA:"Extracellular Space"))

SET Support = "Increased CSF levels of sTREM-2, a new marker of microglial \
activation, in MS and normalization upon treatment with either \
natalizumab or mitoxantrone support a role for microglial activation \
in active MS."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) biomarkerFor bp(GOBP:"microglial cell activation")
path(DO:"multiple sclerosis") positiveCorrelation bp(GOBP:"microglial cell activation")

UNSET Section

# Can't access full article, behind a paywall

##################################################################################
SET Citation = {"PubMed", "J Neurosci. 2013 Nov 13;33(46):18270-6. doi: 10.1523/JNEUROSCI.2211-13.2013.", "24227736"}

SET PublicationType = "Research"

SET Section = "Abstract"

SET Support = "Siglec-E on microglia inhibited phagocytosis of neural debris and \
prevented the production of superoxide radicals induced by challenge \
with neural debris."

SET Species = "10090"
SET ChallengeType = "neural debris"
p(MGI:Siglece, loc(MESHA:Microglia)) -| bp(GOBP:"apoptotic cell clearance")
p(MGI:Siglece, loc(MESHA:Microglia)) -| a(CHEBI:superoxide)
UNSET ChallengeType
UNSET Species

SET Support = "Coculture of mouse microglia and neurons demonstrated a \
neuroprotective effect of microglial Siglec-E that was dependent on \
neuronal sialic acid residues."

SET Species = "10090"
p(MGI:Siglece, loc(MESHA:Microglia)) positiveCorrelation act(p(MGI:Siglece), ma(MESHPP:Neuroprotection))
act(p(MGI:Siglece), ma(MESHPP:Neuroprotection)) positiveCorrelation a(CHEBI:"sialic acid", loc(MESHA:Neurons))
UNSET Species

SET Support = "Siglec-E prevents phagocytosis of neural debris"

SET Species = "10090"
p(MGI:Siglece) -| bp(GOBP:"apoptotic cell clearance")
UNSET Species

SET Support = "Increased neurotoxicity of microglia after knockdown of Siglece mRNA \
was neutralized by the reactive oxygen species scavenger Trolox."

SET Species = "10090"
p(MGI:Siglece, loc(MESHA:Microglia)) -> a(MESHA:Neurons)
a(CHEBI:Trolox) positiveCorrelation a(MESHA:Neurons)
a(CHEBI:Trolox) =| a(CHEBI:"reactive oxygen species")
UNSET Species

SET Support = "Data suggest that Siglec-E recognizes the intact neuronal glycocalyx \
and has neuroprotective function by preventing phagocytosis and the \
associated oxidative burst."

# glycocalyx refers to sialic acid residues
SET Species = "10090"
p(MGI:Siglece) -- a(CHEBI:"sialic acid", loc(MESHA:Neurons))
p(MGI:Siglece) positiveCorrelation bp(MESHPP:Neuroprotection)
p(MGI:Siglece) -| bp(GOBP:phagocytosis)
p(MGI:Siglece) -| bp(GOBP:"respiratory burst")
UNSET Species

UNSET Section

SET Section = "Results"

SET Support = "Although no Siglece mRNA was detected in primary neurons, primary \
microglia and a microglial line showed gene transcripts of Siglece \
(Fig. 1A)."

SET Species = "10090"
r(MGI:Siglece) positiveCorrelation a(MESHA:Microglia)
a(MESHA:Neurons) cnc r(MGI:Siglece)

SET Support = "Next, we determined the protein expression of Siglec-E on microglia. \
Low constitutive expression of Siglec-E was detected by flow cytometry \
on the microglial line (Fig. 1B)."

SET Assay = "flow cytometry"
SET Species = "10090"
a(MESHA:Microglia) -> p(MGI:Siglece)
UNSET Assay

SET Support = "Treatment with interferons (IFN-γ or IFN-α) did not change Siglece \
transcription as determined by qRT-PCR (data not shown) but slightly \
increased the cell-surface expression of Siglec-E (Fig. 1B)."

SET Assay = "qRT-PCR"
SET Species = "10090"
p(MGI:Ifng) cnc r(MGI:Siglece, loc(MESHA:Microglia))
p(SFAM:"IFNA Family") cnc r(MGI:Siglece, loc(MESHA:Microglia))
UNSET Assay
p(MGI:Ifng) -> p(MGI:Siglece, loc(MESHA:Microglia))
p(SFAM:"IFNA Family") -> p(MGI:Siglece, loc(MESHA:Microglia))

SET Support = "Engulfment of fluorescently labeled neural debris into the microglial \
line was determined by confocal microscopy and 3D reconstruction (Fig. \
2A)."

SET TechnologyUsed = "Confocal Microscopy"
SET Species = "10090"
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"phagocytosis, engulfment")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"apoptotic cell clearance")
UNSET TechnologyUsed
UNSET Species

SET Support = "Although overexpression of Siglec-E decreased the uptake of neural \
debris, reduced expression of Siglec-E led to an increase of uptake \
(Fig. 2B)."

# Knock downs refer to impeding Siglece RNA
SET ChallengeType = "neural debris"
SET Species = "10090"
r(MGI:Siglece) negativeCorrelation bp(GOBP:"phagocytosis, engulfment")
UNSET ChallengeType
UNSET Species

SET Support = "Treatment with neural debris increased microglial superoxide \
production after knockdown of Siglec-E as determined by the intensity \
of the superoxide-sensitive fluorescent dye DHE (Fig. 2C)."

SET Species = "10090"
SET ChallengeType = "neural debris"
p(MGI:Siglece) -| a(CHEBI:superoxide, loc(MESHA:Microglia))
UNSET Species
UNSET ChallengeType

SET Support = "The increased DHE staining of microglia having phagocytozed neural \
debris after knockdown of Siglec-E was abrogated by addition of SOD1 \
or the radical scavenger Trolox, a vitamin E derivate that is known to \
scavenge radicals (Fig. 2D)."

SET Species = "10090"
SET ChallengeType = "neural debris"
p(HGNC:SOD1) -| a(CHEBI:superoxide, loc(MESHA:Microglia))
a(CHEBI:Trolox) =| a(CHEBI:superoxide, loc(MESHA:Microglia))
UNSET Species
UNSET ChallengeType

SET Support = "Thus, Siglec-E acted as negative regulator of extracellular superoxide \
released by microglia after challenge with neural debris."

SET Species = "10090"
SET ChallengeType = "neural debris"
p(MGI:Siglece) negativeCorrelation cellSecretion(a(CHEBI:superoxide, loc(MESHA:Microglia)))
UNSET Species
UNSET ChallengeType

SET Support = "Gene transcription of Il-1β and Tnfsf2 (TNF-α) was significantly \
increased in microglia challenged with neural debris after knockdown \
of Siglece mRNA, although no effect on Nos2 mRNA was observed (Fig. \
3A)."

SET Species = "10090"
SET ChallengeType = "neural debris"
p(MGI:Siglece) -| r(MGI:Il1b, loc(MESHA:Microglia))
p(MGI:Siglece) -| r(MGI:Tnf, loc(MESHA:Microglia))
p(MGI:Siglece) cnc r(MGI:Nos2, loc(MESHA:Microglia))
UNSET Species
UNSET ChallengeType

SET Support = "Therefore, we used a fusion protein consisting of the extracellular \
domains linked to an Ig (Siglec-E:Fc fusion protein) and demonstrated \
that Siglec-E recognized neurons and astrocytes as demonstrated via \
immunofluorescence staining (Fig. 3B,C). The staining was abrogated \
after enzymatic removal of the sialic acid residues on the glycocalyx \
(Fig. 3B,C). Siglec-E also bound to microglia but to a lower extent \
(data not shown). Thus, Siglec-E of microglia can sense the glycocalyx \
of the neighboring cells."

SET Species = "10090"
p(MGI:Siglece) -- a(MESHA:Neurons)
p(MGI:Siglece) -- a(MESHA:Astrocytes)
p(MGI:Siglece) -- a(CHEBI:"sialic acid", loc(MESHA:"Cell Membrane"))
UNSET Species

SET Support = "In cocultures with an intact neuronal glycocalyx, overexpression of \
Siglec-E increased, whereas knockdown of Siglec-E decreased the \
relative neurite length (Fig. 4A)."

SET Species = "10090"
SET MeSHAnatomy = "Neurons"
p(MGI:Siglece) -| bp(HP:"decreased size")
UNSET MeSHAnatomy
UNSET Species

SET Support = "Thus, Siglec-E of microglia recognized sialic acid residues on neurons \
and inhibited the removal of neurites."

SET Species = "10090"
p(MGI:Siglece, loc(MESHA:Microglia)) positiveCorrelation a(CHEBI:"sialic acid", loc(MESHA:Neurons))
p(MGI:Siglece) => complex(p(MGI:Siglece), a(CHEBI:"sialic acid"))
SET MeSHAnatomy = "Neurons"
p(MGI:Siglece, loc(MESHA:Microglia)) -| bp(HP:"decreased size")
UNSET MeSHAnatomy
UNSET Species

UNSET PublicationType
UNSET Section

##################################################################################
SET Citation = {"PubMed", "EMBO Mol Med. 2016 Sep 1;8(9):992-1004. doi: 10.15252/emmm.201606370. Print 2016 Sep.", "27402340"}

SET Section = "Abstract"

SET Support = "Antibodies against amyloid β-peptide (Aβ) bind to amyloid plaques and \
induce their clearance by microglia via Fc receptor-mediated \
phagocytosis."

a(MESHA:Microglia, loc(CHEBI:"amyloid-beta")) -> bp(GOBP:phagocytosis)
bp(GOBP:phagocytosis) -| p(MGI:App, frag("1_42"))

SET Support = "Dysfunctions of microglia may play a pivotal role in AD pathogenesis \
and could result in reduced efficacy of antibody-mediated Aβ \
clearance."

a(MESHA:Microglia) -- path(MESHD:"Alzheimer Disease")
a(MESHA:Microglia) -- bp(GOBP:"beta-amyloid clearance")

SET Support = "Recently, heterozygous mutations in the triggering receptor expressed \
on myeloid cells 2 (TREM2), a microglial gene involved in \
phagocytosis, were genetically linked to late onset AD Loss of TREM2 \
reduces the ability of microglia to engulf Aβ."

# This evidence is missing a period between "...late onset AD" and "Lost of TREM2..."
g(HGNC:TREM2, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
p(HGNC:TREM2, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:"phagocytosis, engulfment")
p(HGNC:TREM2, loc(MESHA:Microglia)) negativeCorrelation a(CHEBI:"amyloid-beta")

SET Support = "However, TREM2-deficient N9 microglial cell lines, macrophages as well \
as primary microglia showed significantly reduced uptake of antibody- \
bound Aβ and as a consequence reduced clearance of amyloid plaques."

# N9 cells most accurately represent primary microglia
p(MGI:Trem2, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:"beta-amyloid clearance")
p(MGI:Trem2, loc(MESHA:Macrophages)) positiveCorrelation bp(GOBP:"beta-amyloid clearance")

UNSET Section

SET Section = "Results"

SET Support = "In line with our previous findings (Kleinberger et al, 2014), loss of \
TREM2 significantly reduced uptake of pre‐aggregated HiLyte™ Fluor \
488‐labeled Aβ42 (fAβ42) (Fig 1C)."

p(MGI:Trem2) negativeCorrelation p(MGI:App, frag("1_42"))

SET Support = "We then studied fAβ42 uptake in the presence of 0, 1, 5, or 10 μg/ml \
of antibody 2D8 or a non‐binding control antibody 6687. BMDM readily \
internalized fAβ42, which could be blocked entirely by addition of \
cytochalasin D (Fig 1E)."

# BMDM = Bone-marrow derived macrophages which are primary macrophages
SET MeSHAnatomy = "Macrophages"
bp(GOBP:phagocytosis) -| p(MGI:App, frag("1_42"))
a(CHEBI:"cytochalasin D") -| bp(GOBP:phagocytosis)
UNSET MeSHAnatomy

SET Support = "Consistent with our previous findings (Kleinberger et al, 2014), BMDM \
derived from Trem2 ko mice showed a significantly reduced phagocytic \
activity compared to BMDM derived from wt mice (Fig 1E)."

p(MGI:Trem2, loc(MESHA:Macrophages)) positiveCorrelation bp(GOBP:phagocytosis)

SET Support = "These findings suggest that fAβ42 uptake is greatly stimulated upon \
antibody binding in both wt and Trem2 ko BMDM; however, the overall \
uptake capacity is reduced in Trem2 ko BMDM."

complex(a(MESHC:"Antigen-Antibody Complex"), p(MGI:App, frag("1_42"))) -> bp(GOBP:"beta-amyloid clearance")
complex(a(MESHC:"Antigen-Antibody Complex"), p(MGI:App, frag("1_42"))) -| p(MGI:App, frag("1_42"))

SET Support = "sTREM2 even added at a 10‐fold higher concentration as compared to its \
physiologically concentration in plasma of mice (approximately 10 \
ng/ml) did not rescue fAβ42 uptake efficacy of Trem2 ko cells in the \
presence or absence of mAb11 (Fig 1I). Thus, receptor‐mediated \
signaling in a cell autonomous manner appears to trigger fAβ42 uptake."

SET MeSHAnatomy = "Macrophages"
p(MGI:Trem2, loc(MESHA:"Cell Membrane")) positiveCorrelation bp(GOBP:phagocytosis)
p(MGI:Trem2, loc(MESHA:"Cell Membrane")) negativeCorrelation p(MGI:App, frag("1_42"))
UNSET MeSHAnatomy

SET Support = "Finally, we further confirmed our findings in primary microglia \
derived from wt or Trem2 ko mice. Western blotting confirmed the \
absence of sTREM2 and full‐length, membrane‐bound TREM2 in primary \
microglia from Trem2 ko mice (Fig 1J). In line with our previous \
findings (Kleinberger et al, 2014), microglia from Trem2 ko mice \
showed significantly less uptake of fAβ42 (Fig 1K). Again, \
phagocytosis was similarly stimulated by mAb11 in a \
concentration‐dependent manner in both microglia derived from wt and \
microglia derived from Trem2 ko mice (Fig 1K and L)."

p(MGI:Trem2, loc(MESHA:Microglia)) negativeCorrelation p(MGI:App, frag("1_42"))
complex(a(MESHC:"Antigen-Antibody Complex"), p(MGI:App, frag("1_42"), loc(MESHA:Microglia))) -> bp(GOBP:phagocytosis)

SET Support = "Compared to wt BMDM, cell surface FcγRI, FcγRIIB, and (or) FcγRIII \
were significantly increased in Trem2 ko cells (Fig 2A and B)."

p(MGI:Trem2) -| p(MGI:Fcgr1, loc(MESHA:"Cell Membrane"))
p(MGI:Trem2) -| p(MGI:Fcgr2b, loc(MESHA:"Cell Membrane"))
p(MGI:Trem2) -| p(MGI:Fcgr3, loc(MESHA:"Cell Membrane"))

SET Support = "Correspondingly, mRNA levels of the respective FcγR (I, IIB, III) were \
all significantly increased in Trem2 ko cells (Fig 2C). mRNA level of \
FcγRIV was also increased, but it did not result in enhanced cell \
surface protein level (Fig 2A–C)."

p(MGI:Trem2) -| r(MGI:Fcgr1)
p(MGI:Trem2) -| r(MGI:Fcgr2b)
p(MGI:Trem2) -| r(MGI:Fcgr3)
p(MGI:Trem2) -| r(MGI:Fcgr4)

SET Support = "It is well known that upon stimulation of TREM2 as well as \
Fcγ‐receptors, Syk gets phosphorylated and activates a variety of \
downstream signaling cascades (Crowley et al, 1997; Paradowska‐Gorycka \
& Jurkowska, 2013; Ulrich & Holtzman, 2016)."

act(p(HGNC:TREM2)) -> p(HGNC:SYK, pmod(Ph))
act(p(SCOMP:"FcR Gamma Chain Complex")) -> p(HGNC:SYK, pmod(Ph))
p(HGNC:SYK, pmod(Ph)) -> bp(GOBP:"protein activation cascade")

SET Support = "As expected, Syk phosphorylation strongly increases upon incubation of \
wt BMDM with 2D8‐bound Aβ, which was not the case when using an \
isotype control antibody together with Aβ (Fig 2D and E)."

complex(a(MESHC:"Antigen-Antibody Complex"), p(MGI:App, frag("1_42"), loc(MESHA:Macrophages))) -> p(MGI:Syk, pmod(Ph), loc(MESHA:Macrophages))

SET Support = "When cryosections of APP/PS1 mice were incubated with BMDM derived \
from wt or Trem2 ko mice, a significant TREM2‐dependent reduction in \
amyloid plaque clearance was observed (Fig 4A and B)."

p(MGI:Trem2) negativeCorrelation p(MGI:App, frag("1_42"), loc(MESHA:Brain))

SET Support = "Upon pre‐incubation of cryosections with mAb11 (1 μg/ml), amyloid \
plaque clearance by wt BMDM was significantly stimulated (Fig 4A and \
B)."

complex(a(MESHC:"Antigen-Antibody Complex"), p(MGI:App, frag("1_42"), loc(MESHA:Brain))) positiveCorrelation bp(GOBP:"beta-amyloid clearance")

SET Support = "In line with the results in Fig 1, TREM2‐deficient BMDM were \
significantly less efficient in clearing amyloid plaques than wt BMDM \
under the same experimental conditions (Fig 4A and B)."

p(MGI:Trem2, loc(MESHA:Macrophages)) negativeCorrelation complex(a(MESHC:"Antigen-Antibody Complex"), p(MGI:App, frag("1_42"), loc(MESHA:Brain)))


UNSET Section

##################################################################################
SET Citation = {"PubMed", "Alzheimers Dement. 2016 Dec;12(12):1259-1272. doi: 10.1016/j.jalz.2016.06.005. Epub 2016 Jul 14.", "27423963"}

SET Section = "Abstract"

SET Support = "TREM2 is involved in the regulation of inflammatory response and \
phagocytosis."

p(HGNC:TREM2) -- bp(GOBP:"regulation of inflammatory response")
p(HGNC:TREM2) -- bp(GOBP:"regulation of phagocytosis")

SET Support = "A soluble fragment (sTREM2) is often found abnormally increased in \
cerebrospinal fluid (CSF) in Alzheimer's disease (AD)."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(MESHD:"Alzheimer Disease")

# mild cognitive impairment = MCI

SET Support = "In MCI patients, a positive correlation between CSF sTREM2 and gray \
matter volume was found in the bilateral inferior and middle temporal \
cortices, precuneus, the supramarginal, and angular gyri, after \
controlling by age, sex, and p-tau."

SET MeSHDisease = "Cognition Disorders"
SET Anatomy = {"temporal cortex", "precuneus cortex", "supramarginal gyrus", "angular gyrus"}
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation a(MESHA:"Gray Matter")
UNSET MeSHDisease
UNSET Anatomy

UNSET Section

##################################################################################
SET Citation = {"PubMed", "EMBO Mol Med. 2016 May 2;8(5):466-76. doi: 10.15252/emmm.201506123. Print 2016 May.", "26941262"}

SET Section = "Abstract"

SET Support = "Loss-of-function mutations of TREM2 are associated with increased risk \
of Alzheimer's disease (AD)."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

# suspected non-AD pathology = SNAP

SET Support = "CSF sTREM2 levels were higher in mild cognitive impairment due to AD \
than in all other AD groups and controls."

SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(HP:"Cognitive impairment")
UNSET MeSHDisease

SET Support = "Moreover, increased CSF sTREM2 levels were associated with higher CSF \
total tau and phospho-tau181P, which are markers of neuronal \
degeneration and tau pathology."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation p(HGNC:MAPT)
# phospho-tau181P is Tau phosphorylated at position threonine 181
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation p(HGNC:MAPT, pmod(Ph, Thr, 181))
p(HGNC:MAPT) biomarkerFor path(HP:Neurodegeneration)
p(HGNC:MAPT, pmod(Ph, Thr, 181)) biomarkerFor path(HP:Neurodegeneration)

SET Support = "Our data demonstrate that CSF sTREM2 levels are increased in the early \
symptomatic phase of AD, probably reflecting a corresponding change of \
the microglia activation status in response to neuronal degeneration."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(MESHD:"Alzheimer Disease")
path(HP:Neurodegeneration) positiveCorrelation bp(GOBP:"microglial cell activation")

UNSET Section

SET Section = "Results"

SET Support = "Age was positively correlated with CSF sTREM2 in the pooled group of \
subjects (Pearson r = +0.391, P < 0.0001). The correlation was still \
significant when tested within each diagnostic group, including the \
control group (Pearson r = +0.177, P = 0.030), preclinical AD (Pearson \
r = +0.510, P < 0.0001), MCI‐AD (Pearson r = +0.289, P = 0.002), and \
AD dementia (Pearson r = +0.310, P < 0.0001) (Fig 1)."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation bp(HP:aging)

SET Support = "Levels of CSF sTREM2 were significantly higher in MCI‐AD than in \
controls (P = 0.002) and AD dementia (P = 0.013) groups."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(HP:"Cognitive impairment")

SET Support = "In the whole sample of subjects including the controls and all the AD \
continuum groups, increased CSF sTREM2 was associated with higher \
levels of T‐tau (β = +0.336, P = 0.001) (Fig 3A and B) and P‐tau181P \
(β = +0.370, P = 0.001) (Fig 3C and D), and lower levels of Aβ1–42 (β \
= −0.098, P = 0.014) (Fig 3E and F)."

# T-tau = total MAPT and P-tau = phos MAPT
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation p(HGNC:MAPT)
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation p(HGNC:MAPT, pmod(Ph, Thr, 181))
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) negativeCorrelation p(HGNC:APP, frag("1_42"))
UNSET MeSHDisease

SET Support = "Interestingly, we found that CSF sTREM2 levels differed between groups \
(F 2,242 = 7.5, P = 0.0007) and were particularly increased in the \
SNAP group compared to the control (P = 0.0004) and the preclinical AD \
group (P = 0.024) (Fig 4A). These findings may indicate that an \
increase in CSF sTREM2 levels in response to neuronal injury (as \
measured by CSF tau levels) can occur without amyloidosis."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation p(HGNC:MAPT)
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation p(HGNC:MAPT, pmod(Ph, Thr, 181))

SET Support = "Particularly, MCI‐noAD patients showed lower levels of CSF sTREM2 \
compared to the MCI‐AD patients (P < 0.0001) but were similar to those \
of controls (Fig 4B)."

SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(HP:"Cognitive impairment")
UNSET MeSHDisease

UNSET Section

##################################################################################
SET Citation = {"PubMed", "EMBO J. 2017 Jul 3;36(13):1837-1853. doi: 10.15252/embj.201796516. Epub 2017 May 30.", "28559417"}

SET Section = "Abstract"

SET Support = "Genetic variants in the triggering receptor expressed on myeloid cells \
2 (TREM2) increase the risk for several neurodegenerative diseases \
including Alzheimer's disease and frontotemporal dementia (FTD)."

p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")
p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")

SET Support = "Homozygous TREM2 missense mutations, such as p.T66M, lead to the FTD- \
like syndrome, but how they cause pathology is unknown."

g(HGNC:TREM2, var("p.Thr66Met")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")

SET Support = "Consistent with a loss-of-function mutation, we observe an \
intracellular accumulation of immature mutant Trem2 and reduced \
generation of soluble Trem2 similar to patients with the homozygous \
p.T66M mutation."

g(MGI:Trem2, var("p.Thr66Met")) -> p(MGI:Trem2, var("p.Thr66Met"), loc(MESHA:"Intracellular Space"))
g(MGI:Trem2, var("p.Thr66Met")) -| p(MGI:Trem2, loc(MESHA:"Extracellular Space"))

SET Support = "Trem2 p.T66M knock-in mice show delayed resolution of inflammation \
upon in vivo lipopolysaccharide stimulation and cultured macrophages \
display significantly reduced phagocytic activity."

SET ChallengeType = "lipopolysaccharide stimulation"
SET Species = "10090"
g(MGI:Trem2, var("p.Thr66Met")) negativeCorrelation bp(GOBP:"inflammatory response")
UNSET ChallengeType
SET MeSHAnatomy = "Macrophages"
g(MGI:Trem2, var("p.Thr66Met")) negativeCorrelation bp(GOBP:phagocytosis)
UNSET MeSHAnatomy

SET Support = "Thus, we demonstrate that a TREM2 loss-of-function mutation causes \
brain-wide metabolic alterations pointing toward a possible function \
of microglia in regulating brain glucose metabolism."

SET MeSHAnatomy = "Brain"
g(MGI:Trem2, var("p.Thr66Met")) -- bp(GOBP:"glucose metabolic process")
a(MESHA:Microglia) -- bp(GOBP:"regulation of glucose metabolic process")
UNSET Species
UNSET MeSHAnatomy


UNSET Section

SET Section = "Results"

SET Support = "This also accounts for the threonine at position 66, which is fully \
conserved between various mammalian species (Fig 1A)."

g(HGNC:TREM2, var("p.Thr66Met")) -- g(MGI:Trem2, var("p.Thr66Met"))

SET Support = "In line with the accumulation of immature Trem2, analysis of sTrem2 by \
an ELISA that detects both wild‐type and mutant Trem2 protein \
(Appendix Fig S1) revealed a statistically significant gene \
dose‐dependent reduction of sTrem2 in TBS‐soluble brain extracts (Figs \
1E and EV2A), serum (Figs 1F and EV2B), and CSF (Fig 1G), thereby \
recapitulating the findings in patients with heterozygous (Piccio et \
al, 2016) or homozygous TREM2 p.T66M mutation (Kleinberger et al, \
2014; Henjum et al, 2016)."

SET Assay = "ELISA"
g(MGI:Trem2, var("p.Thr66Met")) -| p(MGI:Trem2, loc(MESHA:Brain))
g(MGI:Trem2, var("p.Thr66Met")) -| p(MGI:Trem2, loc(MESHA:Serum))
g(MGI:Trem2, var("p.Thr66Met")) -| p(MGI:Trem2, loc(MESHA:"Cerebrospinal Fluid"))
UNSET Assay

SET Support = "Reduced mutant sTrem2 was observed in all age groups analyzed (Fig \
EV2A and B) although wild‐type (wt) sTrem2 levels in the brain \
significantly increased during aging."

p(MGI:Trem2, var("p.Thr66Met")) negativeCorrelation bp(HP:aging)
p(MGI:Trem2, loc(MESHA:Brain)) positiveCorrelation bp(HP:aging)

SET Support = "Bone marrow‐derived macrophages (BMDMs) express high levels of Trem2 \
(Turnbull et al, 2006) and Trem2 knockout BMDMs show an exaggerated \
inflammatory response upon exposure to Toll‐like receptor agonists \
(Turnbull et al, 2006), decreased proliferation, and survival upon \
CSF1 deprivation (Otero et al, 2012; Wu et al, 2015) as well as \
reduced phagocytic activity (Xiang et al, 2016)."

p(MGI:Trem2, loc(MESHA:Macrophages)) negativeCorrelation bp(GOBP:"inflammatory response")
SET ChallengeType = "CSF1 deprivation"
p(MGI:Trem2, loc(MESHA:Macrophages)) positiveCorrelation bp(GOBP:"macrophage proliferation")
p(MGI:Trem2, loc(MESHA:Macrophages)) negativeCorrelation bp(GOBP:"macrophage apoptotic process")
UNSET ChallengeType
p(MGI:Trem2, loc(MESHA:Macrophages)) positiveCorrelation bp(GOBP:phagocytosis)

SET Support = "We consistently observed a significant reduction in the number of both \
heterozygous and homozygous Trem2 p.T66M BMDMs after 7 days of \
differentiation similar to Trem2 knockout (−/−) BMDMs (Fig 2A)."

p(MGI:Trem2, var("p.Thr66Met"), loc(MESHA:Macrophages)) negativeCorrelation a(MESHA:Macrophages)

SET Support = "BMDMs derived from Trem2 p.T66M knock‐in mice exhibit significantly \
increased caspase 3/7 activation (Fig 2B) as well as reduced \
proliferation as shown by reduced BrdU incorporation (Fig 2C) and \
decreased mRNA levels of cyclin D1 (Fig 2D)."

p(MGI:Trem2, var("p.Thr66Met"), loc(MESHA:Macrophages)) -> act(p(MGI:Casp3, loc(MESHA:Macrophages)))
p(MGI:Trem2, var("p.Thr66Met"), loc(MESHA:Macrophages)) -> act(p(MGI:Casp7, loc(MESHA:Macrophages)))
p(MGI:Trem2, var("p.Thr66Met"), loc(MESHA:Macrophages)) negativeCorrelation bp(GOBP:"macrophage proliferation")
p(MGI:Trem2, var("p.Thr66Met"), loc(MESHA:Macrophages)) -| r(MGI:Ccnd1, loc(MESHA:Macrophages))

SET Support = "Upon shedding of Trem2, a C‐terminal membrane‐associated fragment is \
retained and further degraded by γ‐secretase (Wunderlich et al, 2013)."

complex(SCOMP:"gamma Secretase Complex") =| p(MGI:Trem2, frag("?_*"))

SET Support = "Previously, we and others could show that the TREM2 C‐terminal \
fragment (CTF) is dramatically reduced in cells overexpressing TREM2 \
p.T66M (Kleinberger et al, 2014; Park et al, 2015)."

p(MGI:Trem2, var("p.Thr66Met")) -| p(MGI:Trem2, frag("?_*"))

SET Support = "γ‐Secretase‐mediated degradation of the Trem2 CTF seems to be highly \
efficient in BMDMs, as seen by very low constitutive levels of CTF \
(Fig 3A)."

p(SCOMP:"gamma Secretase Complex", loc(MESHA:Macrophages)) =| p(MGI:Trem2, frag("?_*"))

SET Support = "Trem2 facilitates phagocytosis of E. coli (N'Diaye et al, 2009), \
apoptotic neurons (Takahashi et al, 2005; Hsieh et al, 2009), as well \
as fibrillar amyloid‐β peptides (Xiang et al, 2016)."

p(MGI:Trem2) positiveCorrelation bp(GOBP:phagocytosis)
p(MGI:Trem2) -| a(CHEBI:"amyloid-beta")

SET Support = "Similar to BMDMs derived from Trem2 knockout mice (Xiang et al, 2016), \
homozygous Trem2 p.T66M knock‐in BMDMs showed a significantly reduced \
capacity of engulfing pHrodo‐labeled E. coli particles (Fig 3F) as \
well as fluorescently labeled fibrillar Aβ1–42 (Fig 3G) supporting the \
loss‐of‐function character of the Trem2 p.T66M mutation."

p(MGI:Trem2, var("p.Thr66Met"), loc(MESHA:Macrophages)) negativeCorrelation bp(GOBP:phagocytosis)
p(MGI:Trem2, var("p.Thr66Met"), loc(MESHA:Macrophages)) positiveCorrelation p(MGI:App, frag("1_42"), loc(MESHA:Neurofibrils))

SET Support = "Stimulation of macrophages with lipopolysaccharide (LPS) or \
interferon‐γ (IFN‐γ) causes a robust downregulation of Trem2 \
expression (Turnbull et al, 2006) and studies using Trem2‐deficient \
macrophages or microglial cell lines suggested Trem2 to be involved in \
inhibiting cytokine production upon stimulation with LPS or IFN‐γ \
(Turnbull et al, 2006; Zhong et al, 2015)."

a(CHEBI:lipopolysaccharide) -| p(HGNC:TREM2)
p(HGNC:IFNG) -| p(HGNC:TREM2)
SET ChallengeType = {"IFN-γ stimulation", "lipopolysaccharide stimulation"}
p(HGNC:TREM2, loc(MESHA:Macrophages)) -| bp(GOBP:"macrophage cytokine production")
p(HGNC:TREM2, loc(MESHA:Microglia)) -| p(MESHC:Cytokines)
UNSET ChallengeType

SET Support = "First, we confirmed that LPS injection led to a marked reduction of \
Trem2 mRNA in the brain of both wild‐type and homozygous Trem2 p.T66M \
knock‐in mice (Fig 4B)."

a(CHEBI:lipopolysaccharide) -| r(MGI:Trem2, loc(MESHA:Brain))
a(CHEBI:lipopolysaccharide) -| r(MGI:Trem2, var("p.Thr66Met"), loc(MESHA:Brain))

SET Support = "While only a small increase of IL‐6, TNF‐α, and CCL2 plasma and brain \
expression was seen 18 h after LPS injection in wild‐type mice, \
homozygous Trem2 p.T66M knock‐in mice showed significantly higher \
levels of these cytokines consistent with an exaggerated and/or \
prolonged inflammatory response."

SET TimePoint = "18 hours post-stimulation"
SET ChallengeType = "lipopolysaccharide stimulation"
p(MGI:Trem2, var("p.Thr66Met")) positiveCorrelation p(MGI:Il6, loc(MESHA:Brain))
p(MGI:Trem2, var("p.Thr66Met")) positiveCorrelation p(MGI:Il6, loc(MESHA:Plasma))
p(MGI:Trem2, var("p.Thr66Met")) positiveCorrelation p(MGI:Tnf, loc(MESHA:Brain))
p(MGI:Trem2, var("p.Thr66Met")) positiveCorrelation p(MGI:Tnf, loc(MESHA:Plasma))
p(MGI:Trem2, var("p.Thr66Met")) positiveCorrelation p(MGI:Ccl2, loc(MESHA:Brain))
p(MGI:Trem2, var("p.Thr66Met")) positiveCorrelation p(MGI:Ccl2, loc(MESHA:Plasma))
UNSET TimePoint
UNSET ChallengeType
p(MGI:Trem2, var("p.Thr66Met")) positiveCorrelation bp(GOBP:"inflammatory response")

SET Support = "In line with previous results (Liu et al, 2015; Brendel et al, 2016), \
we found an age‐dependent increase in TSPO μPET signal in wild‐type \
mice (Fig 5A and B)."

# TSPO indicates activated microglia
a(MESHA:Microglia) positiveCorrelation bp(HP:aging)

SET Support = "However, a globally lower retention in Trem2 p.T66M knock‐in mice at 8 \
months was observed which was even more pronounced at 12 months of age \
(Fig 5A). Quantitative cerebral TSPO μPET signals increased in \
parallel in 3‐ and 5‐month‐old wild‐type and Trem2 p.T66M knock‐in \
mice (Fig 5B). While wild‐type mice continued their increase at a \
flattened slope until 12 months of age, the TSPO signal in Trem2 \
p.T66M knock‐in mice started to decline after 8 months (Δ8%, P = \
0.16), resulting in a significantly reduced TSPO μPET signal at 12 \
months of age (Δ23%, P < 0.01) (Fig 5B)."

SET TimePoint = {"3 months", "5 months", "8 months", "12 months"}
p(MGI:Trem2, var("p.Thr66Met")) negativeCorrelation a(MESHA:Microglia)
UNSET TimePoint

SET Support = "Strikingly, we observed an age‐dependent increase in Iba1 \
immunoreactivity in wild‐type but not homozygous Trem2 p.T66M knock‐in \
mice especially in the white matter of the cerebellum (Fig 5C) which \
is consistent with a strong TSPO μPET signal in the cerebellum of \
wild‐type but not homozygous Trem2 p.T66M knock‐in mice (Fig 5A)."

# Iba1-1 = Aif1
p(MGI:Aif1) positiveCorrelation bp(HP:aging)
p(MGI:Trem2, var("p.Thr66Met")) -| p(MGI:Aif1, loc(MESHA:Cerebellum))

SET Support = "Furthermore, we frequently observed strongly Iba1 immunoreactive \
microglia nodule‐like structures in wild‐type but not homozygous Trem2 \
p.T66M knock‐in brains (Fig 5C, arrowheads)."

p(MGI:Trem2, var("p.Thr66Met")) -| p(MGI:Aif1, loc(MESHA:Microglia))

SET Support = "An age‐dependent accumulation of nodule‐like structures of Iba1 \
immunoreactive microglia was also observed throughout the whole brain \
of wild‐type but not Trem2 p.T66M knock‐in mice (Fig 5E and F),"

p(MGI:Aif1, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:aging)

SET Support = "In humans, homozygous TREM2 mutations lead to marked thinning of the \
corpus callosum, cortical atrophy, and periventricular white matter \
abnormalities (Guerreiro et al, 2013b)."

p(HGNC:TREM2, var("?")) negativeCorrelation a(MESHA:"White Matter")

SET Support = "Patients with homozygous TREM2 mutations develop NHD or FTD‐like \
syndrome manifesting with cognitive impairments that have been \
associated with frontal lobe hypometabolism as shown by reduced \
signals by FDG‐PET (Ueki et al, 2000)."

p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
p(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")

SET Support = "Visual interpretation of FDG‐μPET scans indicate a reduced signal \
throughout different areas of the brain (Fig 7B; axial and coronal \
slices) and quantitative analyses confirmed a significant reduction in \
the global cerebral FDG‐μPET signal in Trem2 p.T66M knock‐in mice \
compared to age‐ and sex‐matched wild‐type controls (Fig 7C; Δ23%; P < \
0.01)."

# FDG-PET is used to measure cerebral metabolism of glucose and used as an indirect measure of neauronal activity
SET MeSHAnatomy = "Brain"
p(MGI:Trem2, var("p.Thr66Met")) negativeCorrelation bp(GOBP:"glucose metabolic process")

SET Support = "Correlation of dual quantitative μPET readouts showed a statistically \
significant positive correlation (Fig 7D; r 2 = 0.5269; P = 0.041) in \
Trem2 p.T66M knock‐in mice, suggesting a relationship between reduced \
microglial activity due to the Trem2 mutation and reduced brain \
function."

bp(GOBP:"glucose metabolic process") positiveCorrelation a(MESHA:Microglia)
UNSET MeSHAnatomy


UNSET Section

##################################################################################

SET Citation = {"PubMed", "PLoS One. 2013;8(1):e52982. doi: 10.1371/journal.pone.0052982. Epub 2013 Jan 3.", "23301011"}

SET Section = "Abstract"

SET Support = "Triggering receptor expressed on myeloid cells-2 (TREM2) is a \
microglial surface receptor involved in phagocytosis."

p(HGNC:TREM2, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:phagocytosis)

SET Support = "Quantitative PCR (qPCR) revealed a greatly increased transcription of \
TREM2 after stroke."

SET Assay = "qPCR"
path(MESHD:Stroke) positiveCorrelation r(HGNC:TREM2)
UNSET Assay

SET Support = "However, in the sub-acute phase (7 d reperfusion) following stroke, \
TREM2-KO mice showed a decreased transcription of pro-inflammatory \
cytokines TNFα, IL-1α and IL-1β, associated with a reduced microglial \
activity (CD68, Iba1)."

SET Species = "10090"
SET TimePoint = "7 days reperfusion"
SET MeSHDisease = "Stroke"

p(MGI:Trem2) -> r(MGI:Tnf)
#Tnf = TNFa in mouse
p(MGI:Trem2) -> r(MGI:Il1a)
p(MGI:Trem2) -> r(MGI:Il1b)
#Aif1 is Iba1
p(MGI:Trem2) -> r(MGI:Aif1)
p(MGI:Trem2) -> r(MGI:Cd68)
p(MGI:Trem2) positiveCorrelation bp(GOBP:"microglial cell activation")

UNSET TimePoint

SET Support = "Furthermore, TREM2-KO mice showed a reduced transcription of \
chemokines CCL2 (MCP1), CCL3 (MIP1α) and the chemokine receptor \
CX3CR1, followed by a diminished invasion of CD3-positive T-cells."

p(MGI:Trem2) -> r(MGI:Ccl2)
p(MGI:Trem2) -> r(MGI:Ccl3)
p(MGI:Trem2) -> r(MGI:Cx3cr1)
p(MGI:Trem2) positiveCorrelation a(MESHA:"T-Lymphocytes")

SET Support = "We therefore conclude that TREM2 appears to sustain a distinct \
inflammatory response after stroke."

p(MGI:Trem2) regulates bp(GOBP:"inflammatory response")

UNSET Species
UNSET MeSHDisease

UNSET Section

SET Section = "Results"

SET Species = "10090"

SET Support = "Cerebral TREM2 mRNA expression was up-regulated in the ischemic \
hemisphere, showing a peak at 7 d after MCAO (ratio ipsi vs. contra: \
10±1.9, p≤0.001, n = 4) and remaining up-regulated at 28 d (ratio ipsi \
vs. contra: 4±0.9, p≤0.001, n = 4) (Figure 1A)."

SET TimePoint = {"7 days reperfusion", "28 days reperfusion"}
r(MGI:Trem2, loc(MESHA:Cerebrum)) positiveCorrelation path(MESHD:Stroke)
UNSET TimePoint

SET Support = "A strong up-regulation of TNFα, IL-1α, IL-1β and IL-6 was observed at \
12 h in the ischemic hemisphere after MCAO. The acute inflammatory \
cytokine transcription at 12 h reperfusion was very similar between \
TREM2-KO mice and their littermate controls (Figure 1C)."

SET TimePoint = "12 hours reperfusion"
r(MGI:Tnf, loc(MESHA:Cerebrum)) positiveCorrelation path(MESHD:Stroke)
r(MGI:Il1a, loc(MESHA:Cerebrum)) positiveCorrelation path(MESHD:Stroke)
r(MGI:Il1b, loc(MESHA:Cerebrum)) positiveCorrelation path(MESHD:Stroke)
r(MGI:Il6, loc(MESHA:Cerebrum)) positiveCorrelation path(MESHD:Stroke)
UNSET TimePoint

SET Support = "At 7 d after MCAO, transcription of TNFα, IL-1α and IL-1β was reduced \
in TREM2-KO compared to littermate control mice (Figure 1C)."

SET TimePoint = "7 days reperfusion"
p(MGI:Trem2) -> r(MGI:Tnf)
p(MGI:Trem2) -> r(MGI:Il1a)
p(MGI:Trem2) -> r(MGI:Il1b)
UNSET TimePoint

SET Support = "However, microglial cells in TREM2-KO mice remained in a more non- \
activated ramified phenotype with long Iba1 positive processes (Figure \
2A)."

p(MGI:Trem2) -> bp(GOBP:"microglial cell activation")

SET Support = "Significantly fewer Iba1 positive cells were revealed in the glial \
scar of TREM2-KO mice at day 7 following stroke compared to littermate \
controls (56% ±6.6, p≤0.001, n = 6 each; Figure 2B)."

SET TimePoint = "7 days reperfusion"
p(MGI:Trem2) -> r(MGI:Aif1)

SET Support = "Both gene transcripts, Iba1 and CD68, were reduced in TREM2-KO mice at \
day 7 after stroke (KO vs. WT; Iba1∶46% ±3.7, CD68∶20% ±2.0, p≤0.05, \
n = 5/6 each; Figure 2C), while no significant differences were \
observed at 12 h and 28 d after stroke (Figure 2C)."

p(MGI:Trem2) -> r(MGI:Aif1)
p(MGI:Trem2) -> r(MGI:Cd68)
UNSET TimePoint

SET Support = "Transcripts for the chemokines CCL3, CCL2 and CCL5 were increased in \
the ischemic hemisphere of wildtype control mice following stroke, \
with a peak at 12 h for CCL3 and CCL2 and a peak at 7 d for CCL5 \
(Figure 3A)."

# Ischemic hemisphere in this paper indicates the side of the brain that underwent the stroke (here it is the ipisilateral side)
SET TimePoint = {"12 hours reperfusion", "7 days reperfusion", "28 days reperfusion"}
path(MESHD:Stroke) positiveCorrelation r(MGI:Ccl2)
path(MESHD:Stroke) positiveCorrelation r(MGI:Ccl3)
path(MESHD:Stroke) positiveCorrelation r(MGI:Ccl5)
UNSET TimePoint

SET Support = "Transcripts of chemokine receptors were also increased in the ischemic \
hemisphere of wildtype control mice, with a peak at 12 h for CCR1 and \
a peak at 7 d for CCR2, CCR5 and CX3CR1 after stroke (Figure 3B)."

SET TimePoint = {"12 hours reperfusion", "7 days reperfusion"}
path(MESHD:Stroke) positiveCorrelation r(MGI:Ccr1)
path(MESHD:Stroke) positiveCorrelation r(MGI:Ccr2)
path(MESHD:Stroke) positiveCorrelation r(MGI:Ccr5)
path(MESHD:Stroke) positiveCorrelation r(MGI:Cx3cr1)
UNSET TimePoint

SET Support = "Whereas CCR1, CCR2 and CCR5 expression was found to be unchanged, a \
reduced gene transcription of the microglial-associated chemokine \
receptor CX3CR1 was observed after stroke in TREM2-KO mice compared to \
littermate control mice (12 h: to 79% ±6.7; 7 d: to 50% ±4.6, p≤0.05, \
n = 5/6 each; Figure 3B)."

SET TimePoint = "12 hours reperfusion"
p(MGI:Trem2) -> r(MGI:Cx3cr1)
UNSET TimePoint

SET Support = "At 28 d following stroke, a strong invasion of CD3-positive T-cells \
was observed in the infarct core of littermate control mice, whereas \
such a change was absent in TREM2-KO mice (Figure 4A)."

SET TimePoint = "28 days reperfusion"
p(MGI:Trem2) -> a(MESHA:"T-Lymphocytes")
UNSET TimePoint

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Int J Alzheimers Dis. 2010 Jun 22;2010. pii: 587463. doi: 10.4061/2010/587463.", "20721346"}

SET PublicationType = "Review"
SET Section = "Abstract"

SET Support = "Microglial triggering receptor expressed on myeloid cells-2 (TREM2), \
signal regulatory protein-beta1, and complement receptor-3 \
(CD11b/CD18) signal via the adaptor protein DAP12 and activate \
phagocytic activity of microglia."

# TYROBP = DAP12
SET MeSHAnatomy = "Microglia"
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> bp(GOBP:phagocytosis)
# signal regulatory protein-beta1
complex(p(HGNC:SIRPB1), p(HGNC:TYROBP)) -> bp(GOBP:phagocytosis)
# CD11b = ITGAM, CD18 = ITGB2
complex(p(HGNC:ITGAM), p(HGNC:ITGB2), p(HGNC:TYROBP)) -> bp(GOBP:phagocytosis)
UNSET MeSHAnatomy

SET Support = "Siglecs can suppress the proinflammatory and phagocytic activity of \
microglia via ITIM signaling."

SET MeSHAnatomy = "Microglia"
p(HGNC:SIGLEC1) -| bp(GOBP:phagocytosis)
p(HGNC:SIGLEC1) -| bp(GOBP:"inflammatory response")
UNSET MeSHAnatomy

SET Support = "Moreover, microglial neurotoxicity is alleviated via interaction of \
Siglec-11 with sialic acids on the neuronal glycocalyx."

complex(p(HGNC:SIGLEC11), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -| bp(GOBP:"microglial cell mediated cytotoxicity")

UNSET Section

SET Section = "1. Microglia and Alzheimer's Disease"

SET Support = "Microglial cells originate from myeloid cells of the hematopoietic \
lineage and are the resident immune cells of the central nervous \
system (CNS)."

a(MESHA:"Myeloid Cells") -> a(MESHA:Microglia)
a(MESHA:Microglia) positiveCorrelation a(MESHA:"Central Nervous System")

SET Support = "They are involved in the active immune defense by their ability to \
phagocytose invading bacteria and to release reactive oxygen species \
acting as microbicides."

a(MESHA:Microglia) positiveCorrelation bp(GOBP:"innate immune response")
bp(GOBP:phagocytosis) -| a(MESHA:"Bacterial Structures")
a(MESHA:Microglia) -> a(CHEBI:"reactive oxygen species")

SET Support = "Microglia are involved in tissue maintenance, execution of innate \
immunity, and participation in adaptive immune responses [1–3]."

a(MESHA:Microglia) positiveCorrelation bp(GOBP:"tissue homeostasis")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"innate immune response")
a(MESHA:Microglia) -- bp(GOBP:"adaptive immune response")

SET Support = "In response to injury, ischemia and inflammatory stimuli microglia \
change from an immunologically silent state to an activated state that \
is reflected in different morphological appearance - amoeboid, \
rodlike, or phagocytic."

path(MESHD:"Diffuse Axonal Injury") -> bp(GOBP:"microglial cell activation")
path(MESHD:"Brain Ischemia") -> bp(GOBP:"microglial cell activation")
a(CHEBI:antigen) -> bp(GOBP:"microglial cell activation")

SET Support = "On the one hand, they are neurotoxic by producing pro-inflammatory \
mediators including cytokines and reactive oxygen species such as \
interleukin-1β, tumor necrosis factor-α and nitric oxide, which are \
potent inducers of neuronal damage and cell death [3]."

bp(GOBP:"microglial cell activation") -> bp(GOBP:"cytokine production involved in inflammatory response")
bp(GOBP:"cytokine production involved in inflammatory response") -> p(HGNC:IL1B)
bp(GOBP:"cytokine production involved in inflammatory response") -> p(HGNC:TNF)
bp(GOBP:"microglial cell activation") -> a(CHEBI:"nitric oxide")
bp(GOBP:"microglial cell activation") -> a(CHEBI:"reactive oxygen species")
p(HGNC:IL1B) -| a(MESHA:Neurons)
p(HGNC:TNF) -| a(MESHA:Neurons)
a(CHEBI:"nitric oxide") -| a(MESHA:Neurons)
a(CHEBI:"reactive oxygen species") -| a(MESHA:Neurons)

SET Support = "On the other hand, they can also initiate anti-inflammatory and \
immunosuppressive signaling that results in repair, resolution of \
inflammation and turning back to tissue homeostasis [3, 6, 7]."

a(MESHA:Microglia) -| bp(GOBP:"inflammatory response")
a(MESHA:Microglia) -> bp(GOBP:"tissue regeneration")
a(MESHA:Microglia) -> bp(GOBP:"tissue homeostasis")

SET Support = "Furthermore, microglia act as regulators of neuronal survival and \
development through cytokines and chemokines such as interleukin-6 and \
CCL5 (RANTES)."

a(MESHA:Microglia) -> p(HGNC:IL6)
a(MESHA:Microglia) -> p(HGNC:CCL5)
p(HGNC:IL6) positiveCorrelation a(MESHA:Neurons)
p(HGNC:IL6) positiveCorrelation bp(GOBP:"neuron differentiation")
p(HGNC:CCL5) positiveCorrelation a(MESHA:Neurons)
p(HGNC:CCL5) positiveCorrelation bp(GOBP:"neuron differentiation")

SET Support = "Activated, interleukin-6 producing microglia have been shown to \
decrease in vitro the neurogenesis of neural stem cells and increase \
the number of apoptotic cells in differentiating cultures [8]."

SET StudyType = "in vitro"
a(MESHA:Microglia) -> p(HGNC:IL6)
p(HGNC:IL6) -| bp(GOBP:neurogenesis)
p(HGNC:IL6) -> bp(MESHPP:Apoptosis)
UNSET StudyType

SET Support = "They can either be deleterious by enhancing neurodegeneration through \
secreting cytokines and neurotoxins [10], or might be beneficial by \
principally migrating to the amyloid-β (Aβ) plaques and phagocytosing \
Aβ deposits."

# "They" refers to microglia
a(MESHA:Microglia, loc(CHEBI:"amyloid-beta")) -> bp(GOBP:phagocytosis)
bp(GOBP:phagocytosis) -| a(CHEBI:"amyloid-beta")
a(MESHA:Microglia, loc(CHEBI:"amyloid-beta")) -| a(CHEBI:"amyloid-beta")

SET Support = "Recently, it was shown by in vivo multiphoton microscopy in different \
animal models of AD that Aβ plaques could appear within 24 hours and \
microglial cells are activated and recruited to the newly formed \
plaques within one day [11]."

SET Species = "10090"
SET StudyType = "in vivo"
SET TechnologyUsed = "multiphoton microscopy"
SET MeSHDisease = "Alzheimer Disease"
SET TimePoint = "1 day"
a(CHEBI:"amyloid-beta", loc(MESHA:Brain)) positiveCorrelation bp(GOBP:"microglial cell activation")
bp(GOBP:"microglial cell activation") -> a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))
UNSET Species
UNSET StudyType
UNSET TechnologyUsed
UNSET MeSHAnatomy
UNSET TimePoint

SET Support = "Interestingly, microglial cells seem to contribute to AD progression."

path(MESHD:"Alzheimer Disease") positiveCorrelation a(MESHA:Microglia)

SET Support = "Although maintaining their ability to produce pro-inflammatory \
cytokines, microglia of aging APP/PS1-transgenic mice, a mouse model \
of Aβ plaque formation associated with AD, become dysfunctional and \
display a reduced Aβ clearance capability [12], suggesting that Aβ \
plaques might partially result from impaired microglial removal."

SET Species = "10090"
SET MeSHAnatomy = "Microglia"
SET MouseStrain = "APPPS1"
a(MESHA:Microglia) -> bp(GOBP:"beta-amyloid clearance")
path(MESHD:"Alzheimer Disease") negativeCorrelation bp(GOBP:"beta-amyloid clearance")
UNSET Species
UNSET MeSHAnatomy
UNSET MouseStrain

SET Support = "Nevertheless, several lines of in vitro evidence suggest the \
involvement of innate immune signaling during recognition of Aβ."

SET StudyType = "in vitro"
bp(GOBP:"innate immune response") -- a(CHEBI:"amyloid-beta")
UNSET StudyType

SET Support = "Different receptors expressed on microglia such as CD14 and toll-like \
receptors (TLR) 2 and 4 are known to contribute to the clearance of Aβ \
plaques in AD [14–16]."

SET MeSHDisease = "Alzheimer Disease"
a(MESHA:Microglia) -> p(HGNC:CD14, loc(MESHA:Microglia))
a(MESHA:Microglia) -> p(HGNC:TLR2, loc(MESHA:Microglia))
a(MESHA:Microglia) -> p(HGNC:TLR4, loc(MESHA:Microglia))
p(HGNC:CD14, loc(MESHA:Microglia)) -> bp(GOBP:"beta-amyloid clearance")
p(HGNC:TLR2, loc(MESHA:Microglia)) -> bp(GOBP:"beta-amyloid clearance")
p(HGNC:TLR4, loc(MESHA:Microglia)) -> bp(GOBP:"beta-amyloid clearance")
UNSET MeSHDisease

SET Support = "CD14 is involved in the uptake of the bacterial component \
lipopolysaccharide (LPS) [15]."

p(HGNC:CD14) -> complex(p(HGNC:TLR4), a(CHEBI:lipopolysaccharide))

SET Support = "To transduce activation signals, CD14 interacts with TLR2 and TLR4 \
containing dimeric complexes [16, 17]."

complex(p(HGNC:TLR2), p(HGNC:CD14)) -> bp(GOBP:"signal transduction")
complex(p(HGNC:TLR4), p(HGNC:CD14)) -> bp(GOBP:"signal transduction")

SET Support = "Additionally, CD14 has been shown to specifically mediate Aβ \
phagocytosis in vitro."

SET StudyType = "in vitro"
p(HGNC:CD14) -- bp(GOBP:"beta-amyloid clearance")
p(HGNC:CD14) -- bp(GOBP:phagocytosis)
UNSET StudyType

SET Support = "Cells expressing CD14 internalized significantly higher amounts of Aβ \
compared to CD14-deficient cells while the uptake of microbeads was \
unaffected [15]."

p(HGNC:CD14) -| a(CHEBI:"amyloid-beta")
a(CHEBI:"amyloid-beta") negativeCorrelation bp(GOBP:"beta-amyloid clearance")
p(HGNC:CD14) -> bp(GOBP:"beta-amyloid clearance")

SET Support = "Moreover, CD14 acts together with TLR2 and TLR4 to bind Aβ and \
subsequently activate intracellular signaling leading to phagocytosis \
in vitro."

SET StudyType = "in vitro"
complex(p(HGNC:TLR2), p(HGNC:CD14), a(CHEBI:"amyloid-beta")) -> bp(GOBP:"immune response-regulating cell surface receptor signaling pathway involved in phagocytosis")
complex(p(HGNC:TLR4), p(HGNC:CD14), a(CHEBI:"amyloid-beta")) -> bp(GOBP:"immune response-regulating cell surface receptor signaling pathway involved in phagocytosis")
bp(GOBP:"immune response-regulating cell surface receptor signaling pathway involved in phagocytosis") -> bp(GOBP:phagocytosis)
UNSET StudyType

SET Support = "Recently, CD36, another coreceptor of TLRs, has been described in \
vitro to facilitate the assembly of a heteromeric complex of CD36, \
TLR4, and TLR6 upon binding of Aβ [14]."

SET StudyType = "in vitro"
p(HGNC:CD36) => complex(p(HGNC:TLR4), p(HGNC:CD36), p(HGNC:TLR6), a(CHEBI:"amyloid-beta"))
UNSET StudyType

UNSET Section

SET Section = "2. Microglial ITAM-ITIM Signaling"

SET Support = "Latest publications indicate that immunoreceptor tyrosine-based \
activation motif (ITAM) signaling plays an important role in the \
phagocytic process."

bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif") positiveCorrelation bp(GOBP:phagocytosis)

SET Support = "The mammalian DAP12 molecule is a transmembrane adaptor protein that \
contains two ITAMs."

p(HGNC:TYROBP) positiveCorrelation bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")

SET Support = "It is expressed by microglia and associates with cell membrane \
receptors such as triggering receptor expressed on myeloid cells 2 \
(TREM2) [18] or signal regulatory protein-β1 (SIRPβ1) [22]."

# "It" refers to DAP12
a(MESHA:Microglia) -> p(HGNC:TYROBP)
p(HGNC:TYROBP) => complex(p(HGNC:TYROBP), p(HGNC:TREM2))
p(HGNC:TYROBP) => complex(p(HGNC:TYROBP), p(HGNC:SIRPB1))

SET Support = "Upon stimulation of SIRPβ1 or TREM2, a phosphorylation of DAP12-ITAM \
is induced and the phagocytic activity of microglial cells is \
increased in vitro [22, 24, 25]."

SET MeSHAnatomy = "Microglia"
SET StudyType = "in vitro"
act(p(HGNC:TREM2)) -> act(p(HGNC:TYROBP, pmod(Ph)), ma(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif"))
act(p(HGNC:SIRPB1)) -> act(p(HGNC:TYROBP, pmod(Ph)), ma(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif"))
act(p(HGNC:TYROBP, pmod(Ph)), ma(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")) -> bp(GOBP:phagocytosis)
act(p(HGNC:TYROBP, pmod(Ph)), ma(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")) -> bp(GOBP:"microglial cell activation")
UNSET MeSHAnatomy
UNSET StudyType

SET Support = "ITAMs are counter-regulated by immunoreceptor tyrosine-based \
inhibition motifs (ITIMs; Figure 1, right side)."

bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif") -| bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")

SET Support = "Most CD33-related sialic acid-binding immunoglobulin superfamily \
lectins (Siglecs), a subgroup of the immunoglobulin superfamily that \
recognizes sialic acid residues of glycoproteins and glycolipids, have \
one or more ITIMs in the cytoplasmic domain [27, 29]."

p(HGNC:CD33) => complex(a(CHEBI:"sialic acid"), p(HGNC:CD33))
p(HGNC:CD33) positiveCorrelation bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")

SET Support = "Binding of Siglecs to highly sialylated proteins and lipids such as \
clusterin, apolipoprotein E and gangliosides that are abundantly \
present in AD plaques could in turn mediate an inhibitory signaling \
cascade."

complex(p(HGNC:CD33), a(CHEBI:"sialic acid", loc(HGNC:APP))) -| bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")
complex(p(HGNC:CD33), a(CHEBI:"sialic acid", loc(HGNC:CLU))) -| bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")
complex(p(HGNC:CD33), a(CHEBI:"sialic acid", loc(CHEBI:ganglioside))) -| bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")

UNSET Section

SET Section = "3. Microglial Fc Receptors"

SET Support = "An important group of receptors on the surface of phagocytes, which \
signal via ITAM-Syk signaling and mediate phagocytosis function, \
include the Fc receptors (FcR)."

# Almost all FcRs are gamma
p(SCOMP:"FcR Gamma Chain Complex", loc(MESHA:Phagocytes)) -> act(p(HGNC:SYK), ma(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif"))
p(SCOMP:"FcR Gamma Chain Complex", loc(MESHA:Phagocytes)) positiveCorrelation bp(GOBP:phagocytosis)

SET Support = "Accordingly, microglial cells express the activating FcRs CD16, CD32 \
and CD64 and phagocytose antigens via the corresponding IgG subtypes \
[34, 35]."

# CD16 = FCGR3A, CD32 = FCGR2A, CD64 = FCGR1A
a(MESHA:Microglia) -> p(HGNC:FCGR3A)
a(MESHA:Microglia) -> p(HGNC:FCGR2A)
a(MESHA:Microglia) -> p(HGNC:FCGR1A)
p(HGNC:FCGR3A, loc(MESHA:Microglia)) -> bp(GOBP:phagocytosis)
p(HGNC:FCGR2A, loc(MESHA:Microglia)) -> bp(GOBP:phagocytosis)
p(HGNC:FCGR1A, loc(MESHA:Microglia)) -> bp(GOBP:phagocytosis)

SET Support = "Moreover, in brain areas displaying neurodegeneration such as multiple \
sclerosis lesions, the expression of those FcRs on microglia is \
increased [34], suggesting a role of FcRs in protecting the \
surrounding tissue from IgG-opsonized antigens [35]."

path(HP:Neurodegeneration) positiveCorrelation p(HGNC:FCGR3A, loc(MESHA:Microglia))
path(HP:Neurodegeneration) positiveCorrelation p(HGNC:FCGR2A, loc(MESHA:Microglia))
path(HP:Neurodegeneration) positiveCorrelation p(HGNC:FCGR1A, loc(MESHA:Microglia))

UNSET Section

SET Section = "4. Microglial DAP12 Associated Receptors"

SET Support = "The glycoprotein TREM2 is expressed on microglia [41], and consists of \
one extracellular Ig-like domain, a transmembrane region with a \
charged lysine residue and a short cytoplasmic tail [18]."

a(MESHA:Microglia) -> p(HGNC:TREM2)

SET Support = "As mentioned before, the mammalian adaptor molecule DAP12 is another \
protein besides the common γ chain of FcR that activates an ITAM-Src \
kinase signaling pathway."

p(HGNC:TYROBP) -> act(p(SFAM:"SRC Family"), ma(kin))
p(SCOMP:"FcR Gamma Chain Complex") -> act(p(SFAM:"SRC Family"), ma(kin))

SET Support = "Via signaling through the adaptor protein DAP12, TREM2, a phagocytic \
receptor with still unknown endogenous ligand, leads to activation of \
microglial cells."

act(complex(p(HGNC:TYROBP), p(HGNC:TREM2)), ma(GOBP:signaling)) -> bp(GOBP:"microglial cell activation")

SET Support = "Activated microglia in turn can clear cellular apoptotic material, \
thereby contributing to tissue repair [18, 24, 43]."

bp(GOBP:"microglial cell activation") -> bp(GOBP:"apoptotic cell clearance")
bp(GOBP:"apoptotic cell clearance") -> bp(GOBP:"tissue regeneration")

SET Support = "Interestingly, loss-of-function mutations of DAP12 or TREM2 both lead \
to a chronic neurodegenerative disease called Nasu-Hakola or \
polycystic lipomembranous osteodysplasia with sclerosing \
leukoencephalopathy (PLOSL), an autosomal recessive inherited disease \
[18]."

p(HGNC:TYROBP, var("?")) -> path(DO:"Nasu-Hakola disease")
p(HGNC:TREM2, var("?")) -> path(DO:"Nasu-Hakola disease")

SET Support = "Moreover, it has been demonstrated that TREM2 is down-regulated by \
inflammatory signals [47]."

bp(GOBP:"inflammatory response") negativeCorrelation p(HGNC:TREM2)

SET Support = "Like TREM2, SIRPβ1 is expressed on microglial cells."

a(MESHA:Microglia) -> p(HGNC:SIRPB1)

SET Support = "Furthermore, TREM2 and SIRPβ1 are plaque-associated and increase the \
phagocytic activity of microglia [24, 25, 42, 43]."

p(HGNC:TREM2, loc(MESHA:Microglia)) positiveCorrelation path(MESHD:"Plaque, Amyloid")
p(HGNC:SIRPB1, loc(MESHA:Microglia)) positiveCorrelation path(MESHD:"Plaque, Amyloid")
p(HGNC:SIRPB1, loc(MESHA:Microglia)) -> bp(GOBP:phagocytosis)
p(HGNC:TREM2, loc(MESHA:Microglia)) -> bp(GOBP:phagocytosis)

SET Support = "Upon neurodegenerative signals, TREM2 expression is induced leading to \
increased phagocytosis and decreased pro-inflammatory responses of \
microglial cells [42]."

path(HP:Neurodegeneration) -> p(HGNC:TREM2)
p(HGNC:TREM2) -> bp(GOBP:phagocytosis)
p(HGNC:TREM2) -| bp(GOBP:"inflammatory response")

SET Support = "However, SIRPβ1 does not only specifically clear Aβ but also neural \
debris and microsphere beads [25]."

p(HGNC:SIRPB1) -> bp(GOBP:"apoptotic cell clearance")
p(HGNC:SIRPB1) -| a(CHEBI:"amyloid-beta")

SET Support = "A strong increase of microglial SIRPβ1 gene transcript has been \
revealed in the cerebral hemispheres and cerebellum of an animal model \
of AD, while the gene transcript of DAP12 has only been increased \
slightly."

SET Species = "10090"
r(MGI:Sirpb1a, loc(MESHA:Microglia), loc(MESHA:Cerebrum)) positiveCorrelation path(MESHD:"Alzheimer Disease")
r(MGI:Sirpb1a, loc(MESHA:Microglia), loc(MESHA:Cerebellum)) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET Species

SET Support = "Moreover, it is not directly triggered by the amyloid plaques but by \
other disease-associated processes including interferons (IFNs) like \
IFNβ and IFNγ."

p(HGNC:IFNB1) -> p(HGNC:SIRPB1)
p(HGNC:IFNG) -> p(HGNC:SIRPB1)

SET Support = "While SIRPβ1 could not only be detected on microglial cells associated \
with plaques, but also in those not directly associated with plaques, \
it is regarded as a potent regulator of Aβ42 fibril clearance in vitro \
[25]."

a(MESHA:Microglia, loc(CHEBI:"amyloid-beta")) -> p(HGNC:SIRPB1, loc(MESHA:Microglia))
a(MESHA:Microglia) -> p(HGNC:SIRPB1, loc(MESHA:Microglia))
SET StudyType = "in vitro"
p(HGNC:SIRPB1) reg deg(p(HGNC:APP, frag("1_42")))
UNSET StudyType

SET Support = "Another potential microglial DAP12-associated receptor is the \
complement receptor 3 (CR3), a major heterodimeric receptor consisting \
of the integrins CD11b and CD18, which is involved in the complement \
system."

# CD11b = ITGAM, CD18 = ITGB2, together they make CR3
complex(p(HGNC:ITGAM), p(HGNC:ITGB2)) -- p(HGNC:TYROBP)

SET Support = "The phagocytic receptor CR3 plays an important role in the subsequent \
clearance of C3-opsonized structures [48]."

complex(p(HGNC:ITGAM), p(HGNC:ITGB2)) -- bp(GOBP:phagocytosis)

SET Support = "But, it has been shown that CD18-mediated Syk activation requires the \
ITAM-associated molecules DAP12 and FcRγ [39]."

p(HGNC:ITGB2) -> act(p(HGNC:SYK), ma(kin))
p(HGNC:TYROBP) -> act(p(HGNC:SYK), ma(kin))
p(SCOMP:"FcR Gamma Chain Complex") -> act(p(HGNC:SYK), ma(kin))

SET Support = "Indeed, during formation of amyloid in APP-transgenic mice increased \
mRNA and protein levels of components of the complement system have \
been detected. Among those there have been C1q and C3, at which the \
classical and alternative pathway merge [53]."

SET Species = "10090"
SET MouseStrain = "APP"
a(CHEBI:"amyloid-beta") -> r(MGI:Itgam)
a(CHEBI:"amyloid-beta") -> r(MGI:Itgb2)
a(CHEBI:"amyloid-beta") -> r(MGI:C1qa)
UNSET Species
UNSET MouseStrain

SET Support = "Moreover, complement activation has been described to occur in amyloid \
plaques in AD brains [54, 55] and complement products like the \
membrane attack complex (C5b-9) have been reported to be associated \
with amyloid plaques [56]."

SET MeSHDisease = "Alzheimer Disease"
path(MESHD:"Plaque, Amyloid") positiveCorrelation bp(GOBP:"complement activation")
bp(GOBP:"complement activation") -> p(SCOMP:"Membrane Attack Complex")
p(SCOMP:"Membrane Attack Complex") positiveCorrelation path(MESHD:"Plaque, Amyloid")
UNSET MeSHDisease

SET Support = "Additionally, C3 seems to be involved in the process of plaque \
clearance."

complex(p(HGNC:ITGAM), p(HGNC:ITGB2)) -- bp(GOBP:"beta-amyloid clearance")

SET Support = "Most CD33-related Siglecs, such as the human Siglec-11, are \
predominantly expressed on mature cells of the immune system such as \
monocytes and macrophages."

a(MESHA:Macrophages) -> p(HGNC:SIGLEC11)
a(MESHA:Monocytes) -> p(HGNC:SIGLEC11)

SET Support = "Siglec-11 has been shown to interact with SHP1 and SHP2 upon tyrosine \
phosphorylation [29]."

# SHP1 = PTPN6, SHP2 = PTPN11
p(HGNC:SIGLEC11, pmod(Ph)) -- p(HGNC:PTPN6)
p(HGNC:SIGLEC11, pmod(Ph)) -- p(HGNC:PTPN11)

SET Support = "Interestingly, SHP1 seems to be involved in antiinflammatory signaling \
of microglia."

SET MeSHAnatomy = "Microglia"
p(HGNC:PTPN6) -- bp(GOBP:"inflammatory response")
UNSET MeSHAnatomy

SET Support = "Furthermore, it has been demonstrated that Siglec-11 expressing \
microglial cells show a reduced phagocytic capacity of apoptotic \
material in microglia-neuron coculture experiments [66], indicating \
that ITIM-signaling could be the opponent of the phagocytosis- \
associated ITAM-Syk signaling pathway [21]."

p(HGNC:SIGLEC11, loc(MESHA:Microglia)) negativeCorrelation bp(GOBP:"apoptotic cell clearance")
p(HGNC:SIGLEC11, loc(MESHA:Microglia)) negativeCorrelation bp(GOBP:phagocytosis)

SET Support = "However, few Siglecs have been demonstrated to associate with the \
ITAM-containing adaptor protein DAP12, including the recently \
discovered human Siglec-16."

p(HGNC:SIGLEC16) -- p(HGNC:TYROBP)

SET Support = "Cao et al. [40] have shown that Siglec-16 is expressed on macrophages \
and on rare microglial-like cell populations in the normal human \
brain."

a(MESHA:Macrophages, loc(MESHA:Brain)) -> p(HGNC:SIGLEC16)
a(MESHA:Microglia, loc(MESHA:Brain)) -> p(HGNC:SIGLEC16)

UNSET Section

UNSET PublicationType

##################################################################################

SET PublicationType = "Research"

SET Citation = {"PubMed", "Sci Transl Med. 2014 Jul 2;6(243):243ra86. doi: 10.1126/scitranslmed.3009093.", "24990881"}

SET Section = "Abstract"

SET Support = "Genetic variants in the triggering receptor expressed on myeloid cells \
2 (TREM2) have been linked to Nasu-Hakola disease, Alzheimer's disease \
(AD), Parkinson's disease, amyotrophic lateral sclerosis, \
frontotemporal dementia (FTD), and FTD-like syndrome without bone \
involvement."

g(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Parkinson Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"amyotrophic lateral sclerosis")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")

SET Support = "TREM2 is an innate immune receptor preferentially expressed by \
microglia and is involved in inflammation and phagocytosis."

p(HGNC:TREM2, loc(MESHA:Microglia)) -- bp(GOBP:"inflammatory response")
p(HGNC:TREM2, loc(MESHA:Microglia)) -- bp(GOBP:phagocytosis)

SET Support = "We report that missense mutations associated with FTD and FTD-like \
syndrome reduce TREM2 maturation, abolish shedding by ADAM proteases, \
and impair the phagocytic activity of TREM2-expressing cells."

SET MeSHDisease = "Frontotemporal Dementia"
g(HGNC:TREM2, var("?")) -| act(p(HGNC:TREM2), ma(GOBP:"protein maturation"))
g(HGNC:TREM2, var("?")) -| act(p(HGNC:TREM2), ma(GOBP:"membrane protein ectodomain proteolysis"))
g(HGNC:TREM2, var("?")) -| bp(GOBP:phagocytosis)

SET Support = "As a consequence of reduced shedding, TREM2 is virtually absent in the \
cerebrospinal fluid (CSF) and plasma of a patient with FTD-like \
syndrome."

act(p(HGNC:TREM2), ma(GOBP:"membrane protein ectodomain proteolysis")) -> p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid"))
act(p(HGNC:TREM2), ma(GOBP:"membrane protein ectodomain proteolysis")) -> p(HGNC:TREM2, loc(MESHA:Plasma))

UNSET MeSHDisease

SET Support = "A decrease in soluble TREM2 was also observed in the CSF of patients \
with AD and FTD, further suggesting that reduced TREM2 function may \
contribute to increased risk for two neurodegenerative disorders."

path(MESHD:"Alzheimer Disease") negativeCorrelation p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid"))
path(MESHD:"Frontotemporal Dementia") negativeCorrelation p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid"))

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Mol Neurodegener. 2016 Jan 12;11:3. doi: 10.1186/s13024-016-0071-x.", "26754172"}

SET Section = "Abstract"

SET Support = "The discovery that heterozygous missense mutations in the gene \
encoding triggering receptor expressed on myeloid cells 2 (TREM2) are \
risk factors for Alzheimer's disease (AD), with only the \
apolipoprotein E (APOE) ε4 gene allele conferring a higher risk, has \
led to increased interest in immune biology in the brain."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
# APOE e4 = mutation - https://omim.org/entry/107741
g(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "TREM2 is expressed on microglia, the resident immune cells of the \
brain and has been linked to phagocytotic clearance of amyloid β (Aβ) \
plaques."

a(MESHA:Microglia, loc(MESHA:Brain)) -> p(HGNC:TREM2)
p(HGNC:TREM2) -- bp(GOBP:"beta-amyloid clearance")
p(HGNC:TREM2) -- bp(GOBP:phagocytosis)

SET Support = "Soluble TREM2 (sTREM2) has previously been measured in cerebrospinal \
fluid (CSF) by ELISA but in our hands commercial kits have proved \
unreliable, suggesting that other methods may be required."

SET Assay = "ELISA"
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation a(MESHA:"Cerebrospinal Fluid")
UNSET Assay

SET Support = "We found significantly higher sTREM2 concentration in AD compared to \
control CSF."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "There were significant correlations between CSF sTREM2 and T-tau as \
well as P-tau181."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation p(HGNC:MAPT)
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation p(HGNC:MAPT, pmod(Ph, Thr, 181))

SET Support = "CSF concentrations of sTREM2 are higher in AD than in controls, and \
correlate with markers of neurodegeneration. CSF sTREM2 may be used to \
quantify glial activation in AD."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

SET Section = "Results"

SET Support = "As expected, the AD subjects had significantly lower MMSE scores and \
Aβ42 levels, and significantly higher concentrations of T-tau, P-tau \
and T-tau/Aβ42 ratios compared with controls (Table 1)."

path(MESHD:"Alzheimer Disease") negativeCorrelation p(HGNC:APP, frag("1_42"))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:MAPT)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:MAPT, pmod(Ph))

SET Support = "CSF sTREM2 concentrations were significantly higher in AD patients \
compared with controls (Table 1, Fig. 1b, p = 0.0457)."

p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Analysing the AD and control samples separately, P-tau was positively \
correlated with sTREM2 in the AD subjects (p = 0.0002) but not in \
controls."

SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation p(HGNC:MAPT, pmod(Ph))
UNSET MeSHDisease

SET Support = "However, when analyzing AD and control individuals together, there was \
a positive correlation of sTREM2 levels with both T-tau (Fig. 1c, \
r = 0.3863 p = 0.0023) P-tau (Fig. 1d, r = 0.5331 p = < 0.0001) but \
not with Aβ42 (Fig. 1e)."

p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation p(HGNC:MAPT, pmod(Ph))
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation p(HGNC:MAPT)

SET Support = "In a subset (52 subjects), CSF sTREM2 correlated positively with \
YKL-40 (r =0.3271, p = 0.0204, Fig. 1f) and a similar trend was seen \
for CSF CCL2, although not significant (r = 0.28, p = 0.07)."

p(HGNC:CHI3L1) positiveCorrelation p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid"))
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation p(HGNC:CCL2, loc(MESHA:"Cerebrospinal Fluid"))

UNSET Section


##################################################################################

SET Citation = {"PubMed", "J Neurochem. 2017 Nov;143(4):445-454. doi: 10.1111/jnc.14224. Epub 2017 Oct 23.", "28940294"}

SET Section = "Abstract"

SET Support = "γ-Secretase is an intramembrane cleaving protease involved in the \
generation of the Alzheimer's disease (AD)-associated amyloid β \
peptide (Aβ)."

a(CHEBI:"amyloid-beta") positiveCorrelation path(MESHD:"Alzheimer Disease")
complex(SCOMP:"gamma Secretase Complex") -> a(CHEBI:"amyloid-beta")

SET Support = "Microglia exert important functions during brain development and \
homeostasis in adulthood, and accumulating evidence indicates that \
microglia and neuroinflammatory processes contribute to the \
pathogenesis of neurodegenerative diseases."

a(MESHA:Microglia) positiveCorrelation bp(GOBP:"brain development")
a(MESHA:Microglia) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Curr Opin Neurobiol. 2016 Feb;36:74-81. doi: 10.1016/j.conb.2015.10.004. Epub 2015 Oct 24.", "26517285"}

SET Section = "Abstract"

SET Support = "Neuroinflammation is a pathological hallmark of Alzheimer's disease \
(AD), and microglia, the brain's resident phagocyte, are pivotal for \
the immune response observed in AD."

SET MeSHAnatomy = "Nerve Tissue"
path(MESHD:Inflammation) biomarkerFor path(MESHD:"Alzheimer Disease")
UNSET MeSHAnatomy
SET MeSHAnatomy = "Microglia"
bp(GOBP:"immune response in brain or nervous system") -- path(MESHD:"Alzheimer Disease")
UNSET MeSHAnatomy

SET Support = "Recent Genome Wide Association Studies (GWAS) have identified more \
than 20 gene variants associated with an increased risk of late-onset \
AD (LOAD), the most prevalent form of AD [1]. The findings strongly \
implicate genes related to the immune response (CR1, CD33, MS4A, CLU, \
ABCA7, EPHA1 and HLA-DRB5-HLA-DRB1), endocytosis (BIN1, PICALM, CD2AP, \
EPHA1 and SORL1) and lipid biology (CLU, ABCA7 and SORL1) [2-8], and \
many encode proteins which are highly expressed in microglia [1]."

g(HGNC:CR1) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:CD33) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:MS4A1) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
#membrane-spanning 4-domain family, using MS4A1 for now
g(HGNC:CLU) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:ABCA7) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:EPHA1) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:HLA-DRB5) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:HLA-DRB1) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:BIN1) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:PICALM) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:CD2AP) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:SORL1) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "Furthermore, recent identification of a low frequency mutation in the \
gene encoding the triggering receptor expressed in myeloid cells 2 \
protein (TREM2) confers increased risk of AD in LOAD cohorts with an \
effect size similar to that for APOE, until recently the only \
identified genetic risk factor associated with LOAD [9,10(••)] (Figure \
1)."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Biol Chem. 2016 Feb 26;291(9):4334-41. doi: 10.1074/jbc.R115.704981. Epub 2015 Dec 22.", "26694609"}

SET Section = "Abstract"

SET Support = "Homozygous mutations in TREM2 or DAP12 cause Nasu-Hakola disease, \
which is characterized by bone abnormalities and dementia."

g(HGNC:TREM2, var("?")) -> path(DO:"Nasu-Hakola disease")
g(HGNC:TYROBP, var("?")) -> path(DO:"Nasu-Hakola disease")
path(DO:"Nasu-Hakola disease") positiveCorrelation path(MESHD:Dementia)

SET Support = "Recently, a variant of TREM2 has also been associated with an \
increased risk for Alzheimer disease."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "The selective expression of TREM2 on immune cells and its association \
with different forms of dementia indicate a contribution of this \
receptor in common pathways of neurodegeneration."

p(HGNC:TREM2) positiveCorrelation path(MESHD:Dementia)
p(HGNC:TREM2) -- path(HP:Neurodegeneration)

UNSET Section

SET Section = "Expression, Metabolism, and Signaling of TREMs"

SET Support = "The first gene of the TREM family, TREM1, was identified in natural \
killer cells and shown to be also expressed in different monocytes and \
monocyte-derived macrophages (20)."

a(MESHA:"Killer Cells, Natural") -> p(HGNC:TREM1)
a(MESHA:Monocytes) -> p(HGNC:TREM1)
a(MESHA:Macrophages) -> p(HGNC:TREM1)

SET Support = "Although certain macrophages and neutrophils express both TREM \
homologs, dendritic cells, osteoclasts, and microglia show predominant \
expression of TREM2 (1, 4)."

a(MESHA:Macrophages) -> p(HGNC:TREM2)
a(MESHA:Neutrophils) -> p(HGNC:TREM2)
a(MESHA:Neutrophils) -> p(HGNC:TREM1)
a(MESHA:"Dendritic Cells") -> p(HGNC:TREM2)
a(MESHA:Osteoclasts) -> p(HGNC:TREM2)
a(MESHA:Microglia) -> p(HGNC:TREM2)

SET Support = "Both receptors require engagement of the co-receptor DAP12 for \
transmembrane signaling."

complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> bp(GOBP:"intracellular signal transduction")
complex(p(HGNC:TREM1), p(HGNC:TYROBP)) -> bp(GOBP:"intracellular signal transduction")

SET Support = "Cell biological studies revealed that TREM2 is transported in the \
secretory pathway and shuttles between the plasma membrane and \
cytoplasmic vesicles (23, 24)."

p(HGNC:TREM2) positiveCorrelation a(MESHA:"Cell Membrane")
p(HGNC:TREM2) positiveCorrelation a(MESHA:"Cytoplasmic Vesicles")
p(HGNC:TREM2) positiveCorrelation tloc(p(HGNC:TREM2), fromLoc(MESHA:"Cell Membrane"), toLoc(MESHA:"Cytoplasmic Vesicles"))

SET Support = "During biosynthesis and secretory transport, TREM2 also undergoes \
maturation by complex glycosylation (24, 25, 26), but the biological \
relevance of these modifications remains to be determined."

tloc(p(HGNC:TREM2), fromLoc(MESHA:"Cell Membrane"), toLoc(MESHA:"Cytoplasmic Vesicles")) -> p(HGNC:TREM2, pmod(Glyco))

SET Support = "TREM2 is also subjected to sequential proteolytic processing by \
ectodomain shedding and intramembranous cleavage (24, 25, 26) (Fig. \
2)."

p(HGNC:TREM2) positiveCorrelation bp(GOBP:"membrane protein ectodomain proteolysis")
p(HGNC:TREM2) positiveCorrelation bp(GOBP:"membrane protein proteolysis")

SET Support = "The ectodomain shedding results in the release of the soluble TREM2 \
from cells into extracellular fluids and involves proteases of the a \
disintegrin and metalloprotease (ADAM) family."

bp(GOBP:"membrane protein ectodomain proteolysis") -> p(HGNC:TREM2, loc(MESHA:"Extracellular Space"))
p(PFAM:ADAM_CR) => p(HGNC:TREM2, loc(MESHA:"Extracellular Space"))

SET Support = "Soluble TREM2 has also been detected in cerebrospinal fluid (CSF) and \
blood plasma of humans (26, 27), and levels in CSF were significantly \
reduced in samples of AD and FTD patients, suggesting altered \
proteolytic processing during neurodegeneration (26)."

p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation a(MESHA:"Cerebrospinal Fluid")
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation a(MESHA:Plasma)
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) negativeCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) negativeCorrelation path(MESHD:"Frontotemporal Dementia")

SET Support = "In CSF and plasma of a patient with FTD-like symptoms carrying a \
homozygous TREM T66M mutation, soluble TREM2 was not detected, \
suggesting that this mutation decreases shedding of TREM2 (26)."

SET MeSHAnatomy = {"Cerebrospinal Fluid", "Plasma"}
g(HGNC:TREM2, var("p.Thr66Met")) negativeCorrelation p(HGNC:TREM2, loc(MESHA:"Extracellular Space"))
UNSET MeSHAnatomy

SET Support = "Soluble TREM2 variants have also been found to be altered in multiple \
sclerosis."

p(HGNC:TREM2, loc(MESHA:"Extracellular Space"), var("?")) positiveCorrelation path(DO:"multiple sclerosis")

SET Support = "However, in contrast to the decreased levels found in CSF of AD and \
FTD patients, soluble TREM2 was increased in CSF of multiple sclerosis \
cases and other inflammatory neurological diseases, but was unchanged \
in non-inflammatory neurological diseases (27)."

SET MeSHAnatomy = "Cerebrospinal Fluid"
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation path(DO:"multiple sclerosis")
UNSET MeSHAnatomy

SET Support = "For both TREM1 and TREM2, alternative mRNA transcripts have been \
identified that could also result in the synthesis and secretion of \
truncated variants (28, 29)."

r(HGNC:TREM1, var("?")) -> p(HGNC:TREM1, var("?"))
r(HGNC:TREM2, var("?")) -> p(HGNC:TREM2, var("?"))

SET Support = "It has been shown that soluble TREM1 competes with cell surface-bound \
TREM1 for ligands and thereby affects ligand-induced signaling (30, \
31, 27)."

p(HGNC:TREM1, loc(MESHA:"Extracellular Space")) -| act(p(HGNC:TREM1, loc(MESHA:"Cell Membrane")))
p(HGNC:TREM1, loc(MESHA:"Extracellular Space")) -| bp(GOBP:"intracellular signal transduction")

SET Support = "The C-terminal fragment (CTF) of TREM2 that results from ectodomain \
shedding remains membrane-tethered via its transmembrane domain and \
could be subjected to intramembranous proteolysis by the γ-secretase \
complex that is also involved in the generation of the AD-associated \
amyloid-β (Aβ) peptide (24,–26, 32) (Fig. 2)."

bp(GOBP:"membrane protein ectodomain proteolysis") -> p(HGNC:TREM2, frag("?_*"), loc(MESHA:"Cell Membrane"))
complex(SCOMP:"gamma Secretase Complex") => deg(p(HGNC:TREM2, frag("?_*"), loc(MESHA:"Cell Membrane")))
complex(SCOMP:"gamma Secretase Complex") positiveCorrelation a(CHEBI:"amyloid-beta")

SET Support = "Interestingly, inhibition of γ-secretase also impaired the \
phosphorylation of DAP12 and downstream metabolism of \
phosphatidylinositides (24), cellular rearrangement, and phagocytosis \
(33), suggesting that TREM2-DAP12-mediated signaling is affected by \
γ-secretase activity."

complex(SCOMP:"gamma Secretase Complex") -> p(HGNC:TYROBP, pmod(Ph))
complex(SCOMP:"gamma Secretase Complex") -| a(CHEBI:phosphatidylinositol)
complex(SCOMP:"gamma Secretase Complex") -> bp(GOBP:phagocytosis)
complex(SCOMP:"gamma Secretase Complex") -> bp(GOBP:"cytoskeleton organization")
complex(SCOMP:"gamma Secretase Complex") -> bp(GOBP:"phosphatidylinositol metabolic process")
act(complex(SCOMP:"gamma Secretase Complex")) -- act(complex(p(HGNC:TREM2), p(HGNC:TYROBP)), ma(GOBP:"intracellular signal transduction"))

SET Support = "Overexpression of the TREM2 CTF in microglial BV2 cells also decreased \
LPS-induced pro-inflammatory cytokine production (32), further \
supporting a role of the TREM2 CTF and its processing by γ-secretase \
in signaling."

SET ChallengeType = "lipopolysaccharide stimulation"
SET CustomCellLine = "BV-2"
p(HGNC:TREM2, frag("?_*"), loc(MESHA:Microglia)) negativeCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
UNSET ChallengeType
UNSET CustomCellLine

SET Support = "While TREM1 activation promotes pro-inflammatory signaling and \
cytokine production in response to LPS, TREM2 activation instead \
suppresses these LPS-induced effects (7)."

SET ChallengeType = "lipopolysaccharide stimulation"
p(HGNC:TREM2) -| bp(GOBP:"cytokine production involved in inflammatory response")
p(HGNC:TREM1) -> bp(GOBP:"cytokine production involved in inflammatory response")
UNSET ChallengeType

SET Support = "In general, receptor ligation results in Src kinase-dependent \
phosphorylation of DAP12 at critical tyrosine residues and the \
recruitment of spleen tyrosine kinases (Syk) and the tyrosine kinase \
ζ-chain-associated protein 70 (ZAP70) (1, 4)."

act(p(SFAM:"SRC Family"), ma(kin)) => p(HGNC:TYROBP, pmod(Ph))
p(HGNC:TYROBP, pmod(Ph)) -> p(HGNC:SYK)
p(HGNC:TYROBP, pmod(Ph)) -> p(HGNC:ZAP70)

SET Support = "Syk and ZAP70 can activate additional signaling proteins including \
PI3K, guanine nucleotide exchange factors (GEF), and phosphotyrosine \
kinase 2 (Pyk2), and can also regulate membrane-proximal signaling of \
other cell surface receptors, including TLR4, IFNαR1/2, and Plexin-A1."

p(HGNC:SYK) -> act(p(SFAM:"PIK3C Class IA Family"), ma(kin))
p(HGNC:ZAP70) -> act(p(SFAM:"PIK3C Class IA Family"), ma(kin))
# Pyk2 = PTK2B
p(HGNC:SYK) -> act(p(HGNC:PTK2B), ma(kin))
p(HGNC:ZAP70) -> act(p(HGNC:PTK2B), ma(kin))
p(HGNC:SYK) reg act(p(HGNC:TLR4), ma(GOBP:signaling))
p(HGNC:ZAP70) reg act(p(HGNC:TLR4), ma(GOBP:signaling))
p(HGNC:SYK) reg act(p(HGNC:IFNAR1), ma(GOBP:signaling))
p(HGNC:ZAP70) reg act(p(HGNC:IFNAR1), ma(GOBP:signaling))
p(HGNC:SYK) reg act(p(HGNC:IFNAR2), ma(GOBP:signaling))
p(HGNC:ZAP70) reg act(p(HGNC:IFNAR2), ma(GOBP:signaling))
p(HGNC:SYK) reg act(p(HGNC:PLXNA1), ma(GOBP:signaling))
p(HGNC:ZAP70) reg act(p(HGNC:PLXNA1), ma(GOBP:signaling))

SET Support = "Activation of TREM2 resulted in increased phosphorylation of DAP12."

act(p(HGNC:TREM2)) -> p(HGNC:TYROBP, pmod(Ph))

SET Support = "The complex regulation and interplay of multiple pathways allow fine- \
tuned responses of immune cells, including cytokine production and \
release, migration, phagocytosis, proliferation and differentiation, \
and cell survival (Fig. 1)."

# Went to references to find molecular interactions
SET PMIDSource = "26337043"
a(CHEBI:lipopolysaccharide) -> complex(p(HGNC:TYROBP, pmod(Ph)), p(HGNC:DOK3, pmod(Ph)))
SET ChallengeType = "lipopolysaccharide stimulation"
p(HGNC:TYROBP) -> p(HGNC:DOK3, pmod(Ph))
p(HGNC:TYROBP) -> tloc(p(HGNC:DOK3), fromLoc(MESHA:Cytoplasm), toLoc(MESHA:"Cell Membrane"))
p(HGNC:DOK3, loc(MESHA:"Cell Membrane")) => complex(p(HGNC:DOK3), p(HGNC:GRB2), p(HGNC:SOS1))
complex(p(HGNC:DOK3), p(HGNC:GRB2), p(HGNC:SOS1)) -| act(p(SFAM:"MAPK Erk1/2 Family"))
complex(p(HGNC:DOK3), p(HGNC:GRB2), p(HGNC:SOS1)) -| act(p(SFAM:"RAS Family"))
p(SFAM:"MAPK Erk1/2 Family") -> bp(GOBP:"cytokine production involved in inflammatory response")
act(p(SFAM:"SRC Family"), ma(kin)) => p(HGNC:TYROBP, pmod(Ph))
p(HGNC:TYROBP, pmod(Ph)) -> complex(p(HGNC:TYROBP, pmod(Ph)), p(HGNC:SYK))
complex(p(HGNC:TYROBP, pmod(Ph)), p(HGNC:SYK)) -> act(p(HGNC:VAV2))
complex(p(HGNC:TYROBP, pmod(Ph)), p(HGNC:SYK)) -> act(p(HGNC:VAV3))
act(p(HGNC:VAV2)) -> act(p(HGNC:RAC1))
act(p(HGNC:VAV3)) -> act(p(HGNC:RAC1))
act(p(HGNC:VAV2)) -> act(p(HGNC:CDC42))
act(p(HGNC:VAV3)) -> act(p(HGNC:CDC42))
act(p(HGNC:RAC1)) -> act(complex(SCOMP:"Arp2/3 Protein Complex"))
act(p(HGNC:CDC42)) -> act(complex(SCOMP:"Arp2/3 Protein Complex"))
act(complex(SCOMP:"Arp2/3 Protein Complex")) -> bp(GOBP:phagocytosis)
complex(p(HGNC:CSF1), p(HGNC:CSF1R)) -> act(p(SFAM:"SRC Family"), ma(kin))
p(HGNC:TYROBP, pmod(Ph)) => p(HGNC:SYK, pmod(Ph))
p(HGNC:SYK, pmod(Ph)) -> act(p(HGNC:PTK2B), ma(kin))
# CTNNB1 = beta-catenin
act(p(HGNC:PTK2B), ma(kin)) -> act(p(HGNC:CTNNB1))
UNSET ChallengeType
UNSET PMIDSource
SET PMIDSource = "19230638"
p(HGNC:SYK, pmod(Ph)) -> act(p(SFAM:"PLCG Family"))
act(p(SFAM:"PLCG Family")) -> a(CHEBI:"calcium(2+)", loc(MESHA:Cytosol))
UNSET PMIDSource


SET Support = "Cross-linking of TREM2 also increased phagocytosis of damaged neurons \
by primary mouse microglia (36)."

# Cross-linking = activation
act(p(MGI:Trem2, loc(MESHA:Microglia))) -> bp(GOBP:phagocytosis)

SET Support = "In addition, activation of TREM2 resulted in remodeling of the actin \
cytoskeleton and promoted migratory activity toward chemokines \
accompanied with increased phosphorylation of ERK."

act(p(HGNC:TREM2)) -> bp(GOBP:"actin cytoskeleton reorganization")
SET MeSHAnatomy = "Microglia"
act(p(HGNC:TREM2)) -> bp(GOBP:"cell chemotaxis")
UNSET Microglia
act(p(HGNC:TREM2)) -> p(SFAM:"MAPK Erk1/2 Family", pmod(Ph))

SET Support = "Knockdown of endogenous TREM2 in primary microglia decreased \
phagocytic activity and increased transcription of pro-inflammatory \
cytokines and nitric oxide synthase-2 (36)."

p(HGNC:TREM2, loc(MESHA:Microglia)) -> bp(GOBP:phagocytosis)
p(HGNC:TREM2, loc(MESHA:Microglia)) -| bp(GOBP:"cytokine production involved in inflammatory response")
p(HGNC:TREM2, loc(MESHA:Microglia)) -| r(HGNC:NOS2)

SET Support = "TREM2 KO cells had an elevated cytokine response to TLR stimulation by \
LPS (38)."

# LPS activates TLR4
SET ChallengeType = "lipopolysaccharide stimulation"
p(MGI:Trem2, loc(MESHA:Macrophages)) -| act(p(MGI:Tlr4), ma(GOBP:"cytokine production"))
UNSET ChallengeType

SET Support = "In addition, primary microglia of TREM2 KO cells show decreased \
proliferation in culture and elevated cell death (39)."

p(MGI:Trem2, loc(MESHA:Microglia)) -> bp(GOBP:"microglial cell proliferation")
p(MGI:Trem2, loc(MESHA:Microglia)) -> a(MESHA:Microglia)

UNSET Section

SET Section = "Homozygous and Compound Mutations of TREM2 or DAP12 in Nasu-Hakola Disease and Frontotemporal Dementia"

SET Support = "Interestingly, mutations in DAP12 and TREM2 are associated with \
autosomal recessively inherited NHD, also called polycystic \
lipomembranous osteodysplasia with sclerosing leukoencephalopathy or \
membranous lipodystrophy."

p(HGNC:TREM2, var("?")) -- path(DO:"Nasu-Hakola disease")
p(HGNC:TYROBP, var("?")) -- path(DO:"Nasu-Hakola disease")

SET Support = "This rare disease is characterized by impaired bone metabolism leading \
to cysts and fractures, and by progressive dementia with early onset \
between 15 and 30 years of age (6, 9)."

SET AgeRange = "15-30 years"
path(DO:"Nasu-Hakola disease") positiveCorrelation path(HP:"Bone cyst")
path(DO:"Nasu-Hakola disease") positiveCorrelation path(MESHD:Dementia)
UNSET AgeRange

SET Support = "Disease onset of FTD is typically between the ages of 50 and 65."

path(MESHD:"Frontotemporal Dementia") positiveCorrelation bp(MESHPP:Aging)

SET Support = "Interestingly, rare homozygous or compound mutations in TREM2 have \
been identified in cases with FTD-like symptomatic presentation in the \
absence of associated lesions in the bone system, therefore differing \
in the clinical symptoms from NHD (40, 45, 47,–49) (Table 1)."

p(HGNC:TREM2, var("?")) -- path(MESHD:"Frontotemporal Dementia")

SET Support = "Another rare variant of TREM2 with an R47H substitution has recently \
also been associated with an increased risk for late-onset AD (50, \
51)."

p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

UNSET Section

SET Section = "Molecular Genetics of Alzheimer Disease"

SET Support = "AD is the most common form of dementia and is characterized at the \
neuropathological level by the accumulation of extracellular plaques \
and intraneuronal neurofibrillary tangles that are composed of Aβ \
peptides and the microtubule-associated protein tau, respectively \
(52,–54)."

path(MESHD:"Alzheimer Disease") positiveCorrelation path(MESHD:"Plaque, Amyloid")
path(MESHD:"Alzheimer Disease") positiveCorrelation a(MESHCS:"Neurofibrillary Tangles", loc(MESHA:Neurons))

SET Support = "Aβ derives from the amyloid precursor protein (APP) by sequential \
proteolytic processing by β- and γ-secretase (55, 56)."

deg(p(HGNC:APP)) -> a(CHEBI:"amyloid-beta")
act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:proteolysis)) => deg(p(HGNC:APP))
act(p(HGNC:BACE1), ma(GOBP:proteolysis)) => deg(p(HGNC:APP))
act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:proteolysis)) -> a(CHEBI:"amyloid-beta")
act(p(HGNC:BACE1), ma(GOBP:proteolysis)) -> a(CHEBI:"amyloid-beta")

SET Support = "In an alternative processing pathway, APP can be cleaved by \
α-secretase within the Aβ domain, which precludes the subsequent \
generation of Aβ. α-Secretase activity is exerted by members of the \
ADAM family (57)."

act(p(PFAM:ADAM_CR), ma(GOBP:proteolysis)) => deg(p(HGNC:APP))

SET Support = "As described above, ADAM proteases and γ-secretase are also involved \
in the proteolytic processing of TREM2."

act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:proteolysis)) -- p(HGNC:TREM2)
act(p(PFAM:ADAM_CR), ma(GOBP:proteolysis)) -- p(HGNC:TREM2)

SET Support = "In the vast majority of cases (>95%), AD manifests beyond the age of \
65 years."

path(MESHD:"Alzheimer Disease") positiveCorrelation bp(MESHPP:Aging)

SET Support = "The first gene identified to bear AD-associated mutations was the APP \
gene itself."

g(HGNC:APP, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "The different mutations within APP are localized within or close to \
the Aβ domain and promote the generation or aggregation of the \
peptide."

g(HGNC:APP, var("?")) -> a(CHEBI:"amyloid-beta")

SET Support = "In contrast to the disease-causing mutations of APP, the preventive \
variant decreased the proteolytic generation of Aβ and its propensity \
to aggregate (61, 62)."

g(HGNC:APP, var("?")) -| a(CHEBI:"amyloid-beta")

SET Support = "Besides the APP gene, only two additional genes have been identified \
to contain mutations that also cause dominantly inherited early-onset \
familial AD. The two genes encode homologous proteins called \
presenilin (PS)-1 and PS-2 (63, 64)."

g(HGNC:PSEN1, var("?")) positiveCorrelation path(ADO:"Early_onset_Alzheimer_s_Disease__")
g(HGNC:PSEN2, var("?")) positiveCorrelation path(ADO:"Early_onset_Alzheimer_s_Disease__")

SET Support = "PS proteins are the catalytic components of the γ-secretase complex \
and thus, are critically involved in the proteolytic generation of Aβ \
(65, 66)."

complex(SCOMP:"gamma Secretase Complex") hasComponents list(p(HGNC:PSEN1), p(HGNC:PSEN2))
p(HGNC:PSEN1) -> a(CHEBI:"amyloid-beta")
p(HGNC:PSEN2) -> a(CHEBI:"amyloid-beta")

SET Support = "Mutations in the PS proteins commonly favor the accumulation of Aβ42 \
variants that have increased propensity to aggregate."

p(HGNC:PSEN1, var("?")) positiveCorrelation p(HGNC:APP, frag("1_42"))
p(HGNC:PSEN2, var("?")) positiveCorrelation p(HGNC:APP, frag("1_42"))

SET Support = "By far the most common risk factor for late-onset AD is the ϵ4 allele \
of the apoE gene (67,–69)."

# Leu115Arg, His158Arg is ApoeE4 variant
# p(HGNC:APOE, var("p.Cys112Arg")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
p(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "The apolipoprotein (apo) E4 variant is one of three major isoforms \
occurring in humans that only differ in two amino acids at position \
112 and 158, which could either be Cys or Arg residues (Cys115–Cys158 \
for apoE2, Cys115–Arg158 for apoE3, and Arg115–Arg158 for apoE4) \
(68,–70)."

# ApoEɛ2
p(HGNC:APOE) hasVariant p(HGNC:APOE, var("p.Leu115Cys"), var("p.His158Cys"))
# ApoEɛ3
p(HGNC:APOE) hasVariant p(HGNC:APOE, var("p.Leu115Cys"), var("p.His158Arg"))
# ApoEɛ4
p(HGNC:APOE) hasVariant p(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg"))

SET Support = "ApoE can interact with and affect the aggregation and clearance of Aβ \
in an isoform- and lipidation-state dependent manner (73, 74)."

p(HGNC:APOE) -- bp(GOBP:"beta-amyloid clearance")

SET Support = "Several receptors of the low density lipoprotein receptor (LDLR) \
family have been shown to bind apoE, Aβ, and APP, and thereby could \
affect both generation and degradation of Aβ (67, 75, 76)."

p(HGNC:LDLR) -> complex(p(HGNC:LDLR), p(HGNC:APOE))
p(HGNC:LDLR) -> complex(p(HGNC:LDLR), p(HGNC:APP))
p(HGNC:LDLR) -> complex(p(HGNC:LDLR), a(CHEBI:"amyloid-beta"))

SET Support = "Interestingly, genome-wide association studies of common single \
nucleotide polymorphisms have identified additional genes related to \
lipid and lipoprotein metabolism associated with an increased risk of \
AD. These include the bridging integrator 1 (BIN1), clusterin (CLU), \
ATP-binding cassette A7 (ABCA7), phosphatidylinositol-binding clathrin \
assembly protein (PICALM), and CD2-associated protein (CD2AP) (11, 77, \
78)."

SET StudyType = "GWAS"
path(MESHD:"Alzheimer Disease") positiveCorrelation g(HGNC:BIN1, var("?"))
path(MESHD:"Alzheimer Disease") positiveCorrelation g(HGNC:CLU, var("?"))
path(MESHD:"Alzheimer Disease") positiveCorrelation g(HGNC:ABCA7, var("?"))
path(MESHD:"Alzheimer Disease") positiveCorrelation g(HGNC:PICALM, var("?"))
path(MESHD:"Alzheimer Disease") positiveCorrelation g(HGNC:CD2AP, var("?"))
UNSET StudyType

UNSET Section

SET Section = "Potential Role of TREM2 in the Pathogenesis of AD"

SET Support = "The TREM2 R47H variant showed a significant association with late- \
onset AD with odds ratios between 2 and 4, suggesting that it \
increases the risk for AD (50, 51)."

p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Even before the identification of AD-associated mutations of TREM2, it \
has been shown that microglia surrounding extracellular plaques in APP \
transgenic mouse models have increased expression of TREM2 protein \
(81)."

SET Species = "10090"
a(MESHA:Microglia, loc(CHEBI:"amyloid-beta")) positiveCorrelation p(MGI:Trem2, loc(MESHA:Microglia))

SET Support = "Up-regulation of TREM2 mRNA in brain samples of APP transgenic mice \
was observed upon active vaccination with Aβ (82), and \
immunohistochemical analysis of human brains with TREM2 antibodies \
also showed reactivity in microglia and neurons in different brain \
regions (51)."

a(CHEBI:"amyloid-beta", loc(MESHA:Brain)) -> r(MGI:Trem2, loc(MESHA:Brain))
a(CHEBI:"amyloid-beta", loc(MESHA:Brain)) positiveCorrelation p(MGI:Trem2, loc(MESHA:Microglia))
a(CHEBI:"amyloid-beta", loc(MESHA:Brain)) positiveCorrelation p(MGI:Trem2, loc(MESHA:Neurons))
UNSET Species

SET Support = "In addition, the co-receptor DAP12 was identified in an integrative \
network analysis as an important factor in the pathogenesis of late- \
onset AD (83)."

p(HGNC:TYROBP) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "However, the number and size of microglia in the vicinity of Aβ \
deposits were reduced in heterozygous TREM2 KO mice (84)."

p(MGI:Trem2) -> a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))

SET Support = "Both studies, however, revealed increased expression of TREM2 in \
plaque-associated myeloid cells."

a(MESHA:"Myeloid Cells", loc(CHEBI:"amyloid-beta")) -> p(MGI:Trem2, loc(MESHA:"Myeloid Cells"))

UNSET Section

SET Section = "How Could Mutations in TREM2 Contribute to the Risk of AD?"

SET Support = "Interestingly, missense mutations that decrease the interaction with \
DAP12 have also been identified."

p(HGNC:TREM2, var("?")) -| complex(p(HGNC:TREM2), p(HGNC:TYROBP))

SET Support = "Mutations of DAP12 in NHD also result in the generation of non- \
functional protein or no protein, indicating that the disease is \
caused by complete loss of function in TREM2-DAP12 signaling (9, 10)."

path(DO:"Nasu-Hakola disease") positiveCorrelation p(HGNC:TYROBP, var("?"))
p(HGNC:TYROBP, var("?")) -| act(p(HGNC:TYROBP))
path(DO:"Nasu-Hakola disease") negativeCorrelation act(p(HGNC:TYROBP))

SET Support = "The homozygous or compound FTD- and NHD-associated mutations Y38C and \
T66M strongly impair the transport of TREM2 from the ER to the Golgi, \
and thus, its glycosylation and expression at the cell surface, also \
supporting a loss-of-function mechanism (25, 26). These mutations also \
showed decreased solubility, accumulation in the ER, and induced ER \
stress (25)."

p(HGNC:TREM2, var("p.Tyr38Cys")) positiveCorrelation path(DO:"Nasu-Hakola disease")
p(HGNC:TREM2, var("p.Tyr38Cys")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
p(HGNC:TREM2, var("p.Thr66Met")) positiveCorrelation path(DO:"Nasu-Hakola disease")
p(HGNC:TREM2, var("p.Thr66Met")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
p(HGNC:TREM2, var("p.Tyr38Cys")) -| tloc(p(HGNC:TREM2), fromLoc(MESHA:"Endoplasmic Reticulum"), toLoc(MESHCS:"Golgi Apparatus"))
p(HGNC:TREM2, var("p.Thr66Met")) -| tloc(p(HGNC:TREM2), fromLoc(MESHA:"Endoplasmic Reticulum"), toLoc(MESHCS:"Golgi Apparatus"))
tloc(p(HGNC:TREM2), fromLoc(MESHA:"Endoplasmic Reticulum"), toLoc(MESHCS:"Golgi Apparatus")) -> p(HGNC:TREM2, pmod(Glyco))
p(HGNC:TREM2, var("p.Tyr38Cys")) -| p(HGNC:TREM2, pmod(Glyco))
p(HGNC:TREM2, var("p.Thr66Met")) -| p(HGNC:TREM2, pmod(Glyco))
p(HGNC:TREM2, var("p.Tyr38Cys")) -| p(HGNC:TREM2, loc(MESHA:"Cell Membrane"))
p(HGNC:TREM2, var("p.Thr66Met")) -| p(HGNC:TREM2, loc(MESHA:"Cell Membrane"))
p(HGNC:TREM2, loc(MESHA:"Cell Membrane")) positiveCorrelation p(HGNC:TREM2, pmod(Glyco))
act(p(HGNC:TREM2)) positiveCorrelation p(HGNC:TREM2, loc(MESHA:"Cell Membrane"))
p(HGNC:TREM2, var("p.Tyr38Cys")) -| act(p(HGNC:TREM2))
p(HGNC:TREM2, var("p.Thr66Met")) -| act(p(HGNC:TREM2))
p(HGNC:TREM2, var("p.Tyr38Cys")) -> p(HGNC:TREM2, loc(MESHA:"Endoplasmic Reticulum"))
p(HGNC:TREM2, var("p.Thr66Met")) -> p(HGNC:TREM2, loc(MESHA:"Endoplasmic Reticulum"))


SET Support = "Interestingly, the R47H variant showed reduced binding to anionic and \
zwitterionic lipids in vitro (86)."

SET StudyType = "in vitro"
p(HGNC:TREM2, var("p.Arg47His")) -| act(p(HGNC:TREM2), ma(GOBP:"protein localization to lipid particle"))
UNSET StudyType

SET Support = "Thus, the AD-associated TREM2 R47H mutation might interfere with the \
interaction of microglia with membranes of damaged neurons and \
subsequent clearance by phagocytosis."

p(HGNC:TREM2, var("p.Arg47His")) -| bp(GOBP:phagocytosis)
p(HGNC:TREM2, var("p.Arg47His")) -| a(MESHA:Microglia, loc(MESHA:Neurons))

SET Support = "Indeed, the TREM2 R47H variant reduced the phagocytic activity of \
different cell types in vitro (26, 86)."

SET StudyType = "in vitro"
p(HGNC:TREM2, var("p.Arg47His")) -| bp(GOBP:phagocytosis)
UNSET StudyType

SET Support = "The decreased phagocytic activity could contribute to \
neurodegeneration by impairing the clearance of damaged neurons and \
aggregated proteins, which could promote chronic pro-inflammatory \
reactions in the brain."

SET MeSHAnatomy = "Brain"
bp(GOBP:phagocytosis) -> bp(GOBP:"apoptotic cell clearance")
bp(GOBP:phagocytosis) -> bp(GOBP:"beta-amyloid clearance")
bp(GOBP:phagocytosis) -| bp(GOBP:"inflammatory response")
UNSET MeSHAnatomy

SET Support = "Interestingly, apoE has been identified as a ligand for TREM2 in vitro \
(87, 88), suggesting that apoE might also regulate \
TREM2-DAP12-mediated signaling in microglia."

SET StudyType = "in vitro"
p(HGNC:APOE) => complex(p(HGNC:TREM2), p(HGNC:APOE))
SET MeSHAnatomy = "Microglia"
p(HGNC:APOE) -- act(complex(p(HGNC:TREM2), p(HGNC:TYROBP)), ma(GOBP:signaling))
UNSET MeSHAnatomy
UNSET StudyType

SET Support = "Although the different apoE isoforms E2, E3, and E4 showed very \
similar binding to TREM2, the TREM2 R47H mutation strongly decreased \
this interaction, suggesting that the effects of apoE4 on AD risk \
might not involve differential binding to TREM2."

p(HGNC:TREM2, var("p.Arg47His")) -| complex(p(HGNC:TREM2), p(HGNC:APOE))

SET Support = "TREM2 has also been linked to the hyperphosphorylation and \
accumulation of tau."

p(HGNC:TREM2) -- p(HGNC:MAPT, pmod(Ph))
p(HGNC:TREM2) -- p(HGNC:MAPT)

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2016 Oct;46:235.e1-9. doi: 10.1016/j.neurobiolaging.2016.04.004. Epub 2016 Apr 20.", "27289440"}

SET Section = "Abstract"

SET Support = "We identified coding variability in ABCA7 as contributing to AD risk. \
This locus harbors a low-frequency coding variant (p.G215S, \
rs72973581, minor allele frequency = 4.3%) conferring a modest but \
statistically significant protection against AD (p-value = 0.024, odds \
ratio = 0.57, 95% confidence interval = 0.41-0.80)."

g(HGNC:ABCA7) hasVariant g(dbSNP:rs72973581)
g(dbSNP:rs72973581) negativeCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "In summary, our study confirms the role of ABCA7 in AD and provides \
new insights that should address functional studies."

g(HGNC:ABCA7) -- path(MESHD:"Alzheimer Disease")

UNSET Section

SET Section = "Results"

SET Support = "The main hits of the single variants association test map mainly to \
ABCA7 (Table 2). Particularly, we report ABCA7 p.G215S (rs72973581), \
that was the only low-frequency (MAF = 4.3%) missense variant showing \
a trend toward significance in the single-marker association test \
(p-value = 0.02 and corrected p-value = 0.8) in the discovery set and \
was statistically significant after Bonferroni correction (p-value = 6 \
× 10−4 and corrected p-value = 0.024) in the combined data sets \
(discovery set and follow-up data set)."

g(HGNC:ABCA7) hasVariant g(dbSNP:rs72973581)
g(dbSNP:rs72973581) negativeCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Rs72973581 [A] results in a glycine to serine amino acid change at the \
position 215 of ATP-binding cassette subfamily A member 7 (ABCA7; \
G215S) and its frequency was 1.56-fold higher in controls compared to \
cases (MAF = 4.66% and 7.24% for cases and controls, respectively), \
arguing for a protective effect (OR = 0.6, 95% confidence interval \
[CI] = 0.38–0.95)."

g(dbSNP:rs72973581) -> p(HGNC:ABCA7, var("p.Gly215Ser"))

SET Support = "Finally, we also report a common coding polymorphism in ABCA7 \
(p.R1349Q, rs3745842), that maps 1.3 kb from a reported GWAS hit, \
rs3752246, but clusters within a different common haplotype block (MAF \
> 5%) (Naj et al., 2011; Supplementary Table 2). Rs3745842 major \
allele (G) was more frequent in cases compared to controls, although \
the association was nominally significant after multiple testing \
correction (p-value = 1.4 × 10−3, corrected p-value = 0.081) \
(Supplementary Table 6)."

g(HGNC:ABCA7) hasVariant g(dbSNP:rs3745842)
g(dbSNP:rs3745842) -> p(HGNC:ABCA7, var("p.Arg1349Gln"))
g(dbSNP:rs3745842) negativeCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2014 Dec;35(12):2881.e1-2881.e6. doi: 10.1016/j.neurobiolaging.2014.06.002. Epub 2014 Jun 16.", "25104557"}

SET Section = "Abstract"

SET Support = "We report a novel variant in PSEN1 (p.I168T) and a rare variant in \
PSEN2 (p.A237V), absent in controls and both likely pathogenic. Our \
findings support previous studies, suggesting that (1) rare coding \
variability in PSEN1 and PSEN2 may influence the susceptibility for \
LOAD and (2) GRN, MAPT, and PRNP are not major contributors to LOAD."

g(HGNC:PSEN1, var("p.Ile168Thr")) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:PSEN1, var("p.Ala237Val")) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")

UNSET Section

SET Section = "Results"

SET Support = "Of these, 2 are likely to be pathogenic, one in PSEN1 (p.I168T) and \
the other in PSEN2 (p.A237V)."

g(HGNC:PSEN1, var("p.Ile168Thr")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:PSEN1, var("p.Ala237Val")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "Finally, we report a significant association between APOE ε4 allele \
and LOAD in our cohort (p-value = 0.0002, OR = 2.4)."

g(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")


UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2013 Dec;34(12):2699-714. doi: 10.1016/j.neurobiolaging.2013.05.001. Epub 2013 Jul 12.", "23855984"}

SET Section = "Abstract"

SET Support = "Rare variants in TREM2 cause susceptibility to late-onset Alzheimer's \
disease."

g(HGNC:TREM2, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "Inspection of genes with the highest connectivity to TREM2 suggests \
that it plays a key role in mediating changes in the microglial \
cytoskeleton necessary not only for phagocytosis, but also migration."

SET MeSHAnatomy = "Microglia"
p(HGNC:TREM2) regulates bp(GOBP:"cytoskeletal rearrangement involved in phagocytosis, engulfment")
#Is it cytoskeleton based?
p(HGNC:TREM2) regulates bp(GOBP:"cytoskeleton-mediated cell swimming")
UNSET MeSHAnatomy

SET Support = "Most importantly, we show that the TREM2-containing module is \
significantly enriched for genes genetically implicated in Alzheimer's \
disease, multiple sclerosis, and motor neuron disease, implying that \
these diseases share common pathways centered on microglia and that \
among the genes identified are possible new disease-relevant genes."

p(HGNC:TREM2) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2) positiveCorrelation path(DO:"multiple sclerosis")
p(HGNC:TREM2) positiveCorrelation path(DO:"motor neuron disease")
a(MESHA:Microglia) -- path(MESHD:"Alzheimer Disease")
a(MESHA:Microglia) -- path(DO:"multiple sclerosis")
a(MESHA:Microglia) -- path(DO:"motor neuron disease")

UNSET Section

SET Section = "Results"

SET Support = "This demonstrated significant regional differences in TREM2 gene \
expression with a 4.96-fold difference (paired t test p value = 6.63 × \
10−33) between white matter (the highest-expressing region) and \
cerebellum (lowest-expressing region)."

p(HGNC:TREM2) positiveCorrelation a(MESHA:Brain)

SET Support = "We found suggestive age-related increases in TREM2 expression in 3 \
brain regions: MEDU (p = 0.043), THAL (p = 0.037), and SNIG (p = \
0.048, Fig. 1B)."

r(HGNC:TREM2) positiveCorrelation bp(MESHPP:Aging)
a(MESHA:"Medulla Oblongata") positiveCorrelation p(HGNC:TREM2)
r(HGNC:TREM2) positiveCorrelation a(MESHA:Thalamus)
r(HGNC:TREM2) positiveCorrelation a(MESHA:"Substantia Nigra")

SET Support = "In keeping with the existing literature (on the basis of microarray \
data), we were unable to convincingly detect TREM2 expression in \
monocytes (percentage of samples passing a detection p value of 0.01 = \
4.1%), but could detect expression in nonstimulated macrophages and \
dendritic cells."

r(HGNC:TREM2) positiveCorrelation a(MESHA:Macrophages)
r(HGNC:TREM2) positiveCorrelation a(MESHA:"Dendritic Cells")

SET Support = "Consistent with the expression of TREM2 in microglia, the known \
microglia markers CX3CR1, ITGAM, AIF1, FCER1G, and CD68 were present \
within the TREM2-containing module in all brain regions including the \
independent replication data set."

r(HGNC:TREM2) positiveCorrelation a(MESHA:Microglia)
r(HGNC:TREM2, loc(MESHA:Brain)) -- r(HGNC:CX3CR1, loc(MESHA:Brain))
r(HGNC:TREM2, loc(MESHA:Brain)) -- r(HGNC:ITGAM, loc(MESHA:Brain))
r(HGNC:TREM2, loc(MESHA:Brain)) -- r(HGNC:AIF1, loc(MESHA:Brain))
r(HGNC:TREM2, loc(MESHA:Brain)) -- r(HGNC:FCER1G, loc(MESHA:Brain))
r(HGNC:TREM2, loc(MESHA:Brain)) -- r(HGNC:CD68, loc(MESHA:Brain))
p(HGNC:CX3CR1) positiveCorrelation a(MESHA:Microglia)
p(HGNC:ITGAM) positiveCorrelation a(MESHA:Microglia)
p(HGNC:AIF1) positiveCorrelation a(MESHA:Microglia)
p(HGNC:FCER1G) positiveCorrelation a(MESHA:Microglia)
p(HGNC:CD68) positiveCorrelation a(MESHA:Microglia)

SET Support = "In fact, TREM2 was in the top 90th quantile in the hippocampus, white \
matter, medulla, putamen, and substantia nigra (Table 3), indicating \
that TREM2 is clearly a hub gene in these 5 tissues."

g(HGNC:TREM2) positiveCorrelation a(MESHA:"White Matter")
g(HGNC:TREM2) positiveCorrelation a(MESHA:"Medulla Oblongata")
g(HGNC:TREM2) positiveCorrelation a(MESHA:Putamen)
g(HGNC:TREM2) positiveCorrelation a(MESHA:Hippocampus)
g(HGNC:TREM2) positiveCorrelation a(MESHA:"Substantia Nigra")

SET Support = "In keeping with the existing literature, GO enrichment analysis \
provided strong evidence for a role for TREM2 in the innate immune \
response (minimum Bonferroni-corrected p value = 3.44 × 10−12) and, \
more specifically, lysosome activity in all brain regions."

p(HGNC:TREM2, loc(MESHA:Brain)) -- bp(GOBP:"innate immune response")
p(HGNC:TREM2, loc(MESHA:Brain)) -- bp(GOBP:"lysosome localization")

SET Support = "We also found evidence in support of links to the adaptive immune \
system."

p(HGNC:TREM2) positiveCorrelation bp(GOBP:"adaptive immune response")

SET Support = "There was significant enrichment of genes involved in the regulation \
of lymphocyte activation in all 10 regional TREM2-containing modules \
(minimum Bonferroni-corrected p value = 6.42 × 10−7) with evidence for \
interactions with both B and T cells. The GO term “B cell mediated \
immunity” was significant in all 10 regional TREM2-containing modules \
(minimum Bonferroni-corrected p value = 9.16 × 10−5), whereas “T cell \
activation” was featured in 9 of 10 modules (minimum Bonferroni- \
corrected p value = 8.75 × 10−5)."

p(HGNC:TREM2) positiveCorrelation bp(GOBP:"T cell activation")
p(HGNC:TREM2) positiveCorrelation bp(GOBP:"B cell mediated immunity")

SET Support = "Other major microglial functions were also highlighted in this \
analysis with “antigen processing and presentation via MHC class II” \
appearing as a highly significant term in all 10 regional \
TREM2-containing modules (minimum Bonferroni-corrected p values = 1.15 \
× 10−5) and “the regulation of cytokine production” significant in 8 \
of 10 modules (minimum Bonferroni-corrected p values = 7.68 × 10−7)."

p(HGNC:TREM2) positiveCorrelation bp(GOBP:"antigen processing and presentation of peptide or polysaccharide antigen via MHC class II")
p(HGNC:TREM2) positiveCorrelation bp(GOBP:"regulation of cytokine production")

SET Support = "Investigating the TREM2-containing modules for the enrichment of GO \
molecular functions, highlighted the involvement of G-protein-coupled \
nucleotide receptor signalling in 8 of 10 brain regions (including the \
hippocampus, Bonferroni corrected p value = 3.46 × 10−4)."

p(HGNC:TREM2, loc(MESHA:Brain)) positiveCorrelation bp(GOBP:"G-protein coupled receptor signaling pathway")

SET Support = "After accounting for significant overlaps in the gene memberships of \
these pathways (as calculated within the Molecular Signatures Database \
v3.1, http://www.broadinstitute.org/gsea/msigdb/index.jsp), we \
identified 2 independent pathways of interest, the “Fc gamma \
R-mediated phagocytosis” (Bonferroni-corrected p value = 4.72 × 10−4) \
and “Systemic lupus erythematosus” pathways (Bonferroni-corrected p \
value = 3.46 × 10−4)."

p(HGNC:TREM2) positiveCorrelation bp(GOBP:"Fc-gamma receptor signaling pathway involved in phagocytosis")
p(HGNC:TREM2) positiveCorrelation path(MESHD:"Lupus Erythematosus, Systemic")

SET Support = "As would be expected, both gene lists included members already known \
to be part of the TREM2 intracellular signaling pathway (including \
TYROBP and SYK) and many genes that were previously identified within \
the canonical KEGG pathways."

g(HGNC:TREM2) -- g(HGNC:TYROBP)
g(HGNC:TREM2) -- g(HGNC:SYK)

SET Support = "Interestingly, both lists also included 2 purinergic receptors, P2RY12 \
and P2RY13."

g(HGNC:TREM2) -- g(HGNC:P2RY12)
g(HGNC:TREM2) -- g(HGNC:P2RY13)

SET Support = "The P2YR12 is already known to be expressed on microglia and has been \
shown to play a critical role in the chemotactic response to ATP \
released by apoptotic and dying cells (Haynes et al., 2006; Pocock and \
Kettenmann, 2007)."

a(MESHA:Microglia) -> p(HGNC:P2RY12)
p(HGNC:P2RY12) positiveCorrelation bp(MESHPP:Apoptosis)

SET Support = "In the case of the hippocampus, this list included 9 genes (NCKAP1L, \
APBB1IP, CSF1R, DOCK2, DOCK8, ITGAX, C1QB, LAPTM5, and METTL9) with \
“direct” connection to TREM2 (as marked in pink and using the cut off \
of TOM >0.10)."

g(HGNC:TREM2, loc(MESHA:Hippocampus)) -- g(HGNC:NCKAP1L, loc(MESHA:Hippocampus))
g(HGNC:TREM2, loc(MESHA:Hippocampus)) -- g(HGNC:APBB1IP, loc(MESHA:Hippocampus))
g(HGNC:TREM2, loc(MESHA:Hippocampus)) -- g(HGNC:CSF1R, loc(MESHA:Hippocampus))
g(HGNC:TREM2, loc(MESHA:Hippocampus)) -- g(HGNC:DOCK2, loc(MESHA:Hippocampus))
g(HGNC:TREM2, loc(MESHA:Hippocampus)) -- g(HGNC:DOCK8, loc(MESHA:Hippocampus))
g(HGNC:TREM2, loc(MESHA:Hippocampus)) -- g(HGNC:ITGAX, loc(MESHA:Hippocampus))
g(HGNC:TREM2, loc(MESHA:Hippocampus)) -- g(HGNC:C1QB, loc(MESHA:Hippocampus))
g(HGNC:TREM2, loc(MESHA:Hippocampus)) -- g(HGNC:LAPTM5, loc(MESHA:Hippocampus))
g(HGNC:TREM2, loc(MESHA:Hippocampus)) -- g(HGNC:METTL9, loc(MESHA:Hippocampus))

SET Support = "Only 2 such genes were identified in cerebellum, NCKAP1L and ITGAX."

g(HGNC:TREM2, loc(MESHA:Cerebellum)) -- g(HGNC:ITGAX, loc(MESHA:Cerebellum))
g(HGNC:TREM2, loc(MESHA:Cerebellum)) -- g(HGNC:NCKAP1L, loc(MESHA:Cerebellum))

SET Support = "NCKAP1L (also known as HEM1) encodes a member of the HEM family of \
tissue-specific trans-membrane proteins and forms part of the \
Scar/WAVE complex, which plays an important role in regulating cell \
shape and has been shown to be essential for actin polymerization and \
myosin regulation during neutrophil chemotaxis (Weiner et al., 2006)."

p(HGNC:NCKAP1L) positiveCorrelation bp(GOBP:"actin polymerization or depolymerization")
p(HGNC:NCKAP1L) reg bp(GOBP:"neutrophil chemotaxis")

SET Support = "Similarly DOCK2 is known to be involved in remodeling of the actin \
cytoskeleton required for lymphocyte migration (Sanui et al., 2003), \
and the biological functions of DOCK8 include regulation of cell \
migration and morphology (Harada et al., 2012; Mizesko et al., 2013)."

p(HGNC:DOCK2) -- bp(GOBP:"actin cytoskeleton reorganization")
p(HGNC:DOCK2) -- bp(GOBP:"lymphocyte migration")
p(HGNC:DOCK8) reg bp(GOBP:"cell migration")

SET Support = "APBB1IP (also known as RIAM) encodes the Rap1-GTP-interacting adaptor \
molecule (RIAM) and has been implicated in mediating changes in the \
actin cytoskeleton after integrin-mediated adhesion acting through the \
Vav2-RhoA-ROCK-myosin light chain pathway (Hernandez-Varas et al., \
2011; Lafuente et al., 2004)."

p(HGNC:APBB1IP) -- bp(GOBP:"actin cytoskeleton reorganization")

SET Support = "Finally, ITGAX encodes the integrin alpha X chain protein, which forms \
part of the alpha X beta 2 complex that has been implicated in the \
adherence of neutrophils and monocytes to stimulated endothelium \
cells, and in the phagocytosis of complement-coated particles (Hynes, \
1992; Schoenwaelder and Burridge, 1999; van Lookeren Campagne et al., \
2007)."

p(HGNC:ITGAX) positiveCorrelation complex(a(MESHA:Neutrophils), a(MESHA:"Endothelial Cells"))
p(HGNC:ITGAX) positiveCorrelation complex(a(MESHA:Monocytes), a(MESHA:"Endothelial Cells"))
p(HGNC:ITGAX) positiveCorrelation bp(GOBP:phagocytosis)

SET Support = "Although, the remaining 4 genes (CSF1R, LAPTM5, C1QB, and METTL9) had \
no obvious common features, the identification of CSF1R is notable, as \
the TREM2-TYROBP protein complex has recently been implicated in CSF1R \
signaling providing a biological basis for the high connection between \
CSF1R and TREM2 (Otero et al., 2009)."

complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> act(p(HGNC:CSF1R), ma(GOBP:signaling))
p(HGNC:TREM2) positiveCorrelation p(HGNC:CSF1R)

SET Support = "C1QB has already been highlighted within the canonical pathway for \
“Systemic lupus erythrematosus” and LAPTM5 (lysosomal protein \
transmembrane 5) is a transmembrane receptor that is associated with \
lysosomes."

p(HGNC:C1QB) positiveCorrelation path(MESHD:"Lupus Erythematosus, Systemic")
p(HGNC:LAPTM5) -- a(MESHA:Lysosomes)

SET Support = "Among the genes accounting for this enrichment were MS4A6A, which is \
strongly implicated in AD through recent GWAS (Hollingworth et al., \
2011; Naj et al., 2011) and APBB1IP (Morgan et al., 2007) (full \
details provided in Supplementary Table 6)."

g(HGNC:MS4A6A) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:APBB1IP) positiveCorrelation path(MESHD:"Alzheimer Disease")


UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2014 Jun;35(6):1510.e19-26. doi: 10.1016/j.neurobiolaging.2013.12.010. Epub 2013 Dec 21.", "24439484"}

SET Section = "Abstract"

SET Support = "Recent studies have identified a rare coding variant (p.R47H) in TREM2 \
that confers a high risk for Alzheimer's disease (AD)."

g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Here, we report comprehensive analyses using whole-exome sequencing \
data, cerebrospinal fluid biomarker analyses, meta-analyses (16,254 \
cases and 20,052 controls) and cell-based functional studies to \
support the role of the TREML2 coding missense variant p.S144G \
(rs3747742) as a potential driver of the meta-analysis AD-associated \
genome-wide association studies signal."

#Here the GWAS signal they refer to is that p.S144G is protective for AD, had to read results to find this
SET StudyType = {"GWAS", "Cell-based functional study"}
SET DataType = {"whole-exome sequencing data", "cerebrospinal fluid biomarker data"}
g(HGNC:TREML2, var("p.Ser144Gly")) negativeCorrelation path(MESHD:"Alzheimer Disease")

UNSET StudyType
UNSET DataType

SET Support = "Additionally, we demonstrate that the protective role of TREML2 in AD \
is independent of the role of TREM2 gene as a risk factor for AD."

p(HGNC:TREML2) negativeCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

SET Section = "Results"

SET Support = "Next, we performed a meta-analysis of the data from the ADGC, GERAD, \
EADI, and the Alzheimer's Research UK; studies (16,254 cases and \
20,052 control subjects) we found that the minor alleles of both \
rs9381040 (p = 1.21 × 10−5; OR = 0.92, CI = 0.88–0.95), and rs3747742 \
(p = 8.66 × 10−5; OR = 0.93, CI = 0.89–0.96) reduce risk for AD (Fig. \
1, panel A and B)."

g(dbSNP:rs9381040) negativeCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREML2) hasVariant g(dbSNP:rs3747742)
g(dbSNP:rs3747742) -> p(HGNC:TREML2, var("p.Ser144Gly"))
g(dbSNP:rs3747742) negativeCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "In addition, TREM2-p.R47H (rs75932628) was successfully imputed \
(imputation quality score information = 0.84 and 0.79) in the GERAD \
and EADI studies, and it displays a strong association with AD risk (p \
= 1.3 × 10−3; OR = 1.92, CI = 1.29–2.85) (Fig. 1, panel C)."

g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) -> p(HGNC:TREM2, var("p.Arg47His"))
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "rs9381040 (p = 4.11 × 10−4, beta = −0.02) and rs3747742 (p = 1.4 × \
10−4, beta = −0.02) both exhibit a strong association with CSF ptau \
levels."

g(dbSNP:rs9381040) -- p(HGNC:MAPT, pmod(Ph), loc(MESHA:"Cerebrospinal Fluid"))
g(dbSNP:rs3747742) -- p(HGNC:MAPT, pmod(Ph), loc(MESHA:"Cerebrospinal Fluid"))

SET Support = "In these samples, 3 variants, rs9381040 (p = 0.04, beta = −0.02) (Fig. \
2, panel A), rs3747742 (p = 0.02, beta = −0.02) (Fig. 2, panel B), and \
rs75932628 (p = 0.0016, beta = 0.2) (Fig. 2, panel C) demonstrate a \
nominally significant association with CSF ptau levels."

g(dbSNP:rs9381040) -- p(HGNC:MAPT, pmod(Ph), loc(MESHA:"Cerebrospinal Fluid"))
g(dbSNP:rs3747742) -- p(HGNC:MAPT, pmod(Ph), loc(MESHA:"Cerebrospinal Fluid"))
g(dbSNP:rs75932628) -- p(HGNC:MAPT, pmod(Ph), loc(MESHA:"Cerebrospinal Fluid"))

SET Support = "Importantly, these associations confirmed the direction of the effect \
on CSF ptau levels: the minor allele of rs3747742 is associated with \
lower ptau levels (beta = −0.02) and is predicted to be protective for \
AD risk (OR = 0.91; CI = 0.86–0.97), while the minor allele of \
TREM2-p.R47H is associated with an increased risk for AD (OR = 1.91, \
CI = 1.85–1.97) and higher levels of CSF ptau (beta = 0.2)."

g(dbSNP:rs3747742) negativeCorrelation p(HGNC:MAPT, pmod(Ph), loc(MESHA:"Cerebrospinal Fluid"))
g(dbSNP:rs3747742) negativeCorrelation path(MESHD:"Alzheimer Disease")
g(dbSNP:rs75932628) positiveCorrelation p(HGNC:MAPT, pmod(Ph), loc(MESHA:"Cerebrospinal Fluid"))
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "In addition, TREM and TREM-like receptors modulate the innate immune \
response by either amplifying or dampening Toll-like receptor-induced \
signals, playing critical roles in fine-tuning the inflammatory \
response (Ford and McVicar, 2009)."

p(HGNC:TREM2) -- bp(GOBP:"innate immune response")
p(HGNC:TREM2) reg bp(GOBP:"toll-like receptor signaling pathway")
p(HGNC:TREM2) -- bp(GOBP:"inflammatory response")

SET Support = "Treatment of microglia with IL-1β repressed expression of TREM2 (Fig. \
3, panel A), but increased expression of TREML2 (Fig. 3, panel B)."

complex(a(MESHA:Microglia), p(HGNC:IL1B)) -| p(HGNC:TREM2)
complex(a(MESHA:Microglia), p(HGNC:IL1B)) -> p(HGNC:TREML2)


UNSET Section

##################################################################################

SET Citation = {"PubMed", "N Engl J Med. 2013 Jan 10;368(2):117-27. doi: 10.1056/NEJMoa1211851. Epub 2012 Nov 14.", "23150934"}

SET Section = "Abstract"

SET Support = "We then performed a meta-analysis on imputed data for the TREM2 \
variant rs75932628 (predicted to cause a R47H substitution) from three \
genomewide association studies of Alzheimer's disease and tested for \
the association of the variant with disease."

SET StudyType = "GWAS"
g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) -> p(HGNC:TREM2, var("p.Arg47His"))
UNSET StudyType

SET Support = "Homozygous loss-of-function mutations in TREM2, encoding the \
triggering receptor expressed on myeloid cells 2 protein, have \
previously been associated with an autosomal recessive form of early- \
onset dementia."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:Dementia)

SET Support = "The most commonly associated variant, rs75932628 (encoding R47H), \
showed highly significant association with Alzheimer's disease \
(P<0.001)."

g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

SET Section = "Results"

SET Support = "We found significantly more variants in exon 2 of TREM2 in patients \
with Alzheimer's disease than in those without the disease (P = 0.02)."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "In addition, we observed six variants (H157Y, R98W, D87N, T66M, Y38C, \
and Q33X) that were present in cases and not in controls in the \
discovery series and two variants that were present only in controls \
(N68K and L211P) (Table 2). Of these variants, D87N was significantly \
associated with disease (P = 0.02)."

g(HGNC:TREM2, var("p.Asp87Asn")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "We previously observed the T66M, Y38C, and Q33X variants in the \
homozygous state in patients with a frontotemporal dementia–like \
syndrome."

g(HGNC:TREM2, var("p.Thr66Met")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
g(HGNC:TREM2, var("p.Tyr38Cys")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
g(HGNC:TREM2, var("p.Gln33X")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")

SET Support = "Homozygous Q33X variants have also been identified in patients with \
Nasu–Hakola disease."

g(HGNC:TREM2, var("p.Gln33X")) positiveCorrelation path(DO:"Nasu-Hakola disease")

SET Support = "Of the variants in TREM2 associated with Alzheimer's disease, the R47H \
variant showed the strongest association (P<0.001)."

g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "In human control brain, TREM2 was expressed widely, at high levels in \
white matter and substantial abundance in hippocampus and neocortex \
but at low levels in the cerebellum (Fig. S2 in the Supplementary \
Appendix)."

p(HGNC:TREM2) positiveCorrelation a(MESHA:"White Matter")
p(HGNC:TREM2) positiveCorrelation a(MESHA:Hippocampus)
p(HGNC:TREM2) positiveCorrelation a(MESHA:Neocortex)

SET Support = "We observed increased expression of Trem2 in large activated microglia \
(Fig. 2)."

SET Species = "10090"
r(MGI:Trem2) positiveCorrelation bp(GOBP:"microglial cell activation")

SET Support = "In wild-type mice, Trem2 is expressed in small cytoplasmic granules in \
the cytoplasm of most neurons in the cortex and other brain regions."

a(MESHA:"Cytoplasmic Granules", loc(MESHA:Neurons)) -> p(MGI:Trem2)

SET Support = "We observed Trem2 to be expressed in microglia surrounding the outer \
border of amyloid plaques, as reported previously in the APP23 mouse \
model25 (Fig. 2)."

p(MGI:Trem2) positiveCorrelation a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))

UNSET Species


UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2014 Nov;35(11):2657.e13-2657.e19. doi: 10.1016/j.neurobiolaging.2014.06.018. Epub 2014 Jun 20.", "25042114"}

SET Section = "Abstract"

SET Support = "A genetic association was found between p.T96K and FTD-S (p = 0.013, \
odds ratio = 4.23, 95% Confidence Interval [1.17-14.77])."

#FTD-S = Frontotemporal dementia subtypes, variant refers to TREM2
g(HGNC:TREM2, var("p.Thr96Lys")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")

SET Support = "The present findings provide evidence that p.T96K is associated with \
FTD-S and that p.L211P may contribute to its pathogenic effect."

p(HGNC:TREM2, var("p.Thr96Lys")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
p(HGNC:TREM2, var("p.Leu211Pro")) positiveCorrelation act(p(HGNC:TREM2, var("p.Thr96Lys")))

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Curr Alzheimer Res. 2016;13(4):370-80.", "26567746"}

SET Section = "Abstract"

SET Support = "However, endogenous stimuli such as the presence of misfolded or \
aggregated proteins, as well as systemic inflammatory events may lead \
to the activation of microglial cells, the brain's innate immune \
system, and, subsequently, to neuroinflammation."

bp(GOBP:"inflammatory response") -> bp(GOBP:"microglial cell activation involved in immune response")
SET MeSHAnatomy = "Brain"
bp(GOBP:"microglial cell activation involved in immune response") -> path(MESHD:Inflammation)
UNSET MeSHAnatomy

SET Support = "Alzheimer's disease, the leading cause of dementia, is characterized \
by amyloid beta deposition and tau hyperphosphorylation."

path(MESHD:"Alzheimer Disease") -> path(MESHD:Dementia)
path(MESHD:"Alzheimer Disease") -- a(CHEBI:"amyloid-beta")
p(HGNC:MAPT, pmod(Ph)) biomarkerFor path(MESHD:"Alzheimer Disease")

SET Support = "Neuroinflammation in Alzheimer's disease has been identified as major \
contributor to disease pathogenesis."

SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
path(MESHD:Inflammation) -> bp(GOBP:pathogenesis)
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Support = "Once activated, microglia release several pro and anti-inflammatory \
mediators of which several affect the function and structure of the \
brain."

bp(GOBP:"microglial cell activation") -> bp(GOBP:"cytokine production involved in inflammatory response")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Neurochem. 2011 Jan;116(1):43-52. doi: 10.1111/j.1471-4159.2010.07083.x. Epub 2010 Dec 2.", "21044080"}

SET Section = "Abstract"

SET Support = "Both hypercortisolemia and hippocampal damage are features found in \
patients diagnosed of Alzheimer's disease (AD) and epidemiological \
evidence supports a role for stress as a risk factor for AD."

bp(GOBP:"cortisol secretion") positiveCorrelation path(MESHD:"Alzheimer Disease")
bp(MESHPP:"Stress, Physiological") positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "It is known that immobilization stress is followed by accumulation of \
oxidative/nitrosative mediators in brain after the release of \
proinflammatory cytokines, nuclear factor kappa B activation, nitric \
oxide synthase-2 and cyclooxygenase-2 expression."

SET MeSHAnatomy = "Brain"
bp(MESHPP:"Stress, Physiological") positiveCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
bp(MESHPP:"Stress, Physiological") positiveCorrelation bp(GOBP:"NIK/NF-kappaB signaling")
bp(MESHPP:"Stress, Physiological") positiveCorrelation p(HGNC:NOS2)
bp(MESHPP:"Stress, Physiological") positiveCorrelation p(HGNC:PTGS2)
# PTGS2 = cyclooxygenase-2
UNSET MeSHAnatomy

SET Support = "We therefore studied the effect of chronic immobilization stress on \
amyloid precursor protein/PS1 mice."

SET Species = "10090"

SET Support = "Stress exposure increased AD-induced neuroinflammation characterized \
by astrogliosis, increased inflammatory gene transcription and lipid \
peroxidation."

SET MeSHAnatomy = "Brain"
bp(MESHPP:"Stress, Physiological") positiveCorrelation path(MESHD:Inflammation)
path(MESHD:Inflammation) positiveCorrelation path(HP:Astrocytosis)
path(MESHD:Inflammation) positiveCorrelation bp(MESHPP:"Lipid Peroxidation")
path(MESHD:Inflammation) positiveCorrelation  bp(GOBP:"inflammatory response")
UNSET MeSHAnatomy

UNSET Species

UNSET Section

##################################################################################

SET Citation = {"PubMed", "EMBO Mol Med. 2017 Oct;9(10):1366-1378. doi: 10.15252/emmm.201707673.", "28855301"}

SET Section = "Abstract"

SET Support = "We have characterised the proteolytic cleavage events responsible for \
the shedding of triggering receptor expressed on myeloid cells 2 \
(TREM2) from primary cultures of human macrophages, murine microglia \
and TREM2-expressing human embryonic kidney (HEK293) cells. In all \
cell types, a soluble 17 kDa N-terminal cleavage fragment was shed \
into the conditioned media in a constitutive process that is inhibited \
by G1254023X and metalloprotease inhibitors and siRNA targeting \
ADAM10."

# G1254023X should be written as GI254023X
# The only other metalloprotease inhibitor that worked was GM6001, I checked the paper
SET Species = "9606"
SET CellLine = "HEK293"
a(CC:GI254023X) -| p(HGNC:TREM2, frag("1_?", "17 kDa"))
a(CC:GM6001) -| p(HGNC:TREM2, frag("1_?", "17 kDa"))
p(HGNC:ADAM10) positiveCorrelation p(HGNC:TREM2, frag("1_?", "17 kDa"))
UNSET CellLine
a(CC:GI254023X) -| p(HGNC:TREM2, frag("1_?", "17 kDa"), loc(MESHA:Macrophages))
a(CC:GM6001) -| p(HGNC:TREM2, frag("1_?", "17 kDa"), loc(MESHA:Macrophages))
p(HGNC:ADAM10) positiveCorrelation p(HGNC:TREM2, frag("1_?", "17 kDa"), loc(MESHA:Macrophages))
UNSET Species
SET Species = "10090"
a(CC:GI254023X) -| p(HGNC:TREM2, frag("1_?", "17 kDa"), loc(MESHA:Microglia))
a(CC:GM6001) -| p(HGNC:TREM2, frag("1_?", "17 kDa"), loc(MESHA:Microglia))
p(HGNC:ADAM10) positiveCorrelation p(HGNC:TREM2, frag("1_?", "17 kDa"), loc(MESHA:Microglia))
UNSET Species

SET Support = "Crucially, we also show that the Alzheimer's disease-associated H157Y \
TREM2 variant was shed more rapidly than wild type from HEK293 cells, \
possibly by a novel, batimastat- and ADAM10-siRNA-independent, \
sheddase activity."

g(HGNC:TREM2, var("p.His157Tyr")) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

SET Section = "Results"

SET Support = "Non‐permeabilised primary human macrophages showed high levels of \
TREM2 expression on their cell surfaces when probed with the AF1828 \
polyclonal antibody (Fig 1A, 1, red) but not with control goat serum \
(Fig 1A, 2)."

a(MESHA:Macrophages) positiveCorrelation p(HGNC:TREM2, loc(MESHA:"Cell Membrane"))

SET Support = "It is notable that the surface‐exposed TREM2 has a short half‐life of \
< 1 h and that the bulk of the lost protein appears in the \
supernatant."

p(HGNC:TREM2, loc(MESHA:"Cell Membrane")) -> p(HGNC:TREM2, loc(MESHA:"Extracellular Space"))

SET Support = "Soluble TREM2 NTF was detected by Western blotting the conditioned \
medium from primary murine microglia and primary human macrophages; a \
similar result was also seen for HEK293 cells (Fig 2A)."

p(MGI:Trem2, frag("1_?"), loc(MESHA:"Extracellular Space")) positiveCorrelation a(MESHA:Microglia)
p(HGNC:TREM2, frag("1_?"), loc(MESHA:"Extracellular Space")) positiveCorrelation a(MESHA:Macrophages)

SET Support = "In primary human macrophages, and in wild‐type hTREM2‐expressing \
HEK293 cells, the ADAM10‐specific metalloprotease inhibitor GI254023X \
reduced shedding."

a(CC:GI254023X, loc(MESHA:Macrophages)) -| p(HGNC:TREM2, frag("1_?"), loc(MESHA:"Extracellular Space"))

SET Support = "The H157Y variant of TREM2 carries an increased risk of AD (Jiang et \
al, 2016) although, like the R47H variant, the mechanism is unknown."

p(HGNC:TREM2, var("p.His157Tyr")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Remarkably, while GI254023X inhibited the shedding of wild‐type and \
H157Y variant TREM2 equally (Fig 5E), by contrast batimastat was only \
partially effective in preventing the shedding of H157Y variant TREM2 \
(Fig 5F; batimastat titration in Fig EV2; Fig 5G: schematic of \
expected protease activities)."

a(CC:GI254023X) -| p(HGNC:TREM2, frag("1_?"), loc(MESHA:"Extracellular Space"))
a(CC:GI254023X) -| p(HGNC:TREM2, var("p.His157Tyr"), frag("1_?"), loc(MESHA:"Extracellular Space"))

SET Support = "ADAM10, but not ADAM17, knock‐down reduced the shedding of TREM2 NTF \
for both WT and the H157Y variant."

p(HGNC:ADAM10) -> p(HGNC:TREM2, frag("1_?"), loc(MESHA:"Extracellular Space"))
p(HGNC:ADAM10) -> p(HGNC:TREM2, var("p.His157Tyr"), frag("1_?"), loc(MESHA:"Extracellular Space"))


UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Exp Med. 2016 May 2;213(5):667-75. doi: 10.1084/jem.20151948. Epub 2016 Apr 18.", "27091843"}

SET Section = "Abstract"

SET Support = "Rare TREM2 variants that affect TREM2 function lead to an increased \
risk of developing AD."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "In murine models of AD, TREM2 deficiency prevents microglial \
clustering around Aβ deposits."

SET Species = "10090"
p(MGI:Trem2) positiveCorrelation a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))
UNSET Species

SET Support = "Thus, TREM2 protects from AD by enabling microglia to surround and \
alter Aβ plaque structure, thereby limiting neuritic damage."

p(HGNC:TREM2, loc(MESHA:Microglia)) negativeCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, loc(MESHA:Microglia)) negativeCorrelation path(MESHD:Neuritis)
#Neuritis leads to neuritic damage

UNSET Section

SET Section = "Results and Discussion"

SET Support = "We previously observed an impaired microglial response to Aβ in 8-mo- \
old Trem2−/− 5XFAD mice (Wang et al., 2015). Defective microglial \
clustering around plaques was also detected at earlier time points in \
3- or 4-mo-old Trem2−/− APPPS1-21 mice (Ulrich et al., 2014; Jay et \
al., 2015)."

SET Species = "10090"
SET MouseStrain = "5XFAD"
SET TimePoint = {"3 months", "4 months", "8 months"}
p(MGI:Trem2) -> a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))
UNSET TimePoint

SET Support = "In contrast, at 8 mo of age, 5XFAD mice had a clear increase in total \
microglia, whereas age-matched Trem2−/− 5XFAD had fewer microglia \
(Fig. 2 A)."

SET TimePoint = "8 months"
p(MGI:Trem2) positiveCorrelation a(MESHA:Microglia)
UNSET TimePoint

SET Support = "In 5XFAD mice, the majority of Ki-67+ microglia detected were in close \
proximity to Aβ plaques (mean distance = 21.1 µm; Fig. 2, D and E)."

a(MESHA:Microglia, loc(CHEBI:"amyloid-beta")) positiveCorrelation bp(GOBP:"microglial cell proliferation")

SET Support = "In contrast, plaque-associated Ki-67+ microglia were nearly \
undetectable in Trem2−/− 5XFAD mice (Fig. 2 F)."

p(MGI:Trem2) -> bp(GOBP:"microglial cell proliferation")

SET Support = "We found that, in 5XFAD mice, the number of microglia associated with \
a given plaque was positively correlated to the size of the plaque \
(Fig. 2 H)."

a(MESHA:Microglia, loc(CHEBI:"amyloid-beta")) positiveCorrelation a(CHEBI:"amyloid-beta")

SET Support = "Microglia in Trem2−/− 5XFAD mice incorporated less methoxy-X04 than \
microglia from 5XFAD mice (Fig. 2, I and J), corroborating that TREM2 \
deficiency impairs productive interactions between microglia and \
plaques."

p(MGI:Trem2) -> bp(GOBP:"beta-amyloid clearance")

SET Support = "We found that the number of dystrophic neurites was significantly \
increased in Trem2−/− 5XFAD mice compared with 5XFAD mice (Fig. 4, \
A–C)."

# dystrophic neurites are associated with increased amounts of N-terminal APP
p(MGI:Trem2) -| p(MGI:App, frag("1_?"), loc(MESHA:Neurites))

SET Support = "We also stained sections for hyperphosphorylated tau and found an \
increase in the number and volume of hyperphosphorylated tau punctae \
in Trem2−/− 5XFAD mice compared with 5XFAD mice (Fig. 4, D–E)."

p(MGI:Trem2) -| p(MGI:Mapt, pmod(Ph))

SET Support = "Thus, the altered Aβ plaque shape and density associated with TREM2 \
deficiency observed in Fig. 3 were associated with increased toxicity \
to neural processes."

p(MGI:Trem2) negativeCorrelation path(MESHD:Neuritis)

UNSET MouseStrain
UNSET Species

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Cell. 2017 Aug 10;170(4):649-663.e13. doi: 10.1016/j.cell.2017.07.023.", "28802038"}

SET Section = "Abstract"

SET Support = "Elevated risk of developing Alzheimer's disease (AD) is associated \
with hypomorphic variants of TREM2, a surface receptor required for \
microglial responses to neurodegeneration, including proliferation, \
survival, clustering, and phagocytosis."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, loc(MESHA:Microglia)) -> bp(GOBP:"microglial cell proliferation")
p(HGNC:TREM2, loc(MESHA:Microglia)) -> bp(MESHPP:"Cell Survival")
p(HGNC:TREM2, loc(MESHA:Microglia)) -> bp(GOBP:phagocytosis)

SET Support = "Here, we find that microglia in AD patients carrying TREM2 risk \
variants and TREM2-deficient mice with AD-like pathology have abundant \
autophagic vesicles, as do TREM2-deficient macrophages under growth- \
factor limitation or endoplasmic reticulum (ER) stress."

SET Species = "9606"
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TREM2, var("?"), loc(MESHA:Microglia)) positiveCorrelation a(GOCC:autophagosome)
#Autophagosome is an autophagic vesicle
UNSET Species

SET Species = "10090"
p(MGI:Trem2, loc(MESHA:Microglia)) negativeCorrelation a(GOCC:autophagosome)
p(MGI:Trem2, loc(MESHA:Macrophages)) negativeCorrelation a(GOCC:autophagosome)
SET MeSHAnatomy = "Macrophages"
bp(MESHPP:"Endoplasmic Reticulum Stress") positiveCorrelation a(GOCC:autophagosome)
UNSET MeSHAnatomy
UNSET Species
UNSET MeSHDisease

SET Support = "Combined metabolomics and RNA sequencing (RNA-seq) linked this \
anomalous autophagy to defective mammalian target of rapamycin (mTOR) \
signaling, which affects ATP levels and biosynthetic pathways."

SET Assay = {"metabolomics", "RNA-seq"}
a(GOCC:autophagosome) -- bp(GOBP:"cellular response to rapamycin")

SET Support = "Metabolic derailment and autophagy were offset in vitro through \
Dectin-1, a receptor that elicits TREM2-like intracellular signals, \
and cyclocreatine, a creatine analog that can supply ATP. Dietary \
cyclocreatine tempered autophagy, restored microglial clustering \
around plaques, and decreased plaque-adjacent neuronal dystrophy in \
TREM2-deficient mice with amyloid-Œ≤ pathology."

# Dectin-1 = CLEC7a
SET StudyType = "in vitro"
#p(HGNC:CLEC7A, loc(MESHA:Microglia)) -> bp(GOBP:autophagy)
a(MESHC:cyclocreatine) -> a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))
#a(MESHC:cyclocreatine) -> bp(GOBP:autophagy)
UNSET StudyType

UNSET Section

SET Section = "Results"

SET Support = "We observed dramatically more LC3+ microglia in post-mortem brain \
sections from both R47H and R62H heterozygous AD patients than in \
those from case-matched AD patients homozygous for the common TREM2 \
variant (Fig. 1E, F, Table 1)."

a(MESHA:Microglia, loc(MESHA:Brain)) positiveCorrelation p(HGNC:TREM2, var("p.Arg47His"))
a(MESHA:Microglia, loc(MESHA:Brain)) positiveCorrelation p(HGNC:TREM2, var("p.Arg62His"))

SET Support = "Taken together, these data suggest that autophagic-like vesicles \
accumulate in the microglia of TREM2-deficient mice and humans with \
TREM2 risk alleles during the development of AD."

a(GOCC:autophagosome, loc(MESHA:Microglia)) negativeCorrelation p(MGI:Trem2)
a(GOCC:autophagosome, loc(MESHA:Microglia)) negativeCorrelation p(HGNC:TREM2, var("p.Arg47His"))
a(GOCC:autophagosome, loc(MESHA:Microglia)) negativeCorrelation p(HGNC:TREM2, var("p.Arg62His"))

SET Support = "The ratio of lipidated LC3II to non-lipidated LC3I was markedly higher \
in microglia from Trem2−/− 5XFAD mice than in microglia from 5XFAD \
mice, consistent with the increased number of autophagic vesicles in \
TREM2-deficient microglia (Fig. 2A, B)."

p(MGI:Trem2, loc(MESHA:Microglia)) negativeCorrelation a(GOCC:autophagosome)

SET Support = "The amount of p62 protein was lower in microglia from Trem2−/− 5XFAD \
mice than in microglia from 5XFAD mice."

# p62 = Nup62
p(MGI:Nup62, loc(MESHA:Microglia)) positiveCorrelation p(MGI:Trem2, loc(MESHA:Microglia))

SET Support = "Thus, TREM2 deficiency results in bona fide autophagy."

p(MGI:Trem2) negativeCorrelation bp(GOBP:autophagy)

SET Support = "Autophagy often reflects an adaptive response to stress that can occur \
when cells cannot satisfy increased demands for energy and protein \
synthesis (Kroemer et al., 2010)."

bp(GOBP:"cellular response to stress") positiveCorrelation bp(GOBP:autophagy)

SET Support = "Immunoblotting of sorted microglia from Trem2−/− 5XFAD and 5XFAD mice \
revealed decreased phosphorylation of 4EBP1, an mTORC1 effector, as \
well as Akt (serine 473) and NDRG1, both mTORC2 effectors, in the \
TREM2-deficient microglia (Fig. 2A)."

# 4EBP1 = Eif4ebp1
p(MGI:Trem2, loc(MESHA:Microglia)) positiveCorrelation p(MGI:Eif4ebp1, pmod(Ph), loc(MESHA:Microglia))
p(MGI:Eif4ebp1) -- p(MGI:Mtor)
p(MGI:Trem2, loc(MESHA:Microglia)) positiveCorrelation p(MGI:Akt1, pmod(Ph, Ser, 473), loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) positiveCorrelation p(MGI:Ndrg1, pmod(Ph), loc(MESHA:Microglia))
p(MGI:Ndrg1) -- p(MGI:Mtor)
p(MGI:Akt1) -- p(MGI:Mtor)

SET Support = "Ulk1, a key inducer of autophagy, which is inhibited by mTOR signaling \
through phosphorylation at serine 757 (Kim et al., 2011), was less \
phosphorylated in microglia from Trem2−/− 5XFAD mice, consistent with \
reduced mTOR activation and increased autophagy (Fig. 2A)."

act(p(MGI:Ulk1)) -> bp(GOBP:autophagy)
act(p(MGI:Mtor), ma(kin)) => p(MGI:Ulk1, pmod(Ph, Ser, 757))
p(MGI:Ulk1, pmod(Ph, Ser, 757)) -| act(p(MGI:Ulk1))
p(MGI:Trem2, loc(MESHA:Microglia)) positiveCorrelation p(MGI:Ulk1, pmod(Ph, Ser, 757), loc(MESHA:Microglia))

SET Support = "Impaired mTOR signaling was associated with phosphorylation of AMP- \
activated protein kinase (AMPK), a sensor of low energy states (Fig. \
2A)."

act(p(HGNC:MTOR), ma(GOBP:signaling)) negativeCorrelation complex(SCOMP:"AMP Activated Protein Kinase Complex")

SET Support = "Providing further evidence for defective energetic and anabolic \
metabolism associated with TREM2 deficiency, microglia from Trem2−/− \
5XFAD mice had a lower mitochondrial mass than did microglia from \
5XFAD mice (Fig. 2C, D)."

p(MGI:Trem2, loc(MESHA:Microglia)) positiveCorrelation a(MESHA:Mitochondria, loc(MESHA:Microglia))

SET Support = "Furthermore, gene expression microarray analyses of sorted microglia \
from Trem2−/− 5XFAD and 5XFAD mice revealed that TREM2 deficiency was \
associated with decreased expression of genes encoding translation \
initiation factors, ribosomal proteins, glucose transporters, \
glycolytic enzymes, as well as the transcription factor HIF1α that \
controls glycolysis (Fig. S2B–G)."

p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Hif1a)
p(MGI:Hif1a) reg bp(MESHPP:Glycolysis)

SET Support = "BMDMs contained more autophagic vesicles (Fig. 3A, B) and had a higher \
LC3II/LC3I ratio than did WT cells when CSF1 was limiting (Fig. 3C)."

a(GOCC:autophagosome, loc(MESHA:Macrophages)) negativeCorrelation p(HGNC:CSF1)

SET Support = "As observed in sorted microglia, autophagy in BMDMs was linked to \
impaired mTOR signaling."

act(p(HGNC:MTOR), ma(GOBP:signaling)) negativeCorrelation a(GOCC:autophagosome, loc(MESHA:Macrophages))

SET Support = "Trem2−/− BMDMs had less phosphorylated 4EBP1, Akt (serine 473) and \
NDRG1 (Fig. 3F) and more phosphorylated AMPK (Fig. 3G) in both optimal \
CSF1 and limiting CSF1 than did WT BMDMs."

p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation p(HGNC:EIF4EBP1, pmod(Ph), loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation p(HGNC:AKT1, pmod(Ph, Ser, 473), loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation p(HGNC:NDRG1, pmod(Ph), loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) negativeCorrelation complex(SCOMP:"AMP Activated Protein Kinase Complex")

SET Support = "Thus, lack of TREM2 suppressed mTOR activation and elicited \
compensatory AMPK and Ulk1 activation and autophagy in BMDMs in \
response to metabolic stress, very similar to our observations of \
microglia in 5XFAD mice."

p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation act(p(HGNC:MTOR))
p(HGNC:TREM2, loc(MESHA:Macrophages)) negativeCorrelation act(p(HGNC:ULK1))
p(HGNC:TREM2, loc(MESHA:Macrophages)) negativeCorrelation bp(GOBP:autophagy)

SET Support = "As TREM2 signaling adapters DAP12 and DAP10 have been shown to \
activate PI3-K, which in turn can activate mTOR, we asked whether \
enhanced mTOR signaling in WT BMDM compared to Trem2−/− BMDM was \
dependent on PI3-K."

p(HGNC:TREM2) -- p(HGNC:TYROBP)
# DAP10 = HCST
p(HGNC:TREM2) -- p(HGNC:HCST)
p(HGNC:TYROBP) -> act(p(SFAM:"PIK3C Class IA Family"), ma(kin))
p(HGNC:HCST) -> act(p(SFAM:"PIK3C Class IA Family"), ma(kin))
p(SFAM:"PIK3C Class IA Family") -> act(p(HGNC:MTOR))

SET Support = "Inhibition of PI3-K with wortmannin or LY294002 caused a major \
reduction in phosphorylation of mTOR and its downstream targets in WT \
BMDMs; the residual amount of phosphorylation was similar to that seen \
in Trem2−/− BMDMs (Fig. 3I and S3A)."

a(CHEBI:wortmannin) -| p(SFAM:"PIK3C Class IA Family")
a(CHEBI:LY294002) -| p(SFAM:"PIK3C Class IA Family")
a(CHEBI:wortmannin) -| p(HGNC:MTOR, pmod(Ph))
a(CHEBI:LY294002) -| p(HGNC:MTOR, pmod(Ph))

SET Support = "Thus, TREM2 deficiency affects cell responses to multiple stressors."

p(HGNC:TREM2) -- bp(GOBP:"cellular response to stress")

SET Support = "Thus, in multiple in vitro settings of stress, TREM2-expressing cells \
are more able to sustain mTOR activation and suppress autophagy in a \
PI3-K-dependent manner than are cells lacking TREM2."

p(HGNC:TREM2) positiveCorrelation act(p(HGNC:MTOR))
p(HGNC:TREM2) negativeCorrelation bp(GOBP:autophagy)

SET Support = "Compared to WT BMDMs in optimal CSF1, Trem2−/− BMDMs cultured under \
the same conditions exhibited: 1) a marked decrease of key \
intermediates in the synthesis of nucleotides (e.g. phosphoribosyl \
pyrophosphate), N-glycosylated proteins (e.g. UDP-glucose), and \
phospholipids (e.g. CDP-ethanolamine); 2) a decrease in glycolytic \
metabolites (e.g. glucose 6-phosphate and fructose bisphosphate) and \
tricarboxylic acid (TCA) cycle intermediates (citrate and succinate); \
and 3) an increase in catabolic products of amino acids (e.g. \
indolacetate) and phospholipids (e.g. glycerol 3-phosphate) (Fig. 4A, \
Fig. S4A)."

p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation a(CHEBI:"5-O-phosphono-D-ribofuranosyl diphosphate", loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation a(CHEBI:"UDP-alpha-D-glucose", loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation a(CHEBI:"CDP-ethanolamine", loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation a(CHEBI:"D-glucose 6-phosphate", loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation a(CHEBI:"D-fructose 1,6-bisphosphate", loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation a(CHEBI:"citrate(3-)", loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation a(CHEBI:succinate, loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation a(CHEBI:"indole-3-acetic acid", loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation a(CHEBI:"sn-glycerol 3-phosphate", loc(MESHA:Macrophages))

SET Support = "Moreover, a selective increase in malate and fumarate suggested an \
enhanced malate-aspartate shuttle to sustain defective NADH oxidation \
and NAD regeneration (Fig. 4A)."

p(HGNC:TREM2, loc(MESHA:Macrophages)) negativeCorrelation a(CHEBI:malate, loc(MESHA:Macrophages))
p(HGNC:TREM2, loc(MESHA:Macrophages)) negativeCorrelation a(CHEBI:"fumaric acid", loc(MESHA:Macrophages))
a(CHEBI:malate) -- bp(GOBP:"NADH oxidation")
a(CHEBI:malate) -- bp(GOBP:"NADH regeneration")
a(CHEBI:"fumaric acid") -- bp(GOBP:"NADH oxidation")
a(CHEBI:"fumaric acid") -- bp(GOBP:"NADH regeneration")

SET Support = "Under these conditions, again in comparison to WT BMDMs, Trem2−/− \
BMDMs underwent a marked increase in symmetrical dimethyl arginine, \
indicative of protein catabolism, as well as an increase in ADP- \
ribose, indicative of NAD degradation (Fig. 4C, Fig. S4B,C)."

p(HGNC:TREM2, loc(MESHA:Macrophages)) negativeCorrelation a(CHEBI:"N(omega),N'(omega)-dimethyl-L-arginine", loc(MESHA:Macrophages))
bp(GOBP:"protein catabolic process") positiveCorrelation a(CHEBI:"N(omega),N'(omega)-dimethyl-L-arginine")
p(HGNC:TREM2, loc(MESHA:Macrophages)) negativeCorrelation a(CHEBI:"cyclic ADP-ribose", loc(MESHA:Macrophages))
a(CHEBI:"cyclic ADP-ribose") negativeCorrelation a(CHEBI:NADH)

SET Support = "A luciferase-based ATP assay confirmed an ATP deficiency in Trem2−/− \
BMDMs, which was exacerbated at low CSF1 concentrations (Fig. 4D)."

p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation a(CHEBI:ATP, loc(MESHA:Macrophages))

SET Support = "Trem2−/− BMDMs also had fewer mitochondria than WT BMDMs on a per cell \
basis as measured by MitoTracker Green fluorescence and by the \
mitochondrial-to-nuclear DNA ratio (Fig. 4F, G)."

p(HGNC:TREM2, loc(MESHA:Macrophages)) positiveCorrelation a(MESHA:Mitochondria, loc(MESHA:Macrophages))

SET Support = "Muscle physiology studies have extensively demonstrated that creatine \
phosphate contributes to the regeneration of ATP and to the \
maintenance of uniformly high ATP/ADP ratios in muscle fibers (Walker, \
1979)."

a(CHEBI:N-phosphocreatine) -> bp(GOBP:"ATP biosynthetic process")

SET Support = "Moreover, the creatine analog 1-carboxymethyl-2-iminoimidazolidine \
(cyclocreatine) can, upon phosphorylation, generate a long-acting \
phosphagen that can effectively sustain cellular ATP levels during \
increased energy demand (Kurosawa et al., 2012; Woznicki and Walker, \
1979)."

a(MESHC:cyclocreatine) -> a(CHEBI:phosphagen)
a(CHEBI:phosphagen) positiveCorrelation a(CHEBI:ATP)

SET Support = "Indeed, incubation with cyclocreatine improved ECAR, which was \
accompanied by less autophagy, increased mTOR signaling and cell \
viability (Fig. 5A–C and data not shown)."

a(MESHC:cyclocreatine) -| bp(GOBP:autophagy)
a(MESHC:cyclocreatine) -> act(p(HGNC:MTOR), ma(GOBP:signaling))
a(MESHC:cyclocreatine) -| bp(MESHPP:Apoptosis)

SET Support = "To test whether engagement of receptors that elicit signaling pathways \
similar to those of TREM2 could also mitigate autophagy and support \
cell survival, Trem2−/− and WT BMDMs were cultured with depleted \
zymosan, a selective ligand of Dectin-1, which activates Syk and PI3-K \
signaling independent of DAP12 (Dambuza and Brown, 2015)."

# zymosan = (1->3)-beta-D-glucan
a(CHEBI:"(1->3)-beta-D-glucan") => complex(p(HGNC:CLEC7A), a(CHEBI:"(1->3)-beta-D-glucan"))
complex(p(HGNC:CLEC7A), a(CHEBI:"(1->3)-beta-D-glucan")) -> act(p(HGNC:CLEC7A))
p(HGNC:CLEC7A) -> act(p(SFAM:"PIK3C Class IA Family"))
p(HGNC:CLEC7A) -> act(p(HGNC:SYK))

SET Support = "Dectin-1 activation curbed autophagy in CSF1-starved TREM2-deficient \
BMDMs to levels seen in WT BMDMs in low CSF1, as indicated by a \
reduction in the LC3II/LC3I ratio (Fig. 5D, E) along with increased \
amounts of p62 (Fig. 5F)."

act(p(HGNC:CLEC7A)) -| bp(GOBP:autophagy)

SET Support = "Treatment with zymosan also enhanced cellular ATP levels in Trem2−/− \
BMDMs, restoring them to WT BMDM levels (Fig. 5G)."

a(CHEBI:"(1->3)-beta-D-glucan", loc(MESHA:Macrophages)) -> a(CHEBI:ATP, loc(MESHA:Macrophages))

SET Support = "Furthermore, clustering of microglia around plaques (Fig. 6C, F) and \
the number of microglia/HPF in plaque-bearing regions of the cortex \
(Fig. S5E) were both significantly increased in Trem2−/− 5XFAD mice \
treated with cyclocreatine compared to untreated Trem2−/− 5XFAD mice."

a(MESHC:cyclocreatine, loc(MESHA:Microglia)) -> a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))

SET Support = "These findings indicate that dietary supplementation with \
cyclocreatine is sufficient to partially rescue the defect in \
microgliosis and microglial clustering around plaques in Trem2−/− \
5XFAD mice, while concomitantly mitigating autophagy and death of the \
microglia."

a(MESHC:cyclocreatine, loc(MESHA:Microglia)) -> a(MESHA:Microglia)

SET Support = "To assess the impact of cyclocreatine on microglial activation, which \
is also impaired in Trem2−/− 5XFAD mice, we quantified the percentage \
of microglia that expressed the activation marker osteopontin (Spp1), \
a protein that, in the brain, is upregulated in microglia in the \
context of Aβ deposition (Orre et al., 2014; Wang et al., 2015)."

p(HGNC:SPP1, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:"microglial cell activation")
a(CHEBI:"amyloid-beta", loc(MESHA:Brain)) positiveCorrelation p(HGNC:SPP1, loc(MESHA:Microglia))

SET Support = "Untreated Trem2−/− 5XFAD mice had very few Spp1+ microglia, while \
cyclocreatine-treated Trem2−/− 5XFAD mice had significantly more Spp1+ \
microglia, as did TREM2-sufficient 5XFAD mice (Fig. 7A, B)."

p(HGNC:TREM2, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:"microglial cell activation")
a(MESHC:cyclocreatine, loc(MESHA:Microglia)) -> p(MGI:Spp1, loc(MESHA:Microglia))

SET Support = "Moreover, biochemical analysis of microglia isolated ex vivo \
demonstrated that cyclocreatine treatment of Trem2−/− 5XFAD mice also \
restored microglial mTOR signaling and limited autophagy compared to \
untreated Trem2−/− 5XFAD mice (Fig. 7C, D)."

a(MESHC:cyclocreatine, loc(MESHA:Microglia)) positiveCorrelation act(p(MGI:Mtor, loc(MESHA:Microglia)), ma(GOBP:signaling))
a(MESHC:cyclocreatine, loc(MESHA:Microglia)) -| bp(GOBP:autophagy)


UNSET Section

##################################################################################


SET Citation = {"PubMed", "J Neuroinflammation. 2010 Jan 11;7:2. doi: 10.1186/1742-2094-7-2.", "20064241"}

SET Section = "Abstract"

SET Support = "Norepinephrine enhances the LPS-induced expression of COX-2 and \
secretion of PGE2 in primary rat microglia."

SET Species = "10116"
# Cox-2 = Ptgs2
a(CHEBI:noradrenaline) -> r(RGD:Ptgs2, loc(MESHA:Microglia))
a(CHEBI:lipopolysaccharide) -> r(RGD:Ptgs2, loc(MESHA:Microglia))
a(CHEBI:noradrenaline) -> a(CHEBI:"prostaglandin E2", loc(MESHA:Microglia))

SET Support = "The monoamine norepinephrine reduces the production of cytokines by \
activated microglia in vitro."

SET StudyType = "in vitro"
bp(GOBP:"microglial cell activation") -> p(MESHC:Cytokines)
a(CHEBI:noradrenaline) -| p(MESHC:Cytokines)
UNSET StudyType

SET Support = "Interestingly, norepinephrine increased COX-2 mRNA, but not protein \
expression."

a(CHEBI:noradrenaline) -> r(RGD:Ptgs2, loc(MESHA:Microglia))

SET Support = "Norepinephrine strongly enhanced COX-2 expression and PGE2 production \
induced by lipopolysaccharide (LPS)."

a(CHEBI:noradrenaline) -> r(RGD:Ptgs2)
a(CHEBI:noradrenaline) -> a(CHEBI:"prostaglandin E2")
a(CHEBI:lipopolysaccharide) -> r(RGD:Ptgs2)
a(CHEBI:lipopolysaccharide) -> a(CHEBI:"prostaglandin E2")

SET Support = "Considering that PGE2 displays different roles in neuroinflammatory \
and neurodegenerative disorders, norepinephrine may play an important \
function in the modulation of these processes in pathophysiological \
conditions."

SET MeSHAnatomy = "Brain"
a(CHEBI:"prostaglandin E2") -- path(MESHD:Inflammation)
a(CHEBI:"prostaglandin E2") -- path(HP:Neurodegeneration)
UNSET MeSHAnatomy

UNSET Species

UNSET Section

SET Section = "Results"

SET Support = "We show here that NE dose-dependently enhanced LPS-induced COX-2 \
protein levels in primary rat microglia ​(Fig.1A)"

a(CHEBI:lipopolysaccharide) -> p(HGNC:PTGS2)
# NE = norepinephrine
SET ChallengeType = "lipopolysaccharide stimulation"
SET Species = "10116"
a(CHEBI:noradrenaline) -> p(RGD:Ptgs2, loc(MESHA:Microglia))
UNSET ChallengeType

SET Support = "In contrast to the finding that NE alone did not influence COX-2 \
protein levels, a potent induction of COX-2 mRNA expression was \
observed after 2 h stimulation with NE alone, which resulted in a \
dose-response upregulation starting with 1 nM that peaked at 10 μM NE \
(Fig.1C)."

SET TimePoint = "2 hours post-stimulation"
a(CHEBI:noradrenaline) -> r(RGD:Ptgs2, loc(MESHA:Microglia))
UNSET TimePoint

SET Support = "Addition of LPS, 10 ng/ml, further enhanced the expression of COX-2 \
mRNA (Fig.1D)."

a(CHEBI:lipopolysaccharide) -> r(RGD:Ptgs2, loc(MESHA:Microglia))

SET Support = "LPS plus NE further increased COX-2 immunoreactivity significantly at \
4 and 8 h compared to LPS treatment alone (p < 0.05)."

SET ChallengeType = "lipopolysaccharide stimulation"
SET TimePoint = {"4 hours post-stimulation", "8 hours post-stimulation"}
a(CHEBI:noradrenaline) -> p(RGD:Ptgs2, loc(MESHA:Microglia))
UNSET TimePoint
UNSET ChallengeType

SET Support = "COX-2 mRNA upregulation was already observed at 30 min upon LPS plus \
NE treatment and reached its maximum by 1 h."

SET ChallengeType = "lipopolysaccharide stimulation"
SET TimePoint = {"30 minutes post-stimulation", "1 hour post-stimulation"}
a(CHEBI:noradrenaline) -> r(RGD:Ptgs2, loc(MESHA:Microglia))
UNSET TimePoint
UNSET ChallengeType

SET Support = "LPS, 10 ng/ml alone, significantly increased PGE2 concentration after \
24 h (p < 0.05), whereas there was no significant increase after 4 and \
8 h stimulation with LPS."

SET TimePoint = "24 hours post-stimulation"
a(CHEBI:lipopolysaccharide) -> a(CHEBI:"prostaglandin E2", loc(MESHA:Microglia))
UNSET TimePoint

SET Support = "We observed a significant increase in the secretion of PGE2 after 8 h \
of stimulation with the combination of LPS (10 ng/ml) and NE (1 μM) \
compared to unstimulated cells (p < 0.05). At 24 h, LPS plus NE also \
showed a significant elevation of PGE2 levels compared to treatment \
with LPS alone (p < 0.001)."

SET ChallengeType = "lipopolysaccharide stimulation"
SET TimePoint = {"24 hours post-stimulation", "8 hours post-stimulation"}
a(CHEBI:noradrenaline) -> a(CHEBI:"prostaglandin E2", loc(MESHA:Microglia))
UNSET TimePoint
UNSET ChallengeType

SET Support = "Co-treatment of LPS with the selective α1-agonists phenylephrine only \
induced a modest increase in COX-2 protein expression compared to LPS \
and NE treatment, while clonidine, an α2-adrenergic agonist, did not \
show any effect at all. In contrast, terbutaline and dobutamine, \
β2-and unselective β-adrenergic agonists, respectively, mimicked the \
effect of NE ​(Fig.4A)."

SET ChallengeType = "lipopolysaccharide stimulation"
a(CHEBI:terbutaline) -> p(RGD:Ptgs2, loc(MESHA:Microglia))
a(CHEBI:dobutamine) -> p(RGD:Ptgs2, loc(MESHA:Microglia))
UNSET ChallengeType


UNSET Section


##################################################################################

SET Citation = {"PubMed", "Am J Hum Genet. 2008 Nov;83(5):623-32. doi: 10.1016/j.ajhg.2008.10.008. Epub 2008 Oct 30.", "18976728"}

SET Section = "Abstract"

SET Support = "To date four genes have been established to either cause early-onset \
autosomal-dominant AD (APP, PSEN1, and PSEN2(1-4)) or to increase \
susceptibility for late-onset AD (APOE5)"

p(HGNC:PSEN1) positiveCorrelation path(ADO:"Early_onset_Alzheimer_s_Disease__")
p(HGNC:PSEN2) positiveCorrelation path(ADO:"Early_onset_Alzheimer_s_Disease__")
p(HGNC:APP) positiveCorrelation path(ADO:"Early_onset_Alzheimer_s_Disease__")
p(HGNC:APOE, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "One of these signals (p = 5.7 x 10(-14)) was elicited by SNP rs4420638 \
and probably reflects APOE-epsilon4, which maps 11 kb proximal (r2 = \
0.78)"

g(dbSNP:rs4420638) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "The SNP (rs11159647, on chromosome 14q31) with the strongest \
association signal also showed evidence of association with the same \
allele in GWA data generated in an independent sample of approximately \
1,400 AD cases and controls (p = 0.04)"

g(dbSNP:rs11159647) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section


##################################################################################

SET Citation = {"PubMed", "Nat Genet. 2011 May;43(5):436-41. doi: 10.1038/ng.801. Epub 2011 Apr 3.", "21460841"}

SET Section = "Abstract"

SET Support = "We obtained genome-wide significant results at MS4A4A (rs4938933; \
stages 1 and 2, meta-analysis P (P(M)) = 1.7 × 10(-9), joint analysis \
P (P(J)) = 1.7 × 10(-9); stages 1, 2 and 3, P(M) = 8.2 × 10(-12)), \
CD2AP (rs9349407; stages 1, 2 and 3, P(M) = 8.6 × 10(-9)), EPHA1 \
(rs11767557; stages 1, 2 and 3, P(M) = 6.0 × 10(-10)) and CD33 \
(rs3865444; stages 1, 2 and 3, P(M) = 1.6 × 10(-9)). We also \
replicated previous associations at CR1 (rs6701713; P(M) = 4.6 × \
10(-10), P(J) = 5.2 × 10(-11)), CLU (rs1532278; P(M) = 8.3 × 10(-8), \
P(J) = 1.9 × 10(-8)), BIN1 (rs7561528; P(M) = 4.0 × 10(-14), P(J) = \
5.2 × 10(-14)) and PICALM (rs561655; P(M) = 7.0 × 10(-11), P(J) = 1.0 \
× 10(-10)), but not at EXOC3L2, to late-onset Alzheimer's disease \
susceptibility."

g(HGNC:MS4A6A) hasVariant g(dbSNP:rs4938933)
g(dbSNP:rs4938933) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:CD2AP) hasVariant g(dbSNP:rs9349407)
g(dbSNP:rs9349407) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:EPHA1) hasVariant g(dbSNP:rs11767557)
g(dbSNP:rs11767557) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:CD33) hasVariant g(dbSNP:rs3865444)
g(dbSNP:rs3865444) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:BIN1) hasVariant g(dbSNP:rs7561528)
g(dbSNP:rs7561528) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:PICALM) hasVariant g(dbSNP:rs561655)
g(dbSNP:rs561655) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:CR1) hasVariant g(dbSNP:rs6701713)
g(dbSNP:rs6701713) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:CLU) hasVariant g(dbSNP:rs1532278)
g(dbSNP:rs1532278) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Arch Neurol. 2011 Dec;68(12):1569-79. doi: 10.1001/archneurol.2011.646.", "22159054"}

SET Section = "Abstract"

#The objective of the abstract states they are looking at late onset AD

SET Support = "After adjusting for the confounding effects of APOE genotype, one of \
these SNPs, rs6859 in PVRL2, remained significantly associated with AD \
(P = .0087)."

p(HGNC:NECTIN2) hasVariant g(dbSNP:rs6859)
g(dbSNP:rs6859) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "Association was also observed with SNPs in CLU, PICALM, BIN1, EPHA1, \
MS4A, ABCA7, and CD33, although the effect direction for some SNPs and \
the most significant SNPs differed from findings in data sets \
consisting of whites."

# Do not say specifically in this evidence which SNP/gene is associated with increased risk or protection so leaving it as an association
p(HGNC:CLU) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")
p(HGNC:PICALM) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")
p(HGNC:BIN1) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")
p(HGNC:EPHA1) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")
p(HGNC:MS4A7) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")
p(HGNC:ABCA7) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")
p(HGNC:CD33) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")

UNSET Section

SET Section = "Results"

SET Support = "In the African American cohort, the ε4 allele is very significantly \
associated with AD (P=9.69×10−23)."

SET Ethnicity = "African-American"
p(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET Ethnicity

SET Support = "Analysis of individual genotypes showed evidence of a significant \
protective effect of ε2 (ε2/ε2 and ε2/ε3 genotypes) compared with the \
ε3/ε3 genotype and an exponential increase in risk associated with the \
dose of ε4 (Table 3)."

# APOEe4
g(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")) positiveCorrelation path(MESHD:"Alzheimer Disease")
# APOEe2
g(HGNC:APOE, var("p.Arg158Cys")) negativeCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Analyses of the APOE region in the African American data sets revealed \
a highly significant association with 3 markers within 25 kb of APOE, \
including PVRL2 SNP rs6859 (P=5.39×10−7) and TOMM4 SNPs rs157582 \
(P=3.26×10−6) and rs10119 (P=5.95×10−7) (Table 4 lists top SNPs in the \
region; see eTable 1 in the supplementary material for all nominally \
significant SNPs)."

SET Ethnicity = "African-American"
g(HGNC:NECTIN2) hasVariant g(dbSNP:rs6859)
g(dbSNP:rs6859) positiveCorrelation g(HGNC:APOE)
g(HGNC:TOMM40) hasVariant g(dbSNP:rs157582)
g(HGNC:TOMM40) hasVariant g(dbSNP:rs10119)
g(dbSNP:rs157582) positiveCorrelation g(HGNC:APOE)
g(dbSNP:rs10119) positiveCorrelation g(HGNC:APOE)
UNSET Ethnicity

SET Support = "Only rs6859 remained significant after adjustment for APOE genotype \
(P=.0087)."

g(dbSNP:rs6859) positiveCorrelation g(HGNC:APOE)

SET Support = "After adjustment for APOE genotype, only 7 SNPs remained significant \
at P<.05, including rs6857 (P=4.98×10−7), rs4420638 (P=1.54×10−7), and \
rs2075650 in TOMM40 (P=1.25×10−6)."

g(dbSNP:rs6857) positiveCorrelation g(HGNC:APOE)
g(dbSNP:rs4420638) positiveCorrelation g(HGNC:APOE)
g(HGNC:TOMM40) hasVariant g(dbSNP:rs2075650)
g(dbSNP:rs2075650) positiveCorrelation g(HGNC:APOE)


UNSET Section


##################################################################################

SET Citation = {"PubMed", "Neurol Sci. 2012 Oct;33(5):1021-8. doi: 10.1007/s10072-011-0881-0. Epub 2011 Dec 14.", "22167654"}

SET Section = "Abstract"

SET Support = "We found that higher frequencies of rs10868366 G and rs7019241 C \
carriers (in GOLPH2) were observed in SAD patients compared with \
controls (rs10868366 G: P = 0.026, odds ratio (OR) = 1.4, 95% \
confidence intervals (CI) 1.0-1.9; rs7019241 C: P = 0.019, OR 1.4, 95% \
CI 1.6-1.9)."

#SAD = sporadic_Alzheimer
g(HGNC:GOLM1) hasVariant g(dbSNP:rs10868366)
g(dbSNP:rs10868366) positiveCorrelation path(ADO:"sporadic_Alzheimer")
g(HGNC:GOLM1) hasVariant g(dbSNP:rs7019241)
g(dbSNP:rs7019241) positiveCorrelation path(ADO:"sporadic_Alzheimer")

SET Support = "Our results support the view that rs10868366 and rs7019241 in GOLPH2 \
and rs3826656 in near gene 5' region of CD33 are significantly \
associated with SAD in the north Chinese Han population."

SET Ethnicity = "Chinese Han"

g(HGNC:GOLM1) hasVariant g(dbSNP:rs10868366)
g(dbSNP:rs10868366) positiveCorrelation path(ADO:"sporadic_Alzheimer")
g(HGNC:GOLM1) hasVariant g(dbSNP:rs7019241)
g(dbSNP:rs7019241) positiveCorrelation path(ADO:"sporadic_Alzheimer")
g(HGNC:CD33) hasVariant g(dbSNP:rs3826656)
g(dbSNP:rs3826656) positiveCorrelation path(ADO:"sporadic_Alzheimer")

UNSET Ethnicity
UNSET Section

##################################################################################

SET Citation = {"PubMed", "Hum Genet. 2012 Jul;131(7):1245-9. doi: 10.1007/s00439-012-1154-6. Epub 2012 Mar 1.", "22382309"}

SET Section = "Abstract"

SET Support = "CD33 and MS4A6A genes play potential key roles in the pathogenesis of \
Alzheimer's disease (AD)."

g(HGNC:CD33) -- path(MESHD:"Alzheimer Disease")
g(HGNC:MS4A6A) -- path(MESHD:"Alzheimer Disease")

SET Support = "One recent genome-wide association study has revealed that the \
rs3865444 polymorphism in the CD33 gene and rs610932 polymorphism in \
the MS4A6A gene are associated with susceptibility to AD in \
Caucasians."

SET Ethnicity = "Caucasian"

g(HGNC:CD33) hasVariant g(dbSNP:rs3865444)
g(dbSNP:rs3865444) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:MS4A6A) hasVariant g(dbSNP:rs610932)
g(dbSNP:rs610932) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Ethnicity

SET Support = "Our data revealed the allele (T) of the rs3865444 polymorphism of the \
CD33 gene and the allele (C) of the rs610932 polymorphism of the \
MS4A6A gene may contribute to AD risk in the Chinese Han population."

SET Ethnicity = "Chinese Han"

g(HGNC:CD33) hasVariant g(dbSNP:rs3865444)
#The title of the article says "prevalence"..hence positiveCorrelation
g(dbSNP:rs3865444) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:MS4A6A) hasVariant g(dbSNP:rs610932)
g(dbSNP:rs610932) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section


##################################################################################

SET Citation = {"PubMed", "G3 (Bethesda). 2012 Jan;2(1):71-8. doi: 10.1534/g3.111.000869. Epub 2012 Jan 1.", "22384383"}

SET Section = "Abstract"

SET Support = "Recently genome-wide association studies have identified significant \
association between Alzheimer's disease (AD) and variations in CLU, \
PICALM, BIN1, CR1, MS4A4/MS4A6E, CD2AP, CD33, EPHA1, and ABCA7"

# They only say association so we don't know if it's protective or risk variants

p(HGNC:CLU, var("?")) -- path(MESHD:"Alzheimer Disease")
p(HGNC:PICALM, var("?")) -- path(MESHD:"Alzheimer Disease")
p(HGNC:BIN1, var("?")) -- path(MESHD:"Alzheimer Disease")
p(HGNC:CR1, var("?")) -- path(MESHD:"Alzheimer Disease")
p(HGNC:MS4A6E, var("?")) -- path(MESHD:"Alzheimer Disease")
p(HGNC:CD2AP, var("?")) -- path(MESHD:"Alzheimer Disease")
p(HGNC:CD33, var("?")) -- path(MESHD:"Alzheimer Disease")
p(HGNC:EPHA1, var("?")) -- path(MESHD:"Alzheimer Disease")
p(HGNC:ABCA7, var("?")) -- path(MESHD:"Alzheimer Disease")

UNSET Section

##################################################################################


SET Citation = {"PubMed", "Oxid Med Cell Longev. 2013;2013:860959. doi: 10.1155/2013/860959. Epub 2013 Mar 6.", "23533697"}

SET Section = "Abstract"

SET Support = "Alzheimer's disease (AD) is an age-related neurodegenerative disorder \
characterized by a progressive loss of memory and cognitive skills."

path(MESHD:"Alzheimer Disease") positiveCorrelation bp(MESHPP:Aging)
path(MESHD:"Alzheimer Disease") positiveCorrelation path(MESHD:"Cognitive Dysfunction")
path(MESHD:"Alzheimer Disease") negativeCorrelation bp(GOBP:"long-term memory")

SET Support = "Variants in TREM2 triple one's risk of developing late-onset AD."

p(HGNC:TREM2, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "TREM2 is expressed on microglial cells, the resident macrophages in \
the CNS, and functions to stimulate phagocytosis on one hand and to \
suppress cytokine production and inflammation on the other hand."

a(MESHA:Microglia, loc(MESHA:"Central Nervous System")) -> p(HGNC:TREM2)
a(MESHA:Microglia, loc(MESHA:"Central Nervous System")) isA a(MESHA:Macrophages, loc(MESHA:"Central Nervous System"))
p(HGNC:TREM2) -> bp(GOBP:phagocytosis)
p(HGNC:TREM2) -| bp(GOBP:"cytokine production involved in inflammatory response")

UNSET Section

SET Section = "1. Defining Alzheimer's Disease"

SET Support = "Alzheimer's disease (AD) is classified as a neurodegenerative disorder \
affecting neurons of the brain that are responsible for memory and \
higher cognitive functions."

path(MESHD:"Alzheimer Disease") -- a(MESHA:Neurons)
path(MESHD:"Alzheimer Disease") positiveCorrelation path(HP:"Cognitive impairment")

SET Support = "The loss of these neurons leads to the symptoms of the disease \
including memory impairments, difficulties with language, inability to \
execute motor activities, and the overall decline in cognitive skills \
[1]."

path(MESHD:"Alzheimer Disease") negativeCorrelation a(MESHA:Neurons)
path(HP:"Cognitive impairment") positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Dementia is the umbrella term describing the symptoms of AD, and AD is \
by far the leading cause of dementia in the United States, being \
responsible for over 70% of all known cases of dementia [2]."

path(MESHD:"Alzheimer Disease") -> path(MESHD:Dementia)

SET Support = "Aging is the most well-established risk factor for the development of \
sporadic AD with incidence rates showing an exponential growth between \
the ages of 65 and 85 years, doubling every 5 years [3]."

path(MESHD:"Alzheimer Disease") positiveCorrelation bp(MESHPP:Aging)

UNSET Section

SET Section = "2. Pathology Associated with AD"

SET Support = "AD is diagnosed based upon the extent of senile plaques composed of \
beta-amyloid and neurofibrillary tangles (NFTs) containing abnormally \
phosphorylated and truncated tau [4]."

a(CHEBI:"amyloid-beta") biomarkerFor path(MESHD:"Alzheimer Disease")
a(MESHCS:"Neurofibrillary Tangles", loc(MESHA:Brain)) biomarkerFor path(MESHD:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph)) biomarkerFor path(MESHD:"Alzheimer Disease")

SET Support = "In essence, this hypothesis stipulates that much of the pathology \
associated with AD is driven by an increased load of beta-amyloid in \
the brain of AD patients that can occur years before the first \
symptoms of the disease manifest."

path(MESHD:"Alzheimer Disease") positiveCorrelation a(CHEBI:"amyloid-beta", loc(MESHA:Brain))

SET Support = "Beta-amyloid is formed following sequential cleavage of the amyloid \
precursor protein (APP) by two proteases, β-secretase and γ-secretase."

deg(p(HGNC:APP)) => a(CHEBI:"amyloid-beta")
act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:proteolysis)) => a(CHEBI:"amyloid-beta")
act(p(HGNC:BACE1), ma(GOBP:proteolysis)) -> a(CHEBI:"amyloid-beta")
act(p(HGNC:BACE1), ma(GOBP:proteolysis)) =| p(HGNC:APP)

SET Support = "Once formed, beta-amyloid has the propensity to self-aggregate into \
β-sheet structures that deposit extracellularly forming senile plaques \
(Figure 1)."

a(CHEBI:"amyloid-beta") positiveCorrelation path(MESHD:"Plaque, Amyloid")

SET Support = "In addition, this oligomeric form of beta-amyloid is highly toxic and \
is the trigger for the loss of synapses and neuronal damage [9, 10]."

a(CHEBI:"amyloid-beta") negativeCorrelation a(MESHA:Neurons)

SET Support = "NFTs are primarily composed of aggregated phosphorylated tau protein \
and are a clinical feature not just in AD but other diseases that are \
collectively referred to as “tauopathies” [15]."
a(MESHCS:"Neurofibrillary Tangles") biomarkerFor path(MESHD:Tauopathies)

SET Support = "Tau normally functions to help maintain the stability of the \
cytoskeleton of neurons by binding to microtubules."

SET MeSHAnatomy = "Neurons"
complex(p(HGNC:MAPT), a(GOCC:microtubule)) -> bp(GOBP:"cytoskeleton organization")
UNSET MeSHAnatomy

SET Support = "However, upon hyperphosphorylation and posttranslational cleavage, tau \
loses its binding affinity for microtubules, leading to a \
destabilization of the cytoskeleton and self-assembly of tau into \
tangles of paired helical filaments (PHFs) [16]."

p(HGNC:MAPT, pmod(Ph)) =| complex(p(HGNC:MAPT), a(GOCC:microtubule))
p(HGNC:MAPT, pmod(Ph)) => a(MESHCS:"Neurofibrillary Tangles", loc(MESHA:Brain))

UNSET Section

SET Section = "3. Mechanisms of Neurodegeneration in AD"

SET Support = "Specifically, an important early molecular step is the lost of \
synapses, which correlates highly with the initial memory impairment \
observed in AD [19, 20]."

bp(GOBP:"long-term memory") positiveCorrelation bp(GOBP:"synapse assembly")

SET Support = "Figure 2 summarizes some of the major findings on beta-amyloid-induced \
toxicity that begin with either beta-amyloid activation of apoptotic \
pathways or promotion of oxidative stress."

a(CHEBI:"amyloid-beta") -> bp(MESHPP:Apoptosis)
a(CHEBI:"amyloid-beta") -> bp(MESHPP:"Oxidative Stress")

SET Support = "With regards to apoptosis, since the early nineties, studies have \
supported a general role for apoptosis in AD [21–23]."

bp(MESHPP:Apoptosis) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "In addition, the activation of caspases, including caspase 3, 6, 8, \
and 9, has been documented in the AD brain [24–30]."

act(p(HGNC:CASP3, loc(MESHA:Brain))) positiveCorrelation path(MESHD:"Alzheimer Disease")
act(p(HGNC:CASP6, loc(MESHA:Brain))) positiveCorrelation path(MESHD:"Alzheimer Disease")
act(p(HGNC:CASP8, loc(MESHA:Brain))) positiveCorrelation path(MESHD:"Alzheimer Disease")
act(p(HGNC:CASP9, loc(MESHA:Brain))) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "In turn, evidence suggests that once activated, caspases may cleave \
critical cellular proteins in AD including APP, actin, fodrin, glial \
acidic fibrillary protein, beclin-1, and tau [28, 31–35]."

act(p(SFAM:"CASP Family"), ma(GOBP:proteolysis)) =| p(HGNC:APP)
act(p(SFAM:"CASP Family"), ma(GOBP:proteolysis)) =| p(SFAM:"ACTN Family")
act(p(SFAM:"CASP Family"), ma(GOBP:proteolysis)) =| complex(p(HGNC:SPTAN1), p(HGNC:SPTBN1))
act(p(SFAM:"CASP Family"), ma(GOBP:proteolysis)) =| p(HGNC:GFAP)
act(p(SFAM:"CASP Family"), ma(GOBP:proteolysis)) =| p(HGNC:BECN1)
act(p(SFAM:"CASP Family"), ma(GOBP:proteolysis)) =| p(HGNC:MAPT)

SET Support = "Importantly, several studies have suggested that caspase activation \
and cleavage of tau may precede and contribute to the formation of \
NFTs [31, 32, 36]."

act(p(SFAM:"CASP Family"), ma(GOBP:proteolysis)) positiveCorrelation a(MESHCS:"Neurofibrillary Tangles")
p(HGNC:MAPT) negativeCorrelation a(MESHCS:"Neurofibrillary Tangles")

SET Support = "Oxidative stress either through lipid peroxidation or mitochondrial \
disruption is an early feature found in AD [37–39]."

bp(MESHPP:"Lipid Peroxidation") -> bp(MESHPP:"Oxidative Stress")
bp(MESHPP:"Mitochondrial Degradation") -> bp(MESHPP:"Oxidative Stress")
bp(MESHPP:"Oxidative Stress") positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Finally, tau phosphorylation is upregulated by oxidative stress [42] \
and tau filaments are modified by products of oxidative stress \
[43–45]."

bp(MESHPP:"Oxidative Stress") positiveCorrelation p(HGNC:MAPT, pmod(Ph))

SET Support = "Oxidative stress also activates several kinases that have been \
implicated in the hyperphosphorylation of tau including glycogen \
synthase kinase-3 (GSK3), Jun-N-terminal kinase (JNK), and mitogen- \
activated protein kinases (MAPKs) [46, 47]."

bp(MESHPP:"Oxidative Stress") -> act(p(HGNC:GSK3A), ma(kin))
# MAPK8 = Jun-N-terminal kinase
bp(MESHPP:"Oxidative Stress") -> act(p(HGNC:MAPK8), ma(kin))
bp(MESHPP:"Oxidative Stress") -> act(p(SFAM:"MAPK Erk1/2 Family"), ma(kin))
act(p(HGNC:GSK3A), ma(kin)) -> p(HGNC:MAPT, pmod(Ph))
act(p(HGNC:MAPK8), ma(kin)) -> p(HGNC:MAPT, pmod(Ph))
act(p(SFAM:"MAPK Erk1/2 Family"), ma(kin)) -> p(HGNC:MAPT, pmod(Ph))

UNSET Section

SET Section = "4. Inflammation in AD"

SET Support = "Brain inflammation is a pathological hallmark of AD [48, 49]."

SET MeSHAnatomy = "Brain"
path(MESHD:Inflammation) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET MeSHAnatomy

SET Support = "In this regard, numerous studies have supported a definitive role for \
inflammation in AD, with a key feature being the presence of activated \
microglia [50–52] and reactive astrocytes found within senile plaques \
[53–55]."

path(MESHD:Inflammation) positiveCorrelation a(MESHA:Microglia)
path(MESHD:Inflammation) positiveCorrelation a(MESHA:Astrocytes)

SET Support = "Microglia are key players in mediating immune responses in the CNS \
functioning as the resident macrophages of the CNS and as such \
contribute to a healthy CNS by attacking and removing potential \
pathogens and cell debris and by secreting tissue rebuilding factors \
[57]."

a(MESHA:Microglia, loc(MESHA:"Central Nervous System")) isA a(MESHA:Macrophages)
a(MESHA:Microglia, loc(MESHA:"Central Nervous System")) positiveCorrelation bp(GOBP:"immune complex clearance")
a(MESHA:Microglia, loc(MESHA:"Central Nervous System")) positiveCorrelation bp(GOBP:"tissue regeneration")

SET Support = "Thus, beta-amyloid is a potent inducer of microglia activation [52, \
58–60], and one important role of microglia is to clear beta-amyloid \
deposits out of the AD brain (for recent review, see [61])."

a(CHEBI:"amyloid-beta") -> a(MESHA:Microglia)
a(MESHA:Microglia) -| a(CHEBI:"amyloid-beta", loc(MESHA:Brain))

UNSET Section

SET Section = "5. Triggering Receptor Expressed on Myeloid Cells 2 (TREM2)"

SET Support = "TREM2 is expressed on the cell membrane of many types of immune cells \
including macrophages, dendritic cells, osteoclasts, and microglia \
[62]."

a(MESHA:Macrophages) -> p(HGNC:TREM2, loc(MESHA:"Cell Membrane"))
a(MESHA:"Dendritic Cells") -> p(HGNC:TREM2, loc(MESHA:"Cell Membrane"))
a(MESHA:Osteoclasts) -> p(HGNC:TREM2, loc(MESHA:"Cell Membrane"))
a(MESHA:Microglia) -> p(HGNC:TREM2, loc(MESHA:"Cell Membrane"))

SET Support = "TREM2 is thought to act as a cell surface receptor, and although the \
endogenous ligand has yet to be identified, it is known that it \
requires the adaptor protein 12 (DAP12) for the initiation of \
signaling cascades [63]."

complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> bp(GOBP:"signal transduction")

SET Support = "Activation of the TREM2 receptor on microglia has two important \
function consequences: (1) stimulation of phagocytosis activity and \
(2) decreased microglial proinflammatory responses [64]."

act(p(HGNC:TREM2, loc(MESHA:Microglia))) -> bp(GOBP:phagocytosis)
act(p(HGNC:TREM2, loc(MESHA:Microglia))) -| bp(GOBP:"inflammatory response")

SET Support = "Such a mutation leads to The Nasu-Hakola disease, which manifests as a \
combination of bone cysts and dementia [65]."

p(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")
path(DO:"Nasu-Hakola disease") positiveCorrelation path(HP:"Bone cyst")
path(DO:"Nasu-Hakola disease") positiveCorrelation path(MESHD:Dementia)

SET Support = "Affected individuals show progressive inflammatory neurodegeneration \
with loss of white matter and cystic bone lesions followed by death by \
the fifth decade of life [66]."

path(DO:"Nasu-Hakola disease") negativeCorrelation a(MESHA:"White Matter")
path(DO:"Nasu-Hakola disease") positiveCorrelation path(MESHD:Death)

SET Support = "In one study, blockade of TREM2 function enhanced disease progression \
in an experimental murine model of autoimmune encephalomyelitis [67]."

SET Species = "10090"
act(p(MGI:Trem2)) negativeCorrelation path(MESHD:"Encephalomyelitis, Autoimmune, Experimental")


SET Support = "In the second study, intravenous application of TREM2-transduced \
myeloid cells limited tissue destruction and facilitated repair in a \
murine model of MS [68]."

SET MeSHDisease = "Multiple Sclerosis"
p(MGI:Trem2, loc(MESHA:"Myeloid Cells")) positiveCorrelation a(MESHA:Tissues)
p(MGI:Trem2, loc(MESHA:"Myeloid Cells")) positiveCorrelation bp(GOBP:"tissue regeneration")
UNSET MeSHDisease
UNSET Species

UNSET Section

SET Section = "6. TREM2 in Alzheimer's Disease"

SET Support = "In a murine model of AD, Frank et al. demonstrated that TREM2 is \
upregulated in microglia found at the border of amyloid plaque \
deposits [69]."

SET MeSHDisease = "Alzheimer Disease"
SET Species = "10090"
a(MESHA:Microglia, loc(CHEBI:"amyloid-beta")) positiveCorrelation p(MGI:Trem2, loc(MESHA:Microglia))
UNSET MeSHDisease

SET Support = "Moreover, TREM2 expression has been positively correlated with the \
phagocytic clearance of beta-amyloid in APP transgenic mice [70]."

p(MGI:Trem2) positiveCorrelation bp(GOBP:"beta-amyloid clearance")
UNSET Species

SET Support = "Given the well-documented role that microglia play in removing beta- \
amyloid [61], the expression of TREM2 by beta-amyloid plaque- \
associated microglia may be interpreted as an effort to enhance beta- \
amyloid clearance and to limit the proinflammatory cytokine expression \
in response to microglia activation by beta-amyloid itself."

a(MESHA:Microglia) -> bp(GOBP:"beta-amyloid clearance")
bp(GOBP:"beta-amyloid clearance") negativeCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
bp(GOBP:"microglial cell activation") positiveCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
a(CHEBI:"amyloid-beta") -> bp(GOBP:"microglial cell activation")

SET Support = "Besides clearing beta-amyloid, TREM2 may also function to remove \
debris and participate in synapse remodeling [71]."

p(HGNC:TREM2) positiveCorrelation bp(GOBP:"synapse organization")

SET Support = "The strongest evidence to date supporting a role for inflammation in \
AD comes from two recent studies demonstrating that TREM2 variants \
increase the risk for AD approximately 3-fold [72, 73]."

p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "These studies indicated that individuals that are heterozygous for \
several TREM2 mutations (the most common variant being a R47H change) \
were at a greater risk for AD."

p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Interestingly, it is only individuals that are heterozygous for TREM2 \
that are at risk for AD: homozygous, autosomal recessive mutations for \
TREM2 result in The Nasu-Hakola disease in some individuals \
characterized by bone cysts and dementia [75], while resulting in \
fronttemporal dementia without bone disease in others [76]."

p(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")
p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")

SET Support = "Given the prophagocytic role of TREM2, loss of phagocytic activity of \
microglia could represent one mechanism by which the TREM2 mutations \
increased the risk to develop AD (Figure 3)."

p(HGNC:TREM2) -> bp(GOBP:phagocytosis)
bp(GOBP:"microglial cell activation") -> bp(GOBP:phagocytosis)


UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2014 Dec;35(12):2681-2690. doi: 10.1016/j.neurobiolaging.2014.06.007. Epub 2014 Jun 17.", "25027412"}

SET Section = "Abstract"

SET Support = "A rare heterozygous TREM2 variant p.R47H (rs75932628) has been \
associated with an increased risk for Alzheimer's disease (AD)."

g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) -> p(HGNC:TREM2, var("p.Arg47His"))
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Spanish AD TREM2 p.R47H carriers showed apraxia (9 of 9) and \
psychiatric symptoms such as personality changes, anxiety, paranoia, \
or fears more frequently than in AD noncarriers (corrected p = 0.039)."

SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:Apraxias)
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(HP:"Personality changes")
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(HP:Anxiety)
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(HP:Paranoia)
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(HP:"fear-related behavior")

UNSET MeSHDisease

SET Support = "Independent voxel-based morphometry analysis of the Spanish set as \
well as conjunction and joint analyses revealed substantial gray \
matter loss in orbitofrontal cortex and anterior cingulate cortex with \
relative preservation of parietal lobes in AD and/or mild cognitive \
impairment TREM2 p.R47H carriers, suggesting that TREM2 p.R47H variant \
is associated with certain clinical and neuroimaging AD features in \
addition to the increased TREM2 p.R47H atrophy in temporal lobes as \
described previously."

SET Assay = "voxel-based morphometry"
p(HGNC:TREM2, var("p.Arg47His")) negativeCorrelation a(MESHA:"Gray Matter")
p(HGNC:TREM2, var("p.Arg47His"), loc(MESHA:"Temporal Lobe")) positiveCorrelation path(MESHD:Atrophy)
UNSET Assay


UNSET Section


##################################################################################

SET Citation = {"PubMed", "Biochem Pharmacol. 2014 Apr 15;88(4):495-8. doi: 10.1016/j.bcp.2013.11.021. Epub 2013 Dec 16.", "24355566"}

SET Section = "Abstract"

SET Support = "Late-onset Alzheimer's disease (AD) is a sporadic disorder with \
increasing prevalence in aging."

path(ADO:"Late_Onset_Alzheimer_s_Disease") positiveCorrelation bp(MESHPP:Aging)

SET Support = "Recently, two groups of investigators independently identified \
variants of the TREM2 gene, encoding triggering receptor expressed on \
myeloid cells 2 as causing increased susceptibility to late onset AD \
with an odds ratio similar to that of ApoEɛ4."

p(HGNC:TREM2, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "Using a novel technology called Direct RNA Sequencing wedetermined the \
quantitative transcriptome of microglia, the principal innate \
neuroimmune cells and confirmed that TREM2 is a major microglia- \
specific gene in the central nervous system."

SET TechnologyUsed = "Direct RNA Sequencing"
g(HGNC:TREM2) positiveCorrelation a(MESHA:Microglia, loc(MESHA:"Central Nervous System"))
UNSET TechnologyUsed

SET Support = "Microglia can be protective and promote phagocytosis, degradation and \
ultimately clearance of Aβ, the pathogenic protein deposited in the \
brains of Alzheimer's patients."

a(MESHA:Microglia) positiveCorrelation bp(GOBP:phagocytosis)
a(MESHA:Microglia) positiveCorrelation deg(a(CHEBI:"amyloid-beta"))
a(MESHA:Microglia) negativeCorrelation a(CHEBI:"amyloid-beta", loc(MESHA:Brain))
a(CHEBI:"amyloid-beta") positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "However, with disease progression, microglia become dysfunctional, \
release neurotoxins, lose their ability to clear Aβ and produce pro- \
inflammatory cytokines that promote Aβ production and accumulation."

SET MeSHAnatomy = "Microglia"
path(MESHD:"Alzheimer Disease") positiveCorrelation cellSecretion(a(CHEBI:neurotoxin))
path(MESHD:"Alzheimer Disease") negativeCorrelation bp(GOBP:"beta-amyloid clearance")
path(MESHD:"Alzheimer Disease") positiveCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
UNSET MeSHAnatomy


SET Support = "TREM2 has been shown to regulate the phagocytic ability of myeloid \
cells and their inflammatory response."

SET MeSHAnatomy = "Myeloid Cells"
p(HGNC:TREM2) regulates bp(GOBP:phagocytosis)
p(HGNC:TREM2) regulates bp(GOBP:"inflammatory response")
UNSET MeSHAnatomy


UNSET Section


##################################################################################

SET Citation = {"PubMed", "Mol Neurobiol. 2013 Aug;48(1):180-5. doi: 10.1007/s12035-013-8424-8. Epub 2013 Feb 14.", "23407992"}

SET Section = "Abstract"

SET Support = "Recent works have demonstrated a rare functional variant (R47H) in \
triggering receptor expressed on myeloid cells (TREM) 2 gene, encoding \
TREM2 protein, increase susceptibility to late-onset Alzheimer's \
disease (AD), with an odds ratio similar to that of the apolipoprotein \
E ε4 allele."

p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "The reduced function of TREM2 was speculated to be the main cause in \
the pathogenic effects of this risk variant, and TREM2 is highly \
expressed in white matter, as well as in the hippocampus and \
neocortex, which is partly consistent with the pathological features \
reported in AD brain, indicating the possible involvement of TREM2 in \
AD pathogenesis."

p(HGNC:TREM2) positiveCorrelation a(MESHA:"White Matter")
p(HGNC:TREM2) positiveCorrelation a(MESHA:Hippocampus)
p(HGNC:TREM2) positiveCorrelation a(MESHA:Neocortex)
p(HGNC:TREM2) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Emerging evidence has demonstrated that TREM2 could suppress \
inflammatory response by repression of microglia-mediated cytokine \
production and secretion, which may prevent inflammation-induced \
bystander damage of neurons."

SET MeSHAnatomy = "Microglia"
p(HGNC:TREM2) -| bp(GOBP:"cytokine production involved in inflammatory response")
bp(GOBP:"cytokine production involved in inflammatory response") -> bp(GOBP:"inflammatory response")
UNSET MeSHAnatomy

SET Support = "TREM2 also participates in the regulation of phagocytic pathways that \
are responsible for the removal of neuronal debris."

p(HGNC:TREM2) -- bp(GOBP:"regulation of phagocytosis")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Am J Hum Genet. 2002 Sep;71(3):656-62. Epub 2002 Jun 21.", "12080485"}

SET Section = "Abstract"

SET Support = "Elsewhere, we have identified PLOSL mutations in TYROBP (DAP12), which \
codes for a membrane receptor component in natural-killer and myeloid \
cells, and also have identified genetic heterogeneity in PLOSL, with \
some patients carrying no mutations in TYROBP"

a(MESHA:"Myeloid Cells") -> p(HGNC:TYROBP)
a(MESHA:"Natural Killer T-Cells") -> p(HGNC:TYROBP)
#PLOSL = Polycystic lipomembranous osteodysplasia with sclerosing leukoencephalopathy = Nasu-Hakola disease
g(HGNC:TYROBP, var("?")) -- path(DO:"Nasu-Hakola disease")

SET Support = "Here we complete the molecular pathology of PLOSL by identifying TREM2 \
as the second PLOSL gene."

g(HGNC:TREM2) positiveCorrelation path(DO:"Nasu-Hakola disease")

SET Support = "TREM2 forms a receptor signaling complex with TYROBP and triggers \
activation of the immune responses in macrophages and dendritic cells."

SET MeSHAnatomy = {"Macrophages", "Dendritic Cells"}
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> bp(GOBP:"activation of immune response")
UNSET MeSHAnatomy


UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Neurochem. 2009 Jul;110(1):284-94. doi: 10.1111/j.1471-4159.2009.06130.x. Epub 2009 Apr 29.", "19457124"}

SET Section = "Abstract"

SET Support = "Triggering receptor expressed in myeloid (TREM) cells 2, a receptor \
expressed by myeloid cells, osteoclasts and microglia, is known to \
play a protective role in bones and brain."

a(MESHA:"Myeloid Cells") -> p(HGNC:TREM2)
a(MESHA:Osteoclasts) -> p(HGNC:TREM2)
a(MESHA:Microglia) -> p(HGNC:TREM2)
SET MeSHAnatomy = {"Bone and Bones", "Brain"}
p(HGNC:TREM2) positiveCorrelation bp(MESHPP:Neuroprotection)
UNSET MeSHAnatomy

SET Support = "Mutations of the receptor (or of its coupling protein, DAP12) sustain \
in fact a genetic disease affecting the two organs, the polycystic \
lipomembraneous osteodysplasia with sclerosing leukoencephalopathy \
(PLOSL or Nasu-Hakola disease)."

g(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")
g(HGNC:TYROBP, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")

SET Support = "By using constructs including the extracellular domain of TREM2 and \
the Fc domain of IgGs we have identified Hsp60 as the only \
TREM2-binding protein exposed at the surface of neuroblastoma N2A \
cells and astrocytes, and lacking in U373 astrocytoma."

#Hsp60 == HSPD1
a(MESHA:Astrocytes) -> p(HGNC:HSPD1)
complex(p(HGNC:TREM2), p(HGNC:HSPD1)) positiveCorrelation a(MESHA:Astrocytes)

SET Support = "Treatment with Hsp60 was found to stimulate the best known \
TREM2-dependent process, phagocytosis, however, only in the microglial \
N9 cells rich in the receptor."

SET MeSHAnatomy = "Microglia"
p(HGNC:HSPD1) -> bp(GOBP:phagocytosis)
UNSET MeSHAnatomy

UNSET Section


##################################################################################
# Bruce working alone
##################################################################################

SET Citation = {"PubMed", "J Exp Med. 2005 Feb 21;201(4):647-57.", "15728241"}

SET PublicationType = "Research"
SET Section = "Abstract"

SET Support = "Elimination of apoptotic neurons without inflammation is crucial for \
brain tissue homeostasis, but the molecular mechanism has not been \
firmly established."

SET MeSHAnatomy = "Brain"
bp(GOBP:"apoptotic cell clearance") negativeCorrelation bp(GOBP:"tissue homeostasis")
UNSET MeSHAnatomy

SET Support = "Here, we show expression of TREM2 in microglia."

a(MESHA:Microglia) -> p(HGNC:TREM2)

SET Support = "TREM2 stimulation induced DAP12 phosphorylation, extracellular signal- \
regulated kinase phosphorylation, and cytoskeleton reorganization and \
increased phagocytosis."

p(HGNC:TREM2) -> p(HGNC:TYROBP, pmod(Ph))
p(HGNC:TREM2) -> bp(GOBP:"activation of protein kinase activity")
p(HGNC:TREM2) -> bp(GOBP:"cytoskeleton organization")
p(HGNC:TREM2) -> bp(GOBP:"phagocytosis")

SET Support = "Knockdown of TREM2 in microglia inhibited phagocytosis of apoptotic \
neurons and increased gene transcription of tumor necrosis factor \
alpha and nitric oxide synthase-2, whereas overexpression of TREM2 \
increased phagocytosis and decreased microglial proinflammatory \
responses."

p(HGNC:TREM2, loc(MESHA:Microglia)) -> bp(GOBP:"apoptotic cell clearance")
p(HGNC:TREM2, loc(MESHA:Microglia)) -| r(HGNC:TNF, loc(MESHA:Microglia))
p(HGNC:TREM2, loc(MESHA:Microglia)) -| r(HGNC:NOS2, loc(MESHA:Microglia))
p(HGNC:TREM2, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:phagocytosis)
p(HGNC:TREM2, loc(MESHA:Microglia)) negativeCorrelation bp(GOBP:"inflammatory response")

UNSET Section

SET Section = "Results"

# All experiments were performed using cells from mice
SET Support = "Gene transcripts for TREM2 and DAP12 were detected in microglia and \
splenocytes but not in neuronal cultures (Fig. 1 A)."

SET Species = "10090"
a(MESHA:Microglia) -> r(MGI:Trem2)
a(MESHA:Leukocytes, loc(MESHA:Spleen)) -> r(MGI:Trem2)
a(MESHA:Microglia) -> r(MGI:Tyrobp)
a(MESHA:Leukocytes, loc(MESHA:Spleen)) -> r(MGI:Tyrobp)
UNSET Species

SET Support = "Expression of TREM2 was detected on isolated microglia identified by \
double labeling with antibodies directed against CD11b (Fig. 1 B)."

SET Species = "10090"
p(MGI:Trem2) positiveCorrelation p(MGI:Itgam)
UNSET Species

SET Support = "Western blotting was performed to confirm that stimulation of fTREM2 \
induces phosphorylation of DAP12."

SET Species = "10090"
act(p(MGI:Trem2)) -> p(MGI:Tyrobp, pmod(Ph))
UNSET Species

SET Support = "Only the chemokine receptor CCR7 was slightly induced in microglia \
after stimulation of TREM2 (Fig. 3 A)."

SET Species = "10090"
act(p(MGI:Trem2)) -> p(MGI:Ccr7, loc(MESHA:Microglia))
UNSET Species

SET Support = "Microglial cells were stimulated via TREM2 and placed in a two-chamber \
system. The number of microglial cells migrating toward the chemokine \
ligands CCL19 and CCL21 was quantified (Fig. 3 B)."

SET Species = "10090"
act(p(MGI:Trem2, loc(MESHA:Microglia))) -> bp(GOBP:"cell chemotaxis")
act(p(MGI:Trem2, loc(MESHA:Microglia))) -> a(MESHA:Microglia, loc(MGI:Ccl19))
act(p(MGI:Trem2, loc(MESHA:Microglia))) -> a(MESHA:Microglia, loc(MGI:Ccl21a))
UNSET Species

SET Support = "One important microglial function is the removal and clearance of \
apoptotic cells and debris."

SET Species = "10090"
a(MESHA:Microglia) -> bp(GOBP:"apoptotic cell clearance")
UNSET Species

SET Support = "Because TREM2 is involved in bone resorption of osteoclasts, we \
speculated that TREM2 molecules of microglia might be involved in \
phagocytosis."

p(HGNC:TREM2) -- a(MESHA:Osteoclasts)

SET Support = "The fTREM2 microglia showed increased phagocytosis of multiple \
microsphere beads after cross-linking of TREM2 compared with microglia \
transduced with the control vector (Fig. 4 A). Furthermore, the \
increased phagocytosis was dependent on stimulation of TREM2 (Fig. 4 \
A)."

SET Species = "10090"
act(p(MGI:Trem2, loc(MESHA:Microglia))) -> bp(GOBP:phagocytosis)

SET Support = "However, cotransduction of fTREM2 and the GFP control vector showed \
up-regulation of microglial phagocytosis by 29.1 ± 11.6% (mean ± SEM) \
after TREM2 stimulation compared with nonstimulated cells (Fig. 4 C)."

p(MGI:Tyrobp, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:phagocytosis)

SET Support = "Phagocytosis requires kinase signaling and reorganization of actin \
cytoskeleton."

bp(GOBP:phagocytosis) positiveCorrelation bp(GOBP:"activation of protein kinase activity")
bp(GOBP:phagocytosis) positiveCorrelation bp(GOBP:"actin cytoskeleton reorganization")

SET Support = "Thus, cross-linking of TREM2 induced polarization and reorganization \
of F-actin in microglia as a first sign of phagocytosis (Fig. 5, A and \
B)."

act(p(MGI:Trem2, loc(MESHA:Microglia))) -> bp(GOBP:"actin cytoskeleton reorganization")

SET Support = "Because TREM2/DAP12 signaling induced ERK phosphorylation in immature \
dendritic cells, we analyzed phosphorylation of ERK after cross- \
linking stimulation of fTREM2 transduced microglia by Western \
blotting."

# They used an ERK antibody that targets Mapk1 and Mapk3
act(complex(p(MGI:Trem2), p(MGI:Tyrobp)), ma(GOBP:signaling)) -> p(MGI:Mapk1, pmod(Ph), loc(MESHA:"Dendritic Cells"))
act(complex(p(MGI:Trem2), p(MGI:Tyrobp)), ma(GOBP:signaling)) -> p(MGI:Mapk3, pmod(Ph), loc(MESHA:"Dendritic Cells"))

SET Support = "Stimulation of TREM2 of microglia induced phosphorylation of ERK as \
demonstrated by a specific antibody recognizing the phosphorylated \
form of ERK (Fig. 5 C)."

act(p(MGI:Trem2, loc(MESHA:Microglia))) -> p(MGI:Mapk1, pmod(Ph))
act(p(MGI:Trem2, loc(MESHA:Microglia))) -> p(MGI:Mapk3, pmod(Ph))

SET Support = "Indeed, increased bead phagocytosis after TREM2 stimulation was \
neutralized by treatment with the ERK inhibitor (Fig. 5 D)."

p(MGI:Mapk1, pmod(Ph)) -> bp(GOBP:phagocytosis)
p(MGI:Mapk3, pmod(Ph)) -> bp(GOBP:phagocytosis)

SET Support = "However, phagocytosis of apoptotic membrane fragments was barely \
detected under the fluorescence microscope after 1 h, when the TREM2 \
receptor was knocked down in microglia (Fig. 8 B)."

p(MGI:Trem2, loc(MESHA:Microglia)) -> bp(GOBP:"apoptotic cell clearance")

SET Support = "Gene transcripts for IL-12p40 were only detected in microglia after \
knockdown of TREM2, but no other samples (not depicted)."

p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Il12b, loc(MESHA:Microglia))

SET Support = "Knockdown of TREM2 in microglia increased the relative level of TNF-α \
from 1 to 1.9 ± 0.3 (mean ± SEM), of IL-1β to 1.3 ± 0.3, and of NOS2 \
to 1.8 ± 0.5 compared with the shControl-transduced microglia."

p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tnf, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Il1b, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Nos2, loc(MESHA:Microglia))

SET Support = "In contrast, overexpression of TREM2 in microglia reduced the relative \
level of inflammatory gene transcripts after 48 h of coculture with \
apoptotic neurons."

p(MGI:Trem2, loc(MESHA:Microglia)) -| bp(GOBP:"inflammatory response")

UNSET Species

UNSET Section
UNSET PublicationType

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2017 May;53:194.e13-194.e22. doi: 10.1016/j.neurobiolaging.2017.01.015. Epub 2017 Jan 20.", "28214109"}

SET Section = "Abstract"

SET Support = "Nasu-hakola disease (NHD) is a rare disease characterized by bone \
cysts and fractures, frontal lobe syndrome, and progressive presenile \
dementia."

path(DO:"Nasu-Hakola disease") positiveCorrelation path(HP:"Bone cyst")
path(DO:"Nasu-Hakola disease") positiveCorrelation path(MESHD:Dementia)

SET Support = "Mutations in TREM2 and TYROBP genes are known to cause NHD."

g(HGNC:TREM2, var("?")) -> path(DO:"Nasu-Hakola disease")
g(HGNC:TYROBP, var("?")) -> path(DO:"Nasu-Hakola disease")

SET Support = "Interestingly, recent evidence-associated rare genetic variants of \
TREM2 gene with increased risk of Alzheimer's disease, frontotemporal \
dementia, amyotrophic lateral sclerosis, and Parkinson's disease."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Amyotrophic Lateral Sclerosis")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Parkinson Disease")

# Rest of abstract focused on one case study so I will not include

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurology. 2005 May 10;64(9):1502-7.", "15883308"}

SET Section = "Abstract"

SET Support = "Polycystic lipomembranous osteodysplasia with sclerosing \
leukoencephalopathy (PLOSL), or Nasu-Hakola disease, is a presenile \
dementia associated with loss of myelin, basal ganglia calcification, \
and bone cysts."

path(DO:"Nasu-Hakola disease") isA path(MESHD:Dementia)
path(DO:"Nasu-Hakola disease") negativeCorrelation bp(GOBP:myelination)
path(DO:"Nasu-Hakola disease") positiveCorrelation path(HP:"Bone cyst")
path(DO:"Nasu-Hakola disease") positiveCorrelation path(HP:"Basal ganglia calcification")

SET Support = "It is caused by recessively inherited mutations in two genes encoding \
subunits of a cell membrane-associated receptor complex: TREM2 and \
DAP12."

g(HGNC:TREM2, var("?")) -> path(DO:"Nasu-Hakola disease")
g(HGNC:TYROBP, var("?")) -> path(DO:"Nasu-Hakola disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2016 Jun;42:132-41. doi: 10.1016/j.neurobiolaging.2016.03.004. Epub 2016 Mar 16.", "27143430"}

SET Section = "Abstract"

SET Support = "Mutations in triggering receptor expressed on myeloid cells 2 (TREM2), \
which has been proposed to regulate the inflammatory responses and the \
clearance of apoptotic neurons and/or amyloid-β, are genetically \
linked to increased risk for late-onset Alzheimer's disease (AD)."

p(HGNC:TREM2, var("?")) regulates bp(GOBP:"inflammatory response")
SET MeSHAnatomy = "Neurons"
p(HGNC:TREM2, var("?")) regulates bp(GOBP:"apoptotic cell clearance")
UNSET MeSHAnatomy
p(HGNC:TREM2, var("?")) regulates bp(GOBP:"beta-amyloid clearance")
g(HGNC:TREM2, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "Interestingly, a missense variant in TREM-like transcript 2 (TREML2), \
a structurally similar protein encoded by the same gene cluster with \
TREM2 on chromosome 6, has been shown to protect against AD."

g(HGNC:TREML2) negativeCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "Here, we show that lipopolysaccharide (LPS) stimulation significantly \
suppressed TREM2 but increased TREML2 expression in mouse brain."

SET Species = "10090"
a(CHEBI:lipopolysaccharide) -| p(MGI:Trem2, loc(MESHA:Brain))
a(CHEBI:lipopolysaccharide) -> p(MGI:Treml2, loc(MESHA:Brain))

SET Support = "Consistent with this in vivo result, LPS or oligomeric amyloid-β \
treatment down regulated TREM2 but up-regulated TREML2 expression in \
primary microglia."

a(CHEBI:lipopolysaccharide) -| p(MGI:Trem2, loc(MESHA:Microglia))
a(CHEBI:lipopolysaccharide) -> p(MGI:Treml2, loc(MESHA:Microglia))
a(CHEBI:"amyloid-beta") -| p(MGI:Trem2, loc(MESHA:Microglia))
a(CHEBI:"amyloid-beta") -> p(MGI:Treml2, loc(MESHA:Microglia))

SET Support = "Most important, modulation of TREM2 or TREML2 levels had opposing \
effects on inflammatory responses with enhancement or suppression of \
LPS-induced proinflammatory cytokine gene expression observed on TREM2 \
or TREML2 down regulation, respectively."

a(CHEBI:lipopolysaccharide) -> bp(GOBP:"inflammatory response")
p(MGI:Trem2) negativeCorrelation bp(GOBP:"inflammatory response")
p(MGI:Treml2) positiveCorrelation bp(GOBP:"inflammatory response")

SET Support = "In addition, the proliferation of primary microglia was significantly \
decreased when TREM2 was down regulated, whereas it was increased on \
TREML2 knockdown."

p(MGI:Trem2, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:"microglial cell proliferation")
p(MGI:Treml2, loc(MESHA:Microglia)) negativeCorrelation bp(GOBP:"microglial cell proliferation")

UNSET Species

SET Section = "Results"

SET Support = "Neuro-inflammation is a prominent feature of AD (Heneka, et al., \
2015)."

SET MeSHAnatomy = "Brain"
path(MESHD:Inflammation) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET MeSHAnatomy

SET Support = "Intriguingly, treatment of primary microglia with LPS significantly \
suppressed the expression of TREM2 but increased the expression of \
TREML2 in a dose-dependent manner (Fig. 1A)."

# Analyzed by RT-PCR
a(CHEBI:lipopolysaccharide) -| r(HGNC:TREM2, loc(MESHA:Microglia))
a(CHEBI:lipopolysaccharide) -> r(HGNC:TREML2, loc(MESHA:Microglia))

SET Support = "To further examine the effect of LPS on TREM2 and TREML2 levels in \
vivo, C57BL/6J mice were intraperitoneally injected with LPS (2 μg/g \
body weight) and the PBS perfused brains were harvested for RNA \
extraction. Consistent with our in vitro results, LPS stimulation \
significantly suppressed TREM2 mRNA levels but promoted TREML2 mRNA \
levels in the brain when compared with saline-treated control groups \
(Fig. 1D)."

a(CHEBI:lipopolysaccharide) -| r(MGI:Trem2, loc(MESHA:Brain))
a(CHEBI:lipopolysaccharide) -> r(MGI:Treml2, loc(MESHA:Brain))

SET Support = "TREM2 has previously been shown to inhibit the production of \
inflammatory cytokines in macrophages (Takahashi, et al., 2005, \
Turnbull, et al., 2006)."

p(HGNC:TREM2, loc(MESHA:Macrophages)) -| bp(GOBP:"macrophage cytokine production")

SET Support = "Interestingly, knockdown of TREM2 significantly increased the \
expression of TREML2 in response to LPS compared to controls, \
suggesting that TREM2 might regulate microglial functions by \
modulating the expression of TREML2 (Fig. 2B)."

SET ChallengeType = "lipopolysaccharide stimulation"
p(HGNC:TREM2, loc(MESHA:Microglia)) reg p(HGNC:TREML2, loc(MESHA:Microglia))
UNSET ChallengeType

SET Support = "We found that the mRNA levels of two pro-inflammatory cytokines, IL-1β \
and TNF-α, were significantly increased in TREM2-knockdown (TREM2-KD) \
microglia treated with 10 ng/ml LPS, although no obvious change was \
observed in the absence of LPS (Fig. 2C and 2D)."

SET ChallengeType = "lipopolysaccharide stimulation"
p(HGNC:TREM2, loc(MESHA:Microglia)) -| r(HGNC:IL1B)
p(HGNC:TREM2, loc(MESHA:Microglia)) -| r(HGNC:TNF)
UNSET ChallengeType

SET Support = "Moreover, the protein levels of IL-1β and TNF-α in the media were \
obviously increased in TREM2-KD microglia compared to those of the \
control group (Fig. 2E and 2F)."

p(HGNC:TREM2, loc(MESHA:Microglia)) -| p(HGNC:IL1B, loc(MESHA:Microglia))
p(HGNC:TREM2, loc(MESHA:Microglia)) -| p(HGNC:TNF, loc(MESHA:Microglia))

SET Support = "Consistent with the results in TREM2-KD microglia, IL-1β and TNF-α \
were dramatically increased in Trem2−/− microglia treated with 10 \
ng/ml LPS (Fig. 3C and 3D)."

SET Species = "10090"
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Il1b, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tnf, loc(MESHA:Microglia))

SET Support = "In addition, knockdown of TREML2 in microglia significantly suppressed \
the mRNA levels of IL-1β and TNF-α in the presence of LPS compared to \
the control group, whereas there was no significant effect in the \
absence of LPS treatment (Fig. 4C and 4D)."

SET ChallengeType = "lipopolysaccharide stimulation"
p(MGI:Treml2, loc(MESHA:Microglia)) -> r(MGI:Il1b, loc(MESHA:Microglia))
p(MGI:Treml2, loc(MESHA:Microglia)) -> r(MGI:Tnf, loc(MESHA:Microglia))
UNSET ChallengeType

SET Support = "The protein levels of IL-1β and TNF-α released from TREML2-KD \
microglia were dramatically decreased compared to the control group \
(Fig. 4E and 4F)."

SET ChallengeType = "lipopolysaccharide stimulation"
p(MGI:Treml2, loc(MESHA:Microglia)) -> p(MGI:Il1b)
p(MGI:Treml2, loc(MESHA:Microglia)) -> p(MGI:Tnf)
UNSET ChallengeType

SET Support = "We found that TREM2 knockdown significantly suppressed microglia \
proliferation (Fig. 5B), whereas TREML2 knockdown increased microglia \
proliferation (Fig. 5D)."

p(MGI:Trem2) -> bp(GOBP:"microglial cell proliferation")
p(MGI:Treml2) -| bp(GOBP:"microglial cell proliferation")

UNSET Species

SET Support = "It has been shown that microglia numbers surrounding the amyloid \
plaques are increased in AD and in the APPV717F AD mouse model \
(Mitrasinovic and Murphy, 2002)."

path(MESHD:"Alzheimer Disease") positiveCorrelation a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))

SET Support = "Aβ is one of the crucial pathological molecules that trigger \
microglial activation in AD."

SET MeSHDisease = "Alzheimer Disease"
a(CHEBI:"amyloid-beta") -> bp(GOBP:"microglial cell activation")
UNSET MeSHDisease

SET Support = "Interestingly, we found that the oligomeric Aβ42, the species that was \
shown to be more toxic to synapses (Nimmrich and Ebert, 2009, Tu, et \
al., 2014), significantly decreased TREM2 mRNA levels but increased \
TREML2 mRNA levels in a dose-dependent manner, whereas Aβ42 peptide \
had no significant effects on their levels (Fig. 6A and 6B)."

p(HGNC:APP, frag("1_42")) -| r(HGNC:TREM2)
p(HGNC:APP, frag("1_42")) -> r(HGNC:TREML2)


UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Biol Chem. 2013 Nov 15;288(46):33027-36. doi: 10.1074/jbc.M113.517540. Epub 2013 Sep 27.", "24078628"}

SET Section = "Abstract"

SET Support = "Triggering receptor expressed on myeloid cells-2 (TREM2) and its \
signaling adaptor protein TYROBP/DAP12 play important roles in signal \
transduction in dendritic cells, osteoclasts, tissue macrophages, and \
microglia."

SET MeSHAnatomy = {"Dendritic Cells", "Macrophages", "Osteoclasts", "Microglia"}
p(HGNC:TREM2) positiveCorrelation bp(GOBP:"signal transduction")
p(HGNC:TYROBP) positiveCorrelation bp(GOBP:"signal transduction")
UNSET MeSHAnatomy

SET Support = "Recently, TREM2 variants have been shown to be linked to late onset \
Alzheimer disease."

p(HGNC:TREM2, var("?")) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "Here, we demonstrate that TREM2 undergoes sequential proteolytic \
processing by ectodomain shedding and intramembrane proteolysis."

p(HGNC:TREM2) positiveCorrelation bp(GOBP:"membrane protein ectodomain proteolysis")
p(HGNC:TREM2) positiveCorrelation bp(GOBP:"membrane protein intracellular domain proteolysis")

SET Support = "The C-terminal fragment (CTF) of TREM2 generated by ectodomain \
shedding is cleaved by γ-secretase."

# p(HGNC:TREM, frag("?_*")) = CTF fragment of TREM2
bp(GOBP:"membrane protein ectodomain proteolysis") -> p(HGNC:TREM2, frag("?_*"))
act(complex(SCOMP:"gamma Secretase Complex")) =| p(HGNC:TREM2, frag("?_*"))

SET Support = "Thus, the accumulated TREM2 CTF thereby might limit the interaction of \
DAP12 with the functional full-length receptor, resulting in decreased \
DAP12 phosphorylation and impaired metabolism of phosphatidylinositol \
4,5-bisphosphate."

p(HGNC:TREM2, frag("?_*")) negativeCorrelation p(HGNC:TYROBP, pmod(Ph))
p(HGNC:TREM2, frag("?_*")) negativeCorrelation bp(GOBP:"phosphatidylinositol metabolic process")

UNSET Section

SET Section = "Results"

SET Support = "TREM2 has been identified as a novel strong risk factor for LOAD, but \
it is unknown how TREM2 contributes to the disease (10, 11)."

path(ADO:"Late_Onset_Alzheimer_s_Disease") positiveCorrelation p(HGNC:TREM2)

SET Support = "As members of the matrix metalloprotease (MMP) family or the A \
Disintegrin And Metalloprotease (ADAM) family are major proteases \
involved in the cleavage of cell surface proteins, COS7 cells were \
incubated in absence or presence of batimastat, a well known inhibitor \
of proteases of both families (19, 20)."

p(SFAM:"MMP Family") -> bp(GOBP:"membrane protein ectodomain proteolysis")
# batimastat and marimastat work the same way
a(CHEBI:marimastat) =| act(p(SFAM:"MMP Family"))

SET Support = "Interestingly, treatment with batimastat resulted in a ∼3-fold \
increase of cell surface-localized TREM2 (313.4 ± 18.8% versus 100 ± \
10.7%; Fig. 1A)."

a(CHEBI:marimastat) -> p(HGNC:TREM2, loc(MESHA:"Cell Membrane"))

SET Support = "Western immunoblotting further confirmed the inhibition of TREM2 \
ectodomain shedding by batimastat, as the treatment increased the \
levels of full-length TREM2 (TREM2 FL) in the membrane fraction and \
decreased the amount of the secreted TREM2 ectodomain variants (TREM2 \
ECD; 14.6 ± 2.4% versus 100 ± 14.6%; Fig. 1B)."

a(CHEBI:marimastat) -| bp(GOBP:"membrane protein ectodomain proteolysis")

SET Support = "Consistent with decreased ectodomain shedding, levels of the TREM2 CTF \
were decreased upon incubation with batimastat (17.2 ± 0.5% versus 100 \
± 10.6%; Fig. 1B)."

a(CHEBI:marimastat) -| p(HGNC:TREM2, frag("?_*"))

SET Support = "Interestingly, the pharmacological inhibition of γ-secretase with DAPT \
led to a significant increase of TREM2 CTFs (239.6 ± 10.8% versus 100 \
± 18.2%; Fig. 2A)."

act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:"membrane protein ectodomain proteolysis")) =| p(HGNC:TREM2, frag("?_*"))
a(CHEBI:DAPT) =| act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:"membrane protein ectodomain proteolysis"))

SET Support = "The pulldown of biotinylated proteins and subsequent detection of \
TREM2 FL or its CTF demonstrated a strong accumulation of the TREM2 \
CTF and a less pronounced increase of TREM2 FL at the cell surface \
upon γ-secretase inhibition (Fig. 3A)."

act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:"membrane protein ectodomain proteolysis")) =| p(HGNC:TREM2, frag("?_*"), loc(MESHA:"Cell Membrane"))

SET Support = "Cell treatment with DAPT increased the surface levels of TREM2 CTFs \
(319.2 ± 40.8% versus 100 ± 10.32%)."

a(CHEBI:DAPT) -> p(HGNC:TREM2, frag("?_*"), loc(MESHA:"Cell Membrane"))

SET Support = "Upon γ-secretase inhibition, the co-localization between TREM2 CTF and \
DAP12 increased and changed from punctuated pattern to larger, spiry \
structures that covered substantial areas of the plasma membrane \
(0.671 ± 0.0029 versus 0.458 ± 0.0034; Fig. 4A). Thus, accumulated \
TREM2 CTFs upon γ-secretase inhibition induced a redistribution of its \
interaction partner DAP12."

complex(SCOMP:"gamma Secretase Complex") -- complex(p(HGNC:TYROBP), p(HGNC:TREM2))

SET Support = "32P radiolabeling strongly increased after the inhibition of tyrosine \
phosphatases by orthovanadate, indicating efficient phosphorylation \
and rapid dephosphorylation of DAP12 upon activation of TREM2 (Fig. \
4B)."

act(p(HGNC:TREM2)) -> p(HGNC:TYROBP, pmod(Ph))

SET Support = "Interestingly, the inhibition of γ-secretase led to a complete \
suppression of phosphate incorporation into the lower migrating \
variant of DAP12, even in the presence of orthovanadate (Fig. 4B), \
demonstrating impaired signaling of TREM2 to its adaptor protein \
DAP12."

act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:"membrane protein ectodomain proteolysis")) -> act(p(HGNC:TREM2))
act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:"membrane protein ectodomain proteolysis")) -> p(HGNC:TYROBP, pmod(Ph))

SET Support = "DAP12 has been previously linked to the activation of PLCγ, which \
hydrolyzes PIP2 to inositol 1,4,5-trisphosphate and diacylglycerol (7, \
26)."

p(HGNC:TYROBP) -> act(p(SFAM:"PLCG Family"))
p(SFAM:"PLCG Family") => a(CHEBI:"1D-myo-inositol 1,4,5-trisphosphate")
p(SFAM:"PLCG Family") => a(CHEBI:"diglyceride")
p(SFAM:"PLCG Family") => deg(a(CHEBI:"1-phosphatidyl-1D-myo-inositol 4,5-bisphosphate"))

SET Support = "In contrast, the fluorescence intensity in cells incubated without \
DAPT steadily decreased after application of antibody (Fig. 5A, red \
line), indicating a reduction of PIP2 levels."

act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:"membrane protein ectodomain proteolysis")) -| a(CHEBI:"1-phosphatidyl-1D-myo-inositol 4,5-bisphosphate")


UNSET Section

##################################################################################

SET Citation = {"PubMed", "JAMA Neurol. 2014 Apr;71(4):449-53. doi: 10.1001/jamaneurol.2013.6237.", "24535663"}

SET Section = "Abstract"

SET Support = "Amyotrophic lateral sclerosis (ALS) is a fatal neurodegenerative \
disease in which microglia play a significant and active role."

path(MESHD:"Amyotrophic Lateral Sclerosis") isA path(MESHD:"Neurodegenerative Diseases")
path(MESHD:"Amyotrophic Lateral Sclerosis") -- a(MESHA:Microglia)

SET Support = "Recently, a rare missense variant (p.R47H) in the microglial \
activating gene TREM2 was found to increase the risk of several \
neurodegenerative diseases, including Alzheimer disease."

g(HGNC:TREM2, var("p.Arg47His"), loc(MESHA:Microglia)) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("p.Arg47His"), loc(MESHA:Microglia)) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")

SET Support = "The TREM2 variant p.R47H was more common in patients with ALS than in \
the controls and is therefore a significant risk factor for ALS (odds \
ratio, 2.40; 95% CI, 1.29-4.15; P = 4.1×10-3)."

g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Amyotrophic Lateral Sclerosis")

SET Support = "Furthermore, TREM2 expression was increased in spinal cord samples \
from ALS patients and SOD1G93A mice (P = 2.8×10-4 and P = 2.8×10-9, \
respectively), confirming dysregulated TREM2 in disease."

SET Species = "9606"
p(HGNC:TREM2, loc(MESHA:"Spinal Cord")) positiveCorrelation path(MESHD:"Amyotrophic Lateral Sclerosis")
UNSET Species
SET Species = "10090"
SET MouseStrain = "SOD1G93A"
p(MGI:Trem2, loc(MESHA:"Spinal Cord")) positiveCorrelation path(MESHA:"Spinal Cord")
UNSET Species
UNSET MouseStrain

SET Support = "Expression of TREM2 in the human spinal cord was negatively correlated \
with survival (P = .04) but not with other phenotypic aspects of \
disease."

SET Species = "9606"
p(HGNC:TREM2, loc(MESHA:"Spinal Cord")) positiveCorrelation path(MESHD:Death)
UNSET Species

UNSET Section

SET Section = "Results"

SET Support = "1.09% (10/920) of sporadic ALS subjects and 0.162% (3/1848) of normal \
controls were heterozygous carriers of the p.R47H variant, showing a \
significant enrichment in ALS (OR=6.77; 95% CI 1.86-24.65; p=0.0016)."

g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Amyotrophic Lateral Sclerosis")

SET Support = "We examined spinal cord expression of TREM2 in lumbar spinal cord \
sections from 18 subjects with ALS and found a 2.8-fold upregulation \
compared to controls (p=2.8×10-4; Figure panel A)."

p(HGNC:TREM2, loc(MESHA:Spine)) positiveCorrelation path(MESHD:"Amyotrophic Lateral Sclerosis")

SET Support = "Because markers of microglial activation are also upregulated in \
models of SOD1 ALS18, we evaluated TREM2 expression in SOD1G93A \
transgenic mice and found a 13-fold increase compared to non- \
transgenic littermates (p=2.8×10-9; Figure panel B)."

SET Species = "10090"
SET MouseStrain = "SOD1G93A"
p(HGNC:TREM2, loc(MESHA:Spine)) positiveCorrelation path(MESHD:"Amyotrophic Lateral Sclerosis")
UNSET MouseStrain
UNSET Species


UNSET Section

##################################################################################

SET Citation = {"PubMed", "EMBO Rep. 2017 Jul;18(7):1186-1198. doi: 10.15252/embr.201743922. Epub 2017 May 8.", "28483841"}

SET Section = "Abstract"

SET Support = "Sequence variations in the triggering receptor expressed on myeloid \
cells 2 (TREM2) have been linked to an increased risk for \
neurodegenerative disorders such as Alzheimer's disease and \
frontotemporal lobar degeneration."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Lobar Degeneration")

SET Support = "In the brain, TREM2 is predominantly expressed in microglia."

a(MESHA:Microglia, loc(MESHA:Brain)) -> p(HGNC:TREM2)

SET Support = "Several disease-associated TREM2 variants result in a loss of function \
by reducing microglial phagocytosis, impairing lipid sensing, \
preventing binding of lipoproteins and affecting shielding of amyloid \
plaques."

SET MeSHAnatomy = "Microglia"
g(HGNC:TREM2, var("?")) -| bp(GOBP:phagocytosis)
UNSET MeSHAnatomy
g(HGNC:TREM2, var("?")) -| complex(p(HGNC:TREM2), a(CHEBI:lipoprotein))
g(HGNC:TREM2, var("?")) -| a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))

SET Support = "Functional analyses confirm that loss of TREM2 impairs appropriate \
microglial responses to injury and signals that normally evoke \
chemotaxis on multiple levels."

SET MeSHAnatomy = "Microglia"
p(MGI:Trem2) -> bp(GOBP:"response to wounding")
p(MGI:Trem2) -> bp(GOBP:"cell chemotaxis")
UNSET MeSHAnatomy

SET Support = "In an ex vivo organotypic brain slice assay, absence of TREM2 reduces \
the distance migrated by microglia."

SET Assay = {"organotypic brain slice assay", "ex vivo"}
p(MGI:Trem2) -> bp(GOBP:"microglial cell migration")
UNSET Assay

SET Support = "Moreover, migration towards defined chemo-attractants is reduced upon \
ablation of TREM2 and can be rescued by TREM2 re-expression."

SET MeSHAnatomy = "Microglia"
p(MGI:Trem2) -> bp(GOBP:"positive chemotaxis")
UNSET MeSHAnatomy

SET Support = "In vivo, microglia lacking TREM2 migrate less towards injected \
apoptotic neurons, and outgrowth of microglial processes towards sites \
of laser-induced focal CNS damage in the somatosensory cortex is \
slowed."

SET Assay = "in vivo"
p(MGI:Trem2) -> bp(GOBP:"microglial cell migration")
UNSET Assay

UNSET Section

SET Section = "Results"
# All work was done using microglia isolated from mice

SET Support = "Within the set of 482 genes analysed, more than half them were \
unchanged, 88 were upregulated and 34 were downregulated (Dataset \
EV1)."

# Dataset was analyzed by Bruce
SET Species = "10090"
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Rab6b, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Slc7a8, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Kcnd1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Ldlrad4, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Mecp2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Lrrc3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Limk1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Nrip1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Stab1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Inpp4b, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Pmepa1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Nuak1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Cmklr1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Gas6, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Cxxc5, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Etv5, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Garnl3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Slco4a1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Mef2c, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Basp1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tanc2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Il10ra, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Ak1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Mertk, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Hist1h2ab, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Crybb1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Eng, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Rapgef5, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Rap1gds1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Gbgt1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Rgmb, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Fgd2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:P2ry6, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Khdrbs3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Gab1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Sema4d, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tspan7, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Ptgs1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Sesn1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tjp1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Spata13, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Ptms, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Frmd4a, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Extl3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Plxdc2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Fscn1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Sparc, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tnfrsf11a, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Icam1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tgfb1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tm9sf4, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Timp2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Sall3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Dst, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Inpp5d, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Lair1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tgfbr2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Abcc3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Csf3r, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Epb41l2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Cx3cr1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Abi3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Usp2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Lrrc8a, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Bin1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Capn3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Pros1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Atp8a2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Golm1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Gtf2h2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Rasal3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Bach1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Abi1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:F11r, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Eps15l1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Fads1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Pla2g15, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Cables1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Ckb, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Pik3r4, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tmem119, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Abhd12, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Olfml3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tgfbr1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Sall1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:St3gal6, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:P2ry12, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tmem100, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Cbr1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Pgrmc1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Cxcl16, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Ly96, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Csf1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Rasgef1b, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Cd83, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Ptprm, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Spry1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Ptgfrn, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Camk2n1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Apoe, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Nos2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:4933406P04Rik, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Adcyap1r1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Vegfa, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Mrgpra4, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Spp1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Olfr920, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Ttr, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Cntn1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Il1b, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Ccl4, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Trem2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Itgax, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Olfr110, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Tnf, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Cpne2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Cbr3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Ccl3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Lgals3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Ccl2, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Atf3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -> r(MGI:Clec7a, loc(MESHA:Microglia))

SET Support = "Literature searches pointed out that specifically, the downregulated \
genes Ccl2, Il1b, Tnf and Spp1 of the category cell motility (Table 1) \
are putative direct targets of Trem2 20, 32, 33."

bp(GOBP:"cell motility") positiveCorrelation p(MGI:Ccl2)
bp(GOBP:"cell motility") positiveCorrelation p(MGI:Il1b)
bp(GOBP:"cell motility") positiveCorrelation p(MGI:Tnf)
bp(GOBP:"cell motility") positiveCorrelation p(MGI:Spp1)
p(MGI:Ccl2) positiveCorrelation complex(p(MGI:Trem2), p(MGI:Ccl2))
p(MGI:Il1b) positiveCorrelation complex(p(MGI:Trem2), p(MGI:Il1b))
p(MGI:Tnf) positiveCorrelation complex(p(MGI:Trem2), p(MGI:Tnf))
p(MGI:Spp1) positiveCorrelation complex(p(MGI:Trem2), p(MGI:Spp1))

SET Support = "Significantly downregulated genes (e.g. Il1b, Vegfa) involved in \
chemotaxis and migration are highlighted in Fig EV1."

bp(GOBP:"cell chemotaxis") positiveCorrelation p(MGI:Il1b)
bp(GOBP:"cell chemotaxis") positiveCorrelation p(MGI:Vegfa)

SET Support = "Microglia were visualized using Cx3cr1‐GFP 36 as a microglia reporter \
as well as staining of CD68 ​(Fig2A) and analysed after 7 days in \
vitro (DIV). Baseline migration of CD68‐positive cells was observed, \
when slices from young WT mice were cultured alone, and was \
significantly reduced when brain slices were prepared from young \
Trem2−/− mice (Fig2B; for quantification see Fig ​Fig2D)."

SET MouseStrain = "APPPS1"
p(MGI:Trem2) positiveCorrelation bp(GOBP:"microglial cell migration")
UNSET MouseStrain

SET Support = "We therefore tested whether inhibition of Syk activation in the N9 \
microglia cell line 39 might affect their response to chemokines in a \
transwell assay. Indeed, migration was almost entirely blocked when N9 \
microglia were pre‐incubated with the general tyrosine kinase \
inhibitor genistein or the Syk‐selective inhibitor piceatannol \
​(Fig3A)."

SET CustomCellLine = "N9 cell"
a(CHEBI:genistein) negativeCorrelation bp(GOBP:"microglial cell migration")
a(CHEBI:piceatannol) negativeCorrelation bp(GOBP:"microglial cell migration")

SET Support = "Using either CCL2 or C5a as chemotactic stimulus, we observed a \
significantly decreased migration of Trem2‐deficient N9 microglia \
towards both chemotactic stimuli ​(Fig3B and C)."

p(MGI:Trem2) positiveCorrelation bp(GOBP:"microglial cell migration")
p(MGI:Trem2) positiveCorrelation a(MESHA:Microglia, loc(MGI:Ccl2))
UNSET CustomCellLine

SET Support = "Apoptotic neurons are an important hallmark of neurodegeneration, and \
they are known to release “find‐me” signals to attract phagocytes 42, \
43."

SET MeSHAnatomy = "Brain"
a(MESHA:Phagocytes) positiveCorrelation bp(GOBP:"apoptotic cell clearance")
UNSET MeSHAnatomy

SET Support = "WT microglia were attracted to and accumulated around the injected \
dead neurons as early as 6 h postinjection ​(Fig4A)."

SET MeSHAnatomy = "Brain"
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"apoptotic cell clearance")
UNSET MeSHAnatomy

SET Support = "In contrast, we observed a significant reduction in the number of \
clustered microglia around apoptotic neurons in the absence of Trem2 \
(Fig4B for quantification see Fig4C)."

p(MGI:Trem2) -> a(MESHA:Microglia, loc(MESHA:Neurons))
SET MeSHAnatomy = "Brain"
p(MGI:Trem2) positiveCorrelation bp(GOBP:"apoptotic cell clearance")
UNSET MeSHAnatomy

SET Support = "This revealed a significant delay in the response of Trem2−/− \
microglia close to the injury (immediate area) but not further away \
(intermediate and distant areas; Fig5B; for quantification Fig5C) \
confirming that the ability of Trem2‐deficient microglia to react to \
nervous system damage is reduced in vivo."

SET StudyType = "in vivo"
p(MGI:Trem2, loc(MESHA:Microglia)) -> a(MESHA:Microglia, loc(MESHA:"Central Nervous System"))
p(MGI:Trem2, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:"immune response in brain or nervous system")
UNSET StudyType

SET Support = "In mouse models for AD and ALS, many genes typically expressed under \
physiological conditions such as Csf1r, Cx3cr1, Tmem119, Tgfbr1, \
P2ry12 or Il10ra are selectively downregulated 49, 50."

path(MESHD:"Alzheimer Disease") negativeCorrelation r(MGI:Csf1r)
path(MESHD:"Alzheimer Disease") negativeCorrelation r(MGI:Cx3cr1)
path(MESHD:"Alzheimer Disease") negativeCorrelation r(MGI:Tmem119)
path(MESHD:"Alzheimer Disease") negativeCorrelation r(MGI:Tgfbr1)
path(MESHD:"Alzheimer Disease") negativeCorrelation r(MGI:P2ry12)
path(MESHD:"Alzheimer Disease") negativeCorrelation r(MGI:Il10ra)
path(MESHD:"Amyotrophic Lateral Sclerosis") negativeCorrelation r(MGI:Csf1r)
path(MESHD:"Amyotrophic Lateral Sclerosis") negativeCorrelation r(MGI:Cx3cr1)
path(MESHD:"Amyotrophic Lateral Sclerosis") negativeCorrelation r(MGI:Tmem119)
path(MESHD:"Amyotrophic Lateral Sclerosis") negativeCorrelation r(MGI:Tgfbr1)
path(MESHD:"Amyotrophic Lateral Sclerosis") negativeCorrelation r(MGI:P2ry12)
path(MESHD:"Amyotrophic Lateral Sclerosis") negativeCorrelation r(MGI:Il10ra)

SET Support = "This revealed that expression of homoeostatic genes was not suppressed \
but rather fully maintained and even slightly but significantly \
increased (Fig6B)."

# Figure was analyzed
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Bin1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Cx3cr1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Il10ra, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Mertk, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Olfml3, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:P2ry12, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Sall1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tgfbr1, loc(MESHA:Microglia))
p(MGI:Trem2, loc(MESHA:Microglia)) -| r(MGI:Tmem119, loc(MESHA:Microglia))

UNSET Species

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Clin Epigenetics. 2016 Apr 5;8:37. doi: 10.1186/s13148-016-0202-9. eCollection 2016.", "27051467"}

SET Section = "Abstract"

SET Support = "Recent genome-wide association studies revealed TREM2 rs75932628-T \
variant to be associated with Alzheimer's disease (AD) and other \
neurodegenerative diseases."

SET StudyType = "GWAS"
g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")
UNSET StudyType

# All results were derived from hippocampus (stated in another, unlisted sentence in abstract)
SET Support = "A 3.4-fold increase in TREM2 mRNA levels was observed in the \
hippocampus of AD cases compared to controls (p = 1.1E-05)."

r(HGNC:TREM2, loc(MESHA:Hippocampus)) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Interestingly, TREM2 methylation was higher in AD cases compared to \
controls (76.2 % ± 15.5 versus 57.9 % ± 17.1; p = 0.0016)."

SET MeSHDisease = "Alzheimer Disease"
bp(GOBP:"DNA methylation") positiveCorrelation g(HGNC:TREM2)
UNSET MeSHDisease

# Next evidence also occurs in AD hippocampus, that is why I did nto UNSET previous MeSHDisease
SET Support = "Moreover, TREM2 mRNA levels in the AD hippocampus correlated with \
enrichment in 5hmC at the TREM2 gene body (r = 0.771; p = 0.005)."

SET MeSHDisease = "Alzheimer Disease"
r(HGNC:TREM2, loc(MESHA:Hippocampus)) positiveCorrelation a(CHEBI:"5-(hydroxymethyl)cytosine")
UNSET MeSHDisease

UNSET Section

SET Section = "Results"

SET Support = "A 3.4-fold increase in TREM2 mRNA levels was observed in the \
hippocampus of AD cases compared to controls (mean ± SD mRNA levels in \
AD versus controls: 6.65 ± 4.30 % versus 1.73 ± 1.24 %; p = 1.1E-05) \
(Fig. 1a)."

r(HGNC:TREM2, loc(MESHA:Hippocampus)) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Interestingly, a significant association between APS and TREM2 mRNA \
levels was found in the human hippocampus (r = 0.593, p = 1.4E-04)."

# APS = amyloid plaque score
r(HGNC:TREM2, loc(MESHA:Hippocampus)) positiveCorrelation path(MESHD:"Plaque, Amyloid")
r(HGNC:TREM2, loc(MESHA:Hippocampus)) positiveCorrelation a(CHEBI:"amyloid-beta", loc(MESHA:Hippocampus))

SET Support = "Indeed, the average area of p-tau deposition, which included \
neurofibrillary tangles, neuropil threads, and neuritic plaques, was \
positively correlated to TREM2 mRNA levels (r = 0.509, p = 0.002)."

r(HGNC:TREM2, loc(MESHA:Hippocampus)) positiveCorrelation p(HGNC:MAPT, pmod(Ph), loc(MESHA:Hippocampus))
r(HGNC:TREM2, loc(MESHA:Hippocampus)) positiveCorrelation a(MESHCS:"Neurofibrillary Tangles", loc(MESHA:Hippocampus))

SET Support = "Following that approach, a significant increase in DNA methylation at \
the TSS-associated region of TREM2 gene was observed in the AD cases \
group compared to the control group (76.2 ± 15.5 % versus 57.9± 17.1 \
%; p = 0.0016) (Fig. 2b)."

SET MeSHDisease = "Alzheimer Disease"
bp(GOBP:"DNA methylation") positiveCorrelation g(HGNC:TREM2)
UNSET MeSHDisease

SET Support = "Noticeably, amplicon covering exon 2 included the rs75932628-T variant \
associated with AD in GWAS (Fig. 3a)."

g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Interestingly, a significant positive correlation was found between \
5hmC enrichment in exon 2 and TREM2 mRNA levels (r = 0.669; p = 0.005) \
(Fig. 3c) for the whole set of samples."

r(HGNC:TREM2, loc(MESHA:Hippocampus)) positiveCorrelation a(CHEBI:"5-(hydroxymethyl)cytosine")


UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Neurosci. 2015 Apr 22;35(16):6532-43. doi: 10.1523/JNEUROSCI.4586-14.2015.", "25904803"}

SET Section = "Abstract"

SET Support = "Alzheimer's disease (AD) is characterized by a robust inflammatory \
response elicited by the accumulation and subsequent deposition of \
amyloid (Aβ) within the brain."

SET MeSHAnatomy = "Brain"
a(CHEBI:"amyloid-beta") -> bp(GOBP:"inflammatory response")
bp(GOBP:"inflammatory response") biomarkerFor path(MESHD:"Alzheimer Disease")
UNSET MeSHAnatomy

SET Support = "Treatment of murine models of AD with agonists of the nuclear \
receptors PPARγ, PPARδ, LXR, and RXR stimulated microglial \
phagocytosis in vitro and rapidly induced the expression of the \
phagocytic receptors Axl and MerTK."

# LXR = NR1H3, PPARγ = Pparg, PPARδ = Ppard, and RXR = Rxra
SET Species = "10090"
SET Assay = "in vitro"
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Microglia"
act(p(MGI:Pparg)) -> bp(GOBP:phagocytosis)
act(p(MGI:Nr1h3)) -> bp(GOBP:phagocytosis)
act(p(MGI:Ppard)) -> bp(GOBP:phagocytosis)
act(p(MGI:Rxra)) -> bp(GOBP:phagocytosis)
UNSET MeSHAnatomy
act(p(MGI:Pparg)) -> p(MGI:Axl)
act(p(MGI:Nr1h3)) -> p(MGI:Axl)
act(p(MGI:Ppard)) -> p(MGI:Axl)
act(p(MGI:Rxra)) -> p(MGI:Axl)
act(p(MGI:Pparg)) -> p(MGI:Mertk)
act(p(MGI:Nr1h3)) -> p(MGI:Mertk)
act(p(MGI:Ppard)) -> p(MGI:Mertk)
act(p(MGI:Rxra)) -> p(MGI:Mertk)
UNSET Assay

SET Support = "In murine models of AD, we found that plaque-associated macrophages \
expressed Axl and MerTK and treatment of the cells with an RXR agonist \
further induced their expression, coincident with the rapid reduction \
in plaque burden."

a(MESHA:Macrophages, loc(CHEBI:"amyloid-beta")) -> p(MGI:Mertk)
a(MESHA:Macrophages, loc(CHEBI:"amyloid-beta")) -> p(MGI:Axl)
act(p(MGI:Rxra, loc(MESHA:Macrophages))) -> p(MGI:Mertk)
act(p(MGI:Rxra, loc(MESHA:Macrophages))) -> p(MGI:Axl)
act(p(MGI:Rxra, loc(MESHA:Macrophages))) positiveCorrelation bp(GOBP:"beta-amyloid clearance")

SET Support = "Further characterization of MerTK(+)/Axl(+) macrophages revealed that \
they also expressed the phagocytic receptor TREM2 and high levels of \
CD45, consistent with a peripheral origin of these cells."

a(MESHA:Macrophages) -> p(MGI:Trem2)
# Ptprc = CD45 in mice
a(MESHA:Macrophages) -> p(MGI:Ptprc)

SET Support = "Importantly, in an ex vivo slice assay, nuclear receptor agonist \
treatment reversed the AD-related suppression of phagocytosis through \
a MerTK-dependent mechanism."

SET Assay = {"ex vivo", "brain slice assay"}
# Checked paper and this was the agonist tested
act(p(MGI:Rxra)) -> bp(GOBP:phagocytosis)
UNSET Assay

UNSET MeSHDisease
UNSET Species

UNSET Section

SET Section = "Results"

SET Support = "We treated the immortalized N9 microglial cell line with vehicle, the \
RXR agonist bexarotene (bex), the PPARγ agonist pioglitazone (pio), \
the LXR agonist GW3965, or the PPARδ agonist GW0742 for 18 h before \
testing the cells' phagocytic capacity with polystyrene beads (Fig. \
1B). The nuclear receptor agonists increased basal levels of \
phagocytosis (Fig. 1B)."

SET Species = "10090"
SET CustomCellLine = "N9 cell"
SET TimePoint = "18 hours post-stimulation"
a(CHEBI:bexarotene) -> bp(GOBP:phagocytosis)
a(CHEBI:pioglitazone) -> bp(GOBP:phagocytosis)
a(CC:GW3965) -> bp(GOBP:phagocytosis)
a(MESHC:GW0742) -> bp(GOBP:phagocytosis)
UNSET TimePoint

SET Support = "As has been previously reported, incubation with fibrillar amyloid \
(Paresce et al., 1996; Bamberger et al., 2003; Reed-Geaghan et al., \
2009; Stewart et al., 2010) also stimulated N9 cell phagocytosis (Fig. \
1A,C)."

a(CHEBI:"amyloid fibril") -> bp(GOBP:phagocytosis)

SET Support = "Furthermore, pretreatment of N9 microglia with nuclear receptor \
agonists increased their phagocytic response to fibrillar amyloid \
(Fig. 1A,C) and the TAM receptor ligand Gas6, an adaptor molecule that \
recognizes apoptotic cells (Fig. 1D)."

p(MGI:Gas6) -> bp(GOBP:phagocytosis)

SET Support = "We treated cultured N9 microglia with the RXR agonist bexarotene for \
18 h and found that treated microglia expressed higher levels of MerTK \
and Axl mRNA (Fig. 2A,B) than naive microglia."

SET TimePoint = "18 hours post-stimulation"
a(CHEBI:bexarotene) -> r(MGI:Mertk)
a(CHEBI:bexarotene) -> r(MGI:Axl)
UNSET TimePoint

SET Support = "Treating microglia with the proinflammatory polarizing agent LPS \
reduced MerTK mRNA levels, and pretreatment with bexarotene was able \
to prevent this reduction (Fig. 2A)."

a(CHEBI:lipopolysaccharide) -| r(MGI:Mertk)
a(CHEBI:bexarotene) -| a(CHEBI:lipopolysaccharide)

SET Support = "Interestingly, LPS treatment did not affect Axl mRNA levels, although \
LPS treatment reversed nuclear receptor mediated increases in Axl mRNA \
expression (Fig. 2B)."

a(CHEBI:lipopolysaccharide) -| r(MGI:Axl)

SET Support = "Microglia treated with either bexarotene or GW3965 showed increased \
levels of MerTK protein expression (Fig. 2C,D). Furthermore, N9 \
microglia treated with bexarotene, GW3965, the PPARγ agonist \
pioglitazone, or the PPARδ agonist GW0742 also showed increased levels \
of MerTK protein (Fig. 2E,F)."

UNSET CustomCellLine
a(CHEBI:bexarotene) -> p(MGI:Mertk)
a(CC:GW3965) -> p(MGI:Mertk)
SET CustomCellLine = "N9 cell"
a(CHEBI:bexarotene) -> p(MGI:Mertk)
a(CHEBI:pioglitazone) -> p(MGI:Mertk)
a(CC:GW3965) -> p(MGI:Mertk)
a(MESHC:GW0742) -> p(MGI:Mertk)
UNSET CustomCellLine

SET Support = "We next investigated the ability of bexarotene to regulate MerTK \
levels in vivo. Interestingly, mRNA levels of MerTK are significantly \
lower in microglia isolated from 12-month-old APP/PS1Δe9 mice than \
control mice (Fig. 3A)."

SET MouseStrain = "APP/PS1dE9"
SET Assay = "in vivo"
SET AgeRange = "12 months"
r(MGI:Mertk) negativeCorrelation a(MESHA:Microglia)

SET Support = "To determine whether bexarotene could regulate MerTK levels in vivo, \
we treated 12-month-old APP/PS1Δe9 mice for 7 d with 100 mg/kg/d \
bexarotene or vehicle by oral gavage. Indeed, MerTK mRNA levels were \
returned to nontransgenic levels after 7 d of bexarotene treatment \
(Fig. 3A). Interestingly, Axl expression was higher in microglia \
isolated from APP/PS1Δe9 mice than nontransgenic mice and was further \
increased upon 7 d of bexarotene treatment (Fig. 3A). Furthermore, the \
levels of MerTK in combined cortical and hippocampal homogenates were \
measured by Western analysis. Animals treated with bexarotene had \
significantly higher levels of MerTK than vehicle-treated control \
animals (Fig. 3B,C)."

r(MGI:Axl) positiveCorrelation a(MESHA:Microglia)
SET TimePoint = "7 days post-treatment"
a(CHEBI:bexarotene) -> r(MGI:Mertk)
a(CHEBI:bexarotene) -> r(MGI:Axl)
a(CHEBI:bexarotene) -> p(MGI:Mertk)
UNSET TimePoint
UNSET MouseStrain

SET Support = "However, in both APP/PS1Δe9 and 5XFAD animals, MerTK colocalized with \
plaque-associated myeloid cells (Fig. 4A,B,E–G)."

SET MouseStrain = {"APP/PS1dE9", "5XFAD"}
p(MGI:Mertk) positiveCorrelation a(MESHA:"Myeloid Cells", loc(CHEBI:"amyloid-beta"))
UNSET MouseStrain

SET Support = "Additionally, MerTK and Gas6 were coexpressed on plaque-associated \
macrophages in 12-month-old APP/PS1Δe9 animals (Fig. 4E)."

SET MouseStrain = "APP/PS1dE9"
p(MGI:Mertk) positiveCorrelation a(MESHA:Macrophages, loc(CHEBI:"amyloid-beta"))
p(MGI:Gas6) positiveCorrelation a(MESHA:Macrophages, loc(CHEBI:"amyloid-beta"))

SET Support = "Significantly, MerTK expression increased in Iba1+ cells after 7 d of \
bexarotene treatment (Fig. 4A)."

# Iba1 = microlgia marker
SET TimePoint = "7 days post-treatment"
a(CHEBI:bexarotene) -> p(MGI:Mertk, loc(MESHA:Microglia))
UNSET TimePoint
UNSET MouseStrain
UNSET AgeRange

SET Support = "Similarly, in plaque-associated cells, MerTK expression was also \
higher in 4-month-old 5XFAD mice treated with bexarotene or the PPARδ \
agonist GW0742 for 14 d compared with vehicle treated animals (Fig. \
4F)."

SET TimePoint = "14 days post-treatment"
SET MouseStrain = "5XFAD"
SET AgeRange = "4 months"
a(CHEBI:bexarotene) -> p(MGI:Mertk, loc(CHEBI:"amyloid-beta"))
a(MESHC:GW0742) -> p(MGI:Mertk, loc(CHEBI:"amyloid-beta"))
UNSET AgeRange
UNSET MouseStrain
UNSET TimePoint
UNSET Assay

SET Support = "CD45 is a canonical marker of myeloid cells (Sedgwick et al., 1991)."

# Ptprc is CD45
a(MESHA:"Myeloid Cells") -> p(MGI:Ptprc)

SET Support = "MerTK colocalized with CD45-expressing cells surrounding amyloid \
deposits in both 5XFAD and APP/PS1Δe9 animals (Fig. 4B,G)."

SET MouseStrain = {"APP/PS1dE9", "5XFAD"}
p(MGI:Mertk) positiveCorrelation a(MGI:Ptprc, loc(CHEBI:"amyloid-beta"))
p(MGI:Mertk) positiveCorrelation a(MESHA:"Myeloid Cells", loc(CHEBI:"amyloid-beta"))
UNSET MouseStrain

SET Support = "Moreover, those MerTK+/CD45+ cells surrounding plaques were more \
abundant in 12-month-old APP/PS1Δe9 animals treated with bexarotene \
(Fig. 4B, quantified in Fig. 4D)."

SET MouseStrain = "APP/PS1dE9"
SET AgeRange = "12 months"
a(CHEBI:bexarotene) -> a(MGI:Mertk, loc(CHEBI:"amyloid-beta"))
a(CHEBI:bexarotene) -> a(MGI:Ptprc, loc(CHEBI:"amyloid-beta"))
UNSET MouseStrain
UNSET AgeRange

SET Support = "Similarly, 4-month-old 5XFAD animals also showed increased ratios of \
MerTK/CD45 mean fluorescence intensity in response to 14 d of \
treatment with bexarotene or GW0742 (Fig. 4G, quantified in Fig. 4I)."

SET TimePoint = "14 days post-treatment"
SET MouseStrain = "5XFAD"
SET AgeRange = "4 months"
a(CHEBI:bexarotene) -> p(MGI:Mertk, loc(CHEBI:"amyloid-beta"))
a(MESHC:GW0742) -> p(MGI:Mertk, loc(CHEBI:"amyloid-beta"))
UNSET AgeRange
UNSET MouseStrain
UNSET TimePoint

SET Support = "However, Axl was abundantly expressed in cells surrounding plaques in \
both 12-month-old APP/PS1Δe9 and 4-month-old 5XFAD animals (Fig. \
5A,C)."

SET MouseStrain = "5XFAD"
SET AgeRange = "4 months"
p(MGI:Axl) positiveCorrelation a(CHEBI:"amyloid-beta")
UNSET AgeRange
UNSET MouseStrain
SET MouseStrain = "APP/PS1dE9"
SET AgeRange = "12 months"
p(MGI:Axl) positiveCorrelation a(CHEBI:"amyloid-beta")
UNSET AgeRange
UNSET MouseStrain

SET Support = "MerTK and Axl are expressed by the same Iba1+ cells in APP/PS1Δe9 \
animals (Fig. 5A)."

p(MGI:Mertk, loc(MESHA:Microglia)) positiveCorrelation p(MGI:Axl, loc(MESHA:Microglia))

SET Support = "Axl expression was increased in 4-month-old 5XFAD mice treated with \
bexarotene or PPARδ agonist GW0742 for 14 d compared with vehicle- \
treated animals (Fig. 5D)."

SET TimePoint = "14 days post-treatment"
SET MouseStrain = "5XFAD"
SET AgeRange = "4 months"
a(CHEBI:bexarotene) -> p(MGI:Axl)
a(MESHC:GW0742) -> p(MGI:Axl)
UNSET AgeRange
UNSET MouseStrain
UNSET TimePoint

SET Support = "Similar to what we observed with the expression pattern of MerTK, Axl \
also colocalized with CD45 expressing cells in both 5XFAD and \
APP/PS1Δe9 animals (Fig. 5B,E)."

SET MouseStrain = {"APP/PS1dE9", "5XFAD"}
p(MGI:Axl) positiveCorrelation p(MGI:Ptprc)
UNSET MouseStrain

SET Support = "Upon quantification, 4-month-old 5XFAD animals demonstrate higher \
Axl/CD45 mean fluorescence intensity in response to 14 d of treatment \
with either bexarotene or PPARδ agonist GW0742 (Fig. 5F)."

SET TimePoint = "14 days post-treatment"
SET MouseStrain = "5XFAD"
SET AgeRange = "4 months"
a(CHEBI:bexarotene) -> p(MGI:Axl)
a(MESHC:GW0742) -> p(MGI:Axl)
UNSET AgeRange
UNSET MouseStrain
UNSET TimePoint

SET Support = "TREM2 is expressed exclusively by microglia in the brain, but also by \
peripheral monocytes and macrophages (Colonna, 2003)."

a(MESHA:Microglia, loc(MESHA:Brain)) -> p(MGI:Trem2)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> p(MGI:Trem2)
a(MESHA:Macrophages, loc(MESHA:Blood)) -> p(MGI:Trem2)

SET Support = "We found that the same population of cells in the brain, which are \
MerTK+, CD45+ also expressed TREM2. Indeed, MerTK and TREM2 colocalize \
in cells surrounding plaques in both APP/PS1Δe9 (Fig. 6A) and 5XFAD \
(Fig. 6B) mice."

p(MGI:Mertk) positiveCorrelation p(MGI:Trem2)
p(MGI:Ptprc) positiveCorrelation p(MGI:Trem2)
SET MouseStrain = {"APP/PS1dE9", "5XFAD"}
p(MGI:Mertk, loc(CHEBI:"amyloid-beta")) positiveCorrelation p(MGI:Trem2, loc(CHEBI:"amyloid-beta"))
UNSET MouseStrain

SET Support = "Jay et al. (2015) have reported that plaque-associated macrophages are \
derived from blood-borne monocytes that infiltrate the brain and are \
identified by their high levels of CD45 expression."

a(MESHA:Macrophages, loc(CHEBI:"amyloid-beta")) -> p(MGI:Ptprc)

SET Support = "FACS analysis revealed that MerTK expression was largely restricted to \
the CD45hi cells in 5XFAD animals (Fig. 7C)."

# CD45hi = myeloid cells
SET TechnologyUsed = "FACS"
p(MGI:Mertk) positiveCorrelation a(MESHA:"Myeloid Cells")
UNSET TechnologyUsed

SET Support = "We treated 8-month-old 5XFAD mice with bexarotene or vehicle for 7 d \
before isolating myeloid cells from the brain. We gated the CD11b+ \
population into CD45lo and CD45hi populations (Fig. 7A). Indeed, \
35%–40% of CD11b+/CD45hi cells in 8-month-old 5XFAD mice expressed \
TREM2, whereas <1% of CD11b+/CD45hi cells in nontransgenic littermates \
were TREM2+ (Fig. 7B), demonstrating that TREM2 expression is \
restricted to peripherally derived cells infiltrating the CNS."

SET MouseStrain = "5XFAD"
SET AgeRange = "8 months"
SET TimePoint = "7 days post-treatment"
a(MESHA:"Myeloid Cells", loc(MESHA:Blood)) -> p(MGI:Trem2)

SET Support = "Unsurprisingly, MerTK was expressed at a higher level in TREM2+ cells \
as well (Fig. 7D,E), indicating that peripherally derived CD45hi, \
TREM2+ macrophages also express detectable levels of MerTK."

p(MGI:Mertk) positiveCorrelation p(MGI:Trem2)

SET Support = "Bexarotene treatment increased both the total MerTK levels, as well as \
the MerTK levels in TREM2+ cells (Fig. 7E), indicating RXR agonist \
treatment increased MerTK levels of both CD45lo and CD45hi cells in \
the brains of 5XFAD mice."

a(CHEBI:bexarotene) -> p(MGI:Mertk)
UNSET TimePoint
UNSET MouseStrain
UNSET AgeRange

SET Support = "We treated 12-month-old APP/PS1Δe9 animals with bexarotene by oral \
gavage for 7 d. We found acute bexarotene treatment reduced the number \
of plaques in both cortex and hippocampus by 20% and 30%, \
respectively, as measured by the number of thioflavin S-positive \
plaques (Fig. 8A–D). Additionally, insoluble levels of Aβ in combined \
cortical and hippocampal homogenates were reduced as shown by ELISA \
(Fig. 8E,F)."

SET MouseStrain = "APP/PS1dE9"
SET AgeRange = "12 months"
SET TimePoint = "7 days post-treatment"
a(CHEBI:bexarotene) negativeCorrelation path(MESHD:"Plaque, Amyloid")
a(CHEBI:bexarotene) -| a(CHEBI:"amyloid-beta", loc(MESHA:Hippocampus))
a(CHEBI:bexarotene) -| a(CHEBI:"amyloid-beta", loc(MESHA:"Cerebral Cortex"))

SET Support = "The diminished number of plaques was correlated with the appearance of \
Iba1+ microglia containing intracellular amyloid in bexarotene-, but \
not vehicle-treated, mice (Fig. 8G)."

# Aif1 = Iba1
a(CHEBI:"amyloid-beta", loc(MESHA:Hippocampus)) negativeCorrelation p(MGI:Aif1, loc(MESHA:Microglia))
a(CHEBI:"amyloid-beta", loc(MESHA:"Cerebral Cortex")) negativeCorrelation p(MGI:Aif1, loc(MESHA:Microglia))
UNSET TimePoint
UNSET MouseStrain
UNSET AgeRange

SET Support = "Phagocytosis is suppressed by the sustained presence of elevated \
amyloid levels in vivo (Krabbe et al., 2013)."

a(CHEBI:"amyloid-beta") -| bp(GOBP:phagocytosis)

SET Support = "Cultured microglia are competent phagocytes and readily take up fAβ \
introduced into the media (Koenigsknecht and Landreth, 2004)."

a(MESHA:Microglia) -| a(CHEBI:"amyloid fibril")

SET Support = "We treated 12-month-old APP/PS1Δe9 animals with bexarotene or vehicle \
for 5 d before preparing brain slices and performing a phagocytosis \
assay. Indeed, 12-month-old APP/PS1Δe9 animals show reduced \
phagocytosis compared with nontransgenic littermates (Fig. 9A,B), as \
previously reported in APPPS1 and APP23 mice (Krabbe et al., 2013). \
Remarkably, 5 d of oral bexarotene treatment restored phagocytic \
competence to nontransgenic levels (Fig. 9C)."

SET MouseStrain = "APP/PS1dE9"
SET AgeRange = "12 months"
SET TimePoint = "5 days post-treatment"
SET Assay = "phagocytosis assay"
a(CHEBI:bexarotene) -> bp(GOBP:phagocytosis)

SET Support = "Furthermore, incubation with MerTK and Axl ligand Gas6 restored \
APP/PS1Δe9 microglial phagocytosis to nontransgenic levels (Fig. 9C)."

p(MGI:Gas6) -> bp(GOBP:phagocytosis)

SET Support = "Most strikingly, incubation with a function-blocking anti-MerTK \
antibody reduced phagocytosis in both nontransgenic slices as well as \
those from APP/PS1Δe9 animals, which had been treated with bexarotene \
for 5 d before the assay (Fig. 9C)."

act(p(MGI:Mertk)) -> bp(GOBP:phagocytosis)
UNSET TimePoint
UNSET MouseStrain
UNSET AgeRange
act(p(MGI:Mertk)) -> bp(GOBP:phagocytosis)
UNSET Assay

UNSET Species
UNSET Section

##################################################################################

SET Citation = {"PubMed", "Res Rep Biochem. 2015;5:89-100. Epub 2015 Mar 17.", "26478868"}

SET Section = "Abstract"

SET Support = "Nasu-Hakola disease or polycystic lipomembranous osteodysplasia with \
sclerosing leukoencephalopathy (PLOSL) is a rare recessively inherited \
disease that is associated with early dementia and bone cysts with \
fractures."

path(DO:"Nasu-Hakola disease") positiveCorrelation path(MESHD:Dementia)
path(DO:"Nasu-Hakola disease") positiveCorrelation path(HP:"Bone cyst")

SET Support = "TREM2 and DAP12 form an immunoreceptor signaling complex that mediates \
myeloid cell, including microglia and osteoclasts, development, \
activation, and function."

complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -- bp(GOBP:"microglia development")
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -- bp(GOBP:"microglial cell activation")
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -- bp(GOBP:"osteoclast development")

SET Support = "Functionally, TREM2-DAP12 mediates osteoclast multi-nucleation, \
migration, and resorption."

complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -- bp(GOBP:"multinuclear osteoclast differentiation")
SET MeSHAnatomy = "Osteoclasts"
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -- bp(GOBP:"cell migration")
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -- bp(GOBP:"bone resorption")
UNSET MeSHAnatomy

SET Support = "In microglia, TREM2-DAP12 participates in recognition and apoptosis of \
neuronal debris and amyloid deposits."

complex(p(HGNC:TREM2, loc(MESHA:Microglia)), p(HGNC:TYROBP, loc(MESHA:Microglia))) -- a(MESHA:Neurons)
complex(p(HGNC:TREM2, loc(MESHA:Microglia)), p(HGNC:TYROBP, loc(MESHA:Microglia))) positiveCorrelation a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Acta Neuropathol Commun. 2016 Sep 2;4(1):98. doi: 10.1186/s40478-016-0367-7.", "27589997"}

SET Section = "Abstract"

SET Support = "Rare variation in TREM2 has been associated with greater risk for \
Alzheimer's disease (AD)."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "TREM2 encodes a cell surface receptor expressed on microglia and \
related cells, and the R47H variant associated with AD appears to \
affect the ability of TREM2 to bind extracellular ligands."

a(MESHA:Microglia) -> p(HGNC:TREM2)
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:TREM2, var("p.Arg47His")) -| act(p(HGNC:TREM2), ma(HP:"receptor ligand activity"))

SET Support = "In addition, other rare TREM2 mutations causing early-onset \
neurodegeneration are thought to impair cell surface expression."

p(HGNC:TREM2, var("?")) positiveCorrelation path(HP:Neurodegeneration)
p(HGNC:TREM2, var("?")) -| act(p(HGNC:TREM2), ma(GOBP:"protein localization to membrane"))

SET Support = "Using a sequence kernel association (SKAT) analysis in two independent \
AD cohorts, we found significant enrichment of rare TREM2 variants not \
previously characterized at the protein level. Heterologous expression \
of the identified variants showed that novel variants S31F and R47C \
displayed significantly reduced cell surface expression."

SET MeSHDisease = "Alzheimer Disease"
SET StudyType = "SKAT"
p(HGNC:TREM2, var("p.Ser31Phe")) -| act(p(HGNC:TREM2), ma(GOBP:"protein localization to membrane"))
p(HGNC:TREM2, var("p.Arg47Cys")) -| act(p(HGNC:TREM2), ma(GOBP:"protein localization to membrane"))
UNSET MeSHDisease
UNSET StudyType

# Another mutation was included in one case study, but one patient is not enough

UNSET Section

SET Section = "Results"

SET Support = "The most significant gene SNP set in our discovery analysis was TREM2 \
(p = 0.001)."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "The most significant gene SNP set in the replication cohort was also \
TREM2 (p = 2.88x10−4). After multiple testing correction using the \
Bonferroni method, the p-value for TREM2 was 0.02. The variants \
included in the TREM2 SNP set and their amino acid coding changes are \
summarized in Table 3."

# Variants were collected from Table 3
g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) -> p(HGNC:TREM2, var("p.Arg47His"))
g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("p.His215Gln")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("p.Ala196Thr")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) hasVariant g(dbSNP:rs371702633)
g(dbSNP:rs371702633) -> p(HGNC:TREM2, var("p.Ser162Arg"))
g(HGNC:TREM2, var("p.Ser162Arg")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) hasVariant g(dbSNP:rs2234255)
g(dbSNP:rs2234255) -> p(HGNC:TREM2, var("p.His157Tyr"))
g(HGNC:TREM2, var("p.His157Tyr")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) hasVariant g(dbSNP:rs79011726)
g(dbSNP:rs79011726) -> p(HGNC:TREM2, var("p.Glu151Lys"))
g(HGNC:TREM2, var("p.Glu151Lys")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) hasVariant g(dbSNP:rs142232675)
g(dbSNP:rs142232675) -> p(HGNC:TREM2, var("p.Asp87Asn"))
g(HGNC:TREM2, var("p.Asp87Asn")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) hasVariant g(dbSNP:rs201258663)
g(dbSNP:rs201258663) -> p(HGNC:TREM2, var("p.Thr66Met"))
g(HGNC:TREM2, var("p.Thr66Met")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) hasVariant g(dbSNP:rs753325601)
g(dbSNP:rs753325601) -> p(HGNC:TREM2, var("p.Arg47Cys"))
g(HGNC:TREM2, var("p.Arg47Cys")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) hasVariant g(dbSNP:rs104894002)
g(dbSNP:rs104894002) -> p(HGNC:TREM2, var("p.Glu33*"))
g(HGNC:TREM2, var("p.Glu33*")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) hasVariant g(dbSNP:rs746216516)
g(dbSNP:rs746216516) -> p(HGNC:TREM2, var("p.Ser31Phe"))
g(HGNC:TREM2, var("p.Ser31Phe")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) hasVariant g(dbSNP:rs2234252)
g(dbSNP:rs2234252) -> p(HGNC:TREM2, var("p.Ala28Val"))
g(HGNC:TREM2, var("p.Ala28Val")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) hasVariant g(dbSNP:rs768745050)
g(dbSNP:rs768745050) -> p(HGNC:TREM2, var("p.Val27Met"))
g(HGNC:TREM2, var("p.Val27Met")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) hasVariant g(dbSNP:rs777808487)
g(dbSNP:rs777808487) -> p(HGNC:TREM2, var("p.Ser16Phe"))
g(HGNC:TREM2, var("p.Ser16Phe")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "As an internal control for our analyses, we also generated the Y38C \
variant involved in early-onset frontotemporal dementia (FTD) [32], \
which is known to be defective for protein maturation."

p(HGNC:TREM2, var("p.Tyr38Cys")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")

SET Support = "These variants were transfected into HEK-293T cells and their \
expression analyzed by immunoblotting. All variants other than Y38C \
showed apparently normal protein maturation (Fig. 1a)."

SET CellLine = "HEK293T"
SET Assay = "immunohistochemistry"
p(HGNC:TREM2, var("p.Tyr38Cys")) negativeCorrelation bp(GOBP:"protein maturation")

SET Support = "We observed a modest but statistically significant reduction in \
surface expression for variant R136Q (Fig. 1b, c)."

p(HGNC:TREM2, var("p.Arg136Gln")) -| act(p(HGNC:TREM2), ma(GOBP:"protein localization to membrane"))

SET Support = "In addition, we observed an even larger defect for R136W, highlighting \
the importance of residue Arg 136 for normal TREM2 surface expression \
levels. Analysis of the overall expression level of these variants in \
whole-cell lysates indicated that R136W was significantly reduced \
(Fig. 1b, c)."

p(HGNC:TREM2, var("p.Arg136Trp")) -| act(p(HGNC:TREM2), ma(GOBP:"protein localization to membrane"))

SET Support = "However, we observed significant reductions in overall expression as \
well as cell surface expression for variants S31F and R47C (Fig. 2a, \
b)."

p(HGNC:TREM2, var("p.Ser31Phe")) -| act(p(HGNC:TREM2), ma(GOBP:"protein localization to membrane"))
p(HGNC:TREM2, var("p.Arg47Cys")) -| act(p(HGNC:TREM2), ma(GOBP:"protein localization to membrane"))

SET Support = "Finally, variant A28V showed a significant increase in cell surface \
expression."

p(HGNC:TREM2, var("p.Ala28Val")) -> act(p(HGNC:TREM2), ma(GOBP:"protein localization to membrane"))

UNSET Assay
UNSET CellLine
UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Neural Transm (Vienna). 2015 Jul;122(7):1069-76. doi: 10.1007/s00702-015-1369-5. Epub 2015 Jan 18.", "25596843"}

SET Section = "Abstract"

SET Support = "The role of neuroinflammation in the pathogenesis of neurodegenerative \
diseases has become more evident in recent years."

SET MeSHAnatomy = "Brain"
path(MESHD:Inflammation) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")
UNSET MeSHAnatomy

SET Support = "We found that CX3CL1 and TREM2, two genes related to \
neuroinflammation, are more highly expressed in brain regions with \
pronounced vulnerability to AD-related changes, such as the \
hippocampus, and that the expression levels reflect the course of the \
disease, whereas regions with low vulnerability to AD, seemed \
generally less affected by neuroinflammation."

SET MeSHAnatomy = "Brain"
p(HGNC:TREM2, loc(MESHA:Brain)) positiveCorrelation path(MESHD:Inflammation)
p(HGNC:CX3CL1, loc(MESHA:Brain)) positiveCorrelation path(MESHD:Inflammation)
UNSET MeSHAnatomy
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:TREM2, loc(MESHA:Hippocampus))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:CX3CL1, loc(MESHA:Hippocampus))

SET Support = "Furthermore, our results support previous findings of significantly \
higher CX3CL1 plasma levels in patients with mild to moderate AD than \
in patients with severe AD."

p(HGNC:CX3CL1, loc(MESHA:Plasma)) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

# Paper behind paywall

##################################################################################

SET Citation = {"PubMed", "N Engl J Med. 2013 Jan 10;368(2):107-16. doi: 10.1056/NEJMoa1211103. Epub 2012 Nov 14.", "23150908"}

SET Section = "Abstract"

SET Support = "Sequence variants, including the ε4 allele of apolipoprotein E, have \
been associated with the risk of the common late-onset form of \
Alzheimer's disease."

g(HGNC:APOE, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "A rare missense mutation (rs75932628-T) in the gene encoding the \
triggering receptor expressed on myeloid cells 2 (TREM2), which was \
predicted to result in an R47H substitution, was found to confer a \
significant risk of Alzheimer's disease in Iceland (odds ratio, 2.92; \
95% confidence interval [CI], 2.09 to 4.09; P=3.42×10(-10))."

g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) -> p(HGNC:TREM2, var("p.Arg47His"))
SET Ethnicity = "Icelanders"
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET Ethnicity

SET Support = "We also found that carriers of rs75932628-T between the ages of 80 and \
100 years without Alzheimer's disease had poorer cognitive function \
than noncarriers (P=0.003)."

SET AgeRange = "80-100 years old"
g(dbSNP:rs75932628) positiveCorrelation path(HP:"Cognitive impairment")
UNSET AgeRange

SET Section = "Results"
SET Ethnicity = "Icelanders"

SET Support = "Through whole-genome sequencing of samples from 2261 Icelanders, we \
found 191,777 nonsynonymous SNPs, frameshift variants, splicing \
variants, and stop gain–loss variants and imputed these variants in \
patients with Alzheimer’s disease and controls. A total of 3550 \
patients with Alzheimer’s disease were included in the analysis. Our \
control group included persons who had reached the age of 85 without a \
diagnosis of Alzheimer’s disease. With the exclusion of the ApoE locus \
and the A673T variant in APP,11 only one marker, rs75932628, showed a \
genomewide association, on the basis of either the Bonferroni-adjusted \
threshold of P<2.60×10−7 for 191,777 tests or the conventional \
threshold for genome-wide association studies (5×10−8)."

SET Assay = "whole-genome sequencing"
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "The T allele of rs75932628, which encodes a substitution of histidine \
for arginine at position 47 (R47H) in the gene encoding the triggering \
receptor expressed on myeloid cells 2 (TREM2) on chromosome 6p21.1, \
with an allelic frequency of 0.63% in Iceland, was found to confer a \
significant risk of Alzheimer’s disease (odds ratio, 2.92; 95% \
confidence interval [CI], 2.09 to 4.09; P= 3.42×10−10) (Table 1)."

g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) -> p(HGNC:TREM2, var("p.Arg47His"))
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Assay

UNSET Ethnicity

SET Support = "In an attempt to replicate the association between rs75932628-T and \
Alzheimer’s disease, we genotyped rs75932628 in cohorts from the \
United States (Emory), Germany, the Netherlands (Rotterdam Study), and \
Norway. We found that rs75932628-T conferred a risk of Alzheimer’s \
disease in all replication cohorts, with a combined odds ratio of 2.83 \
(95% CI, 1.45 to 5.40; P = 0.002 (Table 2)."

SET Ethnicity = {"United States", "Netherlands", "German", "Norway"}
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Cognitive function declined steadily with age in elderly persons \
between the ages of 80 and 100 (Fig. 1)."

SET AgeRange = "80-100 years old"
path(HP:"Cognitive impairment") positiveCorrelation bp(GOBP:aging)

SET Support = "We found that carriers of rs75932628-T showed worse cognition (a mean \
increase of 0.87 units on the CPS) than did noncarriers (P = 0.003)."

g(dbSNP:rs75932628) positiveCorrelation path(HP:"Cognitive impairment")

UNSET AgeRange
UNSET Ethnicity
UNSET Section

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Brain Pathol. 2017 Oct 7. doi: 10.1111/bpa.12564.", "28987033"}

SET Section = "Abstract"

SET Support = "Mutation in the triggering receptor expressed on myeloid cells (TREM) \
2 gene has been identified as a risk factor for several \
neurodegenerative diseases including Alzheimer's disease (AD)."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")

SET Support = "As part of investigations of microglia using post-mortem resources \
provided by the Medical Research Council Cognitive Function and Ageing \
Studies (MRC-CFAS), we immunostained the cerebral cortex of 299 \
participants for TREM2 using the Sigma antibody HPA010917 and compared \
with the macrophage/microglial markers Iba1 and CD68."

# Evidence included to set the environment for next evidence(s)
SET Assay = "immunostaining"

SET Support = "However, in most cases (284/299), the TREM2 antibody labelled \
monocytes within vascular lumens, but not microglia or perivascular \
macrophages. In contrast, in 5 out of 6 cases with acute infarcts, \
TREM2 immunoreaction identified cells within the brain parenchyma \
interpreted as recruited monocytes."

p(HGNC:TREM2, loc(MESHA:"Cerebral Cortex")) positiveCorrelation a(MESHA:Monocytes, loc(MESHA:"Blood Vessels"))
p(HGNC:TREM2, loc(MESHA:"Cerebral Cortex")) positiveCorrelation path(HP:infarct)

SET Support = "Six cases with old infarcts contained phagocytic foamy macrophages \
which were CD68-positive but TREM2 negative."

path(HP:infarct) positiveCorrelation p(HGNC:CD68, loc(MESHA:Macrophages))

UNSET Assay

UNSET Section

# Results behind paywall
##################################################################################

SET Citation = {"PubMed", "Neuropharmacology. 2016 Jun;105:196-206. doi: 10.1016/j.neuropharm.2016.01.028. Epub 2016 Jan 21.", "26802771"}

SET Section = "Abstract"

SET Support = "As a novel risk gene for Alzheimer's disease (AD), triggering receptor \
expressed on myeloid cells 2 (TREM2) gene encodes a type I \
transmembrane receptor that is uniquely expressed by the microglia in \
the brain."

g(HGNC:TREM2) positiveCorrelation path(MESHD:"Alzheimer Disease")
a(MESHA:Microglia, loc(MESHA:Brain)) -> p(HGNC:TREM2)

SET Support = "Emerging evidence indicates a strong association between TREM2 and tau \
pathology in the cerebral spinal fluid or brain tissue of AD patients."

SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) -- p(HGNC:MAPT, pmod(Ph))
p(HGNC:TREM2, loc(MESHA:Brain)) -- p(HGNC:MAPT, pmod(Ph))
UNSET MeSHDisease

SET Support = "In line with these clinical findings, we found that TREM2 was \
upregulated in the brain of P301S mice, an animal model of tau \
pathology, during disease progression."

SET Species = "10090"
# P301S a mouse strain used to study tau pathology
SET MouseStrain = "P301S"
a(MESHA:Brain) positiveCorrelation p(MGI:Trem2)

# Mouse and mousestrain are the same for next evidence(s)

SET Support = "In our recent studies, we revealed that silencing microglial TREM2 \
expression in P301S mice exacerbated spatial cognitive deficits and \
tau pathology."

SET MeSHAnatomy = "Microglia"
p(MGI:Trem2) negativeCorrelation path(HP:"Impaired visuospatial constructive cognition")
p(MGI:Trem2) negativeCorrelation p(MGI:Mapt, pmod(Ph))
UNSET MeSHAnatomy

SET Support = "For the first time, we showed that TREM2 overexpression rescued \
spatial cognitive impairments and ameliorated neuropathologies \
including neuronal and synaptic loss as well as tau \
hyperphosphorylation."

p(MGI:Trem2) negativeCorrelation path(HP:"Impaired visuospatial constructive cognition")
p(MGI:Trem2) -| p(MGI:Mapt, pmod(Ph))
p(MGI:Trem2) -> a(MESHA:Neurons)

SET Support = "Meanwhile, this protective effect was likely attributed to the \
suppression of neuroinflammation and subsequent attenuation of tau \
kinase activity, since the expression of pro-inflammatory cytokines \
including Tnf, Il1b and Il6 as well as the activity of tau kinase \
including glycogen synthase kinase 3β and cyclin-dependent kinase 5 \
was significantly reduced following TREM2 overexpression."

p(MGI:Trem2) -| p(MGI:Tnf)
p(MGI:Trem2) -| p(MGI:Il1b)
p(MGI:Trem2) -| p(MGI:Il6)
p(MGI:Trem2) -| act(p(MGI:Gsk3b), ma(kin))
p(MGI:Trem2) -| act(p(MGI:Cdk5), ma(kin))
SET MeSHAnatomy = "Brain"
p(MGI:Trem2) -| path(MESHD:Inflammation)
UNSET MeSHAnatomy

SET Support = "Additionally, the suppressed neuroinflammation might be ascribed to \
the M2 activation of microglia induced by TREM2, as the expression of \
M2 phenotype makers including Arg1, Retnla, Il4 and Il10 was markedly \
increased."

p(MGI:Trem2) -> p(MGI:Arg1)
p(MGI:Trem2) -> p(MGI:Retnla)
p(MGI:Trem2) -> p(MGI:Il4)
p(MGI:Trem2) -> p(MGI:Il10)

UNSET Species
UNSET MouseStrain

UNSET Section

# Results behind paywall
##################################################################################

SET Citation = {"PubMed", "J Alzheimers Dis. 2013;35(1):45-9. doi: 10.3233/JAD-122311.", "23380991"}

SET Section = "Abstract"

SET Support = "The rs75932628-T variant of the gene encoding the triggering receptor \
expressed on myeloid cells 2 (TREM2) has recently been identified as a \
rare risk factor for late-onset Alzheimer's disease (AD)."

g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "In this study we examined the association between TREM2 exon 2 \
variants and early-onset AD in a sample of Caucasian subjects of \
French origin including 726 patients with age of onset ≤65 years and \
783 controls. Only the rs75932628-T variant (predicted to cause an \
R47H substitution) conferred a significant risk for early-onset AD \
(OR, 4.07; 95% CI, 1.3 to 16.9; p = 0.009)."

SET Ethnicity = "Caucasian with French origin"
g(dbSNP:rs75932628) -> p(HGNC:TREM2, var("p.Arg47His"))
g(dbSNP:rs75932628) positiveCorrelation path(ADO:"Early_onset_Alzheimer_s_Disease__")
UNSET Ethnicity

UNSET Section

##################################################################################

SET Citation = {"PubMed", "PLoS Med. 2007 Apr;4(4):e124.", "17425404"}

SET PublicationType = "Research"
SET Section = "Abstract"

SET Support = "Microglial cells, the resident phagocytes of the central nervous \
system (CNS), are hematopoietic-derived myeloid cells and express the \
triggering receptor expressed on myeloid cells 2 (TREM2), an innate \
immune receptor."

a(MESHA:Microglia, loc(MESHA:"Central Nervous System")) isA a(MESHA:Phagocytes, loc(MESHA:"Central Nervous System"))
a(MESHA:Microglia, loc(MESHA:"Central Nervous System")) -> p(HGNC:TREM2)

SET Support = "Intravenous application of TREM2-transduced bone marrow-derived \
myeloid precursor cells at the EAE peak led to an amelioration of \
clinical symptoms, reduction in axonal damage, and prevention of \
further demyelination."

SET Species = "10090"
SET MeSHDisease = "Encephalomyelitis, Autoimmune, Experimental"
p(MGI:Trem2) positiveCorrelation bp(GOBP:myelination)
p(MGI:Trem2) -| path(MESHD:"Diffuse Axonal Injury")
UNSET Species
UNSET MeSHDisease

SET Support = "TREM2-transduced myeloid cells applied intravenously migrated into the \
inflammatory spinal cord lesions of EAE-diseased mice, showed \
increased lysosomal and phagocytic activity, cleared degenerated \
myelin, and created an anti-inflammatory cytokine milieu within the \
CNS."

SET MeSHDisease = "Encephalomyelitis, Autoimmune, Experimental"
SET MeSHAnatomy = "Central Nervous System"
SET Species = "10090"
p(MGI:Trem2) -> a(MESHA:"Myeloid Cells", loc(MESHA:"Spinal Cord"))
p(MGI:Trem2) -> bp(GOBP:phagocytosis)
p(MGI:Trem2) -| path(MESHD:Inflammation)
UNSET Species
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Support = "Intravenously applied bone marrow-derived and TREM2-tranduced myeloid \
precursor cells limit tissue destruction and facilitate repair within \
the murine CNS by clearance of cellular debris during EAE."

SET MeSHDisease = "Encephalomyelitis, Autoimmune, Experimental"
SET Species = "10090"
SET MeSHAnatomy = "Central Nervous System"
p(MGI:Trem2) positiveCorrelation bp(GOBP:"apoptotic cell clearance")
UNSET MeSHAnatomy
UNSET MeSHDisease
UNSET Species

UNSET Section

SET Section = "Results"
# experimental autoimmune encephalomyelitis = EAE

SET Support = "TREM2 has been shown to be expressed on certain subtypes of myeloid \
cells, particularly osteoclasts, immature dendritic cells, and \
microglia [21]."

a(MESHA:Osteoclasts) -> p(HGNC:TREM2)
a(MESHA:"Dendritic Cells") -> p(HGNC:TREM2)
a(MESHA:Microglia) -> p(HGNC:TREM2)

SET Support = "To study the expression kinetics of TREM2 during EAE, we performed \
flow cytometry analysis of TREM2 on freshly isolated microglia and \
resident CNS cells identified by immmunostaining for CD11b. Flow \
cytometry analysis demonstrated TREM2 expression on a minority of \
microglial cells under normal conditions in adult C57BL/6 mice (Figure \
1A)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = "flow cytometry"
a(MESHA:Microglia) -> p(MGI:Trem2)
UNSET Assay
UNSET MouseStrain
UNSET Species

SET Support = "Next, we induced EAE by immunization with the myelin oligodendrocyte \
glycoprotein peptide 35–55. At day 4 after onset of clinical symptoms \
of EAE, TREM2 was detected on approximately half of the CD11b+ cells \
in the spinal cord, the tissue region mostly affected by the \
inflammatory lesions (Figure 1A and ​1B)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = "flow cytometry"
SET MeSHDisease = "Encephalomyelitis, Autoimmune, Experimental"
SET TimePoint = "4 days post-immunization"
a(MESHA:Microglia, loc(MESHA:"Spinal Cord")) -> p(MGI:Trem2)
UNSET MeSHDisease
UNSET Assay
UNSET MouseStrain
UNSET Species
UNSET TimePoint

SET Support = "In detail, the percentage of TREM2 and CD11b+ cells in the spinal cord \
increased from 23.3% (standard error of the mean [SEM] 16.1) in the \
normal tissue to 58.8% (SEM 4.2) at day 4 after onset of clinical \
signs of the disease (Figure 1B)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = "flow cytometry"
SET TimePoint = "4 days post-immunization"
p(MGI:Trem2, loc(MESHA:Microglia), loc(MESHA:"Spinal Cord")) positiveCorrelation path(MESHD:"Encephalomyelitis, Autoimmune, Experimental")
UNSET TimePoint
UNSET Assay
UNSET MouseStrain
UNSET Species

SET Support = "To study the involvement of microglial TREM2 in EAE, we isolated bone \
marrow–derived myeloid precursors, lentivirally transduced them with \
TREM2, and applied them as putative precursors of microglia in \
diseased mice. Bone marrow cells were isolated from female adult \
C57BL/6 mice and cultured in GM-CSF–containing medium for 10–11 d. \
Under these culture conditions, the number of CD11b+ cells increased \
at least 20-fold as determined by counting the CD11b+ cells before and \
after culture. Flow cytometry analysis of the cultured BM-MC showed \
expression of CD45, CD11b, CD11c, CD36, and F4/80 (Figure 2A)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = "flow cytometry"
# BM-MC means Bone marrow-derived myeloid cells
SET TimePoint = "10-11 days cell culture"
# Csf2 = GM-CSF, ITGAM = CD11b
p(MGI:Csf2) -> p(MGI:Itgam)
SET ChallengeType = "GM-CSF"
a(MESHA:"Myeloid Cells", loc(MESHA:"Bone Marrow")) -> p(MGI:Ptprc)
a(MESHA:"Myeloid Cells", loc(MESHA:"Bone Marrow")) -> p(MGI:Itgam)
a(MESHA:"Myeloid Cells", loc(MESHA:"Bone Marrow")) -> p(MGI:Itgax)
a(MESHA:"Myeloid Cells", loc(MESHA:"Bone Marrow")) -> p(MGI:Cd36)
a(MESHA:"Myeloid Cells", loc(MESHA:"Bone Marrow")) -> p(MGI:Adgre1)
UNSET Assay
UNSET TimePoint
UNSET ChallengeType
UNSET MouseStrain
UNSET Species

SET Support = "However, RT-PCR demonstrated gene transcripts of the TREM2-associated \
signaling molecule DAP12 (Figure 2)."

SET Assay = "RT-PCR"
SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET TimePoint = "10-11 days cell culture"
SET ChallengeType = "GM-CSF"
a(MESHA:"Myeloid Cells", loc(MESHA:"Bone Marrow")) -> r(MGI:Tyrobp)
UNSET Assay
UNSET TimePoint
UNSET ChallengeType
UNSET MouseStrain
UNSET Species

SET Support = "Stimulation of TREM2 has been shown to induce phagocytosis in primary \
microglia [14]."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
act(p(MGI:Trem2, loc(MESHA:"Myeloid Cells"))) -> bp(GOBP:phagocytosis)

SET Support = "Therefore, we studied the effect of TREM2 stimulation on LPS-treated \
BM-MC. Quantitative real-time RT-PCR of several cytokines was \
performed 48 h after TREM2 cross-linking of TREM2-transduced BM-MC and \
control GFP vector–transduced BM-MC, which have been stimulated with \
100 ng/ml LPS. Interestingly, stimulation by TREM2 antibody 1 h before \
LPS treatment showed increased IL-10 (5.7-fold [1.3 SEM]) and \
decreased interleukin-1β (IL-1β; 0.5-fold [0.1 SEM]) gene \
transcription in TREM2-transduced BM-MC compared to control GFP \
vector–transduced BM-MC (Figure 3B)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = "qRT-PCR"
SET TimePoint = "48 hours post-stimulation"
act(p(MGI:Trem2, loc(MESHA:"Myeloid Cells"))) -> r(MGI:Il10)
act(p(MGI:Trem2, loc(MESHA:"Myeloid Cells"))) -| r(MGI:Il1b)
UNSET TimePoint
UNSET Assay
UNSET MouseStrain
UNSET Species

SET Support = "Moreover, we investigated gene transcript levels of cytokines in BM-MC \
cocultured for 48 h with apoptotic mixed neuron-enriched cultures. \
Gene transcripts of IL-1β and nitric oxide synthase-2 (NOS2) were \
decreased in TREM2-transduced BM-MC after challenge with apoptotic \
cells (Figure 3B)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = "qRT-PCR"
SET ChallengeType = {"lipopolysaccharide stimulation", "apoptotic neurons"}
SET TimePoint = "48 hours post-stimulation"
act(p(MGI:Trem2, loc(MESHA:"Myeloid Cells"))) -| r(MGI:Il10)
act(p(MGI:Trem2, loc(MESHA:"Myeloid Cells"))) -| r(MGI:Nos2)
UNSET ChallengeType
UNSET TimePoint
UNSET Assay
UNSET MouseStrain
UNSET Species

SET Support = "Transduction by TREM2 enabled BM-MC to release a substantial amount of \
IL-10 after stimulation as determined by ELISA (Figure 3D)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET TimePoint = "48 hours post-stimulation"
SET ChallengeType = {"lipopolysaccharide stimulation", "apoptotic neurons"}
SET Assay = "ELISA"
act(p(MGI:Trem2, loc(MESHA:"Myeloid Cells"))) -> r(MGI:Il10)
UNSET ChallengeType
UNSET TimePoint
UNSET Assay
UNSET MouseStrain
UNSET Species

SET Support = "The GFP+ cells were detected in the spinal cord of EAE-diseased mice, \
preferentially in perivascular inflammatory lesions (Figure 4A)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
path(MESHD:"Encephalomyelitis, Autoimmune, Experimental") positiveCorrelation p(MGI:Trem2, loc(MESHA:"Spinal Cord"))
path(MESHD:Inflammation) positiveCorrelation p(MGI:Trem2)
UNSET MouseStrain
UNSET Species

SET Support = "Invaded myeloid cells were round in shape and were clearly positive \
for the common hematopoietic surface marker CD45 (Figure 4A), but not \
for the microglial typical marker Iba1 (Figure 4B)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
a(MESHA:"Myeloid Cells") positiveCorrelation p(MGI:Ptprc)
UNSET MouseStrain
UNSET Species

SET Support = "To determine phagocytosis in situ, we analyzed the lysosomal activity \
within the inflammatory spinal cord lesions by immunostaining of \
methanol-fixed tissue with antibody directed against Lamp2. An \
increased number of cells showing Lamp2 staining was detected in the \
spinal cord of EAE-diseased mice at day 2 after treatment with \
TREM2-transduced myeloid cells (37.5 [SEM 4.2] cells/mm2) compared to \
mice treated with GFP-transduced myeloid cells (11.0 [SEM 3.1] \
cells/mm2) or vehicle control (7.7 [SEM 0.9] cells/mm2) (Figure 5A and \
​5B)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = {"in vivo", "in situ", "immunostaining"}
SET TimePoint = "2 days post-transduction"
SET MeSHDisease = "Encephalomyelitis, Autoimmune, Experimental"
p(MGI:Trem2) -> p(MGI:Lamp2, loc(MESHA:"Spinal Cord"))
UNSET Assay
UNSET MeSHDisease
UNSET TimePoint
UNSET MouseStrain
UNSET Species

SET Support = "These data indicated that TREM2-transduced myeloid cells show \
increased lysosomal degradation associated with phagocytosis of \
debris."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = {"in vivo", "in situ", "immunostaining"}
SET TimePoint = "2 days post-transduction"
SET MeSHDisease = "Encephalomyelitis, Autoimmune, Experimental"
p(MGI:Trem2, loc(MESHA:"Myeloid Cells")) positiveCorrelation bp(GOBP:phagocytosis)
UNSET Assay
UNSET MeSHDisease
UNSET TimePoint
UNSET MouseStrain
UNSET Species

SET Support = "Next, we investigated in vivo the distribution of degenerated myelin \
basic protein (MBP) immunoreactivity, which has been phagocytosed and \
degraded in lysosomes by myeloid cells in inflammatory lesions. \
Degenerated MBP was detected with a specific antibody directed against \
an epitope unmasked during the process of myelin degeneration [22]. \
The number of cells immunostained for degenerated MBP in the spinal \
cord of EAE-diseased mice was reduced at day 2 after treatment with \
TREM2-transduced myeloid cells (34.5 [SEM 3.5] cells/mm2) compared to \
mice treated with GFP-transduced myeloid cells (50.5 [SEM 1.5] \
cells/mm2) or vehicle control (48.0 [SEM 9.0] cells/mm2) (Figure 5A \
and 5B)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = {"in vivo", "in situ", "immunostaining"}
SET TimePoint = "2 days post-transduction"
SET MeSHDisease = "Encephalomyelitis, Autoimmune, Experimental"
# Degeneration == degredate?
a(MESHA:"Myeloid Cells") => deg(p(MGI:Mbp))
p(MGI:Trem2, loc(MESHA:"Myeloid Cells")) -| deg(p(MGI:Mbp))
UNSET Assay
UNSET MeSHDisease
UNSET TimePoint
UNSET MouseStrain
UNSET Species

SET Support = "Quantitative RT-PCR analysis at day 2 after cell injection \
demonstrated a significant decrease of gene transcription of tumor \
necrosis factor-α (TNFα), interferon-γ (IFNγ), and IL-1β, and an \
increase of gene transcript of IL-10 (3.7-fold) in the spinal cord of \
the TREM2-BM-MC–injected EAE-diseased mice (Figure 5C)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = "qRT-PCR"
SET TimePoint = "2 days post-transduction"
SET MeSHDisease = "Encephalomyelitis, Autoimmune, Experimental"
p(MGI:Trem2, loc(MESHA:"Myeloid Cells")) -| r(MGI:Tnf, loc(MESHA:"Spinal Cord"))
p(MGI:Trem2, loc(MESHA:"Myeloid Cells")) -| r(MGI:Ifng, loc(MESHA:"Spinal Cord"))
p(MGI:Trem2, loc(MESHA:"Myeloid Cells")) -| r(MGI:Il1b, loc(MESHA:"Spinal Cord"))
p(MGI:Trem2, loc(MESHA:"Myeloid Cells")) -> r(MGI:Il10, loc(MESHA:"Spinal Cord"))
UNSET Assay
UNSET MeSHDisease
UNSET TimePoint
UNSET MouseStrain
UNSET Species

SET Support = "At day 14 after cell therapy with TREM2-transduced myeloid cells, a \
clear reduction of axonal damage and demyelination was detected as \
determined by immunostaining for APP, dephosphorylated neurofilament \
(SMI32), and staining of myelin (LFB) (Figure 7A)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET TimePoint = "14 days post-transduction"
SET MeSHDisease = "Encephalomyelitis, Autoimmune, Experimental"
SET Assay = {"in vivo", "immunostaining"}
p(MGI:Trem2, loc(MESHA:"Myeloid Cells")) -| p(MGI:App, loc(MESHA:"Spinal Cord"))
# SMI32 stains Neurofilament H
p(MGI:Trem2, loc(MESHA:"Myeloid Cells")) -| p(MGI:Nefh, loc(MESHA:"Spinal Cord"))
# LFB stains for demyelination
p(MGI:Trem2, loc(MESHA:"Myeloid Cells")) positiveCorrelation bp(GOBP:myelination)
UNSET TimePoint
UNSET MeSHDisease
UNSET Assay
UNSET MouseStrain
UNSET Species

UNSET Section
UNSET PublicationType

##################################################################################

SET Citation = {"PubMed", "Mol Neurodegener. 2015 Apr 10;10:19. doi: 10.1186/s13024-015-0016-9.", "25886450"}

SET Section = "Abstract"

SET Support = "TREM2 encodes for triggering receptor expressed on myeloid cells 2 and \
has rare, coding variants that associate with risk for late-onset \
Alzheimer's disease (LOAD) in Caucasians of European and North- \
American origin."

SET Ethnicity = {"Caucasian with European origin", "Caucasian with North American origin"}
g(HGNC:TREM2, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
UNSET Ethnicity

#Following results were found in those of African-American heritage
SET Support = "We identified significant LOAD risk association with p.L211P (p=0.01, \
OR=1.27, 95%CI=1.05-1.54) and suggestive association with p.W191X \
(p=0.08, OR=1.35, 95%CI=0.97-1.87)."

SET Ethnicity = "African-American"
g(HGNC:TREM2, var("p.Leu211Pro")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
UNSET Ethnicity

UNSET Section

SET Section = "Results"

SET Support = "Pooled sequencing in 202 AD cases and 136 controls from WUSM \
identified eleven non-synonymous TREM2 variants in this AA cohort \
(Additional file 1: Table S7 and Figure 1A), one of which (p.A105V) \
has not been identified in previous AD studies [6,19,20]."

# AA = African-Americans
# Checked attached file for variants
SET Ethnicity = "African-American"
SET Assay = "Pooled sequencing"
SET AssayLocation = "Washington University School of Medicine in St. Louis"
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Arg62His"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Thr66Met"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Asp87Asn"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Thr96Lys"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Ala105Val"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Glu151Lys"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.His157Tyr"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Trp191X"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Glu202Asp"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Leu211Pro"))
UNSET Assay
UNSET AssayLocation

SET Support = "Sanger sequencing of an independent AA cohort from Mayo, followed by \
direct genotyping in 179 AD cases and 334 controls identified seven \
non-synonymous TREM2 variants (Additional file 1: Table S8 and Figure \
1A)."

SET Assay = "Sanger sequencing"
SET AssayLocation = "Mayo Clinic Florida"
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Arg47His"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Arg62His"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Thr96Lys"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Thr96Met"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Glu151Lys"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Trp191X"))
g(HGNC:TREM2) hasVariant g(HGNC:TREM2, var("p.Leu211Pro"))
UNSET Assay
UNSET Ethnicity
UNSET AssayLocation

SET Support = "In contrast, p.L211P in exon 4 of TREM2 is significantly associated \
with increased risk of AD (p = 0.01, OR = 1.27 [1.05-1.54]; Table 1)."

g(HGNC:TREM2, var("p.Leu211Pro")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Additionally, p.W191X, a stop-gain variant that is only present in the \
shortest transcript of TREM2, shows a trend toward association with AD \
risk (p = 0.08, OR = 1.35 [0.97-1.87]; Table 1)."

g(HGNC:TREM2, var("p.Trp191X")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "REM2 p.L211P and p.W191X are in tight LD but only weakly correlated \
(D’ = 0.98, R2 = 0.25)."

g(HGNC:TREM2, var("p.Leu211Pro")) positiveCorrelation g(HGNC:TREM2, var("p.Trp191X"))

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Alzheimers Dement. 2015 Oct;11(10):1163-70. doi: 10.1016/j.jalz.2014.10.013. Epub 2014 Dec 9.", "25499537"}

SET Section = "Abstract"

SET Support = "The TREM2 variant is associated with: (i) AD (odds ratio: 4.76; P = \
.014); (ii) increased density of amyloid plaques and neurofibrillary \
tangles in multiple brain regions; (iii) increased TREM2 (P = .041) \
and TYROBP (P = .006) gene expression; (iv) decreased TREM2 protein \
levels (P = .016); and (v) upregulation of proinflammatory cytokines \
(regulated on activation, normal T cell expressed and secreted \
[RANTES] and interferon [IFN] gamma) (P = .003) and nominal \
downregulation of protective markers (α2-macroglobulin, interleukin 4 \
or IL-4, and ApoA1) (P = .018)."

#Acorrding to an earlier sentence, they were investigating thr R47H variant
g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")
SET MeSHAnatomy = "Brain"
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation a(CHEBI:"amyloid-beta", loc(MESHA:Brain))
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation a(MESHCS:"Neurofibrillary Tangles", loc(MESHA:Brain))
UNSET MeSHAnatomy
g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation r(HGNC:TREM2)
g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation r(HGNC:TYROBP)
g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation p(HGNC:TREM2)
#RANTES = CCL5
g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation p(HGNC:CCL5)
g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation p(HGNC:IFNG)
g(HGNC:TREM2, var("p.Arg47His")) negativeCorrelation p(HGNC:A2M)
g(HGNC:TREM2, var("p.Arg47His")) negativeCorrelation p(HGNC:IL4)
g(HGNC:TREM2, var("p.Arg47His")) negativeCorrelation p(HGNC:APOA1)

UNSET Section

SET Section = "Results"

SET Support = "The T allele of the rs75932628 conferred an increased risk of AD using \
cases with a diagnosis of definite AD by CERAD criteria [19] (N=265) \
and controls (N=225) (odds ratio [OR]: 4.76; 95% confidence interval \
[CI]: 1.37–16.54; P=0.014) (Table 1)."

g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "As expected, the strong association of ApoE ε4+ variant was replicated \
in the current cohort (OR: 3.19; 95%CI: 2.08–4.89; P=9.87×10−8; \
Supplement Table 4)."

p(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Carriers of rs75932628-T allele had increased NP density in all \
examined brain regions, with more prominent changes in the orbital \
frontal cortex (P < 0.0001 at FDR = 0.002) and inferior parietal \
cortex (P = 0.0001 at FDR = 0.001)."

SET BRCO = {"Inferior Parietal Cortex", "Orbital Frontal Cortex"}
g(dbSNP:rs75932628) positiveCorrelation a(CHEBI:"amyloid-beta")
UNSET BRCO

SET Support = "Similarly, they had increased densities of NFTs; this effect was \
significant after multiple testing corrections in the middle frontal \
gyrus (P = 0.0037 at FDR = 0.0149) and entorhinal cortex (P = 0.0161 \
at FDR = 0.0428)."

SET BRCO = {"Frontal Gyrus", "Entorhinal Cortex"}
g(dbSNP:rs75932628) positiveCorrelation a(MESHCS:"Neurofibrillary Tangles")
UNSET BRCO

SET Support = "The association of rs75932628-T allele with gene expression was \
examined in STG with analysis of covariance using sex, age, PMI and \
RIN as covariates. The ANCOVA revealed a significant main effect of \
group for TREM2 [F(2,41) = 3.454; P = 0.041; η2 = 0.144] and TYROBP \
[F(2,41) = 5.767; P = 0.006; η2 = 0.220] gene expression."

SET BRCO = "Superior Temporal Gyrus"
r(HGNC:TREM2) positiveCorrelation path(MESHD:"Alzheimer Disease")
r(HGNC:TYROBP) positiveCorrelation path(MESHD:"Alzheimer Disease")
SET MeSHDisease = "Alzheimer Disease"
r(HGNC:TREM2) positiveCorrelation g(dbSNP:rs75932628)
r(HGNC:TYROBP) positiveCorrelation g(dbSNP:rs75932628)

SET Support = "The association of rs75932628-T allele with protein levels was \
examined with analysis of covariance using sex, age and PMI as \
covariates. The ANCOVA revealed a significant main effect of group for \
TREM2 [F(2,21) = 5.092; P = 0.016; η2 = 0.327] and TYROBP [F(2,21) = \
4.487; P = 0.024; η2 = 0.299] protein levels. Bonferroni post hoc \
analysis showed that controls have higher TREM2 (P = 0.014) and lower \
TYROBP (P = 0.024) protein level compared to ADcarriers and \
ADnoncarriers, respectively (Figure 2b; Supplement Figure 3)."

p(HGNC:TREM2) negativeCorrelation g(dbSNP:rs75932628)
p(HGNC:TYROBP) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Only the association with factor 6 survived corrections for multiple \
testing (FDR < 0.05). Factor 6 conformed to a proinflammatory profile \
and included the markers RANTES and IFN-gamma. Bonferroni post hoc \
analysis showed that both ADcarriers (P = 0.002) and ADnoncarriers (P \
= 0.019) have higher factor 6 values compared to controls (Supplement \
Figure 4; Supplement Table 9)."

g(dbSNP:rs75932628) positiveCorrelation p(HGNC:IFNG)
g(dbSNP:rs75932628) positiveCorrelation p(HGNC:CCL5)
p(HGNC:IFNG) positiveCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
p(HGNC:CCL5) positiveCorrelation bp(GOBP:"cytokine production involved in inflammatory response")


SET Support = "Factor 4 was consistent with a “protective” role and included the \
markers α2 macroglobulin, IL-4 and ApoA1. Bonferroni post hoc analysis \
showed that ADcarriers have decreased factor 4 values compared to \
controls (P = 0.017)."

g(dbSNP:rs75932628) negativeCorrelation p(HGNC:A2M)
g(dbSNP:rs75932628) negativeCorrelation p(HGNC:IL4)
g(dbSNP:rs75932628) negativeCorrelation p(HGNC:APOA1)

UNSET MeSHDisease
UNSET BRCO

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Nat Rev Neurosci. 2016 Apr;17(4):201-7. doi: 10.1038/nrn.2016.7. Epub 2016 Feb 25.", "26911435"}

SET Section = "Abstract"

SET Support = "Genome-wide association studies have identified rare variants of the \
gene that encodes triggering receptor expressed on myeloid cells 2 \
(TREM2) - an immune receptor that is found in brain microglia - as \
risk factors for non-familial Alzheimer disease (AD)."

a(MESHA:Microglia, loc(MESHA:Brain)) -> p(HGNC:TREM2)
SET StudyType = "GWAS"
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET StudyType

UNSET Section

# Behind a paywall
##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2015 Dec;36(12):3176-3186. doi: 10.1016/j.neurobiolaging.2015.08.019. Epub 2015 Aug 24.", "26364736"}

SET Section = "Abstract"

SET Support = "Tau pathology is a pathological hallmark for several neurodegenerative \
diseases including Alzheimer's disease and frontotemporal dementia."

# Tau pathology means hyperphosphorylated MAPT protein
p(HGNC:MAPT, pmod(Ph)) biomarkerFor path(MESHD:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph)) biomarkerFor path(MESHD:"Frontotemporal Dementia")

SET Support = "As a novel susceptibility gene for these 2 diseases, triggering \
receptor expressed on myeloid cells 2 (TREM2) gene encodes an immune \
receptor that is uniquely expressed by microglia."

g(HGNC:TREM2) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
a(MESHA:Microglia) -> p(HGNC:TREM2)

SET Support = "Recently, a correlation between TREM2 expression and \
hyperphosphorylated tau has been revealed in the brain of Alzheimer's \
disease patients, suggesting a potential association between TREM2 and \
tau pathology."

SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TREM2, loc(MESHA:Brain)) -- p(HGNC:MAPT, pmod(Ph), loc(MESHA:Brain))
UNSET MeSHDisease

SET Support = "Herein, using P301S mice, we showed that TREM2 was upregulated in \
microglia during disease progression."

SET Species = "10090"
SET MouseStrain = "P301S"
p(MGI:Trem2, loc(MESHA:Microglia)) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Silencing of brain TREM2 exacerbated tau pathology in P301S mice."

p(MGI:Trem2) -| p(MGI:Mapt, pmod(Ph))

SET Support = "Additionally, more severe neurodegenerative changes and spatial \
learning deficits were observed following TREM2 silencing."

p(MGI:Trem2) positiveCorrelation bp(HP:"spatial learning")

UNSET MouseStrain
UNSET Species

UNSET Section

# Behind a paywall
##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2015 Jan;36(1):546.e9-13. doi: 10.1016/j.neurobiolaging.2014.08.001. Epub 2014 Aug 27.", "25260849"}

SET Section = "Abstract"

SET Support = "Recently, a variant in the TREM2 gene (rs75932628), causing a C-to-T \
base-pair change that results in the substitution of histidine for \
arginine at amino acid position 47 (R47H) in the TREM2 protein, has \
been associated with an increased risk of AD."

g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) -> g(HGNC:TREM2, var("p.Arg47His"))
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "We show a significant association of the T allele of the rs75932628 \
variant of TREM2 with AD (allelic odds ratio 11.08, 95% confidence \
interval 2.55-48.09, and Yates' corrected p value = 0.000146)."

g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

# Behind a paywall
##################################################################################

SET Citation = {"PubMed", "Immunity. 2017 Sep 19;47(3):566-581.e9. doi: 10.1016/j.immuni.2017.08.008.", "28930663"}

SET Section = "Abstract"

SET Support = "Microglia play a pivotal role in the maintenance of brain homeostasis \
but lose homeostatic function during neurodegenerative disorders."

SET MeSHAnatomy = "Brain"
a(MESHA:Microglia) -- bp(GOBP:"homeostatic process")
path(MESHD:"Neurodegenerative Diseases") negativeCorrelation bp(GOBP:"homeostatic process")
UNSET MeSHAnatomy

SET Support = "We identified a specific apolipoprotein E (APOE)-dependent molecular \
signature in microglia from models of amyotrophic lateral sclerosis \
(ALS), multiple sclerosis (MS), and Alzheimer's disease (AD) and in \
microglia surrounding neuritic β-amyloid (Aβ)-plaques in the brains of \
people with AD."

p(HGNC:APOE, loc(MESHA:Microglia)) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:APOE, loc(MESHA:Microglia)) positiveCorrelation path(MESHD:"Amyotrophic Lateral Sclerosis")
p(HGNC:APOE, loc(MESHA:Microglia)) positiveCorrelation path(MESHD:"Multiple Sclerosis")
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:APOE, loc(MESHA:Microglia)) positiveCorrelation a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))
UNSET MeSHDisease

SET Support = "The APOE pathway mediated a switch from a homeostatic to a \
neurodegenerative microglia phenotype after phagocytosis of apoptotic \
neurons."

SET MeSHAnatomy = "Microglia"
bp(GOBP:"apoptotic cell clearance") -> bp(HP:Neurodegeneration)
bp(GOBP:"apoptotic cell clearance") -| bp(GOBP:"homeostatic process")
act(p(HGNC:APOE), ma(GOBP:signaling)) -- bp(HP:Neurodegeneration)
act(p(HGNC:APOE), ma(GOBP:signaling)) -- bp(GOBP:"homeostatic process")
UNSET MeSHAnatomy

SET Support = "TREM2 (triggering receptor expressed on myeloid cells 2) induced APOE \
signaling, and targeting the TREM2-APOE pathway restored the \
homeostatic signature of microglia in ALS and AD mouse models and \
prevented neuronal loss in an acute model of neurodegeneration."

p(HGNC:TREM2) -> act(p(HGNC:APOE), ma(GOBP:signaling))
SET Species = "10090"
SET MeSHDisease = {"Amyotrophic Lateral Sclerosis", "Alzheimer Disease"}
SET MeSHAnatomy = "Microglia"
p(MGI:Trem2) -| bp(GOBP:"homeostatic process")
UNSET MeSHAnatomy
p(MGI:Trem2) -| a(MESHA:Neurons)
act(p(HGNC:APOE), ma(GOBP:signaling)) -| a(MESHA:Neurons)
UNSET MeSHDisease
UNSET Species

UNSET Section

# Tony
##################################################################################

SET Citation = {"PubMed", "Cell. 2015 Mar 12;160(6):1061-71. doi: 10.1016/j.cell.2015.01.049. Epub 2015 Feb 26.", "25728668"}

SET Section = "Abstract"

SET Support = "Triggering receptor expressed on myeloid cells 2 (TREM2) is a \
microglial surface receptor that triggers intracellular protein \
tyrosine phosphorylation."

a(MESHA:Microglia) -> p(HGNC:TREM2)
p(HGNC:TREM2, loc(MESHA:Microglia)) -> bp(GOBP:"peptidyl-tyrosine phosphorylation")

SET Support = "Recent genome-wide association studies have shown that a rare R47H \
mutation of TREM2 correlates with a substantial increase in the risk \
of developing Alzheimer's disease (AD)."

SET StudyType = "GWAS"
g(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET StudyType

SET Support = "To address the basis for this genetic association, we studied TREM2 \
deficiency in the 5XFAD mouse model of AD. We found that TREM2 \
deficiency and haploinsufficiency augment β-amyloid (Aβ) accumulation \
due to a dysfunctional response of microglia, which fail to cluster \
around Aβ plaques and become apoptotic."

SET Species = "10090"
SET MouseStrain = "5XFAD"
SET MeSHDisease = "Alzheimer Disease"
p(MGI:Trem2) -| a(CHEBI:"amyloid-beta")
p(MGI:Trem2) -> a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))
UNSET Species
UNSET MouseStrain
UNSET MeSHDisease

SET Support = "We further demonstrate that TREM2 senses a broad array of anionic and \
zwitterionic lipids known to associate with fibrillar Aβ in lipid \
membranes and to be exposed on the surface of damaged neurons."

a(GOCC:"lipid particle", loc(CHEBI:"amyloid-beta")) positiveCorrelation act(p(HGNC:TREM2), ma(GOBP:"protein localization to lipid particle"))

SET Support = "Remarkably, the R47H mutation impairs TREM2 detection of lipid \
ligands."

p(HGNC:TREM2, var("p.Arg47His")) -| act(p(HGNC:TREM2), ma(GOBP:"protein localization to lipid particle"))

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Mol Neurodegener. 2013 Jun 21;8:19. doi: 10.1186/1750-1326-8-19.", "23800361"}

SET Section = "Abstract"

SET Support = "A rare variant in the Triggering Receptor Expressed on Myeloid cells 2 \
(TREM2) gene has been reported to be a genetic risk factor for \
Alzheimer's disease by two independent groups (Odds ratio between \
2.9-4.5)."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Given the key role of TREM2 in the effective phagocytosis of apoptotic \
neuronal cells by microglia, we hypothesized that dysfunction of TREM2 \
may play a more generalized role in neurodegeneration."

SET MeSHAnatomy = "Microglia"
p(HGNC:TREM2) -> bp(GOBP:phagocytosis)
p(HGNC:TREM2) -> bp(GOBP:"apoptotic cell clearance")
UNSET MeSHAnatomy

SET Support = "A significant association was observed for the TREM2 p.R47H \
substitution in susceptibility to frontotemporal dementia (OR = 5.06; \
p-value = 0.001) and Parkinson's disease (OR = 2.67; p-value = 0.026), \
while no evidence of association with risk of amyotrophic lateral \
sclerosis, progressive supranuclear palsy or ischemic stroke was \
observed."

p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Parkinson Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Exp Med. 2015 Mar 9;212(3):287-95. doi: 10.1084/jem.20142322. Epub 2015 Mar 2.", "25732305"}

SET Section = "Abstract"

SET Support = "Variants in triggering receptor expressed on myeloid cells 2 (TREM2) \
confer high risk for Alzheimer's disease (AD) and other \
neurodegenerative diseases."

p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Here, we report that TREM2 is up-regulated on myeloid cells \
surrounding amyloid deposits in AD mouse models and human AD tissue."

SET Species = {"10090", "9606"}
SET MeSHDisease = "Alzheimer Disease"
a(MESHA:"Myeloid Cells", loc(CHEBI:"amyloid-beta")) -> p(HGNC:TREM2)
a(MESHA:"Myeloid Cells", loc(CHEBI:"amyloid-beta")) -> p(MGI:Trem2)
UNSET Species

SET Support = "In AD mice deficient for TREM2, the CD45(hi)Ly6C(+) macrophages are \
virtually eliminated, resulting in reduced inflammation and \
ameliorated amyloid and tau pathologies."

SET Species = "10090"
p(MGI:Trem2) -> path(MESHD:Inflammation)
p(MGI:Trem2) -| a(CHEBI:"amyloid-beta")
p(MGI:Trem2) -| p(HGNC:MAPT, pmod(Ph))
UNSET Species

UNSET MeSHDisease

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Alzheimers Res Ther. 2013 May 21;5(3):24. doi: 10.1186/alzrt178. eCollection 2013.", "23692967"}

SET Section = "Abstract"

SET Support = "In addition, genetic, pathologic and biological studies of APOE have \
also indicated that the genetic risk for AD associated with APOE4 can \
be attributed, at least in part, to its pro-amyloidogenic effect on \
Aβ."

p(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")) -> a(CHEBI:"amyloid-beta")

SET Support = "Very recently, two large consortiums demonstrated that rare variants \
in the triggering receptor expressed on myeloid cells 2 (TREM2) gene \
confer significant risk for AD."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "TREM2 is a type 1 membrane receptor protein primarily expressed on \
microglia in the central nervous system that has been shown to \
regulate phagocytosis and activation of monocytes."

a(MESHA:Microglia, loc(MESHA:"Central Nervous System")) -> p(HGNC:TREM2)
p(HGNC:TREM2, loc(MESHA:Microglia)) reg bp(GOBP:phagocytosis)
p(HGNC:TREM2, loc(MESHA:Microglia)) reg bp(GOBP:"monocyte activation")

SET Support = "Previously it had been shown that homozygous loss of function \
mutations in TREM2 cause polycystic lipomembranous osteodysplasia with \
sclerosing leukoencephalopathy (PLOSL, Nasu Hakola disease) and also a \
pure form of early-onset dementia."

p(HGNC:TREM2) negativeCorrelation path(DO:"Nasu-Hakola disease")
p(HGNC:TREM2) negativeCorrelation path(MESHD:Dementia)

SET Support = "The association of TREM2 variants with AD brings innate immune \
signaling into the light, affirming innate immunity's role as a \
significant factor in AD pathogenesis."

bp(GOBP:"innate immune response") -- path(MESHD:"Alzheimer Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Alzheimers Dis Parkinsonism. 2014 Nov;4(5). pii: 160.", "25664220"}

SET Section = "Abstract"

SET Support = "Alzheimer's disease (AD) is the leading cause of dementia worldwide."

path(MESHD:"Alzheimer Disease") -> path(MESHD:Dementia)

SET Support = "Several risk factors have been discovered that are associated with AD, \
with advancing age being the most prominent."

bp(GOBP:aging) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "The most well known genetic risk factor for LOAD is inheritance of the \
apolipoprotein (apo) E4 allele."

g(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "Recently, rare variants of TREM2 have been reported as a significant \
risk factor for LOAD, comparable to inheritance of apoE4."

g(HGNC:TREM2, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Alzheimers Dement. 2017 Jun;13(6):663-673. doi: 10.1016/j.jalz.2016.10.005. Epub 2016 Dec 8.", "27939925"}

SET Section = "Abstract"

SET Support = "A variant within a DNase hypersensitive site 5' of TREM2, rs9357347-C, \
associates with reduced AD risk and increased TREML1 and TREM2 levels \
(uncorrected P = 6.3 × 10-3 and 4.6 × 10-2, respectively)."

g(dbSNP:rs9357347) negativeCorrelation path(MESHD:"Alzheimer Disease")
g(dbSNP:rs9357347) positiveCorrelation p(HGNC:TREM2)
g(dbSNP:rs9357347) positiveCorrelation p(HGNC:TREML1)

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurol Genet. 2016 Jul 14;2(4):e85. doi: 10.1212/NXG.0000000000000085. eCollection 2016 Aug.", "27458607"}

SET Section = "Abstract"

SET Support = "Concomitant Alzheimer disease-related pathology including amyloid \
plaques and, to a lesser degree, neurofibrillary tangles are often \
present."

# This article is discussing Dementia with Lewy bodies (DLB)
path(DO:"Lewy body dementia") -- path(MESHD:"Plaque, Amyloid")
path(DO:"Lewy body dementia") -- a(MESHCS:"Neurofibrillary Tangles")

SET Support = "A recent genetic association study examining known hits from PD and AD \
identified variants at both the α-synuclein (SNCA) and APOE loci as \
influencing the individual risk to DLB."

g(HGNC:SNCA, var("?")) positiveCorrelation path(DO:"Lewy body dementia")
g(HGNC:APOE, var("?")) positiveCorrelation path(DO:"Lewy body dementia")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Sci Signal. 2010 May 18;3(122):ra38. doi: 10.1126/scisignal.2000500.", "20484116"}

SET Section = "Abstract"

SET Support = "The activation and fusion of macrophages and of osteoclasts require \
the adaptor molecule DNAX-activating protein of 12 kD (DAP12), which \
contains immunoreceptor tyrosine-based activation motifs (ITAMs)."

p(HGNC:TYROBP) -> bp(GOBP:"macrophage fusion")
p(HGNC:TYROBP) -> bp(GOBP:"macrophage activation")
p(HGNC:TYROBP) -> bp(GOBP:"osteoclast fusion")

SET Support = "TREM2 (triggering receptor expressed on myeloid cells-2) is the main \
DAP12-associated receptor in osteoclasts and, similar to DAP12 \
deficiency, loss of TREM2 in humans leads to Nasu-Hakola disease, \
which is characterized by bone cysts and dementia."

p(HGNC:TREM2) -- p(HGNC:TYROBP, loc(MESHA:Osteoclasts))
p(HGNC:TREM2) negativeCorrelation path(DO:"Nasu-Hakola disease")
path(DO:"Nasu-Hakola disease") -> path(HP:"Bone cyst")
path(DO:"Nasu-Hakola disease") -> path(MESHD:Dementia)

SET Support = "Furthermore, in vitro experiments have shown that deficiency in DAP12 \
or TREM2 leads to impaired osteoclast development and the formation of \
mononuclear osteoclasts."

SET Assay = "in vitro"
p(HGNC:TREM2) -> bp(GOBP:"osteoclast development")
p(HGNC:TYROBP) -> bp(GOBP:"osteoclast development")
p(HGNC:TREM2) -> bp(GOBP:"multinuclear osteoclast differentiation")
p(HGNC:TYROBP) -> bp(GOBP:"multinuclear osteoclast differentiation")
UNSET Assay

SET Support = "Here, we demonstrate that the ligation of TREM2 activated \
phosphatidylinositol 3-kinase (PI3K), extracellular signal-regulated \
kinase 1 (ERK1) and ERK2, and the guanine nucleotide exchange factor \
Vav3; induced the mobilization of intracellular calcium (Ca(2+)) and \
the reorganization of actin; and prevented apoptosis."

# Performed in bone marrow derived macrophages
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> act(p(SFAM:"PIK3C Class IA Family"), ma(kin))
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> act(p(HGNC:MAPK3), ma(kin))
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> act(p(HGNC:MAPK1), ma(kin))
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> act(p(HGNC:VAV3))
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> bp(GOBP:"calcium ion import into cell")
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> bp(GOBP:"actin cytoskeleton reorganization")
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -| bp(MESHPP:Apoptosis)

SET Support = "Src homology 2 (SH2) domain-containing inositol phosphatase-1 (SHIP1) \
inhibited TREM2- and DAP12-induced signaling by binding to DAP12 in an \
SH2 domain-dependent manner and preventing the recruitment of PI3K to \
DAP12."

# INPP5D = SHIP1
complex(p(HGNC:INPP5D), p(HGNC:TYROBP)) -| act(complex(p(HGNC:TREM2), p(HGNC:TYROBP)), ma(GOBP:signaling))
complex(p(HGNC:INPP5D), p(HGNC:TYROBP)) -| complex(p(HGNC:INPP5D), p(SFAM:"PIK3C Class IA Family"))

UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Neurol Sci. 2015 Aug 15;355(1-2):193-5. doi: 10.1016/j.jns.2015.05.010. Epub 2015 May 16.", "26026943"}

SET Section = "Abstract"

SET Support = "Although, rs75932628 in triggering receptor expressed on myeloid cells \
2 (TREM2) was shown to increase the risk for Alzheimer's disease, \
there is no agreement on the association between this variant and the \
risk for amyotrophic lateral sclerosis (ALS)."

g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "ASN Neuro. 2017 Jul-Aug;9(4):1759091417716610. doi: 10.1177/1759091417716610.", "28683563"}

SET Section = "Abstract"

SET Support = "Microglia are the primary innate immune cell type in the brain, and \
their dysfunction has been linked to a variety of central nervous \
system disorders."

a(MESHA:Microglia, loc(MESHA:Brain)) negativeCorrelation path(MESHD:"Central Nervous System Diseases")

SET Support = "Previous studies have reported that microglia-like cells can be \
derived from human monocytes or pluripotent stem cells."

a(MESHA:Monocytes) -> a(MESHA:Microglia)
a(MESHA:"Pluripotent Stem Cells") -> a(MESHA:Microglia)

SET Support = "We hypothesized that functional microglia could be derived from human \
stem cells by employing BMP-4 mesodermal specification followed by \
exposure to microglia-relevant cytokines, M-CSF, GM-CSF, IL-34, and \
TGF-β."

SET MeSHAnatomy = "Stem Cells"
# CSF1 = M-CSF, CSF2 = GM-CSF, TGFB1 = TGF-β
p(HGNC:BMP4) -> a(MESHA:Microglia)
p(HGNC:CSF1) -> a(MESHA:Microglia)
p(HGNC:CSF2) -> a(MESHA:Microglia)
p(HGNC:IL34) -> a(MESHA:Microglia)
p(HGNC:TGFB1) -> a(MESHA:Microglia)
UNSET MeSHAnatomy

SET Support = "Using immunofluorescence microscopy, flow cytometry, and reverse \
transcription polymerase chain reaction, we observed cells with \
microglia morphology expressing a repertoire of markers associated \
with microglia: Iba1, CX3CR1, CD11b, TREM2, HexB, and P2RY12."

SET Assay = {"immunofluorescence microscopy", "flow cytometry", "RT-PCR"}
# AIF1 = Iba1, ITGAM = CD11b
a(MESHA:Microglia) -> p(HGNC:AIF1)
a(MESHA:Microglia) -> p(HGNC:CX3CR1)
a(MESHA:Microglia) -> p(HGNC:ITGAM)
a(MESHA:Microglia) -> p(HGNC:TREM2)
a(MESHA:Microglia) -> p(HGNC:HEXB)
a(MESHA:Microglia) -> p(HGNC:P2RY12)
a(MESHA:Microglia) positiveCorrelation p(HGNC:AIF1)
a(MESHA:Microglia) positiveCorrelation p(HGNC:CX3CR1)
a(MESHA:Microglia) positiveCorrelation p(HGNC:ITGAM)
a(MESHA:Microglia) positiveCorrelation p(HGNC:TREM2)
a(MESHA:Microglia) positiveCorrelation p(HGNC:HEXB)
a(MESHA:Microglia) positiveCorrelation p(HGNC:P2RY12)
UNSET Assay

SET Support = "These microglia-like cells maintain myeloid functional phenotypes \
including Aβ peptide phagocytosis and induction of pro-inflammatory \
gene expression in response to lipopolysaccharide stimulation."

a(CHEBI:lipopolysaccharide) -> bp(GOBP:"beta-amyloid clearance")
# "Pro-inflammatory genes" measured in paper are IL-1β, CCL2, and TNFAIP3
a(CHEBI:lipopolysaccharide)  -> r(HGNC:IL1B, loc(MESHA:Microglia))
a(CHEBI:lipopolysaccharide)  -> r(HGNC:CCL2, loc(MESHA:Microglia))
a(CHEBI:lipopolysaccharide)  -> r(HGNC:TNFAIP3, loc(MESHA:Microglia))

UNSET Section

##################################################################################

SET Citation = {"PubMed", "EMBO Mol Med. 2017 Oct;9(10):1356-1365. doi: 10.15252/emmm.201707672.", "28855300"}

SET Section = "Abstract"

SET Support = "Sequence variations occurring in the gene encoding the triggering \
receptor expressed on myeloid cells 2 (TREM2) support an essential \
function of microglia and innate immunity in the pathogenesis of \
Alzheimer's disease (AD) and other neurodegenerative disorders."

g(HGNC:TREM2, var("?")) negativeCorrelation bp(GOBP:"microglial cell activation")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")

SET Support = "Missense mutations in the immunoglobulin (Ig)-like domain such as \
p.T66M and p.Y38C retain TREM2 within the endoplasmic reticulum and \
reduce shedding as well as TREM2-dependent phagocytosis."

p(HGNC:TREM2, var("p.Thr66Met")) -| act(p(HGNC:TREM2), ma(GOBP:"membrane protein ectodomain proteolysis"))
p(HGNC:TREM2, var("p.Thr66Met")) -| bp(GOBP:phagocytosis)
p(HGNC:TREM2, var("p.Tyr38Cys")) -| act(p(HGNC:TREM2), ma(GOBP:"membrane protein ectodomain proteolysis"))
p(HGNC:TREM2, var("p.Tyr38Cys")) -| bp(GOBP:phagocytosis)

SET Support = "Using mass spectrometry, we have now determined the cleavage site of \
TREM2. TREM2 is shed by proteases of the ADAM (a disintegrin and \
metalloproteinase domain containing protein) family C-terminal to \
histidine 157, a position where an AD-associated coding variant has \
been discovered (p.H157Y) in the Han Chinese population."

# Checked paper and specifically ADAM10 is the protease investgated
SET TechnologyUsed = "mass spectrometry"
p(HGNC:ADAM10) -> p(HGNC:TREM2, frag("1_157"))
p(HGNC:TREM2) hasVariant p(HGNC:TREM2, var("p.His157Tyr"))
UNSET TechnologyUsed

SET Support = "Opposite to the characterized mutations within the Ig-like domain, \
such as p.T66M and p.Y38C, the p.H157Y variant within the stalk region \
leads to enhanced shedding of TREM2."

p(HGNC:TREM2, var("p.His157Tyr")) -> act(p(HGNC:TREM2), ma(GOBP:"membrane protein ectodomain proteolysis"))
p(HGNC:TREM2, var("p.His157Tyr")) -> p(HGNC:TREM2, frag("1_157"))

SET Support = "Elevated ectodomain shedding reduces cell surface full-length TREM2 \
and lowers TREM2-dependent phagocytosis."

act(p(HGNC:TREM2), ma(GOBP:"membrane protein ectodomain proteolysis")) -| p(HGNC:TREM2, frag("1_157"), loc(MESHA:"Extracellular Space"))
act(p(HGNC:TREM2), ma(GOBP:"membrane protein ectodomain proteolysis")) -| bp(GOBP:phagocytosis)

UNSET Section

##################################################################################

SET Citation = {"PubMed", "ACS Chem Neurosci. 2016 Apr 20;7(4):420-7. doi: 10.1021/acschemneuro.5b00313. Epub 2016 Feb 19.", "26854967"}

SET Section = "Abstract"

SET Support = "Alzheimer's disease (AD), the most common cause of dementia in the \
elderly, is a complex neurodegenerative disease marked by the \
appearance of amyloid-β (Aβ) plaques and hyperphosphorylated tau \
tangles."

path(MESHD:"Alzheimer Disease") isA path(MESHD:"Neurodegenerative Diseases")
path(MESHD:"Alzheimer Disease") positiveCorrelation a(CHEBI:"amyloid-beta", loc(MESHA:Brain))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:MAPT, pmod(Ph))
path(MESHD:"Alzheimer Disease") positiveCorrelation a(MESHCS:"Neurofibrillary Tangles")

SET Support = "Particularly interesting are variants in the microglial-expressed \
receptor TREM2 which are associated with a 2-4-fold increased risk of \
developing AD."

p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Although some confounding conflicting data have emerged from these \
studies regarding the role of TREM2 in regulating Aβ deposition within \
the hippocampus, the most consistent and striking observation is a \
strong decrease in microgliosis surrounding Aβ plaques in TREM2 \
haploinsufficient and TREM2 deficient mice."

SET Species = "10090"
# microgliosis just means accumulation of microglia at specific sites
g(MGI:Trem2) positiveCorrelation a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))
UNSET Species

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neuron. 2017 Apr 19;94(2):237-248. doi: 10.1016/j.neuron.2017.02.042.", "28426958"}

SET Section = "Abstract"

SET Support = "Genetic factors, such as rare variants in the microglial-expressed \
gene TREM2, strongly impact the lifetime risk of developing AD."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

# Rest of abstract was describing what they will review in this paper so no new information was given

UNSET Section

##################################################################################

SET Citation = {"PubMed", "JAMA Neurol. 2013 Jan;70(1):78-84. doi: 10.1001/jamaneurol.2013.579.", "23318515"}

SET Section = "Abstract"

SET Support = "In 3 probands with FTD-like disease, we identified different \
homozygous mutations in TREM2 that had previously been associated with \
polycystic lipomembranous osteodysplasia with sclerosing \
leukoencephalopathy (PLOSL)."

# Polycystic lipomembranous osteodysplasia with sclerosing leukoencephalopathy(PLOSL) is Nasu-Hakola disease
g(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")

SET Support = "Our results show that TREM2 is responsible for an unexpectedly high \
number of dementia cases in our cohort, suggesting that this gene \
should be taken into account when mutations in other dementia genes \
are excluded."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:Dementia)

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Brain Pathol. 2015 Jul;25(4):469-80. doi: 10.1111/bpa.12190. Epub 2014 Nov 11.", "25186950"}

SET Section = "Abstract"

SET Support = "Triggering receptor expressed by myeloid cells 2 (TREM2), a member of \
the immunoglobulin superfamily, has anti-inflammatory phagocytic \
function in myeloid cells."

SET MeSHAnatomy = "Myeloid Cells"
p(HGNC:TREM2) -> bp(GOBP:phagocytosis)
bp(GOBP:phagocytosis) -| path(MESHD:Inflammation)
UNSET MeSHAnatomy

SET Support = "Several studies have shown that TREM2 gene variant rs75932628-T \
increased the risks for Alzheimer's disease (AD), Parkinson's disease, \
frontotemporal dementia and amyotrophic lateral sclerosis."

g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Parkinson Disease")
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Amyotrophic Lateral Sclerosis")

SET Support = "Indeed, new evidence showed that several mutations in the \
immunoglobulin-like V-region led to low cell surface expression of \
TREM2 and reduced phagocytic function."

p(HGNC:TREM2, var("?")) -| bp(GOBP:phagocytosis)
p(HGNC:TREM2, var("?")) -| p(HGNC:TREM2, loc(MESHA:"Cell Membrane"))

SET Support = "Because of the emerging importance in understanding TREM2 expression \
and functions in human neurodegenerative diseases, we conducted \
biochemical and morphological studies of TREM2 expression in human \
post-mortem temporal cortical samples from AD and normal cases. \
Increased expression of TREM2 protein was found to significantly \
correlate with increases of phosphorylated-tau and active caspase 3, a \
marker of apoptosis, and also loss of the presynaptic protein SNAP25."

SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TREM2, loc(MESHA:"Temporal Lobe")) positiveCorrelation p(HGNC:MAPT, pmod(Ph), loc(MESHA:"Temporal Lobe"))
p(HGNC:TREM2) positiveCorrelation act(p(HGNC:CASP3, loc(MESHA:"Temporal Lobe")))
act(p(HGNC:CASP3)) biomarkerFor bp(GOBP:"apoptotic process")
p(HGNC:TREM2) negativeCorrelation p(HGNC:SNAP25)
UNSET MeSHDisease

SET Support = "Strong intensities of TREM2 immunoreactivity were observed in the \
microglia associated with amyloid plaques and in neuritic pathology- \
enriched areas."

p(HGNC:TREM2) positiveCorrelation a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))


UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Alzheimers Dis. 2014;41(4):1031-8. doi: 10.3233/JAD-140225.", "24762945"}

SET Section = "Abstract"

SET Support = "Rare non-synonymous variants of TREM2 have recently been shown to be \
associated with Alzheimer's disease (AD) in Caucasians."

SET Ethnicity = "Caucasian"
p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET Ethnicity

# Rest of abstract showed no significant data

UNSET Section

##################################################################################

SET Citation = {"PubMed", "FASEB J. 2015 May;29(5):1754-62. doi: 10.1096/fj.14-262683. Epub 2015 Jan 15.", "25593125"}

SET Section = "Abstract"

SET Support = "Moreover, immune activation is heightened in apolipoprotein E (APOE) \
ε4 carriers; inhibitors of prostaglandin (PG) synthesis show a \
partially protective effect on AD risk from APOE ε4; and genetic \
variants in triggering receptor expressed on myeloid cells 2 (TREM2) \
are a rare but potent risk for AD."

p(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")) positiveCorrelation bp(GOBP:"activation of immune response")
a(CHEBI:prostaglandin) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "We tested the hypothesis that APOE ε4 inheritance modulates both the \
PGE2 pathway and TREM2 expression using primary murine microglia from \
targeted replacement (TR) APOE3/3 and APOE4/4 mice. Microglial \
cyclooxygenase-2, microsomal PGE synthase, and PGE2 expression were \
increased 2- to 25-fold in both genotypes by TLR activators; however, \
this induction was significantly (P < 0.01) greater in TR APOE4/4 \
microglia with TLR3 and TLR4 activators."

# TLR activators include: TLR2 = Pam (N-palmitoyl-S-[2,3-bis(palmitoyloxy)propyl]-Cys-Ser-Lys-Lys-Lys-Lys),  TLR3 = poly(I:C), TLR4 = LPS \
# TLR7 = Loxoribine, TLR9 = CpG d(CG)12
# Ptgs2 = COX2, Ptges = mPGE2
# APOE4/4 mice are mice that express human APOE4
SET Species = "10090"
SET MouseStrain = {"TR APOE3/3", "TR APOE4/4"}
a(CHEBI:"N-palmitoyl-S-[2,3-bis(palmitoyloxy)propyl]-Cys-Ser-Lys-Lys-Lys-Lys") -> r(MGI:Ptgs2, loc(MESHA:Microglia))
a(CHEBI:"poly(I:C)") -> r(MGI:Ptgs2, loc(MESHA:Microglia))
a(CHEBI:lipopolysaccharide) -> r(MGI:Ptgs2, loc(MESHA:Microglia))
a(MESHC:loxoribine) -> r(MGI:Ptgs2, loc(MESHA:Microglia))
a(CHEBI:"d(CG)12") -> r(MGI:Ptgs2, loc(MESHA:Microglia))
a(CHEBI:"N-palmitoyl-S-[2,3-bis(palmitoyloxy)propyl]-Cys-Ser-Lys-Lys-Lys-Lys") -> r(MGI:Ptges, loc(MESHA:Microglia))
a(CHEBI:"poly(I:C)") -> r(MGI:Ptges, loc(MESHA:Microglia))
a(CHEBI:lipopolysaccharide) -> r(MGI:Ptges, loc(MESHA:Microglia))
a(MESHC:loxoribine) -> r(MGI:Ptges, loc(MESHA:Microglia))
a(CHEBI:"d(CG)12") -> r(MGI:Ptges, loc(MESHA:Microglia))
UNSET MouseStrain
SET ChallengeType = {"lipopolysaccharide stimulation", "poly(I:C) stimulation"}
g(MGI:Apoe, var("p.Leu115Arg"), var("p.His158Arg")) -| r(MGI:Ptges, loc(MESHA:Microglia))
g(MGI:Apoe, var("p.Leu115Arg"), var("p.His158Arg")) -| r(MGI:Ptgs2, loc(MESHA:Microglia))
UNSET ChallengeType

SET Support = "Microglial TREM2 expression was reduced approximately 85% by all TLR \
activators; this reduction was approximately one-third greater in \
microglia from TR APOE4/4 mice."

a(CHEBI:"N-palmitoyl-S-[2,3-bis(palmitoyloxy)propyl]-Cys-Ser-Lys-Lys-Lys-Lys") -| r(MGI:Trem2, loc(MESHA:Microglia))
a(CHEBI:"poly(I:C)") -| r(MGI:Trem2, loc(MESHA:Microglia))
a(CHEBI:lipopolysaccharide) -| r(MGI:Trem2, loc(MESHA:Microglia))
a(MESHC:loxoribine) -| r(MGI:Trem2, loc(MESHA:Microglia))
a(CHEBI:"d(CG)12") -| r(MGI:Trem2, loc(MESHA:Microglia))
SET MouseStrain = "TR APOE4/4"
a(CHEBI:"N-palmitoyl-S-[2,3-bis(palmitoyloxy)propyl]-Cys-Ser-Lys-Lys-Lys-Lys") -| r(MGI:Trem2, loc(MESHA:Microglia))
a(CHEBI:"poly(I:C)") -| r(MGI:Trem2, loc(MESHA:Microglia))
a(CHEBI:lipopolysaccharide) -| r(MGI:Trem2, loc(MESHA:Microglia))
a(MESHC:loxoribine) -| r(MGI:Trem2, loc(MESHA:Microglia))
a(CHEBI:"d(CG)12") -| r(MGI:Trem2, loc(MESHA:Microglia))
UNSET MouseStrain

UNSET Species

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Alzheimers Dement (Amst). 2016 Dec 22;6:50-59. doi: 10.1016/j.dadm.2016.12.002. eCollection 2017.", "28149943"}

SET Section = "Abstract"

SET Support = "Preclinical and MCI carriers showed higher YKL-40 levels. There was a \
significant interaction in the association between YKL-40 levels and \
gray-matter volume according to ε4 status. No similar effects could be \
detected for sTREM2 levels."

# YKL-40 = CHI3L1
# Measured in the CSF (checked paper)
# TODO how to annotate preclinical?
path(ADO:"Mild_cognitive_impairment") positiveCorrelation p(HGNC:CHI3L1, loc(MESHA:"Cerebrospinal Fluid"))
SET ApoEɛ4Status = "positive"
a(MESHA:"Gray Matter") negativeCorrelation p(HGNC:CHI3L1, loc(MESHA:"Cerebrospinal Fluid"))
UNSET ApoEɛ4Status
SET ApoEɛ4Status = "negative"
a(MESHA:"Gray Matter") positiveCorrelation p(HGNC:CHI3L1, loc(MESHA:"Cerebrospinal Fluid"))
UNSET ApoEɛ4Status

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Glia. 2013 Jul;61(7):1122-33. doi: 10.1002/glia.22501. Epub 2013 Apr 30.", "23633299"}

SET PublicationType = "Research"

SET Section = "Abstract"

SET Support = "Sialic-acid-binding immunoglobulin-like lectin-h (Siglec-h) is a \
recently identified mouse-specific CD33-related Siglec that signals \
via DAP12/TYROBP."

SET Species = "10090"
p(MGI:Siglech) -> act(p(MGI:Tyrobp), ma(GOBP:"transmembrane receptor protein tyrosine kinase signaling pathway"))
UNSET Species

SET Support = "Expression of Siglec-h has been observed on plasmacytoid dendritic \
cells and microglia, but the ligand and the function of Siglec-h \
remained elusive."

SET Species = "10090"
a(MESHA:"Dendritic Cells") -> p(MGI:Siglech)
a(MESHA:Microglia) -> p(MGI:Siglech)
UNSET Species

SET Support = "Microglial Siglec-h acted as phagocytosis receptor since targeting of \
microsphere beads to Siglec-h triggered their uptake into the \
microglia."

SET Species = "10090"
act(p(MGI:Siglech)) -> bp(GOBP:phagocytosis)
SET MeSHAnatomy = "Microglia"
act(p(MGI:Siglech)) -> tloc(p(MGI:Siglech), fromLoc(MESHA:"Cell Membrane"), toLoc(MESHA:Cytoplasm))
UNSET MeSHAnatomy
UNSET Species

SET Support = "The extracellular domain of Siglec-h protein bound to mouse glioma \
lines, but not to astrocytes or other normal mouse cells."

SET Species = "10090"
p(MGI:Siglech) positiveCorrelation path(MESHD:Glioma)
UNSET Species

SET Support = "Microglial cells stimulated to express Siglec-h engulfed intact glioma \
cells without prior induction of apoptosis and slightly reduced glioma \
cell number in culture."

SET Species = "10090"
bp(GOBP:"microglial cell activation") -> p(MGI:Siglech)
p(MGI:Siglech, loc(MESHA:Microglia)) -> bp(GOBP:phagocytosis)
p(MGI:Siglech, loc(MESHA:Microglia)) -| path(MESHD:Glioma)
UNSET Species

UNSET Section

UNSET PublicationType

# Results section locked behind a paywall

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2014 Jun;35(6):1252-4. doi: 10.1016/j.neurobiolaging.2013.11.013. Epub 2013 Nov 20.", "24378087"}

SET Section = "Abstract"

SET Support = "Recent studies have found an association between a variant in \
triggering receptor expressed on myeloid cells 2 (TREM2) \
(rs75932628-T) and both Alzheimer's disease (AD) and cognitive \
function in individuals aged 80-100 years."

SET AgeRange = "80-100 years old"
g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(dbSNP:rs75932628) positiveCorrelation path(HP:"Cognitive impairment")
UNSET AgeRange

# Rest of abstract discussed the parents of young people with this mutation

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2017 Jun;54:133-143. doi: 10.1016/j.neurobiolaging.2017.02.012. Epub 2017 Mar 9.", "28365005"}

SET Section = "Abstract"

SET Support = "Heterozygous triggering receptor expressed on myeloid cells (TREM2) \
mutations are an Alzheimer's disease (AD) risk factor."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "TREM2 was significantly upregulated in sAD frontal cortex but stable \
in hippocampus."

# sAD = severe AD
path(ADO:Severe) positiveCorrelation p(HGNC:TREM2, loc(MESHA:"Prefrontal Cortex"))

SET Support = "Iba1 immunopositive microglia counts increased significantly in \
frontal cortex containing plaques in sAD."

# Iba1 = AIF1
p(HGNC:AIF1, loc(MESHA:Microglia)) positiveCorrelation a(CHEBI:"amyloid-beta", loc(MESHA:"Prefrontal Cortex"))
p(HGNC:AIF1, loc(MESHA:Microglia)) positiveCorrelation path(ADO:Severe)

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Am J Alzheimers Dis Other Demen. 2015 Sep;30(6):541-6. doi: 10.1177/1533317515577128.", "25852195"}

SET Section = "Abstract"

SET Support = "The results showed that rs75932628 variant was significantly \
associated with AD in caucasian population (P < .001, odds ratio ¼ \
3.17, 95% confidence interval 2.45-4.09)."

SET Ethnicity = "Caucasian"
# rs75932628 is a trem2 variant
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET Ethnicity

UNSET Section

##################################################################################

SET Citation = {"PubMed", "JAMA Neurol. 2015 Aug;72(8):920-7. doi: 10.1001/jamaneurol.2015.0979.", "26076170"}

SET Section = "Abstract"

SET Support = "The R47H variant in the triggering receptor expressed on myeloid cells \
2 gene (TREM2), a modulator of the immune response of microglia, is a \
strong genetic risk factor for Alzheimer disease (AD) and possibly \
other neurodegenerative disorders."

SET MeSHAnatomy = "Microglia"
p(HGNC:TREM2, var("p.Arg47His")) -- bp(GOBP:"regulation of immune response")
UNSET MeSHAnatomy
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "R47H carriers demonstrated a shortened disease duration (mean [SD], \
6.7 [2.8] vs 11.1 [6.6] years; 2-tailed t test; P = .04) and more \
frequent α-synucleinopathy."

p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(DO:synucleinopathy)

SET Support = "The panmicroglial marker ionized calcium-binding adapter molecule 1 \
was decreased in all AD cases and the decrease was most pronounced in \
R47H carriers (mean [SD], in the hilus: 0.114 [0.13] for R47H_AD vs \
0.574 [0.26] for control individuals; 2-tailed t test; P = .005 and vs \
0.465 [0.32] for AD; P = .02; in frontal cortex gray matter: 0.006 \
[0.004] for R47H_AD vs 0.016 [0.01] for AD; P = .04 and vs 0.033 \
[0.013] for control individuals; P < .001)."

# Ionized calcium-binding adapter molecule 1 = Iba1 = AIF-1
path(MESHD:"Alzheimer Disease") negativeCorrelation p(HGNC:AIF1)
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TREM2, var("p.Arg47His")) negativeCorrelation p(HGNC:AIF1)
UNSET MeSHDisease

SET Support = "Major histocompatibility complex class II, a marker of microglial \
activation, was increased in all patients with AD (AD: 2.5, R47H_AD: \
2.7, and control: 1.0; P < .01)."

# Checked paper, they looked at HLA-DR alpha chain as their "MHCII" molecule
p(HGNC:"HLA-DRA") biomarkerFor bp(GOBP:"microglial cell activation")
p(HGNC:"HLA-DRA") positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Inorg Biochem. 2013 Nov;128:267-9. doi: 10.1016/j.jinorgbio.2013.05.010. Epub 2013 May 29.", "23778113"}

SET Section = "Abstract"

SET Support = "One of the key classical pathological features of Alzheimer's disease \
(AD) is the progressive accumulation of amyloid beta (Aβ42) peptides \
and their coalescence into highly insoluble senile plaque cores."

p(HGNC:APP, frag("1_42")) biomarkerFor path(MESHD:"Alzheimer Disease")
p(HGNC:APP, frag("1_42")) -> path(MESHD:"Plaque, Amyloid")

SET Support = "A major factor driving Aβ42 peptide accumulation is the inability of \
brain cells to effectively clear excessive amounts of Aβ42 via \
phagocytosis."

bp(GOBP:phagocytosis) -| p(HGNC:APP, frag("1_42"), loc(MESHA:Brain))

SET Support = "The trans-membrane spanning, sensor-receptor known as the 'triggering \
receptor expressed in myeloid cells 2' (TREM2; chr6p21) is essential \
in the sensing, recognition, phagocytosis and clearance of noxious \
cellular debris from brain cells, including neurotoxic Aβ42 peptides."

p(HGNC:TREM2) positiveCorrelation bp(GOBP:phagocytosis)
p(HGNC:TREM2) positiveCorrelation bp(GOBP:"apoptotic cell clearance")
p(HGNC:TREM2) negativeCorrelation p(HGNC:APP, frag("1_42"))

SET Support = "Recently, mutations in the TREM2 gene have been associated with \
amyloidogenesis in neurodegenerative diseases including AD."

# amyloidogenesis = amyloid plaque
SET MeSHDisease = "Alzheimer Disease"
g(HGNC:TREM2, var("?")) positiveCorrelation a(CHEBI:"amyloid-beta")
UNSET MeSHDisease

SET Support = "In this report, we provide evidence that aluminum-sulfate, when \
incubated with microglial cells, induces the up-regulation of an NF- \
кB-sensitive micro RNA-34a (miRNA-34a; chr1p36) that is known to \
target the TREM2 mRNA 3'-untranslated region (3'-UTR), significantly \
down-regulating TREM2 expression."

a(CHEBI:"aluminium sulfate (anhydrous)", loc(MESHA:Microglia)) -> r(HGNC:MIR34A)
r(HGNC:MIR34A) -| r(HGNC:TREM2)

SET Support = "The aluminum-induced up-regulation of miRNA-34a and down-regulation of \
TREM2 expression were effectively quenched using the natural phenolic \
compound and NF-kB inhibitor CAPE \
[2-phenylethyl-(2E)-3-(3,4-dihydroxyphenyl) acrylate; caffeic-acid \
phenethyl ester]."

a(CHEBI:"phenethyl caffeate") -| r(HGNC:MIR34A)

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Acta Neuropathol. 2017 Nov;134(5):769-788. doi: 10.1007/s00401-017-1737-3. Epub 2017 Jun 13.", "28612290"}

SET Section = "Abstract"

SET Support = "Mutations and/or differential expression of microglial specific \
receptors such as TREM2, CD33, and CR3 have been associated with \
strong increased risk for developing Alzheimer's disease (AD)."

# CR3 = ITGAM
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:CD33, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:ITGAM, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "DAP12 (DNAX-activating protein 12)/TYROBP, a molecule localized to \
microglia, is a direct partner/adapter for TREM2, CD33, and CR3."

p(HGNC:TYROBP) positiveCorrelation a(MESHA:Microglia)
p(HGNC:TYROBP) positiveCorrelation p(HGNC:TREM2)
p(HGNC:TYROBP) positiveCorrelation p(HGNC:CD33)
p(HGNC:TYROBP) positiveCorrelation p(HGNC:ITGAM)

SET Support = "We and others have previously shown that TYROBP expression is \
increased in AD patients and in mouse models."

p(HGNC:TYROBP) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(MGI:Tyrobp) positiveCorrelation path(MESHD:"Alzheimer Disease")

# Abstract goes on to explain how they used mouse models for AD and TYROBP deficiency to examine effects on genes/behavior \
# but nothing was significant

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2014 Oct;35(10):2419.e23-2419.e25. doi: 10.1016/j.neurobiolaging.2014.04.010. Epub 2014 Apr 18.", "24910390"}

SET Section = "Abstract"

SET Support = "TREM2 mutations were first identified in Nasu-Hakola disease, a rare \
autosomal recessive disease characterized by recurrent fractures \
because of bone cysts and presenile dementia."

g(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")
path(DO:"Nasu-Hakola disease") positiveCorrelation path(HP:"Bone cyst")
path(DO:"Nasu-Hakola disease") positiveCorrelation path(MESHD:Dementia)

SET Support = "Recently, homozygous and compound heterozygous TREM2 mutations were \
identified in rare families with frontotemporal lobar degeneration \
(FTLD) but without bone involvement."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Lobar Degeneration")

# Rest of abstract focuses on one family, which is not statistically significant and not included

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2016 Nov;47:35-40. doi: 10.1016/j.neurobiolaging.2016.07.008. Epub 2016 Jul 16.", "27522519"}

SET Section = "Abstract"

SET Support = "Although mutations within the TREM2 gene have been robustly associated \
with Alzheimer's disease, it is not known whether alterations in the \
regulation of this gene are also involved in pathogenesis."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2013 Dec;34(12):2889.e11-3. doi: 10.1016/j.neurobiolaging.2013.06.004. Epub 2013 Jul 12.", "23855982"}

SET Section = "Abstract"

SET Support = "Recent studies have identified the rs75932628 (R47H) variant in TREM2 \
as an Alzheimer's disease risk factor with estimated odds ratio \
ranging from 2.9 to 5.1."

g(HGNC:TREM2) hasVariant g(dbSNP:rs75932628)
g(dbSNP:rs75932628) -> p(HGNC:TREM2, var("p.Arg47His"))
g(dbSNP:rs75932628) positiveCorrelation path(MESHD:"Alzheimer Disease")

# This abstract describes a study conducted that measures this SNP frequency and confirms the AD association

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Mol Neurodegener. 2015 Sep 4;10:43. doi: 10.1186/s13024-015-0040-9.", "26337043"}

SET Section = "Abstract"

SET Support = "Myeloid-lineage cells accomplish a myriad of homeostatic tasks \
including the recognition of pathogens, regulation of the inflammatory \
milieu, and mediation of tissue repair and regeneration."

# Myeloid cells use PRRs for pathogen recognition
a(MESHA:"Myeloid Cells") -> bp(GOBP:"pattern recognition receptor signaling pathway")
a(MESHA:"Myeloid Cells") -> bp(GOBP:"regulation of inflammatory response")
a(MESHA:"Myeloid Cells") positiveCorrelation bp(GOBP:"tissue regeneration")

SET Support = "In particular, mutations in TREM2 increase the risk for Alzheimer's \
disease and other neurodegenerative disorders."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")

SET Support = "The leading hypothesis is that microglia, the resident immune cells of \
the central nervous system, are the major myeloid cells affected by \
dysregulated TREM2-DAP12 function."

a(MESHA:Microglia) positiveCorrelation a(MESHA:"Central Nervous System")
a(MESHA:Microglia) isA a(MESHA:"Myeloid Cells")
p(HGNC:TREM2, var("?")) -| bp(GOBP:"microglial cell activation")
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> bp(GOBP:"microglial cell activation")

SET Support = "Here, we review how impaired signaling by the TREM2-DAP12 pathway \
leads to altered immune responses in phagocytosis, cytokine \
production, and microglial proliferation and survival, thus \
contributing to disease pathogenesis."

complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -- bp(GOBP:phagocytosis)
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -- p(MESHC:Cytokines)
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -- bp(GOBP:"microglial cell proliferation")
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -- a(MESHA:Microglia)

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neuroscience. 2015 Aug 27;302:138-50. doi: 10.1016/j.neuroscience.2014.09.050. Epub 2014 Oct 2.", "25281879"}

SET Section = "Abstract"

SET Support = "Microglia play major roles in initiation, coordination and execution \
of innate immunity in the brain. In the adult brain, these include \
maintenance of homeostasis, neuron and tissue repair, and eliminating \
infectious agents, apoptotic cells, and misfolded proteins."

a(MESHA:"Microglia", loc(MESHA:Brain)) -- bp(GOBP:"innate immune response")
a(MESHA:"Microglia", loc(MESHA:Brain)) -- bp(GOBP:"neuron cellular homeostasis")
a(MESHA:"Microglia", loc(MESHA:Brain)) -- bp(GOBP:"neuron projection regeneration")
a(MESHA:"Microglia", loc(MESHA:Brain)) -- bp(GOBP:"tissue regeneration")
a(MESHA:"Microglia", loc(MESHA:Brain)) -> bp(GOBP:phagocytosis)
a(MESHA:"Microglia", loc(MESHA:Brain)) -> bp(GOBP:"apoptotic cell clearance")
a(MESHA:"Microglia", loc(MESHA:Brain)) -> bp(GOBP:"misfolded protein transport")

SET Support = "Under normal conditions, triggering receptor expressed on myeloid \
cells 2 (TREM2) belongs to the second category. It pairs with the \
adaptor protein DNAX-activating protein of 12kDa (DAP12) to induce \
phagocytosis of apoptotic neurons without inflammatory responses, and \
to regulate Toll-like receptor-mediated inflammatory responses, and \
microglial activation."

complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> bp(GOBP:phagocytosis)
SET MeSHAnatomy = "Neurons"
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) -> bp(GOBP:"apoptotic cell clearance")
UNSET MeSHAnatomy
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) regulates bp(GOBP:"toll-like receptor signaling pathway")
complex(p(HGNC:TREM2), p(HGNC:TYROBP)) regulates bp(GOBP:"microglial cell activation")

SET Support = "Although ligands for TREM2 are largely unknown, the mitochondrial heat \
shock protein 60, expressed on cell surface of apoptotic neurons, is a \
specific ligand that activates TREM2-mediated phagocytosis by \
microglia."

# mitochondrial heat shock protein 60 = HSPD1
a(MESHA:Neurons) positiveCorrelation p(HGNC:HSPD1, loc(MESHA:"Cell Membrane"), loc(MESHA:"Neurons"))
p(HGNC:HSPD1) -> act(p(HGNC:TREM2))
SET MeSHAnatomy = "Microglia"
p(HGNC:HSPD1) -> bp(GOBP:phagocytosis)
UNSET MeSHAnatomy

SET Support = "Several TREM2 mutations have been identified recently that increase \
the risk of Alzheimer's disease, Frontotemporal dementia, Parkinson's \
disease, and amyotrophic lateral sclerosis."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Parkinson Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Amyotrophic Lateral Sclerosis")

SET Support = "Some of these mutations cause impaired proteolysis of full-length \
TREM2 at the plasma membrane to different degrees. The defects in the \
intramembrane cleavage result in dysfunction of phagocytosis \
signaling."

g(HGNC:TREM2, var("?")) -| act(p(HGNC:TREM2, loc(MESHA:"Cell Membrane")), ma(GOBP:"membrane protein ectodomain proteolysis"))
act(p(HGNC:TREM2, loc(MESHA:"Cell Membrane")), ma(GOBP:"membrane protein ectodomain proteolysis")) -> bp(GOBP:phagocytosis)

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neuropsychopharmacology. 2014 Dec;39(13):2949-62. doi: 10.1038/npp.2014.164. Epub 2014 Jul 22.", "25047746"}

SET Section = "Abstract"

SET Support = "Triggering receptor expressed on myeloid cells 2 (TREM2) gene is a \
recently identified susceptibility gene for Alzheimer's disease (AD), \
as its low-frequency variants increase the risk of this disease with \
an odds ratio similar to that of an APOE ɛ4 allele."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Using APPswe/PS1dE9 mice, a transgenic model of AD, we showed that \
TREM2 was upregulated in microglia during disease progression."

SET Species = "10090"
SET MouseStrain = "APPswe/PS1dE9"
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Trem2, loc(MESHA:Microglia))

SET Support = "By knockdown and overexpression of TREM2 in cultured primary \
microglia, we revealed that TREM2 modulated microglial functions under \
AD context, as it facilitated Aβ(1-42) phagocytosis and inhibited \
Aβ(1-42)-triggered proinflammatory responses."

p(MGI:Trem2, loc(MESHA:Microglia)) -| p(MGI:App, frag("1_42"))
p(MGI:Trem2, loc(MESHA:Microglia)) -| bp(GOBP:"inflammatory response")

SET Support = "Meanwhile, this modulation was dependent on DAP12, the adapter protein \
of TREM2."

p(MGI:Tyrobp) regulates p(MGI:Trem2)

SET Support = "More importantly, overexpression of TREM2 in the brain of \
APPswe/PS1dE9 mice markedly ameliorated AD-related neuropathology \
including Aβ deposition, neuroinflammation, and neuronal and synaptic \
losses, which was accompanied by an improvement in spatial cognitive \
functions."

p(MGI:Trem2, loc(MESHA:Brain)) -| a(CHEBI:"amyloid-beta", loc(MESHA:Brain))
p(MGI:Trem2, loc(MESHA:Brain)) -| path(MESHD:Inflammation)
p(MGI:Trem2, loc(MESHA:Brain)) -> a(MESHA:Neurons)
p(MGI:Trem2, loc(MESHA:Brain)) -> p(MGI:Syp)
p(MGI:Trem2, loc(MESHA:Brain)) negativeCorrelation bp(HP:"Impaired visuospatial constructive cognition")

UNSET Species
UNSET MouseStrain

UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Mol Neurosci. 2017 Jun;62(2):215-221. doi: 10.1007/s12031-017-0928-7. Epub 2017 May 5.", "28477215"}

SET Section = "Abstract"

SET Support = "Late-onset Alzheimer's disease (LOAD) is a multifactorial \
neurodegenerative disorder that corresponds to most Alzheimer's \
disease (AD) cases."

path(ADO:"Late_Onset_Alzheimer_s_Disease") isA path(MESHD:"Neurodegenerative Diseases")

SET Support = "Inflammation is frequently related to AD, whereas microglial cells are \
the major phagocytes in the brain and mediate the removal of Aβ \
peptides."

SET MeSHAnatomy = "Brain"
path(MESHD:Inflammation) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET MeSHAnatomy
a(MESHA:Microglia, loc(MESHA:Brain)) isA a(MESHA:Macrophages)
a(MESHA:Microglia, loc(MESHA:Brain)) negativeCorrelation a(CHEBI:"amyloid-beta", loc(MESHA:Brain))

SET Support = "Genome-wide association studies have reported genetic loci associated \
with the inflammatory pathway involved in AD. Among them, rs3865444 \
CD33, rs3764650 ABCA7, rs6656401 CR1, and rs610932 MS4A6A variants in \
microglial genes are associated with LOAD."

SET StudyType = "GWAS"
g(HGNC:CD33) hasVariant g(dbSNP:rs3865444)
g(dbSNP:rs3865444) -- path(MESHD:Inflammation)
g(dbSNP:rs3865444, loc(MESHA:Microglia)) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:ABCA7) hasVariant g(dbSNP:rs3764650)
g(dbSNP:rs3764650) -- path(MESHD:Inflammation)
g(dbSNP:rs3764650, loc(MESHA:Microglia)) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:CR1) hasVariant g(dbSNP:rs6656401)
g(dbSNP:rs6656401) -- path(MESHD:Inflammation)
g(dbSNP:rs6656401, loc(MESHA:Microglia)) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:MS4A6A) hasVariant g(dbSNP:rs610932)
g(dbSNP:rs610932) -- path(MESHD:Inflammation)
g(dbSNP:rs610932, loc(MESHA:Microglia)) -- path(ADO:"Late_Onset_Alzheimer_s_Disease")
UNSET StudyType

SET Support = "We found that rs3865444 CD33 acts as a protective factor against LOAD."

# Two sentences prior they mention the study was done in Brazil
SET Ethnicity = "Brazilian"
g(dbSNP:rs3865444, loc(MESHA:Microglia)) negativeCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
UNSET Ethnicity

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Mol Neurobiol. 2017 Mar;54(2):1111-1118. doi: 10.1007/s12035-016-9718-4. Epub 2016 Jan 23.", "26803496"}

SET Section = "Abstract"

SET Support = "The cluster of differentiation 33 (CD33) has been proved as a \
susceptibility locus associated with late-onset Alzheimer's disease \
(LOAD) based on recent genetic studies."

g(HGNC:CD33) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Med Sci Monit. 2016 Jan 22;22:234-43.", "26795201"}

SET Section = "Abstract"

SET Support = "The pooled effect of ABCA7 rs3764605 allele G was significantly \
associated with an increased the risk of AD (OR=1.20, 95% CI: \
1.14-1.26, P value <0.001)."

g(HGNC:ABCA7) hasVariant g(dbSNP:rs3764650)
g(dbSNP:rs3865444) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Similarly, our evidence suggested that allele A of TOMM40 rs2075650 \
polymorphism was a risk factor for AD (OR=2.87, 95% CI: 2.46-3.34, P \
value <0.001)."

g(HGNC:TOMM40) hasVariant g(dbSNP:rs2075650)
g(dbSNP:rs2075650) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Alleles A of CD33 rs3865444 and A of TOMM40 rs157580 were both \
protective factors for AD onset (OR=0.94, 95% CI: 0.90-0.98, P \
value=0.003; OR=0.62, 95% CI: 0.57-0.66, P value <0.001)."

g(HGNC:TOMM40) hasVariant g(dbSNP:rs157580)
g(dbSNP:rs157580) negativeCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:CD33) hasVariant g(dbSNP:rs3865444)
g(dbSNP:rs3865444) negativeCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurosci Lett. 2015 Nov 16;609:235-9. doi: 10.1016/j.neulet.2015.10.007. Epub 2015 Oct 9.", "26455864"}

SET Section = "Abstract"

SET Support = "CD33 and MS4A cluster variants have been identified to modulate the \
risk of Alzheimer's disease (AD) in several recent genome-wide \
association studies (GWAS) in Caucasians."

SET StudyType = "GWAS"
SET Ethnicity = "Caucasian"
g(HGNC:CD33, var("?")) -- path(MESHD:"Alzheimer Disease")
# MS4A is a family cluster
UNSET StudyType
UNSET Ethnicity

SET Support = "In the present study, we first conducted a case-control study to \
investigate the CD33 single nucleotide polymorphisms (SNPs) rs3865444 \
and rs3826656 and the MS4A cluster SNPs rs610932 and rs670139 in a \
cohort from eastern China that comprised 126 late-onset Alzheimer's \
disease (LOAD) patients and 129 healthy controls. The results revealed \
that the frequency of rs3826656 major (G) allele carriers was higher \
among the LOAD patients than among the controls [P=0.005; odds ratio \
(OR), 1.760; 95% confidence interval (CI), 1.185-2.615]."

SET Ethnicity = "East Chinese"
g(HGNC:CD33) hasVariant g(dbSNP:rs3865444)
g(HGNC:CD33) hasVariant g(dbSNP:rs3826656)
g(dbSNP:rs3826656) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:MS4A6A) hasVariant g(dbSNP:rs610932)
g(HGNC:MS4A4E) hasVariant g(dbSNP:rs670139)

SET Support = "In apolipoprotein E (APOE) ε4 allele carriers, the G allele of the SNP \
rs3865444 was found to be associated with an increased risk of LOAD \
(P=0.002; OR, 3.391; 95% CI, 1.512-7.605)."

SET ApoEɛ4Status = "positive"
g(dbSNP:rs3865444) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
UNSET ApoEɛ4Status
UNSET Ethnicity

SET Support = "Next, we re-evaluated the association between these variants and LOAD \
by conducting a meta-analysis using data from studies of East Asian \
populations, including the present case-control study, and confirmed \
that rs3826656 increased the risk of LOAD."

SET Ethnicity = {"East Asian", "East Chinese"}
g(dbSNP:rs3826656) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

SET Support = "In addition, we identified a significant association between rs610932 \
and LOAD (P=0.035; OR, 0.79; 95% CI, 0.63-0.98)."

g(dbSNP:rs610932) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
UNSET Ethnicity

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2015 Feb;36(2):571-82. doi: 10.1016/j.neurobiolaging.2014.09.023. Epub 2014 Oct 2.", "25448602"}

SET Section = "Abstract"

SET Support = "Recent findings identified the minor A allele present in the single- \
nucleotide polymorphism rs3865444 in the CD33 gene as being associated \
with the reduced risk of developing Alzheimer's disease (AD)."

g(HGNC:CD33) hasVariant g(dbSNP:rs3865444)
g(dbSNP:rs3865444) negativeCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "CD33 (Siglec-3) is an immune function protein with anti-inflammatory \
signaling, cell adhesion, and endocytosis functions with sialic acid- \
modified proteins or lipids as ligands."

p(HGNC:CD33) negativeCorrelation path(MESHD:Inflammation)
p(HGNC:CD33) positiveCorrelation bp(GOBP:"cell adhesion")
p(HGNC:CD33) positiveCorrelation bp(GOBP:endocytosis)
p(HGNC:CD33) positiveCorrelation a(CHEBI:"sialic acid")

SET Support = "However, increased expression of CD33 mRNA was associated with \
increasing AD pathology in temporal cortex brain samples."

r(HGNC:CD33, loc(BRCO:Temporal_cortex)) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "We also showed that cases with A/A alleles had reduced levels of CD33 \
protein in temporal cortex but increased levels of the microglia \
protein IBA-1."

# A/A alleles indicate rs3865444
# IBA-1 = AIF1
g(dbSNP:rs3865444) negativeCorrelation p(HGNC:CD33, loc(BRCO:Temporal_cortex))
g(dbSNP:rs3865444) positiveCorrelation p(HGNC:AIF1, loc(BRCO:Temporal_cortex))
a(MESHA:Microglia) -> p(HGNC:AIF1)

SET Support = "Using immunohistochemistry on temporal cortex sections, CD33 was \
selectively localized to microglia, with greater expression in \
activated microglia."

SET Assay = "immunohistochemistry"
a(MESHA:Microglia, loc(BRCO:Temporal_cortex)) -> p(HGNC:CD33, loc(MESHA:Microglia))
p(HGNC:CD33, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:"microglial cell activation")
a(BRCO:Temporal_cortex) positiveCorrelation bp(GOBP:"microglial cell activation")
UNSET Assay

SET Support = "Treatment of human microglia isolated from autopsy brains with \
amyloid-beta peptide and a range of other inflammatory activating \
agents resulted in reduced CD33 mRNA and protein levels."

a(CHEBI:"amyloid-beta", loc(MESHA:Microglia)) -| r(HGNC:CD33, loc(MESHA:Microglia))
a(CHEBI:"amyloid-beta", loc(MESHA:Microglia)) -| p(HGNC:CD33, loc(MESHA:Microglia))
a(CHEBI:"poly(I:C)", loc(MESHA:Microglia)) -| r(HGNC:CD33, loc(MESHA:Microglia))
a(CHEBI:"poly(I:C)", loc(MESHA:Microglia)) -| p(HGNC:CD33, loc(MESHA:Microglia))

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Mol Neurobiol. 2015 Aug;52(1):414-21. doi: 10.1007/s12035-014-8880-9. Epub 2014 Sep 4.", "25186233"}

SET Section = "Abstract"

SET Support = "The CD33 rs3865444 polymorphism was first identified to be associated \
with Alzheimer's disease (AD) in European population."

g(HGNC:CD33) hasVariant g(dbSNP:rs3865444)
g(dbSNP:rs3865444) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Collectively, our analysis shows that the CD33 rs3865444 polymorphism \
is associated with AD susceptibility in Chinese, European, and North \
American populations."

SET Ethnicity = {"Chinese", "Caucasian with European origin", "Caucasian with North American origin"}
g(dbSNP:rs3865444) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET Ethnicity

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Oncotarget. 2018 May 15;9(37):24590-24600. doi: 10.18632/oncotarget.25083. eCollection 2018 May 15.", "29872490"}

SET Section = "Abstract"

SET Support = "The apolipoprotein E (APOE) gene on chromosome 19q13.32, was the \
first, and remains the strongest, genetic risk factor for Alzheimer's \
disease (AD)."

g(HGNC:APOE) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Additional signals associated with AD have been located in chromosome \
19, including ABCA7 (19p13.3) and CD33 (19q13.41)."

g(HGNC:ABCA7) -- path(MESHD:"Alzheimer Disease")
g(HGNC:CD33) -- path(MESHD:"Alzheimer Disease")

SET Support = "To evaluate AD risk conferred by ABCA7 rs4147929:G>A and CD33 \
rs3865444:C>A, we used a large Spanish population (1796 AD cases, 2642 \
controls). The ABCA7 rs4147929:G>A SNP effect was nominally replicated \
in the Spanish cohort and reached genome-wide significance after meta- \
analysis (odds ratio (OR)=1.15, 95% confidence interval (95% \
CI)=1.12-1.19; P = 1.60 x 10-19). CD33 rs3865444:C>A was not \
associated with AD in the dataset."

SET Ethnicity = "Spanish"
g(HGNC:ABCA7) hasVariant g(dbSNP:rs4147929)
g(dbSNP:rs4147929) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:CD33) hasVariant g(dbSNP:rs3865444)
UNSET Ethnicity

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Mol Neurodegener. 2017 May 26;12(1):43. doi: 10.1186/s13024-017-0184-x.", "28549481"}

SET Section = "Abstract"

SET Support = "The majority of drugs developed for AD focus on the amyloid cascade \
hypothesis, which implicates Aß plaques as a causal factor in the \
disease."

path(MESHD:"Plaque, Amyloid") -> path(MESHD:"Alzheimer Disease")

SET Support = "Of these genes, several (including CR1, SPI1, the MS4As, TREM2, ABCA7, \
CD33, and INPP5D) are expressed by microglia, the resident immune \
cells of the brain."

a(MESHA:Microglia) -> p(HGNC:CR1)
a(MESHA:Microglia) -> p(HGNC:SPI1)
a(MESHA:Microglia) -> p(HGNC:TREM2)
a(MESHA:Microglia) -> p(HGNC:ABCA7)
a(MESHA:Microglia) -> p(HGNC:CD33)
a(MESHA:Microglia) -> p(HGNC:INPP5D)

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Glia. 2016 Oct;64(10):1710-32. doi: 10.1002/glia.22988. Epub 2016 Apr 21.", "27100611"}

SET Section = "Abstract"

SET Support = "While histological changes in microglia have long been recognized as a \
pathological feature of Alzheimer's disease (AD), recent genetic \
association studies have also strongly implicated microglia in the \
etiology of the disease."

a(MESHA:Microglia) -- path(MESHD:"Alzheimer Disease")

SET Support = "Coding and noncoding polymorphisms in several genes expressed in \
microglia-including APOE, TREM2, CD33, GRN, and IL1RAP-alter AD risk, \
and therefore could be considered as entry points for therapeutic \
intervention."

g(HGNC:APOE, var("?"), loc(MESHA:Microglia)) -- path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("?"), loc(MESHA:Microglia)) -- path(MESHD:"Alzheimer Disease")
g(HGNC:CD33, var("?"), loc(MESHA:Microglia)) -- path(MESHD:"Alzheimer Disease")
g(HGNC:GRN, var("?"), loc(MESHA:Microglia)) -- path(MESHD:"Alzheimer Disease")
g(HGNC:IL1RAP, var("?"), loc(MESHA:Microglia)) -- path(MESHD:"Alzheimer Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neuropathology. 2015 Dec;35(6):529-37. doi: 10.1111/neup.12222. Epub 2015 Jun 18.", "26087043"}

SET Section = "Abstract"

SET Support = "Nasu-Hakola disease (NHD) is a rare autosomal recessive disorder, \
characterized by formation of multifocal bone cysts and development of \
leukoencephalopathy, caused by genetic mutations of either DNAX- \
activation protein 12 (DAP12) or triggering receptor expressed on \
myeloid cells 2 (TREM2)."

path(DO:"Nasu-Hakola disease") positiveCorrelation path(HP:"Bone cyst")
path(DO:"Nasu-Hakola disease") positiveCorrelation path(MESHD:"Leukoencephalopathy, Progressive Multifocal")
g(HGNC:TYROBP, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")

SET Support = "TREM2, by transmitting signals via the immunoreceptor tyrosine-based \
activation motif (ITAM) of DAP12, stimulates phagocytic activity of \
microglia, and ITAM signaling is counterbalanced by sialic acid- \
binding immunoglobulin (Ig)-like lectins (Siglecs)-mediated \
immunoreceptor tyrosine-based inhibitory motif (ITIM) signaling."

p(HGNC:TREM2) -> act(p(HGNC:TYROBP), ma(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif"))
SET MeSHAnatomy = "Microglia"
p(HGNC:TREM2) -> bp(GOBP:phagocytosis)
UNSET MeSHAnatomy
bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif") -| act(p(HGNC:TYROBP), ma(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif"))

SET Support = "To investigate a role of CD33, a member of the Siglecs family acting \
as a negative regulator of microglia activation, in the pathology of \
NHD, we studied CD33 expression patterns in five NHD brains and 11 \
controls by immunohistochemistry."

SET Assay = "immunohistochemistry"
p(HGNC:CD33) -| bp(GOBP:"microglial cell activation")
UNSET Assay

# The rest of the abstract had non-significant data

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2017 Oct;58:240.e1-240.e3. doi: 10.1016/j.neurobiolaging.2017.06.019. Epub 2017 Jun 28.", "28716534"}

SET Section = "Abstract"

SET Support = "Mutations in TYROBP and TREM2 have been shown to cause polycystic \
lipomembranous osteodysplasia with sclerosing leukoencephalopathy."

# polycystic lipomembranous osteodysplasia with sclerosing leukoencephalopathy = PLOSL = Nasu-Hakola disease
g(HGNC:TYROBP, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")

SET Support = "Recently, variants in TREM2 were also associated with frontotemporal \
dementia and Alzheimer's disease."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")

# The rest of the abstract had non-significant data

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Trends Mol Med. 2017 Jun;23(6):512-533. doi: 10.1016/j.molmed.2017.03.008. Epub 2017 Apr 22.", "28442216"}

SET Section = "Abstract"

SET Support = "The neuropathological hallmarks of the disease are extracellular \
amyloid-β (Aβ) plaques and intraneuronal hyperphosphorylated tau \
aggregates."

path(MESHD:"Plaque, Amyloid") biomarkerFor path(MESHD:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph)) biomarkerFor path(MESHD:"Alzheimer Disease")

SET Support = "Genetic variants of TREM2 (triggering receptor expressed on myeloid \
cells 2), a cell-surface receptor expressed selectively in myeloid \
cells, greatly increase the risk of AD, implicating microglia and the \
innate immune system as pivotal factors in AD pathogenesis."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

# Rest of abstract talks about how they analyze the recent findings. This is a review.

UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Neuroinflammation. 2017 Mar 21;14(1):59. doi: 10.1186/s12974-017-0835-4.", "28320424"}

SET Section = "Abstract"

SET Support = "Triggering receptor expressed on myeloid cells 2 (TREM2) and \
apolipoprotein E (APOE) are genetically linked to Alzheimer's disease."

g(HGNC:TREM2) -- path(MESHD:"Alzheimer Disease")
g(HGNC:APOE) -- path(MESHD:"Alzheimer Disease")

SET Support = "ApoE was found to be an agonist to human TREM2 with EC50 in the low nM \
range, and to murine TREM2 with reduced potency."

# They used ApoEe3 to measure, and this is considered the "parent" allele so no mutations marked
complex(p(HGNC:APOE), p(HGNC:TREM2)) -> act(p(HGNC:TREM2))
complex(p(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")), p(HGNC:TREM2)) -> act(p(HGNC:TREM2))
complex(p(HGNC:APOE, var("p.Arg158Cys")), p(HGNC:TREM2)) -> act(p(HGNC:TREM2))
complex(p(HGNC:APOE), p(MGI:Trem2)) -> act(p(MGI:Trem2))
complex(p(HGNC:APOE, var("p.Leu115Arg"), var("p.His158Arg")), p(MGI:Trem2)) -> act(p(MGI:Trem2))
complex(p(HGNC:APOE, var("p.Arg158Cys")), p(MGI:Trem2)) -> act(p(MGI:Trem2))

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Front Aging Neurosci. 2017 Jan 31;9:8. doi: 10.3389/fnagi.2017.00008. eCollection 2017.", "28197095"}

SET Section = "Abstract"

SET Support = "Heterozygous missense mutations in the triggering receptor expressed \
on myeloid cells 2 (TREM2) have been reported to significantly \
increase the risk of developing Alzheimer's disease (AD)."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Since TREM2 is specifically expressed by microglia in the brain, we \
hypothesized that soluble TREM2 (sTREM2) levels may increase together \
with in vivo biomarkers of microglial activity and amyloidosis in an \
AD mouse model as assessed by small animal positron-emission- \
tomography (μPET)."

a(MESHA:Microglia, loc(MESHA:Brain)) -> p(HGNC:TREM2)

SET Support = "In this cross-sectional study, we examined a strong amyloid mouse \
model (PS2APP) of four age groups by μPET with [18F]-GE180 (glial \
activation) and [18F]-florbetaben (amyloidosis), followed by \
measurement of sTREM2 levels and amyloid levels in the brain."

SET Species = "10090"
SET MouseStrain = "PS2APP"
SET TechnologyUsed = "Positron Emission Tomography"
a(CHEBI:"florbetaben ((18)F)", loc(MESHA:Brain)) positiveCorrelation a(CHEBI:"amyloid-beta", loc(MESHA:Brain))

SET Support = "μPET results of both tracers were correlated with terminal TREM2 \
levels. The brain sTREM2 levels strongly increased with age of PS2APP \
mice (5 vs. 16 months: +211%, p < 0.001), and correlated highly with \
μPET signals of microglial activity (R = 0.89, p < 0.001) and \
amyloidosis (R = 0.92, p < 0.001)."

a(CHEBI:"florbetaben ((18)F)") positiveCorrelation p(HGNC:TREM2, loc(MESHA:"Extracellular Space"))
UNSET TechnologyUsed
p(HGNC:TREM2, loc(MESHA:"Extracellular Space")) positiveCorrelation bp(MESHPP:Aging)

UNSET Species
UNSET MouseStrain

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Sci Transl Med. 2016 Dec 14;8(369):369ra178.", "27974666"}

SET Section = "Abstract"

SET Support = "TREM2 is a transmembrane protein involved in innate immunity and is \
selectively expressed by microglia and genetically linked to AD and \
other neurodegenerative disorders."

p(HGNC:TREM2) -- bp(GOBP:"innate immune response")
a(MESHA:Microglia) -> p(HGNC:TREM2)
g(HGNC:TREM2) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(HGNC:TREM2) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")

SET Support = "Its ectodomain is released by proteolysis as a soluble variant \
(sTREM2) and can be detected in the cerebrospinal fluid (CSF)."

act(p(HGNC:TREM2), ma(GOBP:"membrane protein ectodomain proteolysis")) -> p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid"))

SET Support = "We observed that CSF sTREM2 increased in MCs compared to NCs 5 years \
before the expected symptom onset and this difference remained \
significant until 5 years after the expected symptom onset."

# MCs = AD mutation carriers, NCs = non-carriers
p(HGNC:TREM2, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation path(MESHD:"Alzheimer Disease")

UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Neuroinflammation. 2016 Jan 20;13:17. doi: 10.1186/s12974-016-0479-9.", "26792193"}

SET Section = "Abstract"

SET Support = "Triggering receptor expressed on myeloid cells-2 (TREM2) exerts \
important functions in the regulation of monocytes, like dendritic \
cells, osteoclasts, tissue macrophages, and microglia."

p(HGNC:TREM2) -- a(MESHA:Monocytes)
p(HGNC:TREM2) -- a(MESHA:"Dendritic Cells")
p(HGNC:TREM2) -- a(MESHA:Osteoclasts)
p(HGNC:TREM2) -- a(MESHA:Macrophages)
p(HGNC:TREM2) -- bp(GOBP:"microglial cell activation")

SET Support = "Mutations in TREM2 are associated with several diseases, including \
Nasu-Hakola disease, frontotemporal dementia, and Alzheimer's disease \
(AD)."

g(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "TREM2 undergoes sequential proteolytic processing by ectodomain \
shedding and intramembrane proteolysis."

act(p(HGNC:TREM2), ma(GOBP:"membrane protein ectodomain proteolysis")) -> p(HGNC:TREM2, loc(MESHA:"Extracellular Space"))

SET Support = "We show that inhibition of γ-secretase-dependent cleavage of the TREM2 \
C-terminal fragment in cellular membranes interferes with \
TREM2-dependent signaling and cellular function."

act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:"membrane protein ectodomain proteolysis")) =| p(HGNC:TREM2, frag("?_*"), loc(MESHA:"Cell Membrane"))
act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:"membrane protein ectodomain proteolysis")) -> act(p(HGNC:TREM2), ma(GOBP:signaling))

SET Support = "Inhibition of γ-secretase decreases membrane-proximal signaling and \
intracellular Ca(2+) response. Decreased signaling alters \
morphological changes and phagocytic activity of cells upon selective \
stimulation of TREM2."

act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:"membrane protein ectodomain proteolysis")) -> bp(GOBP:"cell-cell signaling")
act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:"membrane protein ectodomain proteolysis")) -> bp(GOBP:"cellular response to calcium ion")
act(p(HGNC:TREM2), ma(GOBP:signaling)) -- path(HP:"Abnormal cell morphology")
act(p(HGNC:TREM2), ma(GOBP:signaling)) -- bp(GOBP:phagocytosis)

UNSET Section

SET Section = "Findings"

SET Support = "The triggering receptor expressed on myeloid cells-2 (TREM2) belongs \
to the immunoglobulin superfamily of cell surface receptors and is \
expressed on monocyte-derived cell types, including dendritic cells \
[1], osteoclasts, tissue macrophages, and microglia [2]."

a(MESHA:"Dendritic Cells") -> p(HGNC:TREM2)
a(MESHA:Macrophages) -> p(HGNC:TREM2)
a(MESHA:Osteoclasts) -> p(HGNC:TREM2)
a(MESHA:Microglia) -> p(HGNC:TREM2)

SET Support = "TREM2 associates with its co-receptor TYROBP/DNAX-activating protein \
of 12 kDa (DAP12)."

p(HGNC:TREM2) -- p(HGNC:TYROBP)

SET Support = "Upon activation of TREM2, DAP12 undergoes phosphorylation at two \
tyrosine residues within its immunoreceptor tyrosine activation motif \
(ITAM)."

act(p(HGNC:TREM2)) -> p(HGNC:TYROBP, pmod(Ph))

SET Support = "Phosphorylated DAP12 recruits and activates the Syk protein kinase \
which mediates mitogen-activated protein kinase (MAPK) and \
phospholipase Cγ (PLCγ)-dependent signaling pathways [3]."

p(HGNC:TYROBP, pmod(Ph)) -> act(p(HGNC:SYK))
act(p(HGNC:SYK)) -> bp(GOBP:"MAPK cascade")
# PLCγ is a subunit of PLC
act(p(HGNC:SYK)) -> bp(GOBP:"phospholipase C-activating G-protein coupled receptor signaling pathway")

SET Support = "TREM2 has been involved in the regulation of phagocytosis and cytokine \
secretion, suggesting a role of TREM2 in clearance of neuronal debris \
and neuroinflammation [4–6]."

p(HGNC:TREM2) -- bp(GOBP:"regulation of phagocytosis")
p(HGNC:TREM2) -- bp(GOBP:"apoptotic cell clearance")
p(HGNC:TREM2) -- bp(GOBP:"regulation of cytokine secretion")

SET Support = "Mutations in either TREM2 or DAP12 can cause polycystic lipomembranous \
osteodysplasia with sclerosing leukoencephalopathy (PLOSL or Nasu- \
Hakola disease), which is also characterized by presenile dementia in \
homozygous carriers [7, 8]."

g(HGNC:TREM2, var("?")) -> path(DO:"Nasu-Hakola disease")
g(HGNC:TYROBP, var("?")) -> path(DO:"Nasu-Hakola disease")
path(DO:"Nasu-Hakola disease") positiveCorrelation path(MESHD:Dementia)

SET Support = "Interestingly, TREM2 variants have recently been associated with late- \
onset Alzheimer’s disease (AD) and frontotemporal lobe dementia (FTD) \
[8–10]."

g(HGNC:TREM2, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Frontotemporal Dementia")

SET Support = "Notably, TREM2 was found to be upregulated in microglia or myeloid \
cells surrounding characteristic amyloid plaques in mouse models of AD \
[11–13]."

SET Species = "10090"
p(MGI:Trem2) positiveCorrelation a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))
p(MGI:Trem2) positiveCorrelation a(MESHA:"Myeloid Cells", loc(CHEBI:"amyloid-beta"))

SET Support = "While deletion of TREM2 increases Aβ plaque load in one transgenic \
mouse model of AD [14], another study rather revealed decreased Aβ \
plaques in the hippocampus upon deletion of TREM2 [15]."

r(MGI:Trem2) negativeCorrelation a(CHEBI:"amyloid-beta")
r(MGI:Trem2) positiveCorrelation a(CHEBI:"amyloid-beta", loc(MESHA:Hippocampus))
UNSET Species

SET Support = "TREM2 undergoes sequential proteolytic processing by ectodomain \
shedding and intramembranous cleavage by γ-secretase [16–18]."

act(complex(SCOMP:"gamma Secretase Complex"), ma(GOBP:"membrane protein ectodomain proteolysis")) => p(HGNC:TREM2, frag("?"))

SET Support = "TREM2 signaling involves the activation of PLCγ and elevation of the \
cytosolic Ca2+ concentration [14, 18]."

act(p(HGNC:TREM2), ma(GOBP:signaling)) -> act(p(SFAM:"PLCG Family"))
act(p(HGNC:TREM2), ma(GOBP:signaling)) -> a(CHEBI:"calcium(2+)", loc(MESHA:Cytosol))

SET Support = "Pharmacological inhibition of γ-secretase with DAPT \
(N-[(3,5-difluorophenyl)acetyl]-l-alanyl-2-phenylglycine-1,1-dimethylethyl \
ester) led to the accumulation of the TREM2 CTF, thereby significantly \
increasing the ratio of CTF/full-length TREM2 (Fig. 1a)."

a(CHEBI:DAPT) -| act(complex(SCOMP:"gamma Secretase Complex"))
a(CHEBI:DAPT) -> p(HGNC:TREM2, frag("?_*"))

SET Support = "Our previous data revealed that inhibition of γ-secretase decreased \
the phosphorylation of the TREM2 co-receptor DAP12 and stabilized PIP2 \
levels at the plasma membrane, suggesting impaired activation of PLCγ \
[18]."

act(complex(SCOMP:"gamma Secretase Complex")) positiveCorrelation p(HGNC:TYROBP, pmod(Ph))
complex(SCOMP:"gamma Secretase Complex") -- a(CHEBI:"1-phosphatidyl-1D-myo-inositol 4,5-bisphosphate", loc(MESHA:"Cell Membrane"))

SET Support = "Because PLCγ plays a major role in the regulation of intracellular \
Ca2+ signaling, we next analyzed the effect of γ-secretase inhibition \
on TREM2-dependent changes in intracellular Ca2+ concentrations \
([Ca2+]i)."

p(SFAM:"PLCG Family") regulates bp(GOBP:"calcium-mediated signaling")

SET Support = "Cell incubation with anti-myc caused a significant increase in \
cytosolic [Ca2+]i (Fig. 1b)."

# anti-myc causes activation of TREM2
act(p(HGNC:TREM2)) -> a(CHEBI:"calcium(2+)", loc(MESHA:Cytosol))

SET Support = "Interestingly, pharmacological inhibition of γ-secretase with DAPT \
efficiently blocked the elevation of [Ca2+]i upon activation of TREM2 \
(Fig. 1b)."

a(CHEBI:DAPT) -| a(CHEBI:"calcium(2+)", loc(MESHA:Cytosol))

SET Support = "It has been shown previously that activation of TREM2 induces changes \
in cellular morphology [19]."

act(p(HGNC:TREM2)) positiveCorrelation path(HP:"Abnormal cell morphology")

SET Support = "Cells expressing myc-tagged TREM2 were monitored by live cell \
microscopy in the presence of anti-myc or isotype control antibodies. \
Cell incubation with anti-myc antibody, but not with the isotype \
control (data not shown), resulted in a time-dependent decrease in \
cell surface area (Fig. 1c)."

SET TechnologyUsed = "Live Microscopy"
act(p(HGNC:TREM2)) -> path(HP:"Abnormal cell morphology")
act(p(HGNC:TREM2)) -- bp(GOBP:"regulation of cell size")

SET Support = "Notably, inhibition of γ-secretase completely blocked the \
TREM2-dependent changes in cellular morphology (Fig. 1c)."

act(complex(SCOMP:"gamma Secretase Complex")) -| path(HP:"Abnormal cell morphology")
UNSET TechnologyUsed

SET Support = "BV-2 cells endogenously express functional PS1 and PS2 proteins as \
indicated by the detection of characteristic N-terminal and C-terminal \
fragments derived from endo-proteolytic processing of the full-length \
proteins during incorporation and maturation of the γ-secretase \
complex (Fig. 2a)."

complex(SCOMP:"gamma Secretase Complex") hasComponents list(p(HGNC:PSEN1), p(HGNC:PSENEN))

SET Support = "The fraction of cells which have internalized six or more beads, thus \
reflecting high phagocytic activity, significantly increased from \
2.55 ± 1.41 % (isotype control) to 13.06 ± 5.50 % upon specific \
activation of TREM2 (with anti-myc antibody), representing an \
approximately fivefold stimulation (Fig. 2c). These data indicate that \
selective activation of TREM2 stimulates the phagocytic activity of \
BV-2 cells."

SET CustomCellLine = "BV-2"
# BV-2 is a mouse cell line but transfected with human TREM2
act(p(HGNC:TREM2)) -> bp(GOBP:phagocytosis)

SET Support = "Interestingly, inhibition of γ-secretase activity by preincubation of \
cells with DAPT significantly decreased the TREM2-mediated stimulation \
of phagocytosis to basal levels of cells incubated with isotype \
control antibodies (Fig. 2c)."

a(CHEBI:DAPT) -| bp(GOBP:phagocytosis)

SET Support = "As described above for COS7 cells, stimulation of TREM2 overexpressing \
BV-2 cells resulted in increased [Ca2+]i."

act(p(HGNC:TREM2)) -> a(CHEBI:"calcium(2+)", loc(MESHA:Cytosol))

SET Support = "Inhibition of γ-secretase by DAPT strongly inhibited the elevation of \
[Ca2+]i (Fig. 2d)."

a(CHEBI:DAPT) -| a(CHEBI:"calcium(2+)", loc(MESHA:Cytosol))

SET Support = "BV-2 cells were stably transfected with a dominant-negative variant of \
PS1 (PS1 DN), which has a mutation in its active site and thus is \
inactive [18]. Since this variant associated with other components of \
the γ-secretase complex, it suppresses endogenous γ-secretase activity \
[18]. Cells stably overexpressing the wild-type PS1 (PS1 WT) were used \
as control. Activation of myc-tagged TREM2 in PS1 WT-overexpressing \
cells increased [Ca2+]i. In contrast, cells expressing the PS1 DN did \
not respond to stimulation of TREM2."

g(HGNC:PSEN1, var("?")) -| act(complex(SCOMP:"gamma Secretase Complex"))
g(HGNC:PSEN1, var("?")) -| act(p(HGNC:TREM2))

UNSET CustomCellLine

SET Support = "On the one hand, macrophages of TREM2 knockout (KO) mice show \
increased production of inflammatory cytokines upon Toll-like receptor \
activation [20] and decreased phagocytic activity [15]."

SET Species = "10090"
p(MGI:Trem2, loc(MESHA:Macrophages)) positiveCorrelation bp(GOBP:phagocytosis)
p(MGI:Trem2, loc(MESHA:Macrophages)) negativeCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
UNSET Species

SET Support = "On the other hand, activation of TREM2 in microglia increased the \
expression of chemokine receptors and cell migration [21, 22]."

act(p(HGNC:TREM2, loc(MESHA:Microglia))) -> p(SFAM:"Chemokine Receptor Family")
act(p(HGNC:TREM2, loc(MESHA:Microglia))) -> bp(GOBP:"microglial cell migration")

SET Support = "Interestingly, homozygous mutations in TREM2 have been shown to cause \
Nasu-Hakola disease and FTD, while the heterozygous R47H variant is \
associated with an increased risk for development of AD [11, 16, 23]."

g(HGNC:TREM2, var("?")) -> path(DO:"Nasu-Hakola disease")
g(HGNC:TREM2, var("?")) -> path(MESHD:"Frontotemporal Dementia")
p(HGNC:TREM2, var("p.Arg47His")) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "HEK293 or BV-2 cells overexpressing mutant TREM2 showed decreased \
phagocytic activity as compared to cells expressing WT TREM2, \
suggesting that the mutations of TREM2 cause a loss of function [16]."

SET CustomCellLine = "BV-2"
SET CellLine = "HEK293"
p(HGNC:TREM2, var("?")) negativeCorrelation bp(GOBP:phagocytosis)
UNSET CustomCellLine
UNSET CellLine

SET Support = "Recent data also showed that the deletion of TREM2 led to decreased \
clustering of microglia around amyloid plaques in a mouse model of AD \
and also decreased the phagocytic activity [14]."

SET Species = "10090"
SET MeSHDisease = "Alzheimer Disease"
p(MGI:Trem2) positiveCorrelation a(MESHA:Microglia, loc(CHEBI:"amyloid-beta"))
p(MGI:Trem2) positiveCorrelation bp(GOBP:phagocytosis)
UNSET MeSHDisease

SET Support = "Another study also showed decreased number of myeloid cells in brains \
of TREM2 KO mice crossed with APP transgenic mice [15]. However, cells \
associated with plaques in this model revealed characteristics of \
peripheral macrophages that might invade the brain [15]."

p(MGI:Trem2) positiveCorrelation a(MESHA:"Myeloid Cells", loc(MESHA:Brain))
path(MESHD:"Plaque, Amyloid") positiveCorrelation a(MESHA:Macrophages)

SET Support = "Isolated microglia from PS2 KO mice showed decreased phagocytosis and \
migration [24, 25], but the molecular mechanisms were unclear."

p(MGI:Psenen, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:phagocytosis)
p(MGI:Psenen, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:"microglial cell migration")
UNSET Species

SET Support = "Together with the present data, these findings suggest that \
accumulated TREM2 CTFs might trap DAP12 and thereby interfere with \
TREM2 signaling (Fig. 3)."

p(HGNC:TREM2, frag("?_*")) -| act(p(HGNC:TREM2), ma(GOBP:signaling))

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Neurobiol Aging. 2018 May 17;69:151-166. doi: 10.1016/j.neurobiolaging.2018.04.019.", "29906661"}

SET Section = "Abstract"

SET Support = "Rare heterozygous coding variants in the triggering receptor expressed \
in myeloid cells 2 (TREM2) gene, conferring increased risk of \
developing late-onset Alzheimer's disease, have been identified."

g(HGNC:TREM2, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")

UNSET Section

SET Section = "Results"

SET Support = "Treml1, the gene immediately downstream of Trem2, was silent in the WT \
but upregulated in the KO. The difference in Treml1 expression be- \
tween WT and KO is the largest effect seen, even more so than that of \
Trem2, at all time points and tissues (Fig. 1B)."

# Tissues here refer to Hippocampus and Cerebral Cortex
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Treml1, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) negativeCorrelation r(MGI:Treml1, loc(MESHA:"Cerebral Cortex"))

SET Support = "We found that the number of DE genes between the WT and KO mice was \
16-fold higher at 4 months cortex, where a total of 1274 genes were \
found to be differentially expressed, compared to 8 months cortex, \
where the expression of 79 genes was altered. The same trend was seen \
in the hippocampus, although fewer genes were differentially expressed \
at the early time point 495 at 4 months compared with time point 126 \
at 8 months."

SET TimePoint = {"4 months", "8 months"}
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation bp(GOBP:"gene expression")
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) negativeCorrelation bp(GOBP:"gene expression")
UNSET TimePoint

SET Support = "The only substantial overlap in DE genes across time and tissue was \
found at 4 months, where the hippocampus and cortex shared 277 DE \
genes (see Supplementary File 6). This represents more than half of \
the DE genes in the hippocampus but less than a quarter of the DE \
genes in the cortex. Among these are genes previously associated with \
AD. For example, Rbm3, which has been shown to mediate structural \
plasticity and have protective effects on cooling in neurodegeneration \
(Peretti et al., 2015), is downregulated in the KO. Nfya, which is \
also downregulated, has been described to have a role in angiogenesis \
(Xu et al., 2016). Treml2, which was upregulated in the cortex at 4 \
months and in both tissues at 8 months, has been reported to contain \
variants with a protective effect in AD (Benitez et al., 2014)."

SET TimePoint = "4 months"
r(MGI:Trem2, loc(MESHA:Hippocampus)) positiveCorrelation r(MGI:Rbm3, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) positiveCorrelation r(MGI:Rbm3, loc(MESHA:"Cerebral Cortex"))
r(MGI:Trem2, loc(MESHA:Hippocampus)) positiveCorrelation r(MGI:Nfya, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) positiveCorrelation r(MGI:Nfya, loc(MESHA:"Cerebral Cortex"))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) negativeCorrelation r(MGI:Treml1, loc(MESHA:"Cerebral Cortex"))
UNSET TimePoint
SET TimePoint = "8 months"
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) negativeCorrelation r(MGI:Treml1, loc(MESHA:"Cerebral Cortex"))
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Treml1, loc(MESHA:Hippocampus))
UNSET TimePoint
p(MGI:Rbm3) -- bp(GOBP:"regulation of synapse structural plasticity")
p(MGI:Rbm3) negativeCorrelation path(HP:Neurodegeneration)
r(MGI:Nfya) -- bp(GOBP:angiogenesis)
g(MGI:Treml1, var("?")) negativeCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "Only Trem2 (as expected), Treml1, Grp17, and Fam107a are \
differentially expressed at all time points and tissues."

# Grp17 is not a member of this species of mouse
r(MGI:Trem2, loc(MESHA:Hippocampus)) -- r(MGI:Fam107a, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) -- r(MGI:Fam107a, loc(MESHA:"Cerebral Cortex"))

SET Support = "Interestingly, Grp17 has been previously associated with Nasu-Hakola \
disease (Satoh et al., 2017), a disease characterized by homozygous \
loss of function of Trem2 or its signaling partner Tyrobp (Paloneva et \
al., 2002), and has been successfully targeted with a marketed \
antiasthmatic drug that reduces neuroinﬂammation and elevates \
hippocampal neurogenesis resulting in improvements to learning and \
memory in older animals (Marschallinger et al., 2015)."

g(MGI:Trem2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")
g(MGI:Tyrobp, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")

SET Support = "Fam107a, also known as Drr1, has been described as a stress-induced \
actine-bundling factor that modulates synaptic efﬁcacy and cognition \
(Schmidt et al., 2011) and has also been connected to brain cancer \
(Dudley et al., 2014; Leet al., 2010 )."

p(MGI:Fam107a) -- bp(GOBP:cognition)
p(MGI:Fam107a) -- bp(GOBP:"modulation of synaptic transmission")
p(MGI:Fam107a) positiveCorrelation bp(GOBP:"actin filament bundle assembly")

SET Support = "Ontology analysis showed enrichment for genes involved in response to \
unfolded proteins and ubiquitination-related functions at 4 months in \
both tissues (Table 1; also see S8 Table). Both functions were driven \
mainly, but not exclusively, by the differential expression of genes \
of the DnaJ family (DnaJA2 and DnajB2) and heat shock proteins, for \
example, HspA8 and Hsph1. Enrichment for “response to heat” was \
signiﬁcant in the hippocampus and was driven by Camk2g and Camk2b, \
members of the calmodulin-dependent protein kinase subfamily involved \
in calcium signaling. A similar trend was seen in the cortex."

# Enrichment means increase
SET TimePoint = "4 months"
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Dnaja2, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) negativeCorrelation r(MGI:Dnaja2, loc(MESHA:"Cerebral Cortex"))
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Dnajb2, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) negativeCorrelation r(MGI:Dnajb2, loc(MESHA:"Cerebral Cortex"))
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Hspa8, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) negativeCorrelation r(MGI:Hspa8, loc(MESHA:"Cerebral Cortex"))
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Hsph1, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) negativeCorrelation r(MGI:Hsph1, loc(MESHA:"Cerebral Cortex"))
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Camk2g, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Camk2b, loc(MESHA:Hippocampus))
UNSET TimePoint

SET Support = "At 8 months, both tissues showed enrichment for membrane targeting- \
related functions, driven mainly by ribosomal proteins such as Rpl10, \
but there were also differences between them. In hippocampus, there \
was enrichment for “regulation of macroautophagy” driven by Uchl1, \
which has been previously implicated in AD, and also for “organelle \
transport along microtubule” driven by Cdc42 and CopG1. In the cortex, \
enrichment for “cellular response to vascular endothelial growth \
factor stimulus” was driven by Flt1 (also know as VEGFR1) and Dll4, \
which have been both described as having a role in endothelial \
sprouting (Eilkenet al., 2017; Pitulescu et al., 2017)."

SET TimePoint = "8 months"
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Rpl10, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) negativeCorrelation r(MGI:Rpl10, loc(MESHA:"Cerebral Cortex"))
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Uchl1, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Cdc42, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:Hippocampus)) negativeCorrelation r(MGI:Copg1, loc(MESHA:Hippocampus))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) negativeCorrelation r(MGI:Flt1, loc(MESHA:"Cerebral Cortex"))
r(MGI:Trem2, loc(MESHA:"Cerebral Cortex")) negativeCorrelation r(MGI:Dll4, loc(MESHA:"Cerebral Cortex"))
UNSET TimePoint
p(MGI:Uchl1) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(MGI:Uchl1) positiveCorrelation bp(GOBP:"regulation of macroautophagy")
p(MGI:Cdc42) positiveCorrelation bp(GOBP:"organelle transport along microtubule")
p(MGI:Copg1) positiveCorrelation bp(GOBP:"organelle transport along microtubule")
p(MGI:Flt1) positiveCorrelation bp(GOBP:"cellular response to vascular endothelial growth factor stimulus")
p(MGI:Dll4) positiveCorrelation bp(GOBP:"cellular response to vascular endothelial growth factor stimulus")
p(MGI:Flt1) -- bp(GOBP:"blood vessel endothelial cell proliferation involved in sprouting angiogenesis")
p(MGI:Dll4) -- bp(GOBP:"blood vessel endothelial cell proliferation involved in sprouting angiogenesis")

# Several of the subsequent sections/paragraphs are about relating modules which yield no useful BEL info

SET Support = "The tan module was of particular interest because it contained Trem2 \
and its signaling partner Tyrobp and, consistently with Trem2 being \
expressed in microglia, was assigned a microglial identity using cell \
type markers."

a(MESHA:Microglia) -> p(MGI:Trem2)

SET Support = "Furthermore, the pink module top hub gene Col8a2, encodes a protein \
whose absence has been described to produce progressive alterations in \
endothelial cell morphology and cell loss(Jun et al., 2012)."

p(MGI:Col8a2) -> a(MESHA:"Endothelial Cells")
p(MGI:Col8a2) -| bp(GOBP:"endothelial cell morphogenesis")

SET Support = "Npc2, one of the genes causing frontotemporal dementia when mutated \
(Zech et al., 2013), is also part of this module."

g(MGI:Npc2, var("?")) -> path(MESHD:"Frontotemporal Dementia")
g(MGI:Npc2) -- g(MGI:Col8a2)

SET Support = "Its top hub gene, Prkar1b, has been previously linked with \
neurodegeneration (Wong et al., 2014) and the second one, Clstn1, has \
been shown to be associated with pathogenic mechanisms of AD (Vagnoni \
et al.,2012)."

# Both of these genes are in the same "module"
g(MGI:Prkar1b) positiveCorrelation path(HP:Neurodegeneration)
g(MGI:Clstn1) positiveCorrelation path(MESHD:"Alzheimer Disease")
g(MGI:Prkar1b) -- g(MGI:Clstn1)

SET Support = "The WT tan module was enriched for microglia cell-type markers and for \
translation-related ontology terms and its top hub gene was the \
ribosomal protein, Rpl18a."

g(MGI:Rpl18a) positiveCorrelation a(MESHA:Microglia)

SET Support = "Interestingly, the KO yellow module top hub gene was Atp6v0c. This \
gene encodes a component of vacuolar ATPase (V-ATPase), a multisubunit \
enzyme that is necessary for intracellular processes receptor-mediated \
endocytosis and synaptic vesicle proton gradient generation (Lee et \
al., 2010)."

g(MGI:Atp6v0c) -- bp(GOBP:"vacuolar proton-transporting V-type ATPase complex assembly")
g(MGI:Atp6v0c) positiveCorrelation bp(GOBP:"establishment or maintenance of transmembrane electrochemical gradient")

SET Support = "Atp6v0c has been shown to alter autophagy-lysosome pathway function \
and metabolism of proteins that accumulate in neurodegenerative \
disease (Mangieriet al ., 2014) and has been described as a promising \
target for therapeutic development (Higashida et al., 2017)."

g(MGI:Atp6v0c) -- bp(GOBP:"protein targeting to lysosome involved in chaperone-mediated autophagy")
g(MGI:Atp6v0c) -- path(MESHD:"Neurodegenerative Diseases")

SET Support = "Mapt, the gene that encodes the tau protein, which has been associated \
with numerous types of neurodegenerative disorders (Spillantini and \
Goedert, 2013), becomes strongly associated with the module."

# The module it is associated with is similar to the "microglia" module but also has metabolic/catabolic processes
g(MGI:Mapt) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")
g(MGI:Mapt) -- a(MESHA:Microglia)

SET Support = "The WT light cyan module was enriched for endothelial cell markers and \
terms associated with endothelial cell functions, like angiogenesis, \
and also for other functions related to AD such as phagocytosis and \
response to unfolded protein. Its top hub gene Ttbk1 (tau tubulin \
kinase 1), whose encoded protein is a neuron-speciﬁc serine/threonine \
and tyrosine kinase that regulates phosphorylation of tau, has been \
implicated in both the pathology of amyotrophic lateral sclerosis \
(Liachko et al., 2014) and AD (Vázquez-Higuera et al., 2011)."

g(MGI:Ttbk1) -- a(MESHA:"Endothelial Cells")
g(MGI:Ttbk1) -- bp(GOBP:"endothelial cell activation")
act(p(MGI:Ttbk1), ma(kin)) regulates p(MGI:Mapt, pmod(Ph))
g(MGI:Ttbk1) -- path(DO:"amyotrophic lateral sclerosis")
g(MGI:Ttbk1) -- path(MESHD:"Alzheimer Disease")

SET Support = "Ptprb, is also reportedly involved in the establishment of endothelial \
cell polarity and lumen formation (Hayashi et al., 2013)."

g(MGI:Ptprb) -- bp(GOBP:"endothelial tube lumen extension")

SET Support = "Flt1 has been described to have a role in motor neuron degeneration \
(Poesen et al., 2008)."

p(MGI:Flt1) -| a(MESHA:"Motor Neurons")

SET Support = "Most importantly, the disruption of the light cyan module in the KO \
network suggested that endothelial cells become dyscoordinated in the \
absence of Trem2, the overexpression of Treml1, or when both effects \
are combined."

p(MGI:Trem2) positiveCorrelation bp(GOBP:"endothelial cell activation")
p(MGI:Treml1) negativeCorrelation bp(GOBP:"endothelial cell activation")

SET Support = "Overall, we found that the absence of Trem2, or potentially the \
overexpression of Treml1 or the combined effect of both, in microglia \
produces a ripple effect that provokes the disruption of a subnetwork \
of genes enriched for endothelial cell gene markers and functions at \
an early time point, aged 4 months, with the effect being stronger in \
the cortex at this early time point. At a later stage, aged 8 months, \
cortex and hippocampus are affected differently with the subnetwork of \
genes with microglial identity being more perturbed in the \
hippocampus. These are time- and tissue-speciﬁc effects that evolve \
with age."

SET TimePoint = "4 months"
SET MeSHAnatomy = {"Hippocampus", "Cerebral Cortex"}
p(MGI:Trem2, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:"endothelial cell activation")
p(MGI:Treml1, loc(MESHA:Microglia)) negativeCorrelation bp(GOBP:"endothelial cell activation")
UNSET TimePoint
UNSET MeSHAnatomy
SET TimePoint = "8 months"
SET MeSHAnatomy = "Hippocampus"
p(MGI:Trem2, loc(MESHA:Microglia)) positiveCorrelation bp(GOBP:"microglial cell activation")
p(MGI:Treml1, loc(MESHA:Microglia)) negativeCorrelation bp(GOBP:"microglial cell activation")
UNSET MeSHAnatomy
UNSET TimePoint

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Elife. 2016 Dec 20;5. pii: e20391. doi: 10.7554/eLife.20391.", "27995897"}

SET Section = "Abstract"

SET Support = "Genetic variations in the myeloid immune receptor TREM2 are linked to \
several neurodegenerative diseases."

g(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")

SET Support = "Biophysical and cellular methods indicate that Nasu-Hakola mutations \
impact protein stability and decrease folded TREM2 surface expression, \
whereas Alzheimer's risk variants impact binding to a TREM2 ligand."

SET Disease = "Nasu-Hakola disease"
p(HGNC:TREM2, var("?")) -| bp(GOBP:"protein stabilization")
p(HGNC:TREM2, var("?")) -| bp(GOBP:"protein folding")
UNSET Disease
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:TREM2, var("?"))
# Do not know if following p() should include var()
p(HGNC:TREM2, var("?")) negativeCorrelation act(p(HGNC:TREM2))

UNSET Section

SET Section = "Results"

# First section only refers to crystal structure -- not relevant for BEL

SET Support = "The side chains of mutations causing NHD (Y38, T66 and V126) are all \
buried within the core of the Ig fold, whereas the side chains of \
verified AD risk variants (R47H and R62H) and possible AD risk \
variants (N68K, D87N and T96K) all lie on the protein surface (Figure \
1b)."

# NHD point mutations also cited in next evidence
path(DO:"Nasu-Hakola disease") positiveCorrelation p(HGNC:TREM2, var("p.Tyr38Cys"))
path(DO:"Nasu-Hakola disease") positiveCorrelation p(HGNC:TREM2, var("p.Thr66Met"))
path(DO:"Nasu-Hakola disease") positiveCorrelation p(HGNC:TREM2, var("p.Val126Gly"))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:TREM2, var("p.Arg47His"))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:TREM2, var("p.Arg62His"))
# Last three variants not included because no definitive proof yet cited

SET Support = "By contrast, although the buried NHD mutants Y38C, T66M, and V126G \
were secreted, they elute much earlier, consistent with aggregation \
and misfolding of these proteins (Figure 2—figure supplement 1m–o)."

g(HGNC:TREM2, var("p.Tyr38Cys")) negativeCorrelation bp(GOBP:"protein stabilization")
g(HGNC:TREM2, var("p.Thr66Met")) negativeCorrelation bp(GOBP:"protein stabilization")
g(HGNC:TREM2, var("p.Val126Gly")) negativeCorrelation bp(GOBP:"protein stabilization")
g(HGNC:TREM2, var("p.Tyr38Cys")) negativeCorrelation bp(GOBP:"protein folding")
g(HGNC:TREM2, var("p.Thr66Met")) negativeCorrelation bp(GOBP:"protein folding")
g(HGNC:TREM2, var("p.Val126Gly")) negativeCorrelation bp(GOBP:"protein folding")
g(HGNC:TREM2, var("p.Tyr38Cys")) -| act(p(HGNC:TREM2, var("p.Tyr38Cys")))
g(HGNC:TREM2, var("p.Thr66Met")) -| act(p(HGNC:TREM2, var("p.Thr66Met")))
g(HGNC:TREM2, var("p.Val126Gly")) -| act(p(HGNC:TREM2, var("p.Val126Gly")))

SET Support = "However, as predicted by our structural analysis, anti-TREM2 staining \
was decreased for the NHD mutants Y38C, T66M, and V126G, but not for \
the AD risk variants R47H, R62H, N68K, D87N, and T96K (Figure 2d)."

SET Assay = "flow cytometry"
g(HGNC:TREM2, var("p.Tyr38Cys")) -| p(HGNC:TREM2, var("p.Tyr38Cys"), loc(MESHA:"Cell Membrane"))
g(HGNC:TREM2, var("p.Thr66Met")) -| p(HGNC:TREM2, var("p.Thr66Met"), loc(MESHA:"Cell Membrane"))
g(HGNC:TREM2, var("p.Val126Gly")) -| p(HGNC:TREM2, var("p.Val126Gly"), loc(MESHA:"Cell Membrane"))
UNSET Assay

SET Support = "We next examined cells expressing TREM2 WT, NHD mutant Y38C, and AD \
risk variant R47H by immuofluorescence confocal microscopy in the \
presence or absence of permeabilization. We found that the surface \
expression pattern in non-permeabilized cells was similar for all cell \
types, whereas in permeabilized cells, the NHD mutant Y38C displayed \
diffuse intracellular accumulation, in comparison to WT and R47H which \
displayed a distinct punctate staining pattern (Figure 2g)."

SET TechnologyUsed = "Confocal Microscopy"
bp(HP:"increased permeability") positiveCorrelation p(HGNC:TREM2, var("p.Tyr38Cys"), loc(MESHA:"Intracellular Space"))
UNSET TechnologyUsed

SET Support = "Consistent with our observation of FLAG-TREM2 on the cell surface for \
all the variant proteins, we found that while both the Y38C and R47H \
variants have some signaling potential, as assessed by phosphorylated- \
ERK blotting in RAW264.7 and Cos-7 cells, neither of these variants \
sustains signaling as efficiently as WT TREM2 (Figure 2i,j)."

SET Assay = "phosphorylated-ERK blotting"
g(HGNC:TREM2, var("p.Tyr38Cys")) -| act(p(HGNC:TREM2, var("p.Tyr38Cys")), ma(GOBP:signaling))
g(HGNC:TREM2, var("p.Arg47His")) -| act(p(HGNC:TREM2, var("p.Arg47His")), ma(GOBP:signaling))
UNSET Assay

SET Support = "Thus the R47H mutation in TREM2 seems to induce a small, but \
measurable, conformational change, whereas the R62H and T96K mutations \
do not induce any structural changes detectable by CD."

# CD = circular dichroism spectroscopy
g(HGNC:TREM2, var("p.Arg47His")) negativeCorrelation bp(GOBP:"protein stabilization")
g(HGNC:TREM2, var("p.Arg47His")) negativeCorrelation bp(GOBP:"protein folding")

SET Support = "Consistent with the results of previous work (Cannon et al., 2012), we \
detected TREM2 binding to phospholipids by both ELISA and liposome \
sedimentation."

SET Assay = {"liposome sedimentation", "ELISA"}
p(HGNC:TREM2) -> complex(p(HGNC:TREM2), a(CHEBI:phospholipid))
UNSET Assay

SET Support = "In initial experiments, we found that both human and mouse TREM2/SA-PE \
tetramers were able to bind Neuro2A (N2A) and THP-1 cells (Figure \
4b–d,f,g and Figure 4—figure supplement 2a,c)."

# THP-1 cells are monocytes
p(HGNC:TREM2) -> complex(p(HGNC:TREM2), a(MESHA:Monocytes))

SET Support = "To validate the specificity of our reagent, we cultured THP-1 cells \
with various immune stimuli and found that overnight treatment with \
PMA/ionomycin dramatically decreased binding of TREM2/SA-PE tetramers \
to THP-1 cells (Figure 4b and c), while the myeloid cell marker CD45 \
remained unchanged (not shown)."

SET TimePoint = "overnight"
composite(a(CHEBI:ionomycin), a(CHEBI:"phorbol 13-acetate 12-myristate")) -| complex(p(HGNC:TREM2), a(MESHA:Monocytes))
UNSET TimePoint

SET Support = "We next sought to interrogate the endogenous target recognized by \
TREM2 on the cell surface, and found that TREM2 binding is sensitive \
to proteinase treatment of cells prior to incubation with our staining \
reagent (Figure 4d and Figure 4—figure supplement 2b). Treatment with \
proteinase K abolished binding while the more specific proteases \
chymotrypsin and elastase had an intermediate effect."

# Proteinase K is a wide spectrum serine protease cocktail, chymotrypsin/elastase did not have a significant effect
a(MESHC:"Serine Proteases") -| complex(p(HGNC:TREM2), a(MESHA:Monocytes))
a(MESHC:"Serine Proteases") -| act(p(HGNC:TREM2, loc(MESHA:"Cell Membrane")), ma(MESHPP:"Protein Binding"))

SET Support = "In CHO (Figure 4e), THP-1 (Figure 4f) and N2A (Figure 4g) cells, \
treatment with heparinases had a pronounced effect on cell surface \
binding while chondroitinase only slightly diminished binding, \
together implicating GAGs, specifically heparin sulfate, as a major \
component of the cell surface TREM2-L."

a(CHEBI:"sulfated glycosaminoglycan", loc(MESHA:"Cell Membrane")) positiveCorrelation p(HGNC:TREM2)
a(CHEBI:"heparan sulfates", loc(MESHA:"Cell Membrane")) -> p(HGNC:TREM2, loc(MESHA:"Cell Membrane"))
complex(a(CHEBI:"heparan sulfates", loc(MESHA:"Cell Membrane")), p(HGNC:TREM2)) -> complex(p(HGNC:TREM2), a(MESHA:Monocytes))

SET Support = "Consistent with our hypothesis, the AD-linked variant R47H displayed \
markedly diminished binding to N2A and THP-1 cells."

g(HGNC:TREM2, var("p.Arg47His")) -| act(p(HGNC:TREM2, loc(MESHA:"Cell Membrane")), ma(MESHPP:"Protein Binding"))

SET Support = "In stark contrast, the potentially AD-protective T96K variant \
significantly increased binding."

g(HGNC:TREM2, var("p.Thr96Lys")) -> act(p(HGNC:TREM2, loc(MESHA:"Cell Membrane")), ma(MESHPP:"Protein Binding"))

SET Support = "These data, together with our direct phospholipid binding experiments, \
suggest that TREM2 AD-risk variants retain phospholipid binding, and \
instead impact binding to cell surface GAGs."

g(HGNC:TREM2, var("p.Arg47His")) -| complex(p(HGNC:TREM2), a(CHEBI:"sulfated glycosaminoglycan", loc(MESHA:"Cell Membrane")))


UNSET Section

##################################################################################

SET Citation = {"PubMed", "Stem Cell Reports. 2017 Jun 6;8(6):1727-1742. doi: 10.1016/j.stemcr.2017.05.017.", "28591653"}

SET Section = "Results"

SET Support = "Finally, in pilot experiments, we tested the ability of different \
media and growth factors to induce ramified microglia-like morphology \
in our macrophages, following previous evidence that astrocyte-derived \
granulocyte M-CSF (GM-CSF), M-CSF, and transforming growth factor β \
(TGFβ) can each induce ramified morphology in microglia (Schilling et \
al., 2001), and that IL-34 and GM-CSF can ramify blood monocytes \
(Etemad et al., 2012, Ohgidani et al., 2014)."

SET MeSHAnatomy = "Microglia"
p(HGNC:CSF1) -> bp(GOBP:"morphogenesis of a branching structure")
p(HGNC:CSF2) -> bp(GOBP:"morphogenesis of a branching structure")
p(HGNC:TGFB1) -> bp(GOBP:"morphogenesis of a branching structure")
UNSET MeSHAnatomy
SET MeSHAnatomy = "Monocytes"
p(HGNC:CSF2) -> bp(GOBP:"morphogenesis of a branching structure")
p(HGNC:IL34) -> bp(GOBP:"morphogenesis of a branching structure")
UNSET MeSHAnatomy

SET Support = "FLT3, a marker of definitive hematopoiesis, showed higher expression \
in the bloodMono samples (5.5-fold, adjusted p < 2.4 × 10−8), along \
with several HLA genes, in agreement with blood monocytes exposure to \
priming cytokines in the blood and their role in antigen presentation \
to T cells."

p(HGNC:FLT3) biomarkerFor bp(MESHPP:Hematopoiesis)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> r(HGNC:FLT3)

SET Support = "Meanwhile MAF, a known marker of primitive hematopoiesis, showed \
higher expression in pMacpre (21.7-fold, adjusted p < 7.8 × 10−7)."

p(HGNC:MAF) biomarkerFor bp(MESHPP:Hematopoiesis)
a(MESHA:"Monocyte-Macrophage Precursor Cells") -> r(HGNC:MAF)

SET Support = "APOE, variants of which are major risk factors for AD, was among the \
most strongly differentially expressed genes, being very low in \
bloodMono and high in all other populations (Figure S3A)."

# Differentiate between fetal microglia, and ipsc microglia?
p(HGNC:APOE) positiveCorrelation path(MESHD:"Alzheimer Disease")
a(MESHA:Monocytes, loc(MESHA:Blood)) -| r(HGNC:APOE, loc(MESHA:Monocytes))
a(MESHA:Microglia) -> r(HGNC:APOE)
a(MESHA:"Monocyte-Macrophage Precursor Cells") -> r(HGNC:APOE)
a(MESHA:Macrophages) -> r(HGNC:APOE)

# SET Evidence = "Comparison with a previously published expression dataset for cells derived from human brain tissue (Zhang et al., 2016) \
# showed that all the cell types in the current study cluster with human cortex myeloid cells and not with cortical oligodendrocytes, \
# astrocytes, neurons, or endothelial cells (Figure 2B)."
# How to show similar gene expression patterns?

SET Support = "As a set, genes previously identified to be associated specifically \
with microglia but not with blood monocytes (Bennett et al., 2016, \
Butovsky et al., 2014, Melief et al., 2012) (Figure 2C) showed similar \
expression in co-pMG and fetalMG. Most notably, the six key microglia- \
specific genes identified by Butovsky et al. (2014), MERTK, GPR34, \
PROS1, C1QA, GAS6, and P2RY12, were all strongly expressed in co-pMG, \
whereas bloodMono poorly expressed most of these markers (highlighted \
in magenta, Figure 2C) These expression profiles were confirmed by \
qRT-PCR (Figure 2D), which also showed that co-pMG had comparable \
levels of expression of these microglial genes with cultured adult \
human microglia and with directly processed adult human microglia, and \
that while blood monocyte-derived macrophages also upregulated several \
of these genes, they mostly did not reach the same expression level. \
Notably, pMacpre, pMac, and pMGL also expressed high levels of most of \
these genes, along with many of those identified by Melief et al. \
(2012)."

SET Assay = "qRT-PCR"
# PROS1 and TMEM119 is not significant in the figure
a(MESHA:Microglia) -> r(HGNC:MERTK)
a(MESHA:Microglia) -> r(HGNC:GPR34)
# a(MESHA:Microglia) positiveCorrelation r(HGNC:PROS1)
a(MESHA:Microglia) -> r(HGNC:C1QA)
a(MESHA:Microglia) -> r(HGNC:GAS6)
a(MESHA:Microglia) -> r(HGNC:P2RY12)
a(MESHA:Microglia) -> r(HGNC:TREM2)
a(MESHA:Monocytes, loc(MESHA:Blood)) -| r(HGNC:MERTK)
a(MESHA:Monocytes, loc(MESHA:Blood)) -| r(HGNC:GPR34)
# a(MESHA:Monocytes, loc(MESHA:Blood)) negativeCorrelation r(HGNC:PROS1)
a(MESHA:Monocytes, loc(MESHA:Blood)) -| r(HGNC:C1QA)
a(MESHA:Monocytes, loc(MESHA:Blood)) -| r(HGNC:GAS6)
a(MESHA:Monocytes, loc(MESHA:Blood)) -| r(HGNC:P2RY12)
a(MESHA:Monocytes, loc(MESHA:Blood)) -| r(HGNC:TREM2)
a(MESHA:Macrophages) -> r(HGNC:MERTK)
a(MESHA:Macrophages) -> r(HGNC:GPR34)
# a(MESHA:Macrophages) positiveCorrelation r(HGNC:PROS1)
a(MESHA:Macrophages) -> r(HGNC:C1QA)
a(MESHA:Macrophages) -> r(HGNC:GAS6)
a(MESHA:Macrophages) -> r(HGNC:TREM2)
UNSET Assay

SET Support = "Gene ontology analysis identified biological processes with \
significant enrichment in these gene sets, demonstrating that co-pMG \
downregulate genes in pathways associated with type I interferon \
responses (involved in antiviral responses), Toll-like receptor 1 \
(TLR1) and TLR2 signaling (bacterial and yeast recognition), and \
antigen presentation, relative to the monoculture populations."

a(MESHA:Microglia) negativeCorrelation bp(GOBP:"type I interferon production")
a(MESHA:Microglia) negativeCorrelation bp(GOBP:"toll-like receptor 1 signaling pathway")
a(MESHA:Microglia) negativeCorrelation bp(GOBP:"toll-like receptor 2 signaling pathway")
a(MESHA:Microglia) negativeCorrelation bp(GOBP:"antigen processing and presentation")

SET Support = "Meanwhile, genes upregulated in co-pMG were enriched for biological \
processes including differentiation, chemotaxis/migration, regulation \
of cell-cell adhesion, and metal ion response (Hancock et al., 2014), \
all of which would be important for microglia to carry out their \
homeostatic surveillance and clearing functions."

a(MESHA:Microglia) positiveCorrelation bp(GOBP:"cell chemotaxis")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"microglia differentiation")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"cell adhesion")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"response to metal ion")

SET Support = "FERMT2, TREM2, APOE, and UCHL1 were expressed in fetalMG and co-pMG \
but not in bloodMono (although TREM2 was upregulated in bloodMono- \
derived macrophages, Figure 2D)."

a(MESHA:Microglia) -> r(HGNC:FERMT2)
a(MESHA:Microglia) -> r(HGNC:TREM2)
a(MESHA:Microglia) -> r(HGNC:APOE)
a(MESHA:Microglia) -> r(HGNC:UCHL1)
a(MESHA:Monocytes, loc(MESHA:Blood)) -| r(HGNC:FERMT2)
a(MESHA:Monocytes, loc(MESHA:Blood)) -| r(HGNC:TREM2)
a(MESHA:Monocytes, loc(MESHA:Blood)) -| r(HGNC:APOE)
a(MESHA:Monocytes, loc(MESHA:Blood)) -| r(HGNC:UCHL1)

SET Support = "Other key AD-related genes expressed in fetalMG and co-pMG (and \
bloodMono) included APP, PICALM, and CD33; PD-related genes included \
PARK15, PINK1, SNCA, and DJ-1; and MND-related genes included C9orf72, \
TDP43, and SOD1. Note that almost all of these genes were also \
expressed in pMacpre, pMac, and pMGL."

path(MESHD:"Alzheimer Disease") positiveCorrelation g(HGNC:APP)
path(MESHD:"Alzheimer Disease") positiveCorrelation g(HGNC:PICALM)
path(MESHD:"Alzheimer Disease") positiveCorrelation g(HGNC:CD33)
path(MESHD:"Parkinson Disease") positiveCorrelation g(HGNC:FBXO7)
path(MESHD:"Parkinson Disease") positiveCorrelation g(HGNC:PINK1)
path(MESHD:"Parkinson Disease") positiveCorrelation g(HGNC:SNCA)
path(MESHD:"Parkinson Disease") positiveCorrelation g(HGNC:PARK7)
path(MESHD:"Motor Neuron Disease") positiveCorrelation g(HGNC:C9orf72)
path(MESHD:"Motor Neuron Disease") positiveCorrelation g(HGNC:TARDBP)
path(MESHD:"Motor Neuron Disease") positiveCorrelation g(HGNC:SOD1)
a(MESHA:Microglia) -> r(HGNC:APP)
a(MESHA:Microglia) -> r(HGNC:PICALM)
a(MESHA:Microglia) -> r(HGNC:CD33)
a(MESHA:Microglia) -> r(HGNC:FBXO7)
a(MESHA:Microglia) -> r(HGNC:PINK1)
a(MESHA:Microglia) -> r(HGNC:SNCA)
a(MESHA:Microglia) -> r(HGNC:PARK7)
a(MESHA:Microglia) -> r(HGNC:C9orf72)
a(MESHA:Microglia) -> r(HGNC:TARDBP)
a(MESHA:Microglia) -> r(HGNC:SOD1)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> r(HGNC:APP)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> r(HGNC:PICALM)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> r(HGNC:CD33)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> r(HGNC:FBXO7)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> r(HGNC:PINK1)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> r(HGNC:SNCA)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> r(HGNC:PARK7)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> r(HGNC:C9orf72)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> r(HGNC:TARDBP)
a(MESHA:Monocytes, loc(MESHA:Blood)) -> r(HGNC:SOD1)
a(MESHA:Macrophages) -> r(HGNC:APP)
a(MESHA:Macrophages) -> r(HGNC:PICALM)
a(MESHA:Macrophages) -> r(HGNC:CD33)
a(MESHA:Macrophages) -> r(HGNC:FBXO7)
a(MESHA:Macrophages) -> r(HGNC:PINK1)
a(MESHA:Macrophages) -> r(HGNC:SNCA)
a(MESHA:Macrophages) -> r(HGNC:PARK7)
a(MESHA:Macrophages) -> r(HGNC:SOD1)

SET Support = "Flow cytometry with directly conjugated antibodies showed CD11b \
(integrin alpha M, a marker for mature myeloid cells and a subunit of \
the complement receptor, CR3, also known as Mac-1), CD14 (a component \
of the receptor for bacterial lipopolysaccharide [LPS]), and CD45 (a \
pan-leukocyte marker and tyrosine phosphatase which dephosphorylates \
several receptor tyrosine kinases), were expressed on all PSC-derived \
macrophage and microglia (Figures 4A and 4B)."

# ITGAM = CD11B
a(MESHA:Microglia) -> p(HGNC:ITGAM)
a(MESHA:Macrophages) -> p(HGNC:ITGAM)
a(MESHA:"Monocyte-Macrophage Precursor Cells") -> p(HGNC:ITGAM)
p(HGNC:ITGAX) positiveCorrelation a(MESHA:"Myeloid Cells")
a(MESHA:Microglia) -> p(HGNC:CD14)
a(MESHA:Macrophages) -> p(HGNC:CD14)
a(MESHA:"Monocyte-Macrophage Precursor Cells") -> p(HGNC:CD14)
# PTPRC = CD45
a(MESHA:Microglia) -> p(HGNC:PTPRC)
a(MESHA:Macrophages) -> p(HGNC:PTPRC)
a(MESHA:"Monocyte-Macrophage Precursor Cells") -> p(HGNC:PTPRC)

SET Support = "CD11c (integrin alpha X, part of the inactivated-C3b receptor 4, CR4), \
was well expressed in monoculture cells, but weakly expressed in co- \
pMG."

# ITGAX = CD11C
a(MESHA:Microglia) -> p(HGNC:ITGAX)
a(MESHA:Macrophages) -> p(HGNC:ITGAX)

SET Support = "The microglia-associated protein MERTK was expressed highly on all \
PSC-derived macrophage and microglia conditions, while the AD- \
associated protein CD33 was detected albeit at low levels."

a(MESHA:Microglia) -> p(HGNC:MERTK)
a(MESHA:Macrophages) -> p(HGNC:MERTK)
a(MESHA:"Monocyte-Macrophage Precursor Cells") -> p(HGNC:MERTK)
a(MESHA:Microglia) -> p(HGNC:CD33)
a(MESHA:Macrophages) -> p(HGNC:CD33)
a(MESHA:"Monocyte-Macrophage Precursor Cells") -> p(HGNC:CD33)

SET Support = "IBA1 (a cytoplasmic calcium-binding protein associated with myeloid \
cells, particularly microglia) was readily detectable in co-pMG by \
immunocytochemistry (Figures 1B and 1C)."

a(MESHA:Microglia) -> p(HGNC:AIF1)

SET Support = "Co-pMG roughly “tile” within the neuronal culture, making direct \
contacts with the neurons (Figure 5A)."

a(MESHA:Microglia) positiveCorrelation a(MESHA:Neurons)

SET Support = "We have previously demonstrated that pMac are competent at \
phagocytosing particles, progressive acidification of the maturing \
phagosome being detectable using particles coupled to pH-sensitive \
fluorophores (Kapellos et al., 2016)."

a(MESHA:Macrophages) positiveCorrelation bp(GOBP:phagocytosis)

SET Support = "In contrast, within 5 hr LPS-stimulated co-pMG migrated to form \
clusters, and some microglia had reduced ramifications and increased \
area-to-perimeter ratio, indicative of transition to activated, \
ameboid microglia (Figures 6A–6C and Movie S5)."

SET TimePoint = "5 hours post-stimulation"
a(CHEBI:lipopolysaccharide) -> bp(GOBP:"microglial cell activation")
UNSET TimePoint

SET Support = "Differentially expressed cytokines were then selected for a more \
detailed investigation using Luminex multiplex array, with \
supernatants from pMac, pMGL, co-pMG, and pNeuron, with or without \
LPS/IFNγ stimulation (Figure 7). There was broad correspondence across \
Proteome Profiler and Luminex platforms. Unstimulated neurons secreted \
only macrophage migration inhibitory factor (MIF) and VEGF-A, and when \
stimulated secreted IL-6, IL-8, and a subset of chemokines. pMac \
secreted very few cytokines constitutively (macrophage inflammatory \
protein 1α [MIP1α] and MIPβ, CXCL1 and CXCL10, IL-8, and MIF), but \
secreted the entire panel of 22 cytokines upon stimulation, in \
concordance with our previous publication (which also includes \
comparison with blood monocyte-derived macrophages [Jiang et al., \
2012]). pMGL had a higher baseline number of cytokines secreted, and \
upregulated most cytokines upon stimulation (except IL-23A)."

SET TechnologyUsed = "Luminex"
a(MESHA:Neurons) -> p(HGNC:MIF)
a(MESHA:Neurons) -> p(HGNC:VEGFA)
SET ChallengeType = "lipopolysaccharide/IFN-γ stimulation"
a(MESHA:Neurons) -> p(HGNC:IL6)
# IL-8 = CXCL8
a(MESHA:Neurons) -> p(HGNC:CXCL8)
UNSET ChallengeType
# MIP1a = CCL3, MIP1b = CCL4
a(MESHA:Macrophages) -> p(HGNC:CCL3)
a(MESHA:Macrophages) -> p(HGNC:CCL4)
a(MESHA:Macrophages) -> p(HGNC:CXCL1)
a(MESHA:Macrophages) -> p(HGNC:CXCL8)
a(MESHA:Macrophages) -> p(HGNC:MIF)
SET ChallengeType = "lipopolysaccharide/IFN-γ stimulation"
a(MESHA:Macrophages) -> p(HGNC:CCL3)
a(MESHA:Macrophages) -> p(HGNC:CCL4)
a(MESHA:Macrophages) -> p(HGNC:CCL5)
a(MESHA:Macrophages) -> p(HGNC:CCL20)
a(MESHA:Macrophages) -> p(HGNC:CXCL1)
a(MESHA:Macrophages) -> p(HGNC:CXCL11)
a(MESHA:Macrophages) -> p(HGNC:CXCL9)
a(MESHA:Macrophages) -> p(HGNC:CXCL5)
a(MESHA:Macrophages) -> p(HGNC:CXCL10)
a(MESHA:Macrophages) -> p(HGNC:IL1B)
a(MESHA:Macrophages) -> p(HGNC:IL6)
a(MESHA:Macrophages) -> p(HGNC:CXCL8)
a(MESHA:Macrophages) -> p(HGNC:IL10)
a(MESHA:Macrophages) -> p(HGNC:IL18)
a(MESHA:Macrophages) -> p(HGNC:IL23A)
a(MESHA:Macrophages) -> p(HGNC:IFNG)
a(MESHA:Macrophages) -> p(HGNC:MIF)
a(MESHA:Macrophages) -> p(HGNC:SERPINE1)
a(MESHA:Macrophages) -> p(HGNC:TNFRSF8)
a(MESHA:Macrophages) -> p(HGNC:TNF)
a(MESHA:Macrophages) -> p(HGNC:VEGFA)
UNSET ChallengeType
a(MESHA:Microglia) -> p(HGNC:CCL3)
a(MESHA:Microglia) -> p(HGNC:CCL4)
a(MESHA:Microglia) -> p(HGNC:CXCL1)
a(MESHA:Microglia) -> p(HGNC:CXCL5)
a(MESHA:Microglia) -> p(HGNC:CXCL10)
a(MESHA:Microglia) -> p(HGNC:IL6)
a(MESHA:Microglia) -> p(HGNC:CXCL8)
a(MESHA:Microglia) -> p(HGNC:IL10)
a(MESHA:Microglia) -> p(HGNC:MIF)
a(MESHA:Microglia) -> p(HGNC:SERPINE1)
a(MESHA:Microglia) -> p(HGNC:VEGFA)
SET ChallengeType = "lipopolysaccharide/IFN-γ stimulation"
a(MESHA:Microglia) -> p(HGNC:CCL3)
a(MESHA:Microglia) -> p(HGNC:CCL4)
a(MESHA:Microglia) -> p(HGNC:CCL5)
a(MESHA:Microglia) -> p(HGNC:CCL20)
a(MESHA:Microglia) -> p(HGNC:CXCL1)
a(MESHA:Microglia) -> p(HGNC:CXCL11)
a(MESHA:Microglia) -> p(HGNC:CXCL9)
a(MESHA:Microglia) -> p(HGNC:CXCL5)
a(MESHA:Microglia) -> p(HGNC:CXCL10)
a(MESHA:Microglia) -> p(HGNC:IL1B)
a(MESHA:Microglia) -> p(HGNC:IL6)
a(MESHA:Microglia) -> p(HGNC:CXCL8)
a(MESHA:Microglia) -> p(HGNC:IL10)
a(MESHA:Microglia) -> p(HGNC:IL18)
a(MESHA:Microglia) -> p(HGNC:IFNG)
a(MESHA:Microglia) -> p(HGNC:MIF)
a(MESHA:Microglia) -> p(HGNC:SERPINE1)
a(MESHA:Microglia) -> p(HGNC:TNF)
a(MESHA:Microglia) -> p(HGNC:VEGFA)
UNSET ChallengeType
UNSET TechnologyUsed

UNSET Section

##################################################################################

SET Citation = {"PubMed", "Nat Neurosci. 2017 Aug;20(8):1162-1171. doi: 10.1038/nn.4597. Epub 2017 Jul 3.", "28671693"}

SET Section = "Results"

SET Support = "Paired-end Illumina high-quality deep sequencing was performed on 39 \
microglial samples, 16 corresponding samples of superior parietal \
cortex tissue (referred to from here on as cortex) and 10 epilepsy \
surgery samples (referred to as biopsy)."

SET TechnologyUsed = "Illumina"

SET Support = "Human microglia expressed known microglial genes such as CX3CR1, \
ITGAM, P2RY12 and TYROBP at high levels."

a(MESHA:Microglia) -> r(HGNC:CX3CR1)
a(MESHA:Microglia) -> r(HGNC:ITGAM)
a(MESHA:Microglia) -> r(HGNC:P2RY12)
a(MESHA:Microglia) -> r(HGNC:TYROBP)

SET Support = "Genes characteristic of neurons, astrocytes and oligodendrocytes were \
equally expressed in cortex and epilepsy biopsy samples but were low \
or not expressed in microglia."

# Associations stated in text
a(MESHA:Neurons) -> r(HGNC:DLG2)
a(MESHA:Neurons) -> r(HGNC:DLG4)
a(MESHA:Neurons) -> r(HGNC:ENO2)
a(MESHA:Neurons) -> r(HGNC:MAP2)
a(MESHA:Neurons) -> r(HGNC:RBFOX3)
a(MESHA:Astrocytes) -> r(HGNC:ALDH1L1)
a(MESHA:Astrocytes) -> r(HGNC:AQP4)
a(MESHA:Astrocytes) -> r(HGNC:GFAP)
a(MESHA:Astrocytes) -> r(HGNC:GJA1)
a(MESHA:Astrocytes) -> r(HGNC:MFGE8)
# Oligodendrocytes = oligodendroglia
a(MESHA:Oligodendroglia) -> r(HGNC:MAG)
a(MESHA:Oligodendroglia) -> r(HGNC:MOBP)
a(MESHA:Oligodendroglia) -> r(HGNC:MOG)
a(MESHA:Oligodendroglia) -> r(HGNC:OLIG1)
a(MESHA:Oligodendroglia) -> r(HGNC:OLIG2)
SET BRCO = "Parietal Cortex"
a(MESHA:Brain) -> r(HGNC:DLG2)
a(MESHA:Brain) -> r(HGNC:DLG4)
a(MESHA:Brain) -> r(HGNC:ENO2)
a(MESHA:Brain) -> r(HGNC:MAP2)
a(MESHA:Brain) -> r(HGNC:RBFOX3)
a(MESHA:Brain) -> r(HGNC:ALDH1L1)
a(MESHA:Brain) -> r(HGNC:AQP4)
a(MESHA:Brain) -> r(HGNC:GFAP)
a(MESHA:Brain) -> r(HGNC:GJA1)
a(MESHA:Brain) -> r(HGNC:MFGE8)
a(MESHA:Brain) -> r(HGNC:MAG)
a(MESHA:Brain) -> r(HGNC:MOBP)
a(MESHA:Brain) -> r(HGNC:MOG)
a(MESHA:Brain) -> r(HGNC:OLIG1)
a(MESHA:Brain) -> r(HGNC:OLIG2)
UNSET BRCO
SET MeSHDisease = "Epilepsy"
a(MESHA:Brain) -> r(HGNC:DLG2)
a(MESHA:Brain) -> r(HGNC:DLG4)
a(MESHA:Brain) -> r(HGNC:ENO2)
a(MESHA:Brain) -> r(HGNC:MAP2)
a(MESHA:Brain) -> r(HGNC:RBFOX3)
a(MESHA:Brain) -> r(HGNC:ALDH1L1)
a(MESHA:Brain) -> r(HGNC:AQP4)
a(MESHA:Brain) -> r(HGNC:GFAP)
a(MESHA:Brain) -> r(HGNC:GJA1)
a(MESHA:Brain) -> r(HGNC:MFGE8)
a(MESHA:Brain) -> r(HGNC:MAG)
a(MESHA:Brain) -> r(HGNC:MOBP)
a(MESHA:Brain) -> r(HGNC:MOG)
a(MESHA:Brain) -> r(HGNC:OLIG1)
a(MESHA:Brain) -> r(HGNC:OLIG2)
UNSET MeSHDisease

SET Support = "As expected, many significantly enriched terms associated with the \
innate immune activity of microglia, such as “immune response,” \
“defense response” and “cytokine production.” The profile was also \
enriched for “phagocytosis,” “cell migration” and “cell motility.”"

a(MESHA:Microglia) positiveCorrelation bp(GOBP:"immune response")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"defense response")
a(MESHA:Microglia) positiveCorrelation p(MESHC:Cytokines)
a(MESHA:Microglia) positiveCorrelation bp(GOBP:phagocytosis)
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"cell migration")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"cell motility")

SET Support = "A network involved in cell motility and inflammation containment was \
detected with intracellular tyrosine kinase SYK, involved in cell \
adhesion and cytoskeletal rearrangements. The human microglial core \
profile contained several members of this signaling pathway, such as \
CD11B (α-integrin), CD18 (β-integrin), FCGR and TYROBP (ITAM-motif- \
containing proteins)."

a(MESHA:Microglia) -> r(HGNC:ITGAM)
a(MESHA:Microglia) -> r(HGNC:ITGB2)
a(MESHA:Microglia) -> r(HGNC:FCGR2A)
a(MESHA:Microglia) -> r(HGNC:TYROBP)

SET Support = "Integrin–SYK signaling is also responsible for negatively regulating \
Toll-like receptor (TLR) signaling and the NF-κB pathway by promoting \
the degradation of MYD88 (ref. 21)."

p(HGNC:SYK) positiveCorrelation bp(GOBP:"integrin-mediated signaling pathway")
act(p(HGNC:SYK)) -| bp(GOBP:"toll-like receptor signaling pathway")
act(p(HGNC:SYK)) -| bp(GOBP:"NIK/NF-kappaB signaling")
act(p(HGNC:SYK)) -> deg(p(HGNC:MYD88))

SET Support = "We also detected a protein–protein network involved in cell \
proliferation. CD74, highly expressed by human microglia, prevents MHC \
class II molecules from binding unprocessed peptide and self antigens. \
Its ligands, CXCR2 and CXCR4, were also highly expressed by human \
microglia. In monocytes, heterodimers between CXCR2 and CD74 or \
between CXCR4 and CD74 induce PI3K–AKT signaling and p53 inhibition, \
leading to increased survival, proliferation and apoptosis evasion22. \
CXCR2–CD74 interaction leads to chemotactic signaling, which is \
activated by MIF (macrophage migration inhibitory factor), a gene \
expressed in cortical tissue."

p(HGNC:CD74) positiveCorrelation bp(GOBP:"cell proliferation")
p(HGNC:CD74) => complex(p(HGNC:CD74), p(HGNC:CXCR2))
p(HGNC:CD74) => complex(p(HGNC:CD74), p(HGNC:CXCR4))
a(MESHA:Microglia) -> r(HGNC:CD74)
a(MESHA:Microglia) -> r(HGNC:CXCR2)
a(MESHA:Microglia) -> r(HGNC:CXCR4)
SET MeSHAnatomy = "Monocytes"
complex(p(HGNC:CD74), p(HGNC:CXCR2)) -> act(p(SFAM:"PIK3C Class IA Family"), ma(kin))
complex(p(HGNC:CD74), p(HGNC:CXCR4)) -> act(p(SFAM:"PIK3C Class IA Family"), ma(kin))
complex(p(HGNC:CD74), p(HGNC:CXCR2)) -| bp(GOBP:"intrinsic apoptotic signaling pathway by p53 class mediator")
complex(p(HGNC:CD74), p(HGNC:CXCR4)) -| bp(GOBP:"intrinsic apoptotic signaling pathway by p53 class mediator")
complex(p(HGNC:CD74), p(HGNC:CXCR2)) -| bp(GOBP:"signal transduction by p53 class mediator")
complex(p(HGNC:CD74), p(HGNC:CXCR4)) -| bp(GOBP:"signal transduction by p53 class mediator")
act(complex(p(HGNC:CD74), p(HGNC:CXCR2))) -> bp(GOBP:"cell proliferation")
act(complex(p(HGNC:CD74), p(HGNC:CXCR4))) -> bp(GOBP:"cell proliferation")
UNSET MeSHAnatomy
act(complex(p(HGNC:CD74), p(HGNC:CXCR2))) -> bp(GOBP:"cell chemotaxis")
act(p(HGNC:MIF)) -> bp(GOBP:"cell chemotaxis")
a(MESHA:Brain) -> p(HGNC:MIF)

SET Support = "Examples of human microglia enriched genes are CD58, an adhesion \
molecule with a central role in the clustering of mature dendritic \
cells and T lymphocyte activation25."

a(MESHA:Microglia) -> r(HGNC:CD58)
p(HGNC:CD58) positiveCorrelation bp(GOBP:"dendritic cell chemotaxis")
p(HGNC:CD58) positiveCorrelation bp(GOBP:"T cell activation")

SET Support = "ERAP2, along with the closely related ERAP1, are peptide editing \
enzymes responsible for the trimming of N-terminal residues of MHC \
class I molecules26."

a(MESHA:Microglia) -> r(HGNC:ERAP1)
a(MESHA:Microglia) -> r(HGNC:ERAP2)

SET Support = "Granulysin (GNLY) is a cytolytic and proinflammatory molecule \
cytolytic against tumors and microorganisms, including gram-positive \
and gram-negative bacteria27. Granulysin acts also as a \
chemoattractant for T lymphocytes, monocytes and other inflammatory \
cells, activating expression of several of cytokines, such as CCL5, \
MCP-1, IL-10, IL-1β, IL-6 and IFN-α."

a(MESHA:Microglia) -> r(HGNC:GNLY)
p(HGNC:GNLY) -> bp(GOBP:"inflammatory response")
p(HGNC:GNLY) positiveCorrelation a(MESHA:"T-Lymphocytes")
p(HGNC:GNLY) positiveCorrelation a(MESHA:Monocytes)
p(HGNC:GNLY) -> p(HGNC:CCL5)
# MCP-1 = CCL2
p(HGNC:GNLY) -> p(HGNC:CCL2)
p(HGNC:GNLY) -> p(HGNC:IL10)
p(HGNC:GNLY) -> p(HGNC:IL1B)
p(HGNC:GNLY) -> p(HGNC:IL6)
p(HGNC:GNLY) -> p(HGNC:IFNA1)

SET Support = "S100A12 is important for antimicrobial activity, and its \
proinflammatory action involves recruitment of leukocytes, production \
of cytokines and chemokines, and regulation of leukocyte adhesion and \
migration. It also acts as an alarmin or a damage-associated molecular \
pattern molecule, stimulating innate immune cells."

a(MESHA:Microglia) -> r(HGNC:S100A12)
p(HGNC:S100A12) -> bp(GOBP:"inflammatory response")
p(HGNC:GNLY) positiveCorrelation a(MESHA:Leukocytes)
p(HGNC:S100A12) -> p(MESHC:Cytokines)
p(HGNC:S100A12) -> bp(GOBP:"chemokine production")
p(HGNC:S100A12) reg bp(GOBP:"leukocyte adhesive activation")
p(HGNC:S100A12) reg bp(GOBP:"leukocyte migration")
p(HGNC:S100A12) -> bp(GOBP:"chemokine production")

SET Support = "Under certain neuropathological conditions—namely, glioma progression \
and multiple sclerosis—immune cells such as macrophages and dendritic \
cells infiltrate the CNS and contribute to disease progression."

path(MESHD:Glioma) positiveCorrelation a(MESHA:Macrophages, loc(MESHA:"Central Nervous System"))
path(MESHD:Glioma) positiveCorrelation a(MESHA:"Dendritic Cells", loc(MESHA:"Central Nervous System"))
path(MESHD:"Multiple Sclerosis") positiveCorrelation a(MESHA:Macrophages, loc(MESHA:"Central Nervous System"))
path(MESHD:"Multiple Sclerosis") positiveCorrelation a(MESHA:"Dendritic Cells", loc(MESHA:"Central Nervous System"))

SET Support = "Microglia are often identified using AIF1 (IBA1), P2RY12 and CX3CR1 as \
markers and differentiated from infiltrating immune cells using CCR2 \
(ref. 29)."

a(MESHA:Microglia) -> p(HGNC:AIF1)
a(MESHA:Microglia) -> p(HGNC:P2RY12)
a(MESHA:Microglia) -> p(HGNC:CX3CR1)
a(MESHA:Microglia) negativeCorrelation p(HGNC:CCR2)

SET Support = "In comparison to monocytes, microglia were enriched for GO terms \
“synapse organization,” “neuron projection morphogenesis”, “cell \
projection morphogenesis” “homophilic cell interactions” “purine \
nucleotide metabolic process” and “sphingolipid metabolic process” \
when compared to macrophages."

a(MESHA:Microglia) positiveCorrelation bp(GOBP:"synapse organization")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"neuron projection morphogenesis")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"cell projection morphogenesis")
a(MESHA:Microglia) -- bp(GOBP:"regulation of homophilic cell adhesion")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"purine nucleotide metabolic process")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"sphingolipid metabolic process")

SET Support = "Two of these transcription factors are essential for microglial \
ontogeny: SPI-1 (PU.1) and IRF8 (ref. 2). Additional transcriptional \
regulators identified were CIITA, a positive regulator of MHC type II \
gene transcription; TRIM22, a transcription activator induced by \
interferon; MNDA, an interferon target gene; IRF5, a factor modulating \
inflammatory responses; TAL1, a transcription factor associated with \
microglial aging31; and IFI16, an interferon-γ-inducible gene. HCLS1 \
and ZPF36, with paralogs ZPF36L1 and ZPF36L2, were also detected."

a(MESHA:Microglia) positiveCorrelation p(HGNC:SPI1)
a(MESHA:Microglia) positiveCorrelation p(HGNC:IRF8)
a(MESHA:Microglia) positiveCorrelation p(HGNC:CIITA)
a(MESHA:Microglia) positiveCorrelation p(HGNC:TRIM22)
a(MESHA:Microglia) positiveCorrelation p(HGNC:MNDA)
a(MESHA:Microglia) positiveCorrelation p(HGNC:IRF5)
a(MESHA:Microglia) positiveCorrelation p(HGNC:TAL1)
a(MESHA:Microglia) positiveCorrelation p(HGNC:IFI16)
a(MESHA:Microglia) positiveCorrelation p(HGNC:HCLS1)
a(MESHA:Microglia) positiveCorrelation p(HGNC:ZFP36)

SET Support = "The target list contains many established microglial genes, such as \
CX3CR1, AIF1 (IBA1), P2RY12 and P2RY13."

a(MESHA:Microglia) -> r(HGNC:AIF1)
a(MESHA:Microglia) -> r(HGNC:P2RY12)
a(MESHA:Microglia) -> r(HGNC:CX3CR1)
a(MESHA:Microglia) -> r(HGNC:P2RY13)
UNSET TechnologyUsed

SET Support = "IBA1 (ionized calcium-binding adaptor molecule 1) and vimentin were \
detected only in microglial protein lysates, whereas β-III-tubulin, \
cyclic nucleotide phosphodiesterase (CNPase) and glial fibrillary \
acidic protein (GFAP), markers for neurons, oligodendrocytes and \
astrocytes, respectively, were detected only in cortical protein \
lysates (Fig. 4a)."

a(MESHA:Microglia) -> p(HGNC:AIF1)
a(MESHA:Microglia) -> p(HGNC:VIM)
a(MESHA:Neurons) positiveCorrelation p(HGNC:TUBB3)
a(MESHA:Oligodendroglia) positiveCorrelation p(HGNC:CNP)
a(MESHA:Astrocytes) positiveCorrelation p(HGNC:GFAP)
SET BRCO = "Parietal Cortex"
a(MESHA:Brain) -> p(HGNC:TUBB3)
a(MESHA:Brain) -> p(HGNC:CNP)
a(MESHA:Brain) -> p(HGNC:GFAP)
UNSET BRCO

SET Support = "Parietal cortical microglia were isolated from seven autopsy tissue \
samples using FACS and pooled in two samples of three and four donors, \
respectively. Spot intensities on the membranes were normalized to \
vimentin and quantified. Expression of known microglial proteins, such \
as ITGAM (CD11B), SPP1 (osteopontin), and multiple TNFα, SIGLEC, \
chemokine and TLRs was detected, supporting the RNA expression data \
(Fig. 4b)."

SET TechnologyUsed = "FACS"
SET BRCO = "Parietal Cortex"
a(MESHA:Microglia) -> p(HGNC:ITGAM)
a(MESHA:Microglia) -> p(HGNC:SPP1)
a(MESHA:Microglia) -> p(HGNC:TNF)
a(MESHA:Microglia) -> p(HGNC:SIGLEC5)
a(MESHA:Microglia) -> p(HGNC:SIGLEC6)
a(MESHA:Microglia) -> p(HGNC:LY9)
a(MESHA:Microglia) -> p(HGNC:LAG3)
UNSET BRCO
UNSET TechnologyUsed

SET Support = "Finally, human parietal cortex was stained with antibodies against \
IBA1, HLA-DR, CD32, CD36, LAG3 and TMEM119 using immunohistochemistry \
and double immunofluorescence (Fig. 4c). Cells with highly ramified \
microglial morphology were detected with the indicated antibodies."

SET BRCO = "Parietal Cortex"
SET Assay = "immunohistochemistry"
a(MESHA:Microglia) -> p(HGNC:AIF1)
a(MESHA:Microglia) -> p(HGNC:FCGR2A)
a(MESHA:Microglia) -> p(HGNC:CD74)
a(MESHA:Microglia) -> p(HGNC:CD36)
a(MESHA:Microglia) -> p(HGNC:TMEM119)
a(MESHA:Microglia) -> p(HGNC:LAG3)

SET Support = "Using double immunofluorescence staining, we observed extensive \
colocalization with established microglial markers IBA1 and HLA-DR."

p(HGNC:AIF1, loc(MESHA:Microglia)) positiveCorrelation p(HGNC:CD74, loc(MESHA:Microglia))
UNSET Assay
UNSET BRCO

SET Support = "LAG3 (lymphocyte activation gene-3; CD223) is typically expressed by \
activated T and natural killer cells and modulates T cell \
activation32. In the CNS, LAG3 was expressed by microglia (Fig. 4b,c)."

a(MESHA:"T-Lymphocytes") -> p(HGNC:LAG3)
p(HGNC:LAG3) reg bp(GOBP:"T cell activation")
a(MESHA:"Natural Killer T-Cells") -> p(HGNC:LAG3)
a(MESHA:Microglia, loc(MESHA:"Central Nervous System")) -> p(HGNC:LAG3)

SET Support = "Noteworthy genes with reduced expression during aging included many \
actin cytoskeleton-associated genes such as TLN1, PFN1, EVL, ARPC1A, \
ARPC1B, CORO1A, CAP1, CTNNA2 and VASP, sensome cell surface receptors \
P2RY12, IL6R and TLR10, and cell adhesion molecules and cell surface \
receptors ICAM3, ROBO2, SEMA3C and SEMA7A."

SET TechnologyUsed = "Illumina"
a(MESHA:"Actin Cytoskeleton") -- p(HGNC:TLN1)
a(MESHA:"Actin Cytoskeleton") -- p(HGNC:PFN1)
a(MESHA:"Actin Cytoskeleton") -- p(HGNC:EVL)
a(MESHA:"Actin Cytoskeleton") -- p(HGNC:ARPC1A)
a(MESHA:"Actin Cytoskeleton") -- p(HGNC:ARPC1B)
a(MESHA:"Actin Cytoskeleton") -- p(HGNC:CORO1A)
a(MESHA:"Actin Cytoskeleton") -- p(HGNC:CAP1)
a(MESHA:"Actin Cytoskeleton") -- p(HGNC:CTNNA2)
a(MESHA:"Actin Cytoskeleton") -- p(HGNC:VASP)
SET MeSHAnatomy = "Microglia"
bp(GOBP:aging) negativeCorrelation r(HGNC:TLN1)
bp(GOBP:aging) negativeCorrelation r(HGNC:PFN1)
bp(GOBP:aging) negativeCorrelation r(HGNC:EVL)
bp(GOBP:aging) negativeCorrelation r(HGNC:ARPC1A)
bp(GOBP:aging) negativeCorrelation r(HGNC:ARPC1B)
bp(GOBP:aging) negativeCorrelation r(HGNC:CORO1A)
bp(GOBP:aging) negativeCorrelation r(HGNC:CAP1)
bp(GOBP:aging) negativeCorrelation r(HGNC:CTNNA2)
bp(GOBP:aging) negativeCorrelation r(HGNC:VASP)
bp(GOBP:aging) negativeCorrelation r(HGNC:P2RY12)
bp(GOBP:aging) negativeCorrelation r(HGNC:IL6R)
bp(GOBP:aging) negativeCorrelation r(HGNC:TLR10)
bp(GOBP:aging) negativeCorrelation r(HGNC:ICAM3)
bp(GOBP:aging) negativeCorrelation r(HGNC:ROBO2)
bp(GOBP:aging) negativeCorrelation r(HGNC:SEMA3C)
bp(GOBP:aging) negativeCorrelation r(HGNC:SEMA7A)
UNSET MeSHAnatomy
bp(GOBP:"cell adhesion") positiveCorrelation r(HGNC:ICAM3)
bp(GOBP:"cell adhesion") positiveCorrelation r(HGNC:ROBO2)
bp(GOBP:"cell adhesion") positiveCorrelation r(HGNC:SEMA3C)
bp(GOBP:"cell adhesion") positiveCorrelation r(HGNC:SEMA7A)

SET Support = "Genes with higher expression during aging included integrin modulators \
DOCK1 and DOCK5, receptors CXCR4, CD163 and IGF2R, growth factor VEGFA \
and transcription factor RUNX3."

SET MeSHAnatomy = "Microglia"
bp(GOBP:aging) positiveCorrelation r(HGNC:DOCK1)
bp(GOBP:aging) positiveCorrelation r(HGNC:DOCK5)
bp(GOBP:aging) positiveCorrelation r(HGNC:CXCR4)
bp(GOBP:aging) positiveCorrelation r(HGNC:CD163)
bp(GOBP:aging) positiveCorrelation r(HGNC:IGF2R)
bp(GOBP:aging) positiveCorrelation r(HGNC:VEGFA)
bp(GOBP:aging) positiveCorrelation r(HGNC:RUNX3)

SET Support = "14 genes were increased in both human and mouse microglia with aging, \
of which CXCR4, VEGFA, TNFAIP2 and GP2 have been previously identified \
by other aging or priming studies4,13. The main biological pathway \
associated with these genes was “positive regulation of cell-matrix \
adhesion.”"

bp(GOBP:"positive regulation of cell-matrix adhesion") positiveCorrelation r(HGNC:CXCR4)
bp(GOBP:"positive regulation of cell-matrix adhesion") positiveCorrelation r(HGNC:GP2)
bp(GOBP:"positive regulation of cell-matrix adhesion") positiveCorrelation r(HGNC:VEGFA)
bp(GOBP:"positive regulation of cell-matrix adhesion") positiveCorrelation r(HGNC:GP2)
bp(GOBP:aging) positiveCorrelation r(HGNC:TNFAIP2)
bp(GOBP:aging) positiveCorrelation r(HGNC:GP2)
SET Species = "10090"
bp(GOBP:aging) positiveCorrelation r(MGI:Cxcr4)
bp(GOBP:aging) positiveCorrelation r(MGI:Vegfa)
bp(GOBP:aging) positiveCorrelation r(MGI:Tnfaip2)
bp(GOBP:aging) positiveCorrelation r(MGI:Gp2)
UNSET Species

SET Support = "Nine genes with reduced expression in aging overlapped between human \
and mouse cortical microglia. These included the previously identified \
ETS1, along with SEMA7A, MRC2, PSTPIP1 and EMP2, with no associated GO \
category"

SET BRCO = "Parietal Cortex"
bp(GOBP:aging) negativeCorrelation r(HGNC:ETS1)
bp(GOBP:aging) negativeCorrelation r(HGNC:SEMA7A)
bp(GOBP:aging) negativeCorrelation r(HGNC:MRC2)
bp(GOBP:aging) negativeCorrelation r(HGNC:PSTPIP1)
bp(GOBP:aging) negativeCorrelation r(HGNC:EMP2)
SET Species = "10090"
bp(GOBP:aging) negativeCorrelation r(MGI:Ets1)
bp(GOBP:aging) negativeCorrelation r(MGI:Sema7a)
bp(GOBP:aging) negativeCorrelation r(MGI:Mrc2)
bp(GOBP:aging) negativeCorrelation r(MGI:Pstpip1)
bp(GOBP:aging) negativeCorrelation r(MGI:Emp2)
UNSET Species
UNSET BRCO


UNSET MeSHAnatomy
UNSET TechnologyUsed
UNSET Section

##################################################################################

SET Citation = {"PubMed", "Science. 2017 Jun 23;356(6344). pii: eaal3222. doi: 10.1126/science.aal3222. Epub 2017 May 25.", "28546318"}

SET Section = "Human microglia transcriptomes"

SET Support = "Relative expression values for the 30 most highly expressed genes \
across each of the patients are illustrated in Figure 1B, indicating \
substantial individual variation for some transcripts."

SET Assay = "ex vivo"

# Figure 1B was checked for the 30 genes associated with human microglia
a(MESHA:Microglia) -> r(HGNC:SPP1)
a(MESHA:Microglia) -> r(HGNC:CD74)
a(MESHA:Microglia) -> r(HGNC:ACTB)
a(MESHA:Microglia) -> r(HGNC:C3)
a(MESHA:Microglia) -> r(HGNC:FTL)
a(MESHA:Microglia) -> r(HGNC:FOS)
a(MESHA:Microglia) -> r(HGNC:CSF1R)
a(MESHA:Microglia) -> r(HGNC:B2M)
a(MESHA:Microglia) -> r(HGNC:C1QB)
a(MESHA:Microglia) -> r(HGNC:C1QC)
a(MESHA:Microglia) -> r(HGNC:PSAP)
a(MESHA:Microglia) -> r(HGNC:A2M)
a(MESHA:Microglia) -> r(HGNC:ITM2B)
a(MESHA:Microglia) -> r(HGNC:LAPTM5)
a(MESHA:Microglia) -> r(HGNC:CTSB)
a(MESHA:Microglia) -> r(HGNC:P2RY12)
a(MESHA:Microglia) -> r(HGNC:C1QA)
a(MESHA:Microglia) -> r(HGNC:SLCO2B1)
a(MESHA:Microglia) -> r(HGNC:RGS1)
a(MESHA:Microglia) -> r(HGNC:APOE)
a(MESHA:Microglia) -> r(HGNC:CCL4L2)
a(MESHA:Microglia) -> r(HGNC:RNASET2)
a(MESHA:Microglia) -> r(HGNC:NEAT1)
a(MESHA:Microglia) -> r(HGNC:CX3CR1)
a(MESHA:Microglia) -> r(HGNC:DUSP1)
a(MESHA:Microglia) -> r(HGNC:SAT1)
a(MESHA:Microglia) -> r(HGNC:ZFP36)
a(MESHA:Microglia) -> r(HGNC:CD81)
a(MESHA:Microglia) -> r(HGNC:HLA-B)
a(MESHA:Microglia) -> r(HGNC:HLA-DRA)

SET Support = "This analysis confirmed that human microglia expressed very high \
levels of numerous genes associated with regulation of microglia \
ramification and motility, including the fractalkine receptor CX3CR1, \
purinergic receptor P2RY12, and genes involved in synaptic remodeling, \
such as complement factors C3, C1QA, C1QB, and C1QC (Table S2)."

p(HGNC:CX3CR1) positiveCorrelation bp(GOBP:"microglial cell activation")
p(HGNC:CX3CR1) positiveCorrelation bp(GOBP:"microglial cell migration")
p(HGNC:P2RY12) positiveCorrelation bp(GOBP:"microglial cell activation")
p(HGNC:P2RY12) positiveCorrelation bp(GOBP:"microglial cell migration")
p(HGNC:C3) positiveCorrelation bp(GOBP:"synapse organization")
p(HGNC:C1QA) positiveCorrelation bp(GOBP:"synapse organization")
p(HGNC:C1QB) positiveCorrelation bp(GOBP:"synapse organization")
p(HGNC:C1QC) positiveCorrelation bp(GOBP:"synapse organization")

SET Support = "With respect to AD, for example, expression for 28 of 48 AD genes was \
higher in microglia, including TREM2, SORL1, INPP5D, MEF2C and CD33 \
(Figure 1E)."

r(HGNC:TREM2) positiveCorrelation path(MESHD:"Alzheimer Disease")
r(HGNC:SORL1) positiveCorrelation path(MESHD:"Alzheimer Disease")
r(HGNC:INPP5D) positiveCorrelation path(MESHD:"Alzheimer Disease")
r(HGNC:MEF2C) positiveCorrelation path(MESHD:"Alzheimer Disease")
r(HGNC:CD33) positiveCorrelation path(MESHD:"Alzheimer Disease")
a(MESHA:Microglia) -> r(HGNC:TREM2)
a(MESHA:Microglia) -> r(HGNC:SORL1)
a(MESHA:Microglia) -> r(HGNC:INPP5D)
a(MESHA:Microglia) -> r(HGNC:MEF2C)
a(MESHA:Microglia) -> r(HGNC:CD33)

SET Support = "For MS, 48 of 81 genes associated with risk alleles were \
preferentially expressed in microglia (Fig. S5)."

a(MESHA:Microglia) positiveCorrelation path(MESHD:"Multiple Sclerosis")

SET Support = "Notable examples included higher expression in human microglia of \
regulators of the complement system (e.g., C2, C3, VSIG4, SERPING1) \
and genes involved in brain structure development, including SYNDIG1, \
GLDN, CTTNBP2, and ROBO3."

p(HGNC:C2) positiveCorrelation bp(GOBP:"regulation of complement activation")
p(HGNC:C3) positiveCorrelation bp(GOBP:"regulation of complement activation")
p(HGNC:VSIG4) positiveCorrelation bp(GOBP:"regulation of complement activation")
p(HGNC:SERPING1) positiveCorrelation bp(GOBP:"regulation of complement activation")
p(HGNC:SYNDIG1) positiveCorrelation bp(GOBP:"brain development")
p(HGNC:GLDN) positiveCorrelation bp(GOBP:"brain development")
p(HGNC:CTTNBP2) positiveCorrelation bp(GOBP:"brain development")
p(HGNC:ROBO3) positiveCorrelation bp(GOBP:"brain development")
a(MESHA:Microglia) -> r(HGNC:VSIG4)
a(MESHA:Microglia) -> r(HGNC:SERPING1)
a(MESHA:Microglia) -> r(HGNC:SYNDIG1)
a(MESHA:Microglia) -> r(HGNC:GLDN)
a(MESHA:Microglia) -> r(HGNC:CTTNBP2)
a(MESHA:Microglia) -> r(HGNC:ROBO3)

SET Support = "In contrast, genes more highly expressed in mouse microglia included \
Hexb, Sparc, and Sall3."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET AgeRange = "7-10 weeks"
a(MESHA:Microglia) -> r(MGI:Hexb)
a(MESHA:Microglia) -> r(MGI:Sparc)
a(MESHA:Microglia) -> r(MGI:Sall3)

SET Support = "We compared the levels of mRNA transcripts in C57BL/6 microglia with \
different resident tissue macrophage populations (26, 56) and observed \
900 mRNAs with levels of expression in microglia that were 10-fold \
greater than the integrated mean of expression levels in the other \
macrophage subsets at a FDR of 0.05 (Fig. 2B)."

# Using Fig 2B to determine genes
a(MESHA:Microglia) -> r(MGI:Sall1)
a(MESHA:Microglia) -> r(MGI:Mlxipl)
a(MESHA:Microglia) -> r(MGI:Gpr84)
a(MESHA:Microglia) -> r(MGI:Olfml3)
a(MESHA:Microglia) -> r(MGI:P2ry12)
a(MESHA:Microglia) -> r(MGI:Cx3cr1)
a(MESHA:Microglia) -> r(MGI:Tmem119)
a(MESHA:Microglia) -> r(MGI:Trem2)
a(MESHA:Microglia) -> r(MGI:Kcnk13)
a(MESHA:Microglia) -> r(MGI:Smad7)
a(MESHA:Microglia) -> r(MGI:Zfp691)

SET Support = "Gene ontology analysis of this extended microglia gene signature \
returned significant functional enrichment for CNS-related categories \
such as regulation of neurogenesis, synaptic signaling, CNS \
development, response to transforming growth factor beta (TGF-β), and \
regulation of neurotransmitter transport (Fig. 2C)."

a(MESHA:Microglia) positiveCorrelation bp(GOBP:"regulation of neurogenesis")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"synaptic signaling")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"central nervous system development")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"response to transforming growth factor beta")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"regulation of neurotransmitter transport")

UNSET AgeRange
UNSET MouseStrain
UNSET Species

SET Support = "Interestingly, of the top 25 most expressed, two genes, APOC1 and \
MPZL1, were essentially not expressed in C57BL/6 microglia; the \
expression of a third one, SORL1, was nearly 20 times lower in mouse \
than human (Fig. 2F)."

a(MESHA:Microglia) -> r(HGNC:APOC1)
a(MESHA:Microglia) -> r(HGNC:MPZL1)
a(MESHA:Microglia) -> r(HGNC:SORL1)

SET Support = "PD-associated genes SNCA, LRRK2, CD46, and CXCL12 were expressed to a \
greater extent in human microglia than mouse microglia."

a(MESHA:Microglia) -> r(HGNC:SNCA)
a(MESHA:Microglia) -> r(HGNC:LRRK2)
a(MESHA:Microglia) -> r(HGNC:CD46)
a(MESHA:Microglia) -> r(HGNC:CXCL12)
p(HGNC:SNCA) positiveCorrelation path(MESHD:"Parkinson Disease")
p(HGNC:LRRK2) positiveCorrelation path(MESHD:"Parkinson Disease")
p(HGNC:CD46) positiveCorrelation path(MESHD:"Parkinson Disease")
p(HGNC:CXCL12) positiveCorrelation path(MESHD:"Parkinson Disease")

UNSET Assay
UNSET Section

# Skipped the section "Enhancer landscape of human microglia" since it was about ATAC-seq data which is about how genes are found in DNA

SET Section = "Alterations of the human microglia transcriptome in vitro"

SET Support = "We performed RNA-seq in human microglia at 6, 24 and 168 h following \
transfer to tissue culture. Highly dynamic responses were observed \
beginning at 6 h and lasting throughout the time course (Fig. 4D). \
Cultured microglia remained positive for Iba1 staining, whereas no \
expression of markers for astrocytes or neurons was observed, \
indicating that changes in RNA expression were not due to \
contaminating cells (Fig. S13A)."

SET Assay = {"in vitro", "RNA-seq"}
SET TimePoint = {"6 hours tissue culture", "24 hours tissue culture", "168 hours tissue culture"}
a(MESHA:Microglia) -> r(HGNC:AIF1)
UNSET TimePoint

SET Support = "Remarkably, more than 2,000 genes were downregulated more than 4-fold \
at 6 h following plating, with some genes exhibiting partial recovery \
at the 7-day time point (Figs. 4E, S13D)."

# Figure was analyzed
SET TimePoint = "168 hours tissue culture"
a(MESHA:Microglia) -> r(HGNC:MARCO)
a(MESHA:Microglia) -> r(HGNC:MMP9)
a(MESHA:Microglia) -> r(HGNC:BATF)
a(MESHA:Microglia) -> r(HGNC:MAFB)
a(MESHA:Microglia) -| r(HGNC:FOSL2)
a(MESHA:Microglia) -| r(HGNC:DBP)
a(MESHA:Microglia) -| r(HGNC:MLXIPL)
a(MESHA:Microglia) -| r(HGNC:TAL1)
a(MESHA:Microglia) -| r(HGNC:MEF2C)
a(MESHA:Microglia) -| r(HGNC:SALL1)
a(MESHA:Microglia) -| r(HGNC:BTG2)
a(MESHA:Microglia) -| r(HGNC:FOS)
a(MESHA:Microglia) -| r(HGNC:ADGRG1)
a(MESHA:Microglia) -| r(HGNC:P2RY12)
a(MESHA:Microglia) -| r(HGNC:CX3CR1)
a(MESHA:Microglia) -| r(HGNC:HLA-DOA)
UNSET Assay
UNSET TimePoint
SET Assay = "ex vivo"
a(MESHA:Microglia) -> r(HGNC:DBP)
a(MESHA:Microglia) -> r(HGNC:MLXIPL)
a(MESHA:Microglia) -> r(HGNC:TAL1)
a(MESHA:Microglia) -> r(HGNC:MEF2C)
a(MESHA:Microglia) -> r(HGNC:SALL1)
a(MESHA:Microglia) -> r(HGNC:BTG2)
a(MESHA:Microglia) -> r(HGNC:FOS)
a(MESHA:Microglia) -> r(HGNC:ADGRG1)
a(MESHA:Microglia) -> r(HGNC:P2RY12)
a(MESHA:Microglia) -> r(HGNC:CX3CR1)
a(MESHA:Microglia) -> r(HGNC:HLA-DOA)
a(MESHA:Microglia) -| r(HGNC:MARCO)
a(MESHA:Microglia) -| r(HGNC:MMP9)
a(MESHA:Microglia) -| r(HGNC:BATF)
a(MESHA:Microglia) -| r(HGNC:MAFB)
UNSET Assay

UNSET Section

SET Section = "An environment-dependent transcriptional network"

SET Support = "We cultured mouse microglia in vitro for 7 days and treated them with \
Tgf-β1 cytokine (Figs. 6C, S15), which is necessary for maintenance of \
the in vivo mouse microglia phenotype (27, 64). Of the 2,398 genes \
downregulated > 2-fold in vitro, Tgf-β1 induced expression > 2-fold of \
429, including Sall1, Tmem119, and P2ry12."

SET Species = "10090"
SET TimePoint = "168 hours tissue culture"
p(MGI:Tgfb1) -> r(MGI:Sall1, loc(MESHA:Microglia))
p(MGI:Tgfb1) -> r(MGI:Tmem119, loc(MESHA:Microglia))
p(MGI:Tgfb1) -> r(MGI:P2ry12, loc(MESHA:Microglia))
UNSET Species

SET Support = "Corresponding studies of TGF-β1 treatment of human microglia in vitro \
yielded similar results, although variation between individuals \
limited the number of genes achieving statistical significance \
following correction for multiple testing. Nonetheless, SALL1 was \
among the genes exhibiting significant upregulation (Fig. S15B)."

p(HGNC:TGFB1) -> r(HGNC:SALL1, loc(MESHA:Microglia))
UNSET TimePoint

UNSET Section

##################################################################################

SET Citation = {"PubMed", "J Neurosci. 2010 Mar 3;30(9):3482-8. doi: 10.1523/JNEUROSCI.3940-09.2010.", "20203208"}

SET PublicationType = "Research"
SET Section = "Abstract"

SET Support = "Sialic acid-binding Ig superfamily lectins (Siglecs) are members of \
the Ig superfamily that recognize sialic acid residues of \
glycoproteins."

p(PFAM:ig) hasMembers list(p(HGNC:SIGLEC1), p(HGNC:CD22), p(HGNC:CD33), p(HGNC:MAG), p(HGNC:SIGLEC5), p(HGNC:SIGLEC6), p(HGNC:SIGLEC7), p(HGNC:SIGLEC8), p(HGNC:SIGLEC9), p(HGNC:SIGLEC10), p(HGNC:SIGLEC11), p(HGNC:SIGLEC12), p(HGNC:SIGLEC14), p(HGNC:SIGLEC15), p(HGNC:SIGLEC16))
p(HGNC:SIGLEC1) => complex(p(HGNC:SIGLEC1), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:CD22) => complex(p(HGNC:CD22), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:CD33) => complex(p(HGNC:CD33), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:MAG) => complex(p(HGNC:MAG), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:SIGLEC5) => complex(p(HGNC:SIGLEC5), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:SIGLEC6) => complex(p(HGNC:SIGLEC6), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:SIGLEC7) => complex(p(HGNC:SIGLEC7), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:SIGLEC8) => complex(p(HGNC:SIGLEC8), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:SIGLEC9) => complex(p(HGNC:SIGLEC9), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:SIGLEC10) => complex(p(HGNC:SIGLEC10), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:SIGLEC11) => complex(p(HGNC:SIGLEC11), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:SIGLEC12) => complex(p(HGNC:SIGLEC12), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:SIGLEC14) => complex(p(HGNC:SIGLEC14), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:SIGLEC15) => complex(p(HGNC:SIGLEC15), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))
p(HGNC:SIGLEC16) => complex(p(HGNC:SIGLEC16), a(CHEBI:"sialic acid", loc(PFAM:Glycoprotein)))

SET Support = "Siglec-11 is a recently identified human-specific CD33-related Siglec \
that binds to alpha2,8-linked polysialic acids and is expressed on \
microglia, the brain resident innate immune cells."

p(HGNC:SIGLEC11) -- p(HGNC:CD33)
a(MESHA:Microglia) -> p(HGNC:SIGLEC11)
p(HGNC:SIGLEC11) => complex(p(HGNC:SIGLEC11), a(CHEBI:"alpha-2,8-linked polymer of sialic acid"))

SET Support = "Polysialylated neuronal cell adhesion molecule (PSA-NCAM) is a \
putative ligand of Siglec-11."

p(HGNC:SIGLEC11) => complex(p(HGNC:SIGLEC11), p(HGNC:NCAM1, pmod(CHEBI:"sialic acid")))

SET Support = "We observed gene transcription and protein expression of Siglec-11 \
splice variant 2 in human brain tissue samples by RT-PCR and Western \
blot analysis."

SET Assay = {"RT-PCR", "Western Blot"}
# Determine proper way to represent this variant, use REF ns?
a(MESHA:Brain) positiveCorrelation r(HGNC:SIGLEC11, var("splice variant 2"))
a(MESHA:Brain) positiveCorrelation p(HGNC:SIGLEC11, var("splice variant 2"))
UNSET Assay

SET Support = "Siglec-11 was detected on microglia in human brain tissue by \
immunohistochemistry."

SET Assay = "immunohistochemistry"
a(MESHA:Microglia) -> p(HGNC:SIGLEC11)
UNSET Assay

SET Support = "Stimulation of Siglec-11 by cross-linking suppressed the \
lipopolysaccharides (LPS)-induced gene transcription of the \
proinflammatory mediators interleukin-1beta and nitric oxide \
synthase-2 in microglia."

a(CHEBI:lipopolysaccharide) -> r(HGNC:IL1B, loc(MESHA:Microglia))
a(CHEBI:lipopolysaccharide) -> r(HGNC:NOS2, loc(MESHA:Microglia))
SET ChallengeType = "lipopolysaccharide stimulation"
act(p(HGNC:SIGLEC11)) -| r(HGNC:IL1B, loc(MESHA:Microglia))
act(p(HGNC:SIGLEC11)) -| r(HGNC:NOS2, loc(MESHA:Microglia))
UNSET ChallengeType
p(HGNC:IL1B) positiveCorrelation bp(GOBP:"inflammatory response")
p(HGNC:NOS2) positiveCorrelation bp(GOBP:"inflammatory response")

SET Support = "Furthermore, phagocytosis of apoptotic neuronal material was reduced \
in Siglec-11 transduced microglia."

p(HGNC:SIGLEC11, loc(MESHA:Microglia)) -| bp(GOBP:"apoptotic cell clearance")

SET Support = "Expression of PSA-NCAM was detected on microglia and neurons by \
immunohistochemistry and RT-PCR."

SET Assay = {"RT-PCR", "immunohistochemistry"}
a(MESHA:Microglia) -> p(HGNC:NCAM1, pmod(CHEBI:"sialic acid"))
a(MESHA:Neurons) -> p(HGNC:NCAM1, pmod(CHEBI:"sialic acid"))
UNSET Assay

SET Support = "Coculture of microglia transduced with Siglec-11 and neurons \
demonstrated neuroprotective function of Siglec-11."

p(HGNC:SIGLEC11, loc(MESHA:Microglia)) -> bp(MESHPP:Neuroprotection)

SET Support = "The neuroprotective effect of Siglec-11 was dependent on polysialic \
acid (PSA) residues on neurons, but independent on PSA on microglia."

bp(MESHPP:Neuroprotection) positiveCorrelation p(HGNC:NCAM1, pmod(CHEBI:"sialic acid"), loc(MESHA:Neurons))
p(HGNC:NCAM1, pmod(CHEBI:"sialic acid"), loc(MESHA:Microglia)) cnc bp(MESHPP:Neuroprotection)

UNSET Section

SET Section = "Results"

SET Support = "Human brain tissue obtained from patients who underwent epilepsy \
surgery were analyzed by RT-PCR using primers designed to specifically \
detect the distinct known splice variants of Siglec-11 (supplemental \
Fig. S1, available at www.jneurosci.org as supplemental material). \
Data demonstrated that splice variant 2 of Siglec-11 was transcribed \
in human brain tissue."

SET Assay = "RT-PCR"
# Determine proper way to represent this variant, use REF ns?
a(MESHA:Brain) positiveCorrelation r(HGNC:SIGLEC11, var("splice variant 2"))
UNSET Assay

SET Support = "Siglec-11 was detected in the lysates of the human brain samples by \
Western blot analysis as a protein of ∼80 kDa (Fig. 1B). The Siglec-11 \
band in the blot showed the same size as the Siglec-11 from protein \
lysate of the 293 cells expressing the Siglec-11 splice variant 2 \
(Fig. 1B)."

SET Assay = "Western Blot"
# Determine proper way to represent this variant, use REF ns?
a(MESHA:Brain) positiveCorrelation p(HGNC:SIGLEC11, var("splice variant 2"))
UNSET Assay

SET Support = "Furthermore frozen human brain sections were double immunostained with \
antibodies directed against Siglec-11 and the typical microglial \
marker ionized calcium binding adaptor molecule 1 (Iba1). Siglec-11 \
colocalized with Iba1 stained cells, demonstrating that Siglec-11 was \
expressed on brain microglia (Fig. 1C)."

SET Assay = "immunostaining"
p(HGNC:AIF1) positiveCorrelation p(HGNC:SIGLEC11, var("splice variant 2"))
a(MESHA:Microglia, loc(MESHA:Brain)) -> p(HGNC:AIF1)
a(MESHA:Microglia, loc(MESHA:Brain)) -> p(HGNC:SIGLEC11, var("splice variant 2"))
UNSET Assay

SET Support = "Capacity of murine microglia transduced with the Siglec-11 vector to \
phagocytose apoptotic neural material was compared to that of \
microglia transduced with the control vector (Fig. 2A). Microglia \
expressing Siglec-11 showed reduced phagocytosis capacity. In detail, \
71.65 ± 14.32% of control vector transduced microglia phagocytosed \
apoptotic neuronal material within 1 h, while only 30.70 ± 14.37% of \
Siglec-11 vector transduced microglia showed phagocytosis of apoptotic \
neuronal material (n = 4; p ≤ 0.01, two-tailed t test) (Fig. 2A)."

# Human protein expressed in murine cells
SET MeSHAnatomy = "Microglia"
p(HGNC:SIGLEC11, var("splice variant 2")) negativeCorrelation bp(GOBP:phagocytosis)
UNSET MeSHAnatomy

SET Support = "Microglial cells expressing flag-Siglec-11 were cultured on plates \
precoated with a monoclonal antibody directed against the flag epitope \
or an isotype control antibody. Cells were either unstimulated or \
stimulated with 500 ng/ml LPS. Gene transcripts of microglial cells \
were studied after 48 h of culture by real-time RT-PCR. Data showed \
that after cross-linking of flag-Siglec-11 by the flag-specific \
antibodies, the relative LPS-induced gene transcription level of \
interleukin-1 β (IL-1β) was reduced in the Siglec-11 vector transduced \
cells (1.81 ± 0.11, mean ± SEM) compared to the control vector \
transduced cells (21.37 ± 4.44) (Fig. 2B)."

SET Assay = "RT-PCR"
SET ChallengeType = "lipopolysaccharide stimulation"
SET LPSDosage = "500 ng/ml"
SET TimePoint = "48 hours post-stimulation"
# act() comes from the cross-linking
act(p(HGNC:SIGLEC11, var("splice variant 2"), loc(MESHA:Microglia))) negativeCorrelation r(MGI:Il1b, loc(MESHA:Microglia))
UNSET TimePoint
UNSET LPSDosage
UNSET Assay
UNSET ChallengeType

SET Support = "The LPS-induced gene transcription of nitric oxide synthase-2 (NOS2) \
also was lower in flag-Siglec-11-expressing cells (1.21 ± 0.59) than \
in the control vector transduced cells (6.32 ± 1.39) (Fig. 2B)."

SET Assay = "RT-PCR"
SET ChallengeType = "lipopolysaccharide stimulation"
SET LPSDosage = "500 ng/ml"
SET TimePoint = "48 hours post-stimulation"
act(p(HGNC:SIGLEC11, var("splice variant 2"), loc(MESHA:Microglia))) negativeCorrelation r(MGI:Nos2, loc(MESHA:Microglia))
UNSET TimePoint
UNSET LPSDosage
UNSET Assay
UNSET ChallengeType

SET Support = "Neurons from C57BL/6 mouse embryonic hippocampal brain tissue were \
cultured, then fixed and immunostained with antibodies directed \
against PSA-NCAM and β-tubulin-III. Expression of PSA-NCAM was \
detected on neuronal cell bodies and neurites as identified by double \
labeling with antibodies directed against β-tubulin-III (Fig. 3A)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = "immunostaining"
SET MeSHAnatomy = "Hippocampus"
a(MESHA:Neurons) -> p(MGI:Tubb3)
p(MGI:Tubb3) positiveCorrelation p(MGI:Ncam1, pmod(CHEBI:"sialic acid"))
a(MESHA:Neurons) positiveCorrelation p(MGI:Ncam1, pmod(CHEBI:"sialic acid"))
UNSET MeSHAnatomy
UNSET Assay
UNSET MouseStrain
UNSET Species

SET Support = "Microglial cells identified by immunolabeling with antibody directed \
against Iba1 also showed coimmunostaining for PSA-NCAM, which was \
undetectable after treatment with EndoN (Fig. 4B)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = "immunostaining"
SET MeSHAnatomy = "Hippocampus"
p(MGI:Aif1) positiveCorrelation p(MGI:Ncam1, pmod(CHEBI:"sialic acid"))
UNSET MeSHAnatomy
UNSET Assay
UNSET MouseStrain
UNSET Species

SET Support = "The expression of NCAM was confirmed by RT-PCR using primers derived \
from sequences present in all NCAM isoforms (Bally-Cuif et al., 1993). \
A 337 bp NCAM PCR product was detected from microglial cDNA as well as \
neuronal cDNA (Fig. 3C)."

SET Species = "10090"
SET MouseStrain = "C57BL/6"
SET Assay = "RT-PCR"
SET MeSHAnatomy = "Hippocampus"
a(MESHA:Neurons) -> r(MGI:Ncam1)
a(MESHA:Microglia) -> r(MGI:Ncam1)
UNSET MeSHAnatomy
UNSET Assay
UNSET MouseStrain
UNSET Species

SET Support = "Microglial cells were transduced with the Siglec-11 vector and \
cocultured with neurons for 48 h. Analysis of neurites immunolabeled \
by antibodies directed against β-tubulin-III revealed that in \
cocultures between PSA-containing neurons and Siglec-11 vector \
transduced microglia neurite density was higher than in cocultures \
with control vector transduced microglia (Fig. 4A)."

SET Assay = "immunohistochemistry"
SET TimePoint = "2 days post-transduction"
p(HGNC:NCAM1, pmod(CHEBI:"sialic acid"), loc(MESHA:Neurons)) -> a(MESHA:Neurites)
p(HGNC:SIGLEC11, var("splice variant 2"), loc(MESHA:Microglia)) -> a(MESHA:Neurites)
UNSET Assay
UNSET TimePoint

SET Support = "A similar neuroprotective effect of Siglec-11 was also observed by \
analysis of neuronal cell body density (Fig. 4C). When PSA was present \
on neurons, the cocultures of neurons with Siglec-11 vector transduced \
microglia resulted in higher neuronal cell body density than those of \
neurons cocultured with control vector transduced microglia."

SET Assay = "immunohistochemistry"
SET TimePoint = "2 days post-transduction"
p(HGNC:NCAM1, pmod(CHEBI:"sialic acid"), loc(MESHA:Neurons)) -> a(MESHA:Neurons)
p(HGNC:SIGLEC11, var("splice variant 2"), loc(MESHA:Microglia)) -> a(MESHA:Neurons)
p(HGNC:SIGLEC11, var("splice variant 2"), loc(MESHA:Microglia)) -> bp(MESHPP:Neuroprotection)
UNSET Assay
UNSET TimePoint

SET Support = "This protective function of Siglec-11 on neuronal cell body survival \
was not observed when PSA was removed from neurons (Fig. 4C)."

p(HGNC:NCAM1, pmod(CHEBI:"sialic acid"), loc(MESHA:Microglia)) cnc bp(MESHPP:Neuroprotection)

SET Support = "In summary, data demonstrated that more neurites and neuronal cell \
bodies survived in cocultures with Siglec-11 transduced microglia \
compared to control vector transduced microglia, demonstrating a \
neuroprotective effect of Siglec-11. The PSA residues expressed on \
neurons, but not those on microglia contributed to the neuroprotective \
function of microglial Siglec-11."

p(HGNC:NCAM1, pmod(CHEBI:"sialic acid"), loc(MESHA:Neurons)) -> bp(MESHPP:Neuroprotection)


UNSET Section
UNSET PublicationType

##################################################################################

SET Citation = {"PubMed", "Neuroscience. 2014 Sep 5;275:113-24. doi: 10.1016/j.neuroscience.2014.05.061. Epub 2014 Jun 9.", "24924144"}

SET PublicationType = "Research"
SET Section = "Abstract"

SET Support = "Sialic acid binding immunoglobulin-like lectins (Siglecs) are cell \
surface receptors of microglia and oligodendrocytes that recognize the \
sialic acid cap of healthy neurons and neighboring glial cells."

a(MESHA:Microglia) -> p(HGNC:SIGLEC1)
a(MESHA:Microglia) -> p(HGNC:CD22)
a(MESHA:Microglia) -> p(HGNC:CD33)
a(MESHA:Microglia) -> p(HGNC:MAG)
a(MESHA:Microglia) -> p(HGNC:SIGLEC5)
a(MESHA:Microglia) -> p(HGNC:SIGLEC6)
a(MESHA:Microglia) -> p(HGNC:SIGLEC7)
a(MESHA:Microglia) -> p(HGNC:SIGLEC8)
a(MESHA:Microglia) -> p(HGNC:SIGLEC9)
a(MESHA:Microglia) -> p(HGNC:SIGLEC10)
a(MESHA:Microglia) -> p(HGNC:SIGLEC11)
a(MESHA:Microglia) -> p(HGNC:SIGLEC12)
a(MESHA:Microglia) -> p(HGNC:SIGLEC14)
a(MESHA:Microglia) -> p(HGNC:SIGLEC15)
a(MESHA:Microglia) -> p(HGNC:SIGLEC16)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC1)
a(MESHA:Oligodendroglia) -> p(HGNC:CD22)
a(MESHA:Oligodendroglia) -> p(HGNC:CD33)
a(MESHA:Oligodendroglia) -> p(HGNC:MAG)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC5)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC6)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC7)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC8)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC9)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC10)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC11)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC12)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC14)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC15)
a(MESHA:Oligodendroglia) -> p(HGNC:SIGLEC16)
p(HGNC:SIGLEC1) => complex(p(HGNC:SIGLEC1), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:CD22) => complex(p(HGNC:CD22), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:CD33) => complex(p(HGNC:CD33), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:MAG) => complex(p(HGNC:MAG), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC5) => complex(p(HGNC:SIGLEC5), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC6) => complex(p(HGNC:SIGLEC6), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC7) => complex(p(HGNC:SIGLEC7), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC8) => complex(p(HGNC:SIGLEC8), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC9) => complex(p(HGNC:SIGLEC9), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC10) => complex(p(HGNC:SIGLEC10), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC11) => complex(p(HGNC:SIGLEC11), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC12) => complex(p(HGNC:SIGLEC12), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC14) => complex(p(HGNC:SIGLEC14), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC15) => complex(p(HGNC:SIGLEC15), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC16) => complex(p(HGNC:SIGLEC16), a(CHEBI:"sialic acid", loc(MESHA:Neurons)))
p(HGNC:SIGLEC1) => complex(p(HGNC:SIGLEC1), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:CD22) => complex(p(HGNC:CD22), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:CD33) => complex(p(HGNC:CD33), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:MAG) => complex(p(HGNC:MAG), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:SIGLEC5) => complex(p(HGNC:SIGLEC5), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:SIGLEC6) => complex(p(HGNC:SIGLEC6), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:SIGLEC7) => complex(p(HGNC:SIGLEC7), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:SIGLEC8) => complex(p(HGNC:SIGLEC8), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:SIGLEC9) => complex(p(HGNC:SIGLEC9), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:SIGLEC10) => complex(p(HGNC:SIGLEC10), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:SIGLEC11) => complex(p(HGNC:SIGLEC11), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:SIGLEC12) => complex(p(HGNC:SIGLEC12), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:SIGLEC14) => complex(p(HGNC:SIGLEC14), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:SIGLEC15) => complex(p(HGNC:SIGLEC15), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))
p(HGNC:SIGLEC16) => complex(p(HGNC:SIGLEC16), a(CHEBI:"sialic acid", loc(MESHA:Neuroglia)))

SET Support = "Upon ligand binding, Siglecs typically signal through an \
immunoreceptor tyrosine-based inhibition motif (ITIM) to keep the cell \
in a homeostatic status and support healthy neighboring cells."

complex(p(HGNC:SIGLEC1), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:CD22), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:CD33), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:MAG), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:SIGLEC5), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:SIGLEC6), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:SIGLEC7), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:SIGLEC8), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:SIGLEC9), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:SIGLEC10), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:SIGLEC11), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:SIGLEC14), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:SIGLEC15), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:SIGLEC16), a(CHEBI:"sialic acid")) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
complex(p(HGNC:SIGLEC1), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:CD22), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:CD33), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:MAG), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:SIGLEC5), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:SIGLEC6), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:SIGLEC7), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:SIGLEC8), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:SIGLEC9), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:SIGLEC10), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:SIGLEC11), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:SIGLEC14), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:SIGLEC15), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
complex(p(HGNC:SIGLEC16), a(CHEBI:"sialic acid")) -> bp(GOBP:"homeostatic process")
bp(GOBP:"homeostatic process")

SET Support = "The conserved Siglec-4/myelin-associated glycoprotein is expressed on \
oligodendrocytes and Schwann cells."

a(MESHA:Oligodendroglia) -> p(HGNC:MAG)
a(MESHA:"Schwann Cells") -> p(HGNC:MAG)

SET Support = "Siglec-4 protects neurons from acute toxicity via interaction with \
sialic acids bound to neuronal gangliosides."

complex(p(HGNC:MAG), a(CHEBI:"sialic acid", loc(CHEBI:ganglioside))) -> bp(MESHPP:Neuroprotection)
p(HGNC:MAG) => complex(p(HGNC:MAG), a(CHEBI:"sialic acid", loc(CHEBI:ganglioside)))

SET Support = "Microglial expression of Siglec-11 is human lineage-specific and \
prevents neurotoxicity via interaction with α2.8-linked sialic acid \
oligomers exposed on the neuronal glycocalyx."

complex(p(HGNC:SIGLEC11), a(CHEBI:"alpha-2,8-linked polymer of sialic acid", loc(MESHA:Neurons))) -> bp(MESHPP:Neuroprotection)

SET Support = "Microglial Siglec-E is a mouse CD33-related Siglec member that \
prevents microglial phagocytosis and the associated oxidative burst."

p(MGI:Siglece) -- p(HGNC:CD33)
p(MGI:Siglece) -| bp(GOBP:phagocytosis)

SET Support = "Mouse Siglec-E of microglia binds to α2.8- and α2.3-linked sialic acid \
residues of the healthy glycocalyx of neuronal and glial cells."

# Cannot find the equiv value in any ns for 2,3-linked residues
p(MGI:Siglece) => complex(p(MGI:Siglece), a(CHEBI:"alpha-2,8-linked polymer of sialic acid", loc(MESHA:Neurons)))
p(MGI:Siglece) => complex(p(MGI:Siglece), a(CHEBI:"alpha-2,8-linked polymer of sialic acid", loc(MESHA:Neuroglia)))

SET Support = "Recently, polymorphisms of the human Siglec-3/CD33 were linked to late \
onset Alzheimer's disease by genome-wide association studies."

SET StudyType = "GWAS"
p(HGNC:CD33, var("?")) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET StudyType

SET Support = "Human Siglec-3 is expressed on microglia and produces inhibitory \
signaling that decreases uptake of particular molecules such as \
amyloid-β aggregates."

p(HGNC:CD33, loc(MESHA:Microglia)) -| tloc(a(CHEBI:"amyloid-beta"), fromLoc(MESHA:"Extracellular Space"), toLoc(MESHA:Cytoplasm))

UNSET Section
UNSET PublicationType

# Results behind paywall
##################################################################################

SET Citation = {"PubMed", "Sci Rep. 2015 Nov 19;5:16800. doi: 10.1038/srep16800.", "26582367"}

SET PublicationType = "Research"
SET Section = "Abstract"

SET Support = "Oligosialic and polysialic acid (oligoSia and polySia) of the \
glycocalyx of neural and immune cells are linear chains, in which the \
sialic acid monomers are α2.8-glycosidically linked."

a(CHEBI:"alpha-2,8-linked polymer of sialic acid") hasMember a(CHEBI:"sialic acid")

SET Support = "Sialic acid-binding immunoglobulin-like lectin-11 (SIGLEC-11) is a \
primate-lineage specific receptor of human tissue macrophages and \
microglia that binds to α2.8-linked oligoSia."

a(MESHA:Microglia) -> p(HGNC:SIGLEC11)
a(MESHA:Macrophages) -> p(HGNC:SIGLEC11)
p(HGNC:SIGLEC11) => complex(p(HGNC:SIGLEC11), a(CHEBI:"alpha-2,8-linked polymer of sialic acid"))

SET Support = "Here, we show that soluble low molecular weight polySia with an \
average degree of polymerization 20 (avDP20) interacts with SIGLEC-11 \
and acts anti-inflammatory on human THP1 macrophages involving the \
SIGLEC-11 receptor."

SET CellLine = "THP-1"
SET NumberOfResidues = "20"
complex(p(HGNC:SIGLEC11), a(CHEBI:"alpha-2,8-linked polymer of sialic acid")) -| bp(GOBP:"inflammatory response")
UNSET NumberOfResidues
UNSET CellLine

SET Support = "Soluble polySia avDP20 inhibited the lipopolysaccharide (LPS)-induced \
gene transcription and protein expression of tumor necrosis factor-α \
(Tumor Necrosis Factor Superfamily Member 2, TNFSF2)."

a(CHEBI:lipopolysaccharide) -> r(HGNC:TNF)
a(CHEBI:lipopolysaccharide) -> p(HGNC:TNF)
SET ChallengeType = "lipopolysaccharide stimulation"
SET NumberOfResidues = "20"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| r(HGNC:TNF)
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| p(HGNC:TNF)
UNSET NumberOfResidues
UNSET ChallengeType

SET Support = "In addition, polySia avDP20 neutralized the LPS-triggered increase in \
macrophage phagocytosis, but did not affect basal phagocytosis or \
endocytosis."

SET MeSHAnatomy = "Macrophages"
SET NumberOfResidues = "20"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| bp(GOBP:phagocytosis)
UNSET MeSHAnatomy
UNSET NumberOfResidues

SET Support = "Moreover, polySia avDP20 prevented the oxidative burst of human \
macrophages triggered by neural debris or fibrillary amyloid-β1-42."

SET MeSHAnatomy = "Macrophages"
SET NumberOfResidues = "20"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| bp(GOBP:"respiratory burst")
p(HGNC:APP, frag("1_42")) -> bp(GOBP:"respiratory burst")
UNSET MeSHAnatomy
UNSET NumberOfResidues

SET Support = "In a human macrophage-neuron co-culture system, polySia avDP20 also \
reduced loss of neurites triggered by fibrillary amyloid-β1-42."

p(HGNC:APP, frag("1_42")) -| a(MESHA:Neurites)
SET MeSHAnatomy = {"Macrophages", "Neurons"}
SET NumberOfResidues = "20"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") positiveCorrelation a(MESHA:Neurites)
UNSET MeSHAnatomy
UNSET NumberOfResidues

UNSET Section

SET Section = "Results"

SET Support = "OligoSia in form of α2.8-linked Sia has been shown to bind to \
SIGLEC-11 receptors that are expressed on human tissue macrophages and \
microglia1,2, but the interaction of SIGLEC-11 with soluble polySia \
has not been analyzed."

a(CHEBI:"alpha-2,8-linked polymer of sialic acid") => complex(p(HGNC:SIGLEC11, loc(MESHA:Microglia)), a(CHEBI:"alpha-2,8-linked polymer of sialic acid"))
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") => complex(p(HGNC:SIGLEC11, loc(MESHA:Macrophages)), a(CHEBI:"alpha-2,8-linked polymer of sialic acid"))

SET Support = "PolySia avDP20 bound to the rhSIGLEC-11/Fc fusion protein in a \
concentration dependent manner, while no binding of dextran was \
observed (Fig. 1d)."

SET NumberOfResidues = "20"
SET Assay = "ELISA"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") => complex(p(HGNC:SIGLEC11), a(CHEBI:"alpha-2,8-linked polymer of sialic acid"))
UNSET NumberOfResidues
UNSET Assay

SET Support = "To test whether polySia avDP20 act anti-inflammatory on macrophages, \
human macrophages differentiated from THP1 monocytes were used. The \
human macrophages showed protein expression of SIGLEC-11 as determined \
by flow cytometry with a newly generated monoclonal antibody directed \
against SIGLEC-11 (Fig. 2a)."

SET CellLine = "THP-1"
SET NumberOfResidues = "20"
a(MESHA:Macrophages) -> p(HGNC:SIGLEC11)
UNSET NumberOfResidues
UNSET CellLine

SET Support = "Next, we analyzed whether polySia avDP20 had anti-inflammatory effects \
on the gene transcription of the pro-inflammatory cytokine tumor \
necrosis factor-α (Tumor Necrosis Factor Superfamily Member 2, TNFSF2) \
that was induced in the macrophages by treatment with 1 μg/ml LPS. \
First, gene transcription of SIGLEC7, SIGLEC10 and SIGLEC11 was \
analyzed in human macrophages, obtained from THP1 monocytes, before \
and after treatment with LPS. Data show that human macrophages clearly \
transcribed SIGLEC11, while SIGLEC10 was barely detected and SIGLEC7 \
was inducible by LPS (Fig. 2b)."

SET CellLine = "THP-1"
SET NumberOfResidues = "20"
SET ChallengeType = "lipopolysaccharide stimulation"
SET LPSDosage = "1 μg/ml"
a(MESHA:Macrophages) -> r(HGNC:SIGLEC11)
a(MESHA:Macrophages) -> r(HGNC:SIGLEC7)
a(MESHA:Macrophages) -> r(HGNC:SIGLEC10)
UNSET LPSDosage
UNSET ChallengeType
UNSET NumberOfResidues
UNSET CellLine

SET Support = "Results showed that polySia avDP20 prevented the LPS-induced gene \
transcription of TNFSF2 at the concentration of 1.5 μM, while Sia as \
monomer or oligoSia with a DP of 6 had no effect (Fig. 2c)."

SET CellLine = "THP-1"
SET NumberOfResidues = "20"
SET ChallengeType = "lipopolysaccharide stimulation"
SET LPSDosage = "1 μg/ml"
SET ChemicalDosage = "1.5 μM"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| r(HGNC:TNF)
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| p(HGNC:TNF)
UNSET ChemicalDosage
UNSET NumberOfResidues
SET NumberOfResidues = "6"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") cnc r(HGNC:TNF)
UNSET LPSDosage
UNSET ChallengeType
UNSET NumberOfResidues
UNSET CellLine

SET Support = "Therefore, we stimulated the transduced macrophage cells (that \
received either SIGLEC11 knock-down vector or control vector) with LPS \
(1 μg/ml) and treated them with 1.5 μM polySia avDP20 for 3 or \
24 hours to assess transcription and protein level (Fig. 2f,g). Knock- \
down of SIGLEC11 neutralized the anti-inflammatory effect of polySia \
avDP20 on the LPS-induced cytokine gene transcription of TNFSF2 (Fig. \
2f) and protein secretion of TNFSF2 (Fig. 2g)."

SET CellLine = "THP-1"
SET NumberOfResidues = "20"
SET ChallengeType = {"lipopolysaccharide stimulation", "alpha-2,8-linked polymer of sialic acid"}
SET LPSDosage = "1 μg/ml"
SET NumberOfResidues = "20"
SET ChemicalDosage = "1.5 μM"
p(HGNC:SIGLEC11) -| r(HGNC:TNF)
p(HGNC:SIGLEC11) -| p(HGNC:TNF)
UNSET ChemicalDosage
UNSET LPSDosage
UNSET ChallengeType
UNSET NumberOfResidues
UNSET CellLine

SET Support = "Thus, polySia avDP20 acted anti-inflammatory on the LPS-induced TNFSF2 \
gene transcription and protein secretion of human macrophages via the \
SIGLEC-11 receptor."

SET ChallengeType = "lipopolysaccharide stimulation"
SET CellLine = "THP-1"
SET NumberOfResidues = "20"
complex(p(HGNC:SIGLEC11), a(CHEBI:"alpha-2,8-linked polymer of sialic acid")) -| r(HGNC:TNF)
complex(p(HGNC:SIGLEC11), a(CHEBI:"alpha-2,8-linked polymer of sialic acid")) -| p(HGNC:TNF)
UNSET NumberOfResidues
UNSET CellLine
UNSET ChallengeType

SET Support = "Next, we determined the half maximum effective concentration of \
polySia avDP20 leading to a 50% reduction of the TNFSF2 gene \
transcripts induced in the human macrophages by 1 μg/ml LPS. This half \
effective concentration was determined as EC50m = 0.14 μM (Fig. 3b)."

SET CellLine = "THP-1"
SET NumberOfResidues = "20"
SET ChallengeType = "lipopolysaccharide stimulation"
SET LPSDosage = "1 μg/ml"
SET ChemicalDosage = "0.14 μM"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| r(HGNC:TNF)
UNSET ChemicalDosage
UNSET LPSDosage
UNSET ChallengeType
UNSET NumberOfResidues
UNSET CellLine

SET Support = "Next, we analyzed whether polySia avDP20 could also antagonize the \
TNFSF2 gene transcription induced by higher concentration of LPS. \
Therefore, human macrophages were treated for 3 hours with 50 μg/ml \
LPS plus 1.5 μM polySia avDP20 (Fig. 3c). Treatment with 50 μg/ml LPS \
increased the gene transcription of TNFSF2 from 1 +/− 0.08 to \
7.3 +/− 0.8, while addition of 1.5 μM polySia avDP20 antagonized this \
increase to only 2.59 +/− 1.1 (p = 0.001; Fig. 3c)."

SET CellLine = "THP-1"
SET NumberOfResidues = "20"
SET ChallengeType = "lipopolysaccharide stimulation"
SET LPSDosage = "50 μg/ml"
SET ChemicalDosage = "1.5 μM"
SET TimePoint = "3 hours post-stimulation"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| r(HGNC:TNF)
UNSET TimePoint
UNSET ChemicalDosage
UNSET LPSDosage
UNSET ChallengeType
UNSET NumberOfResidues
UNSET CellLine

SET Support = "Then, we asked whether polySia avDP20 also interferes with the LPS- \
stimulated TNFSF2 protein secretion of human macrophages. Macrophages \
were treated for 24 hours with LPS +/− 1.5 μM polySia avDP20; and then \
TNFSF2 protein secretion by macrophages was determined by ELISA (Fig. \
3d). LPS (50 μg/ml) led to a production of 978.86 +/− 66.98 pg/ml \
TNFSF2 protein in the supernatant. However addition of polySia avDP20 \
(1.5 μM) reduced the secreted TNFSF2 protein to 741.06 +/− 62.01 pg/ml \
(p = 0.001; Fig. 3d)."

SET CellLine = "THP-1"
SET NumberOfResidues = "20"
SET ChallengeType = "lipopolysaccharide stimulation"
SET LPSDosage = "50 μg/ml"
SET ChemicalDosage = "1.5 μM"
SET TimePoint = "24 hours post-stimulation"
SET Assay = "ELISA"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| p(HGNC:TNF)
UNSET Assay
UNSET TimePoint
UNSET ChemicalDosage
UNSET LPSDosage
UNSET ChallengeType
UNSET NumberOfResidues
UNSET CellLine

SET Support = "Phagocytic uptake of microbeads was relatively increased from \
100 +/− 11.4% to 144 +/− 8.8% after treatment with 1 μg/ml LPS (Fig. \
4a)."

SET LPSDosage = "1 μg/ml"
a(CHEBI:lipopolysaccharide) -> bp(GOBP:phagocytosis)
UNSET LPSDosage

SET Support = "Treatment with 1.5 μM polySia avDP20 prevented this LPS-dependent \
increase in phagocytosis to 103 +/− 3.7% without interference with \
basal phagocytosis of macrophages (Fig. 4a)."

SET CellLine = "THP-1"
SET NumberOfResidues = "20"
SET ChallengeType = "lipopolysaccharide stimulation"
SET LPSDosage = "1 μg/ml"
SET ChemicalDosage = "1.5 μM"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| bp(GOBP:phagocytosis)
UNSET ChemicalDosage
UNSET LPSDosage
UNSET ChallengeType
UNSET NumberOfResidues
UNSET CellLine

SET Support = "Then, we analyzed whether SIGLEC-11 is involved in the inhibitory \
effect of polySia avDP20 on the LPS-stimulated microbead phagocytosis. \
Lentiviral knock-down of SIGLEC11 in the macrophages abolished the \
inhibitory effect of polySia avDP20 on phagocytosis compared to \
control plasmid transduced cells, which showed a reduction in \
microbeads phagocytosis from 171 +/− 4% to 124 +/− 9% (Fig. 4c)."

SET CellLine = "THP-1"
SET NumberOfResidues = "20"
SET ChallengeType = "lipopolysaccharide stimulation"
SET LPSDosage = "1 μg/ml"
SET ChemicalDosage = "1.5 μM"
complex(p(HGNC:SIGLEC11), a(CHEBI:"alpha-2,8-linked polymer of sialic acid")) -| bp(GOBP:phagocytosis)
UNSET ChemicalDosage
UNSET LPSDosage
UNSET ChallengeType
UNSET NumberOfResidues
UNSET CellLine

SET Support = "Next, we analyzed the effect of polySia avDP20 on endocytosis. Basal \
endocytosis of nanobeads was unchanged after treatment with polySia \
avDP20."

SET CellLine = "THP-1"
SET NumberOfResidues = "20"
SET ChallengeType = "lipopolysaccharide stimulation"
SET LPSDosage = "1 μg/ml"
SET ChemicalDosage = "1.5 μM"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") cnc bp(GOBP:endocytosis)
UNSET ChemicalDosage
UNSET LPSDosage
UNSET ChallengeType
UNSET NumberOfResidues
UNSET CellLine

SET Support = "To study the effect of polySia avDP20 on phagocytosis of neural debris \
and the associated oxidative burst, we incubated human macrophages \
with fluorescently labeled neural debris. First, we determined the \
uptake of neural debris into the cells by confocal microscopy and \
3D-reconstruction. We observed ingestion of neural debris into \
macrophages (Fig. 5a)."

SET ChallengeType = "neural debris"
SET TechnologyUsed = "Confocal Microscopy"
a(MESHA:Macrophages) -- bp(GOBP:phagocytosis)
UNSET TechnologyUsed
UNSET ChallengeType

SET Support = "After treatment with polySia avDP20 the number of macrophages showing \
uptake of neural debris was slightly decreased (Fig. 5b). In detail, \
1.5 μM polySia avDP20 reduced the relative uptake of neural debris by \
macrophages from 1 +/− 0.06 to 0.7 +/− 0.07 (p = 0.007; Fig. 5b)."

SET NumberOfResidues = "20"
SET ChallengeType = "neural debris"
SET TechnologyUsed = "Confocal Microscopy"
SET MeSHAnatomy = "Macrophages"
SET ChemicalDosage = "1.5 μM"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| bp(GOBP:phagocytosis)
UNSET ChemicalDosage
UNSET TechnologyUsed
UNSET MeSHAnatomy
UNSET NumberOfResidues
UNSET ChallengeType

SET Support = "To determine the phagocytosis associated oxidative burst, we used the \
superoxide-sensitive fluorescent dye dihydroethidium (DHE)."

# DHE is a reduced form of ethidium
a(CHEBI:ethidium) positiveCorrelation a(CHEBI:superoxide)

SET Support = "Treatment of macrophages with neural debris stimulated the relative \
superoxide production from 1 +/− 0.09 to 1.6 +/− 0.1 (p = 0.004), \
while 1.5 μM polySia avDP20 completely prevented the debris induced \
stimulation of superoxide (0.9 +/− 0.09; p = 0.003; Fig. 5c)."

SET ChallengeType = "neural debris"
SET TechnologyUsed = "Confocal Microscopy"
a(MESHA:Macrophages) -> a(CHEBI:superoxide)
SET NumberOfResidues = "20"
SET ChemicalDosage = "1.5 μM"
SET MeSHAnatomy = "Macrophages"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| a(CHEBI:superoxide)
UNSET ChemicalDosage
UNSET TechnologyUsed
UNSET MeSHAnatomy
UNSET NumberOfResidues
UNSET ChallengeType

SET Support = "As a control we scavenged the superoxide by addition of superoxide \
dismutase-1 (SOD1) or by addition of Trolox, a water-soluble analog of \
vitamin E, into the medium. Both scavengers neutralized the superoxide \
that was induced by neural debris (Fig. 5d)."

SET ChallengeType = "neural debris"
SET TechnologyUsed = "Confocal Microscopy"
SET MeSHAnatomy = "Macrophages"
p(HGNC:SOD1) =| a(CHEBI:superoxide)
a(CHEBI:Trolox) =| a(CHEBI:superoxide)
UNSET TechnologyUsed
UNSET MeSHAnatomy
UNSET ChallengeType

SET Support = "We incubated human macrophages with biotinylated fibrillary \
amyloid-β1–42 and visualized the uptake into the cells by confocal \
microscopy and 3D-reconstruction (Fig. 6a). PolySia avDP20 slightly \
reduced the uptake of amyloid-β1–42 into macrophages."

SET TechnologyUsed = "Confocal Microscopy"
a(MESHA:Macrophages) =| p(HGNC:APP, frag("1_42"), pmod(GOBP:"protein biotinylation"))
SET NumberOfResidues = "20"
SET ChemicalDosage = "1.5 μM"
SET MeSHAnatomy = "Macrophages"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -> p(HGNC:APP, frag("1_42"), pmod(GOBP:"protein biotinylation"))
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| bp(GOBP:"beta-amyloid clearance")
UNSET ChemicalDosage
UNSET TechnologyUsed
UNSET MeSHAnatomy
UNSET NumberOfResidues

SET Support = "Treatment of macrophages with amyloid-β1–42 stimulated the relative \
superoxide production from 1 +/− 0.05 to 1.4 +/− 0.1 (p = 0.004), \
while 1.5 μM polySia avDP20 prevented the amyloid-β1–42 induced \
stimulation of the superoxide production (1.08 +/− 0.03; p = 0.03; \
Fig. 6c)."

SET MeSHAnatomy = "Macrophages"
p(HGNC:APP, frag("1_42"), pmod(GOBP:"protein biotinylation")) -> a(CHEBI:superoxide)
SET NumberOfResidues = "20"
SET ChemicalDosage = "1.5 μM"
SET ChallengeType = "amyloid-β1–42"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| a(CHEBI:superoxide)
UNSET ChallengeType
UNSET ChemicalDosage
UNSET NumberOfResidues
UNSET MeSHAnatomy

SET Support = "SOD1 and Trolox scavenged the amyloid-β1–42 triggered superoxide \
molecules (Fig. 6d)."

SET MeSHAnatomy = "Macrophages"
SET ChallengeType = "amyloid-β1–42"
p(HGNC:SOD1) =| a(CHEBI:superoxide)
a(CHEBI:Trolox) =| a(CHEBI:superoxide)
UNSET ChallengeType
UNSET MeSHAnatomy

SET Support = "THP1 derived macrophage cells were added to the neurons and neurite \
length was determined after 48 hours of co-culture (Fig. 7a). Addition \
of macrophages to neurons slightly decreased the relative neurite \
length (Fig. 7b)."

SET CellLine = "THP-1"
SET MeSHAnatomy = "Neurons"
SET TimePoint = "2 days"
a(MESHA:Macrophages) negativeCorrelation a(MESHA:Neurites)
UNSET TimePoint
UNSET MeSHAnatomy
UNSET CellLine

SET Support = "Addition of fibrillary amyloid-β1–42 to macrophage-neuron co-culture \
further reduced the relative neurite length (Fig. 7b)."

SET CellLine = "THP-1"
SET MeSHAnatomy = "Neurons"
SET TimePoint = "2 days"
composite(p(HGNC:APP, frag("1_42")), a(MESHA:Macrophages)) -| a(MESHA:Neurites)
UNSET TimePoint
UNSET MeSHAnatomy
UNSET CellLine

SET Support = "At the applied concentration of 1 μM amyloid-β1–42 alone had no \
neurite reducing effect (Fig. 7b)."

SET MeSHAnatomy = "Neurons"
SET TimePoint = "2 days"
p(HGNC:APP, frag("1_42")) cnc a(MESHA:Neurites)
UNSET TimePoint
UNSET MeSHAnatomy

SET Support = "PolySia avDP20 did not interfere with the general neurite-reducing \
effect of the macrophages, but it completely inhibited the neurite- \
reducing effect of fibrillary amyloid-β1–42 (Fig. 7c)."

SET CellLine = "THP-1"
SET MeSHAnatomy = "Neurons"
SET TimePoint = "2 days"
SET NumberOfResidues = "20"
a(CHEBI:"alpha-2,8-linked polymer of sialic acid") -| composite(p(HGNC:APP, frag("1_42")), a(MESHA:Macrophages))
UNSET TimePoint
UNSET MeSHAnatomy
UNSET CellLine
UNSET NumberOfResidues

UNSET Section
UNSET PublicationType

##################################################################################

SET Citation = {"PubMed", "Glia. 2013 Jan;61(1):37-46. doi: 10.1002/glia.22359. Epub 2012 May 21.", "22615186"}

SET PublicationType = "Review"
SET Section = "Abstract"

SET Support = "Microglia sense intact or lesioned cells of the central nervous system \
(CNS) and respond accordingly."

a(MESHA:Microglia) positiveCorrelation a(MESHA:Cells, loc(MESHA:"Central Nervous System"))

SET Support = "To fulfill this task, microglia express a whole set of recognition \
receptors. Fc receptors and DAP12 (TYROBP)-associated receptors such \
as microglial triggering receptor expressed on myeloid cells-2 (TREM2) \
and the complement receptor-3 (CR3, CD11b/CD18) trigger the \
immunoreceptor tyrosine-based activation motif (ITAM)-signaling \
cascade, resulting in microglial activation, migration, and \
phagocytosis."

a(MESHA:Microglia) -> p(SCOMP:"FcR Gamma Chain Complex")
a(MESHA:Microglia) -> p(HGNC:TYROBP)
a(MESHA:Microglia) -> p(HGNC:TREM2)
a(MESHA:Microglia) -> complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
p(HGNC:TYROBP) -- p(HGNC:TREM2)
p(HGNC:TYROBP) -- complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
p(SCOMP:"FcR Gamma Chain Complex") -- p(HGNC:TREM2)
p(SCOMP:"FcR Gamma Chain Complex") -- complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
p(HGNC:TREM2) -> bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")
complex(p(HGNC:ITGAM), p(HGNC:ITGB2)) -> bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")
bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif") -> bp(GOBP:"microglial cell activation")
bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif") -> bp(GOBP:"microglial cell migration")
bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif") -> bp(GOBP:phagocytosis)

SET Support = "Those receptors are counter-regulated by immunoreceptor tyrosine-based \
inhibition motif (ITIM)-signaling receptors, such as sialic acid- \
binding immunoglobulin superfamily lectins (Siglecs)."

act(p(HGNC:SIGLEC1), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:CD22), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:CD33), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:MAG), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC5), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC6), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC7), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC8), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC9), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC10), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC11), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC12), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC14), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC15), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC16), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| p(HGNC:TREM2)
act(p(HGNC:SIGLEC1), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:CD22), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:CD33), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:MAG), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:SIGLEC5), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:SIGLEC6), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:SIGLEC7), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:SIGLEC8), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:SIGLEC9), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:SIGLEC10), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:SIGLEC11), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:SIGLEC12), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:SIGLEC14), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:SIGLEC15), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
act(p(HGNC:SIGLEC16), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| complex(p(HGNC:ITGAM), p(HGNC:ITGB2))

SET Support = "Siglecs recognize the sialic acid cap of healthy neurons thus leading \
to an ITIM signaling that turns down microglial immune responses and \
phagocytosis."

complex(p(HGNC:SIGLEC1), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC1), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:CD22), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:CD22), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:CD33), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:CD33), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:MAG), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:MAG), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:SIGLEC5), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC5), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:SIGLEC6), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC6), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:SIGLEC7), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC7), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:SIGLEC8), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC8), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:SIGLEC9), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC9), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:SIGLEC10), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC10), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:SIGLEC11), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC11), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:SIGLEC12), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC12), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:SIGLEC14), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC14), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:SIGLEC15), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC15), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
complex(p(HGNC:SIGLEC16), a(CHEBI:"sialic acid", loc(MESHA:Neurons))) -> act(p(HGNC:SIGLEC16), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif"))
SET MeSHAnatomy = "Microglia"
act(p(HGNC:SIGLEC1), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:CD22), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:CD33), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:MAG), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC5), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC6), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC7), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC8), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC9), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC10), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC11), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC12), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC14), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC15), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC16), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:"immune response")
act(p(HGNC:SIGLEC1), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:CD22), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:CD33), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:MAG), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:SIGLEC5), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:SIGLEC6), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:SIGLEC7), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:SIGLEC8), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:SIGLEC9), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:SIGLEC10), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:SIGLEC11), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:SIGLEC12), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:SIGLEC14), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:SIGLEC15), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
act(p(HGNC:SIGLEC16), ma(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")) -| bp(GOBP:phagocytosis)
UNSET MeSHAnatomy

UNSET Section

SET Section = "ITAM SIGNALING FOR PHAGOCYTOSIS"

SET Support = "Different receptors expressed on the cell surface of phagocytes \
mediate the phagocytic process. An important role play the \
ITAM‐signaling molecules (Flannagan et al.,2011; N'Diaye et al.,2009; \
Ziegenfuss et al.,2008)."

bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif") -- bp(GOBP:phagocytosis)

SET Support = "Myeloid cells express approximately 20 ITAM‐signaling receptors \
(Ivashkiv,2009)."

a(MESHA:"Myeloid Cells") positiveCorrelation bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")

SET Support = "On ligand‐binding, activatory receptors associate with adaptor \
proteins bearing two ITAMs such as the common γ chain of Fc receptors \
or the DAP12 molecule through the interactions between charged amino \
acids within the transmembrane regions (see Fig. 1; Linnartz et \
al.,2010)."

bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif") -- p(SCOMP:"FcR Gamma Chain Complex")
bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif") -- p(HGNC:TYROBP)

SET Support = "The common γ chain of Fc receptors interacts with Fc receptors, \
whereas DAP12 can associate with cell membrane receptors such as the \
triggering receptor expressed on myeloid cells‐2 (TREM2) or the \
complement receptor‐3 (CR3; CD11b/CD18) (Fig. 1)."

# "On ligand binding, ITAM‐signaling receptors like FcγRI, FcγRIII, TREM2, CR3, and SIRPβ1" taken from Figure 1
# FcγRI (CD64) = FCGR1A, FcγRIII = FCGR3A
p(SCOMP:"FcR Gamma Chain Complex") => complex(p(HGNC:FCGR1A), p(SCOMP:"FcR Gamma Chain Complex"))
p(SCOMP:"FcR Gamma Chain Complex") => complex(p(HGNC:FCGR3A), p(SCOMP:"FcR Gamma Chain Complex"))
p(HGNC:TYROBP) => complex(p(HGNC:TYROBP), p(HGNC:TREM2))
p(HGNC:TYROBP) => complex(p(HGNC:TYROBP), p(HGNC:ITGAM), p(HGNC:ITGB2))
p(HGNC:TYROBP) => complex(p(HGNC:TYROBP), p(HGNC:SIRPB1))

SET Support = "The Fc receptors are one well studied group of receptors on the \
surface of phagocytes, which on ligand‐binding signal via the ITAM‐Syk \
cascade and mediate a number of downstream cascades leading to \
cellular activation (Lowell,2011)."

a(MESHA:Phagocytes) -> p(HGNC:FCGR1A)
a(MESHA:Phagocytes) -> p(HGNC:FCGR3A)
complex(p(HGNC:FCGR1A), p(SCOMP:"FcR Gamma Chain Complex")) -> bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")
complex(p(HGNC:FCGR3A), p(SCOMP:"FcR Gamma Chain Complex")) -> bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")
bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif") -> act(p(HGNC:SYK), ma(kin))
act(p(HGNC:SYK), ma(kin)) -> bp(GOBP:"cell activation")

SET Support = "One cellular function that is initiated by the downstream pathways is \
phagocytosis."

act(p(HGNC:SYK), ma(kin)) -> bp(GOBP:phagocytosis)

SET Support = "Subsequently, to stimulation of activatory receptors, members of the \
Src kinase family phosphorylate the tyrosine residues of the ITAMs \
resulting in the establishment of docking sites for Syk kinases (Fig. \
1; Linnartz et al.,2010; Lowell,2011)."

complex(p(HGNC:FCGR1A), p(SCOMP:"FcR Gamma Chain Complex")) -> act(p(SFAM:"SRC Family"), ma(kin))
complex(p(HGNC:FCGR1A), p(SCOMP:"FcR Gamma Chain Complex")) -> act(p(SFAM:"SRC Family"), ma(kin))
complex(p(HGNC:TYROBP), p(HGNC:TREM2)) -> act(p(SFAM:"SRC Family"), ma(kin))
complex(p(HGNC:TYROBP), p(HGNC:ITGAM), p(HGNC:ITGB2)) -> act(p(SFAM:"SRC Family"), ma(kin))
complex(p(HGNC:TYROBP), p(HGNC:SIRPB1)) -> act(p(SFAM:"SRC Family"), ma(kin))
act(p(SFAM:"SRC Family"), ma(kin)) -> p(HGNC:TYROBP, pmod(Ph))
act(p(SFAM:"SRC Family"), ma(kin)) -> p(SCOMP:"FcR Gamma Chain Complex", pmod(Ph))
p(HGNC:TYROBP, pmod(Ph)) => complex(p(HGNC:TYROBP, pmod(Ph)), p(HGNC:SYK))
p(SCOMP:"FcR Gamma Chain Complex", pmod(Ph)) => complex(p(SCOMP:"FcR Gamma Chain Complex", pmod(Ph)), p(HGNC:SYK))
complex(p(HGNC:TYROBP, pmod(Ph)), p(HGNC:SYK)) -> act(p(HGNC:SYK))
complex(p(SCOMP:"FcR Gamma Chain Complex", pmod(Ph)), p(HGNC:SYK)) -> act(p(HGNC:SYK))

SET Support = "Activated Syk in turn initiates a variety of downstream signals (Fig. \
1) mediated through calcium, protein kinase C, phospholipase A2, \
phosphatidyl‐inositol 3‐kinase, extracellular signal‐regulated kinase \
and GTPases of the Rho family leading to phagocytosis of IgG coated \
and opsonized particles and antigens (Garcia‐Garcia and Rosales,2002; \
Nimmerjahn and Ravetch,2006; Stephens et al.,2002)."

act(p(HGNC:SYK), ma(kin)) => a(CHEBI:"phosphatidylinositol trisphosphate")
act(p(HGNC:SYK), ma(kin)) -> act(p(PFAM:Pkinase_C), ma(kin))
act(p(HGNC:SYK), ma(kin)) -> act(p(PFAM:Phospholip_A2_1))
act(p(HGNC:SYK), ma(kin)) -> act(p(SFAM:"PIK3C Class IA Family"), ma(kin))
act(p(HGNC:SYK), ma(kin)) -> act(p(SFAM:"RHO Family"))
act(p(HGNC:SYK), ma(kin)) -> bp(GOBP:phagocytosis)

SET Support = "ITAM‐signaling molecules are counter‐regulated by receptors signaling \
via an ITIM (Fig. 1; Lowell,2011)."

bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif") -| bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")

SET Support = "On ligand binding, ITIM‐signaling receptors recruit SH2 \
domain‐containing inositol 5′‐phosphatase (SHIP) and SH2 \
domain‐containing tyrosine phosphatases (SHP1 and SHP2) which in turn \
can terminate the function of various signaling pathways by the \
dephosphorylation of signaling intermediates (Fig. 1; Cao et al.,2009; \
Crocker et al.,2007; Flannagan et al.,2011; Linnartz et al.,2010; \
Lowell,2011; Salminen and Kaarniranta,2009)."

# Looking at Fig1 for specifics
act(p(HGNC:FCGR2B)) -> bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
act(p(HGNC:SIRPA)) -> bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")
bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif") positiveCorrelation p(HGNC:FCGR2B, pmod(Ph))
bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif") positiveCorrelation p(HGNC:SIRPA, pmod(Ph))
act(p(HGNC:FCGR2B)) -> p(HGNC:FCGR2B, pmod(Ph))
act(p(HGNC:SIRPA)) -> p(HGNC:SIRPA, pmod(Ph))
p(HGNC:FCGR2B, pmod(Ph)) => complex(p(HGNC:FCGR2B, pmod(Ph)), p(HGNC:PTPN6))
p(HGNC:SIRPA, pmod(Ph)) => complex(p(HGNC:SIRPA, pmod(Ph)), p(HGNC:PTPN6))
complex(p(HGNC:FCGR2B, pmod(Ph)), p(HGNC:PTPN6)) => act(p(HGNC:PTPN6), ma(phos))
complex(p(HGNC:SIRPA, pmod(Ph)), p(HGNC:PTPN6)) => act(p(HGNC:PTPN6), ma(phos))
act(p(HGNC:PTPN6), ma(phos)) =| p(HGNC:TYROBP, pmod(Ph))
act(p(HGNC:PTPN6), ma(phos)) =| p(SCOMP:"FcR Gamma Chain Complex", pmod(Ph))

SET Support = "Thus, ITIM‐signaling receptor activation leads to an inhibitory \
signaling cascade resulting in the suppression of processes such as \
phagocytosis (Linnartz et al.,2010)."

bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif") -| bp(GOBP:phagocytosis)

UNSET Section

SET Section = "MICROGLIAL ITAM‐SIGNALING RECEPTORS"

SET Support = "Microglia express a whole amount of activatory ITAM‐signaling \
receptors that might act as phagocytic receptors such as the Fc \
receptors, the TREM2, the CR3, and the signal regulatory protein β1 \
(SIRPβ1) on which we will focus in this review."

a(MESHA:Microglia) -> p(HGNC:TREM2)
a(MESHA:Microglia) -> complex(p(HGNC:ITGAM), p(HGNC:ITGB2))
a(MESHA:Microglia) -> p(HGNC:SIRPB1)

SET Support = "The type I transmembrane glycoproteins Fcγ receptors belong to the \
immunoglobulin superfamily (Nimmerjahn and Ravetch,2008). Although in \
mice four different classes of Fcγ receptors have been distinguished \
(FcγRI, FcγRIIB, FcγRIII, and FcγRIV), six have been described in the \
human system (FcγRI, FcγRIIA, FcγRIIB, FcγRIIC, FcγRIIIA, and \
FcγRIIIB) (Nimmerjahn and Ravetch,2008)."

p(PFAM:ig) hasMembers list(p(MGI:Fcgr1), p(MGI:Fcgr2b), p(MGI:Fcgr3), p(MGI:Fcgr4))
p(PFAM:ig) hasMembers list(p(HGNC:FCGR1A), p(HGNC:FCGR2A), p(HGNC:FCGR2B), p(HGNC:FCGR2C), p(HGNC:FCGR3A), p(HGNC:FCGR3B))

SET Support = "Several ITAM‐signaling activating Fcγ receptors, such as FcγRI (CD64), \
FcγRIIA (CD32a), and FcγRIII (CD16), are expressed on microglia (Lue \
and Walker,2002)."

p(HGNC:FCGR1A) -> bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")
p(HGNC:FCGR2A) -> bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")
p(HGNC:FCGR3A) -> bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")
a(MESHA:Microglia) -> p(HGNC:FCGR1A)
a(MESHA:Microglia) -> p(HGNC:FCGR2A)
a(MESHA:Microglia) -> complex(p(HGNC:FCGR3A), p(HGNC:FCGR3B))

SET Support = "They interact with the Fc part of IgG bound to antigen presented on \
microbial pathogens or autoantigens resulting in their phagocytosis \
(Peress et al.,1989; Ulvestad et al.,1994)."

p(HGNC:FCGR1A) -> bp(GOBP:phagocytosis)
p(HGNC:FCGR2A) -> bp(GOBP:phagocytosis)
complex(p(HGNC:FCGR3A), p(HGNC:FCGR3B)) -> bp(GOBP:phagocytosis)
p(HGNC:FCGR1A) isA p(PFAM:IgGFc_binding)
p(HGNC:FCGR2A) isA p(PFAM:IgGFc_binding)
complex(p(HGNC:FCGR3A), p(HGNC:FCGR3B)) isA p(PFAM:IgGFc_binding)

SET Support = "Fcγ receptors also play a role in various pathological conditions. In \
neurodegenerative areas of the brain‐like multiple sclerosis lesions, \
the expression levels of Fc receptors on microglia are increased \
(Ulvestad et al.,1994)."

path(MESHD:"Multiple Sclerosis") positiveCorrelation p(HGNC:FCGR1A)
path(MESHD:"Multiple Sclerosis") positiveCorrelation p(HGNC:FCGR2A)
path(MESHD:"Multiple Sclerosis") positiveCorrelation p(HGNC:FCGR3A)

SET Support = "A microglial receptor signaling via DAP12 is the glycoprotein TREM2."

a(MESHA:Microglia) -> p(HGNC:TREM2)
p(HGNC:TREM2) => complex(p(HGNC:TYROBP), p(HGNC:TREM2))

SET Support = "The involvement of TREM2 in phagocytosis has been demonstrated in \
vitro and in vivo."

p(HGNC:TREM2) -- bp(GOBP:phagocytosis)

SET Support = "On stimulation of TREM2 in vitro, the ITAMs of DAP12 are \
phosphorylated and the phagocytic activity of microglial cells is \
increased (Takahashi et al.,2005; Tomasello et al.,2000)."

SET Assay = {"in vitro", "in vivo"}
act(p(HGNC:TREM2)) -> p(HGNC:TYROBP, pmod(Ph))
act(p(HGNC:TREM2)) -> act(p(HGNC:TYROBP), ma(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif"))
SET MeSHAnatomy = "Microglia"
p(HGNC:TYROBP, pmod(Ph)) -> bp(GOBP:phagocytosis)
UNSET MeSHAnatomy
UNSET Assay

SET Support = "TREM2 mediates in vitro the binding of bacteria and leads to their \
internalization dependent on tyrosine phosphorylation by Src kinase \
(N'Diaye et al.,2009)."

SET Assay = "in vitro"
p(HGNC:TREM2) -| a(MESHA:"Bacterial Structures")
bp(GOBP:phagocytosis) -| a(MESHA:"Bacterial Structures")
act(p(SFAM:"SRC Family"), ma(kin)) -> p(HGNC:TYROBP, pmod(Ph))
p(HGNC:TYROBP, pmod(Ph)) -| a(MESHA:"Bacterial Structures")
UNSET Assay

SET Support = "In another study, the expression of TREM2 increases the phagocytic \
capacity of mircoglia in vitro as well and even leads to the uptake of \
apoptotic cellular material without inflammation during experimental \
autoimmune encephalomyelitis (EAE), an animal model for multiple \
sclerosis (Takahashi et al.,2007)."

SET Species = "10090"
SET Assay = "in vitro"
SET MeSHAnatomy = "Microglia"
p(MGI:Trem2) -> bp(GOBP:phagocytosis)
UNSET MeSHAnatomy
UNSET Assay
SET MeSHDisease = "Encephalomyelitis, Autoimmune, Experimental"
p(MGI:Trem2, loc(MESHA:Microglia)) -> bp(GOBP:"apoptotic cell clearance")
UNSET MeSHDisease
UNSET Species

SET Support = "The chronic neurodegenerative disease called Nasu–Hakola or polycystic \
lipomembranous osteodysplasia with sclerosing leukoencephalopathy, an \
autosomal recessive inherited disease, is caused by loss‐of‐function \
mutations of DAP12 as well as TREM2 (Colonna,2003)."

path(DO:"Nasu-Hakola disease") positiveCorrelation p(HGNC:TYROBP, var("?"))
path(DO:"Nasu-Hakola disease") positiveCorrelation p(HGNC:TREM2, var("?"))

SET Support = "Patients with mutations in DAP12 show an early onset of presenile \
dementia combined with systemic bone cysts (Lanier and Bakker,2000; \
Paloneva et al.,2000)."

p(HGNC:TYROBP, var("?")) positiveCorrelation path(MESHD:Dementia)
p(HGNC:TYROBP, var("?")) positiveCorrelation path(HP:"Bone cyst")

SET Support = "Patients with TREM2 mutations suffer from early onset presenile \
dementia followed by delayed bone symptoms (Chouery et al.,2008)."

p(HGNC:TREM2, var("?")) positiveCorrelation path(MESHD:Dementia)

SET Support = "A high expression level of TREM2 was found in the outer zone of \
plaque‐associated microglia (Melchior et al.,2010)."

a(MESHA:Microglia, loc(CHEBI:"amyloid-beta")) positiveCorrelation p(HGNC:TREM2)

SET Support = "In brain regions suffering from amyloid pathology in Alzheimer's \
disease, patients as well as in transgenic mouse models for \
Alzheimer's disease the highest expression levels of TREM2 were found \
(Carson et al.,2006; Schmid et al.,2002)."

path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:TREM2, loc(MESHA:Brain))
SET Species = "10090"
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Trem2, loc(MESHA:Brain))
UNSET Species

SET Support = "Moreover, the expression of TREM2 is also upregulated in mice \
suffering from prion disease (Lunnon et al.,2011)."

SET Species = "10090"
path(MESHD:"Prion Diseases") positiveCorrelation p(MGI:Trem2)
UNSET Species

SET Support = "CR3, a heterodimeric receptor consisting of the two integrins CD11b \
and CD18, is another potential microglial ITAM‐signaling receptor."

SET MeSHAnatomy = "Microglia"
complex(p(HGNC:ITGAM), p(HGNC:ITGB2)) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")
UNSET MeSHAnatomy

SET Support = "So far, a direct binding of CD18 to DAP12 has not been detected, but \
it has been shown that CD18 signals via the ITAM of DAP12 \
(Ivashkiv,2009) leading to syk kinase activation, which is essential \
for the phagocytosis of pathogens by CR3 (Shi et al.,2006)."

p(HGNC:ITGB2) -> p(HGNC:TYROBP, pmod(Ph))
p(HGNC:TYROBP, pmod(Ph)) => act(p(HGNC:SYK), ma(kin))
act(p(HGNC:SYK), ma(kin)) -> bp(GOBP:phagocytosis)

SET Support = "Furthermore, during development both DAP12 and CD11b are required for \
the microglia–neuron interaction in the hippocampus that induces cell \
death (Wakselman et al.,2008)."

SET MeSHAnatomy = "Hippocampus"
p(HGNC:TYROBP) -> composite(a(MESHA:Microglia), a(MESHA:Neurons))
p(HGNC:ITGAM) -> composite(a(MESHA:Microglia), a(MESHA:Neurons))
composite(a(MESHA:Microglia), a(MESHA:Neurons)) -> bp(MESHPP:Apoptosis)
UNSET MeSHAnatomy

SET Support = "CR3 is involved in the classical complement cascade. The first \
molecule of the classical complement cascade is the complement \
component 1 (C1) assembled of the subcomponents C1q, C1r, and C1s."

complex(p(HGNC:ITGAM), p(HGNC:ITGB2)) -- bp(GOBP:"complement activation, classical pathway")
bp(GOBP:"complement activation, classical pathway") -- complex(SCOMP:"C1 Complex")
complex(SCOMP:"C1 Complex") hasComponents list(complex(SCOMP:"C1q Complex"), p(HGNC:C1R), p(HGNC:C1S))

SET Support = "Thus, an enhanced binding of C1q has been demonstrated after \
desialylation of pentraxin 3, a widely expressed cell membrane \
anchored protein (Inforzato et al.,2006), or neurons (Linnartz et \
al.,2012)."

a(CHEBI:"sialic acid", loc(HGNC:PTX3)) -| act(complex(SCOMP:"C1q Complex"))
a(CHEBI:"sialic acid", loc(MESHA:Neurons)) -| act(complex(SCOMP:"C1q Complex"))

SET Support = "C1q binding initiates the classical complement cascade resulting in \
the production of the complement component C3b."

act(complex(SCOMP:"C1q Complex")) -> bp(GOBP:"complement activation, classical pathway")
# C3b consists of the alpha and beta chains of C3
bp(GOBP:"complement activation, classical pathway") -> p(HGNC:C3, frag("1_1560", "C3b"))

SET Support = "This C3 fragment covalently opsonizes the surface of the target \
structure and can be recognized by CR3 resulting in the removal by \
phagocytes (Fraser and Tenner,2008)."

act(p(HGNC:C3, frag("1_1560", "C3b")), ma(GOBP:opsonization)) => complex(p(HGNC:C3, frag("1_1560", "C3b")), p(HGNC:ITGAM), p(HGNC:ITGB2))
complex(p(HGNC:C3, frag("1_1560", "C3b")), p(HGNC:ITGAM), p(HGNC:ITGB2)) -> bp(GOBP:phagocytosis)

SET Support = "Thus, recently we have shown that enzymatically desialylated neuronal \
structures are marked by C1q and removed by microglia in a \
CR3‐mediated fashion (Linnartz et al.,2012)."

a(CHEBI:"sialic acid", loc(MESHA:Neurons)) -| act(complex(SCOMP:"C1q Complex"))
complex(p(HGNC:C3, frag("1_1560", "C3b")), p(HGNC:ITGAM), p(HGNC:ITGB2)) -> bp(GOBP:"microglial cell activation")
bp(GOBP:"microglial cell activation") -| a(MESHA:Neurons)

SET Support = "The CR3 also seems to be involved in an animal model of Alzheimer's \
disease because recently the contribution of this receptor has been \
demonstrated in the microglial phagocytosis and clearance of fibrillar \
amyloid β (Fu et al.,2012)."

SET Species = "10090"
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Microglia"
complex(p(MGI:Itgam), p(MGI:Itgb2)) -- bp(GOBP:phagocytosis)
complex(p(MGI:Itgam), p(MGI:Itgb2)) -- bp(GOBP:"beta-amyloid clearance")
UNSET MeSHAnatomy
UNSET MeSHDisease
UNSET Species

SET Support = "However, it is unclear how far the mouse data can be translated into \
human diseases because primates in contrast to mice have beside CR3 \
one additional CR, the CR1, that is capable to bind C3 fragments such \
as C3b and C3bi (Crehan et al.,2011; Ross et al.,1983)."

p(HGNC:CR1) => complex(p(HGNC:C3, frag("1_1560", "C3b")), p(HGNC:CR1))
# TODO C3bi?

SET Support = "New data of genome‐wide association studies hint toward an involvement \
of CR1 in Alzheimer's disease as well (Lambert et al.,2009) that could \
be explained by an increased number of C3b/C4b‐binding sites in one \
isoform (Brouwers et al.,2011)."

SET StudyType = "GWAS"
p(HGNC:CR1) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET StudyType

SET Support = "Both receptors, CR1 and 3, are involved in the clearance of complement \
opsonized structures in primates. CR1 mainly mediates the adhesion of \
particles, whereas the CR3 is involved in the following engulfment \
(Fallman et al.,1993)."

# Clearance of opsonized structures == phagocytosis
p(HGNC:CR1) -- bp(GOBP:"cell adhesion")
complex(p(HGNC:ITGAM), p(HGNC:ITGB2)) -- bp(GOBP:"phagocytosis, engulfment")

SET Support = "However, both receptors seem to initiate transmembrane signaling \
leading to phagocytosis (Fallman et al.,1993)."

p(HGNC:CR1) -> bp(GOBP:"intracellular signal transduction")
complex(p(HGNC:ITGAM), p(HGNC:ITGB2)) -> bp(GOBP:"intracellular signal transduction")
p(HGNC:CR1) -> bp(GOBP:"immune response-regulating cell surface receptor signaling pathway involved in phagocytosis")
complex(p(HGNC:ITGAM), p(HGNC:ITGB2)) -> bp(GOBP:"immune response-regulating cell surface receptor signaling pathway involved in phagocytosis")

SET Support = "SIRPβ1 is another microglial receptor with phagocytic ITAM‐signaling \
capacity with still unknown endogenous ligand."

p(HGNC:SIRPB1) -> a(MESHA:Microglia)
act(p(HGNC:SIRPB1), ma(MESHPP:"Immunoreceptor Tyrosine-Based Activation Motif")) -> bp(GOBP:phagocytosis)

SET Support = "The SIRP family has been studied intensively and consists of the three \
members SIRPα, SIRPβ, and SIRPγ."

# TODO SIRP family?
p(GFAM:"Signal regulatory proteins") hasMembers list(p(HGNC:SIRPA), p(HGNC:SIRPB1), p(HGNC:SIRPG))

SET Support = "It is the only of the three SIRP family members having a very short \
cytoplasmic region that contains charged amino acids in the \
transmembrane region for the association with the adaptor protein \
DAP12 (Barclay and Brown,2006)."

# Refers to SIRPB1
p(HGNC:SIRPB1) => complex(p(HGNC:SIRPB1), p(HGNC:TYROBP))

SET Support = "SIRPβ1 has been demonstrated to be induced in cultured microglia by \
inflammatory stimuli and to increase microglial phagocytic activity \
resulting in the specific clearance of amyloid β, neural debris, and \
microsphere beads (Gaikwad et al.,2009)."

bp(GOBP:"microglial cell activation") -> p(HGNC:SIRPB1, loc(MESHA:Microglia))
SET MeSHAnatomy = "Microglia"
p(HGNC:SIRPB1) -> bp(GOBP:phagocytosis)
p(HGNC:SIRPB1) -| a(CHEBI:"amyloid-beta")
UNSET MeSHAnatomy
SET MeSHAnatomy = {"Microglia", "Neurons"}
p(HGNC:SIRPB1) -> bp(GOBP:"apoptotic cell clearance")
UNSET MeSHAnatomy

UNSET Section

SET Section = "MICROGLIAL ITIM‐SIGNALING RECEPTORS"

SET Support = "The FcγRIIB (CD32b), the most broadly expressed Fcγ receptor, is in \
contrast to other Fcγ receptors an inhibitory receptor (Flannagan et \
al.,2011). It is the first discovered and best‐studied example of an \
receptor that signals via an ITIM in its cytoplasmic domain (Billadeau \
and Leibson,2002)."

p(HGNC:FCGR2B) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")

SET Support = "In mice and humans, the FcγRIIB is conserved (Nimmerjahn and \
Ravetch,2008) and has been shown to be expressed by microglia (Lue and \
Walker,2002)."

a(MESHA:Microglia) -> p(HGNC:FCGR2B)
SET Species = "10090"
a(MESHA:Microglia) -> p(MGI:Fcgr2b)
UNSET Species

SET Support = "The recruitment and activation of SHIP results in the inhibition of \
calcium‐dependent downstream pathways, Ras and ERK to mention some \
examples (Billadeau and Leibson,2002)."

# INPP5D = SHIP
act(p(HGNC:INPP5D)) -| bp(GOBP:"ERK1 and ERK2 cascade")
act(p(HGNC:INPP5D)) -| bp(GOBP:"Ras protein signal transduction")

SET Support = "On receptor crosslinking the FcγRIIB‐2 form mediates endocytosis and \
phagocytosis (Nimmerjahn and Ravetch,2008)."

complex(p(HGNC:FCGR2B), p(HGNC:FCGR2B)) reg bp(GOBP:phagocytosis)
complex(p(HGNC:FCGR2B), p(HGNC:FCGR2B)) reg bp(GOBP:endocytosis)

SET Support = "In vivo, the deletion of FCγRIIB in mice resulted in the production of \
autoantibodies and autoimmune glomerulonephritis in a strain‐dependent \
manner (Billadeau and Leibson,2002)."

SET StudyType = "in vivo"
SET Species = "10090"
p(MGI:Fcgr2b) -| path(HP:glomerulonephritis)
UNSET Species
UNSET StudyType

SET Support = "Siglecs are expressed on different cell types in a restricted pattern \
like on the already mentioned microglia (Hayakawa et al.,2005; Wang \
and Neumann,2010), but also on macrophages, lymphocytes, monocytes, \
Schwann cells, and oligodendrocytes (Matani et al.,2007; Perez‐Oliva \
et al.,2011; Varki and Angata,2006)."

# How to define this family? It is not in SFAM or PFAM
p(GFAM:"Sialic acid binding Ig like lectins") hasMembers list(p(HGNC:SIGLEC1), p(HGNC:CD22), p(HGNC:CD33), p(HGNC:MAG), p(HGNC:SIGLEC5), p(HGNC:SIGLEC6), p(HGNC:SIGLEC7), p(HGNC:SIGLEC8), p(HGNC:SIGLEC9), p(HGNC:SIGLEC10), p(HGNC:SIGLEC11), p(HGNC:SIGLEC12), p(HGNC:SIGLEC14), p(HGNC:SIGLEC15), p(HGNC:SIGLEC16))
a(MESHA:Microglia) -> p(GFAM:"Sialic acid binding Ig like lectins")
a(MESHA:Macrophages) -> p(GFAM:"Sialic acid binding Ig like lectins")
a(MESHA:Lymphocytes) -> p(GFAM:"Sialic acid binding Ig like lectins")
a(MESHA:Monocytes) -> p(GFAM:"Sialic acid binding Ig like lectins")
a(MESHA:"Schwann Cells") -> p(GFAM:"Sialic acid binding Ig like lectins")
a(MESHA:Oligodendroglia) -> p(GFAM:"Sialic acid binding Ig like lectins")

SET Support = "Siglecs are type 1 transmembrane proteins displaying different numbers \
of Ig‐like constant region type 2 (C2‐set Ig‐like) domains and the \
sialic acid‐binding site in an amino‐terminal Ig‐like variable (V‐set \
Ig‐like) domain (Crocker,2005; Crocker et al.,2007; Varki,2009; Varki \
and Angata,2006)."

p(GFAM:"Sialic acid binding Ig like lectins") -- a(CHEBI:"sialic acid")
p(GFAM:"Sialic acid binding Ig like lectins") -- p(PFAM:ig)

SET Support = "The linkage of the sialic acids to the subterminal sugars, the \
structure of underlying sugar residues and other modifications are \
important for the specific binding of Siglecs to sialic acids (Jandus \
et al.,2011)."

p(GFAM:"Sialic acid binding Ig like lectins") => complex(p(GFAM:"Sialic acid binding Ig like lectins"), a(CHEBI:"sialic acid"))

SET Support = "Thus, interactions of Siglecs with highly sialylated structures \
typically lead to a lot of inhibitory processes like the suppression \
of the immune response (Varki,2007), cellular activity (Paul et \
al.,2000) and phagocytosis (Linnartz et al.,2010), the induction of \
cell death (Nguyen et al.,2006) and endocytosis (Lock et al.,2004)."

complex(p(GFAM:"Sialic acid binding Ig like lectins"), a(CHEBI:"sialic acid")) -| bp(GOBP:"immune response")
complex(p(GFAM:"Sialic acid binding Ig like lectins"), a(CHEBI:"sialic acid")) -| bp(GOBP:"cell activation")
complex(p(GFAM:"Sialic acid binding Ig like lectins"), a(CHEBI:"sialic acid")) -| bp(GOBP:phagocytosis)
complex(p(GFAM:"Sialic acid binding Ig like lectins"), a(CHEBI:"sialic acid")) -> bp(MESHPP:Apoptosis)
complex(p(GFAM:"Sialic acid binding Ig like lectins"), a(CHEBI:"sialic acid")) -| bp(GOBP:endocytosis)

SET Support = "Thus, Siglec E is upregulated after LPS stimulation and can control \
Toll‐like receptor responses (Boyd et al.,2009)."

# Checked reference and work was done in mice (that's why Siglec 'E' is used)
SET Species = "10090"
a(CHEBI:lipopolysaccharide) -> p(MGI:Siglece)
p(MGI:Siglece) reg bp(GOBP:"toll-like receptor signaling pathway")
UNSET Species

SET Support = "Moreover, gene transcription of Siglec F was increased in the brain of \
a mouse model of prion disease (Lunnon et al.,2011)."

SET Species = "10090"
p(MGI:Siglecf, loc(MESHA:Brain)) positiveCorrelation path(MESHD:"Prion Diseases")
UNSET Species

SET Support = "So far, no other Siglec than Siglec 11 has been found to be expressed \
on human microglia (Salminen and Kaarniranta,2009)."

a(MESHA:Microglia) -> p(HGNC:SIGLEC11)

SET Support = "On ligand binding, Siglec 11 interacts with SHP‐1 and SHP‐2 (Angata et \
al.,2002) leading to a reduced phagocytic capacity of Siglec 11 \
expressing microglia (Wang and Neumann,2010)."

# SHP1 = PTPN6, SHP2 = PTPN11
# Figure 1 only depicts interaction with SHP1, so I assume these are separate interactions
act(p(HGNC:SIGLEC11)) => complex(p(HGNC:SIGLEC11), p(HGNC:PTPN11))
act(p(HGNC:SIGLEC11)) => complex(p(HGNC:SIGLEC11), p(HGNC:PTPN6))
SET MeSHAnatomy = "Microglia"
complex(p(HGNC:SIGLEC11), p(HGNC:PTPN6)) -| bp(GOBP:phagocytosis)
complex(p(HGNC:SIGLEC11), p(HGNC:PTPN11)) -| bp(GOBP:phagocytosis)
UNSET MeSHAnatomy

SET Support = "SIRPα also known as CD172A is another member of the SIRP family \
expressed on myeloid cells (Barclay and Brown,2006)."

a(MESHA:"Myeloid Cells") -> p(HGNC:SIRPA)

SET Support = "In contrast to the already mentioned SIRPβ, SIRPα has a highly \
conserved cytoplasmic region that contains ITIMs (Barclay and \
Brown,2006)."

p(HGNC:SIRPA) -- bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")

SET Support = "Thus, mice expressing a mutant SIRPα without cytoplasmic region show \
amongst other symptoms an increased clearance of red blood cells \
(Barclay and Brown,2006)."

SET Species = "10090"
p(MGI:Sirpa, var("?")) -> bp(GOBP:"erythrocyte clearance")
UNSET Species

SET Support = "Different ligands for SIRPα have been described so far such as \
surfactant proteins A and D (SP‐A and SP‐D) and CD47."

# SFTPA1 = surfactant protein A, SFTPD = surfactant protein D
p(HGNC:SIRPA) => complex(p(HGNC:SIRPA), p(HGNC:SFTPA1))
p(HGNC:SIRPA) => complex(p(HGNC:SIRPA), p(HGNC:SFTPD))
p(HGNC:SIRPA) => complex(p(HGNC:SIRPA), p(HGNC:CD47))

SET Support = "The interaction with CD47 plays an important role in the “self” \
recognition of myelin. Myelin regulates its own phagocytosis by \
microglia through the expression of CD47."

p(HGNC:CD47, loc(MESHA:Microglia)) reg a(MESHA:"Myelin Sheath")
bp(GOBP:phagocytosis) -| a(MESHA:"Myelin Sheath")
a(MESHA:Microglia) -| a(MESHA:"Myelin Sheath")

SET Support = "This protein is recognized by SIRPα on phagocytes and an \
ITIM‐signaling cascade is initiated (Gitik et al.,2011)."

# "This protein" refers to CD47
complex(p(HGNC:SIRPA, loc(MESHA:Phagocytes)), p(HGNC:CD47, loc(MESHA:Phagocytes))) -> bp(MESHPP:"Immunoreceptor Tyrosine-Based Inhibition Motif")

#SET Evidence = "The family of CD200R consists of four members. CD200R2‐R4 seem to be activatory receptors (Wright et al.,2003), whereas CD200R1 is \
#another inhibitory receptor (Wright et al.,2000) expressed on myeloid cells (Barclay and Brown,2006), including microglia (Lyons et al.,2007)."


UNSET Section
UNSET PublicationType

##################################################################################

SET Citation = {"PubMed", "J Neuroimmunol. 2007 Mar;184(1-2):92-9. Epub 2007 Jan 18.", "17239445"}

SET PublicationType = "Research"
SET Section = "Abstract"

SET Support = "Recently, it was shown that loss-of-function mutations of the innate \
microglial immune receptor triggering receptor expressed on myeloid \
cells-2 (TREM2) led to a chronic neurodegenerative disease, named \
Nasu-Hakola disease or polycystic lipomembranous osteodysplasia with \
sclerosing leukoencephalopathy (PLOSL)."

p(HGNC:TREM2, var("?")) positiveCorrelation path(DO:"Nasu-Hakola disease")

SET Support = "PLOSL is a recessively inherited disease characterized by early onset \
adult dementia associated with bone cysts."

path(DO:"Nasu-Hakola disease") positiveCorrelation path(MESHD:Dementia)
path(DO:"Nasu-Hakola disease") positiveCorrelation path(HP:"Bone cyst")

SET Support = "Since microglial cells are the major TREM2-producing cell type in the \
central nervous system (CNS), they appear to be plausible candidates \
for the neurodegenerative pathogenesis of PLOSL."

a(MESHA:Microglia, loc(MESHA:"Central Nervous System")) -> p(HGNC:TREM2)

SET Support = "Indeed, TREM2 of microglia fulfils important function of tissue debris \
clearance and resolution of latent inflammatory reactions."

p(HGNC:TREM2, loc(MESHA:Microglia)) -- bp(GOBP:"apoptotic cell clearance")
p(HGNC:TREM2, loc(MESHA:Microglia)) -- bp(GOBP:"inflammatory response")

SET Support = "Absence of TREM2 expression on microglia impairs their capacity to \
phagocytose cell membrane debris and increases their gene \
transcription of pro-inflammatory cytokines."

p(HGNC:TREM2, loc(MESHA:Microglia)) -> bp(GOBP:phagocytosis)
p(HGNC:TREM2, loc(MESHA:Microglia)) -| bp(GOBP:"cytokine production involved in inflammatory response")

UNSET Section
UNSET PublicationType
# Results behind paywall
##################################################################################

SET Citation = {"PubMed", "Eur J Immunol. 2005 Jan;35(1):243-51.", "15597323"}

SET PublicationType = "Research"
SET Section = "Results"

SET Support = "Engagement of CD33 on peripheral blood human monocytes by specific mAb \
resulted in the release of high levels of IL‐1β, IL‐8, and TNF‐α (Fig. \
1A)."

# Blocking CD33 activity increases production of inflammatory cytokines
SET MeSHAnatomy = "Monocytes"
p(HGNC:CD33) -| p(HGNC:IL1B)
p(HGNC:CD33) -| p(HGNC:TNF)
p(HGNC:CD33) -| p(HGNC:CXCL8)
UNSET MeSHAnatomy

SET Support = "Furthermore, CD33 engagement greatly enhanced IL‐1β production when \
monocytes were activated by PMA or by contact with T cells (Fig. 1C)."

SET MeSHAnatomy = "Monocytes"
composite(p(HGNC:CD33), a(CHEBI:"phorbol 13-acetate 12-myristate")) -| p(HGNC:IL1B)
UNSET MeSHAnatomy
composite(p(HGNC:CD33), a(MESHA:Monocytes), a(MESHA:T-Lymphocytes)) -| p(HGNC:IL1B)

SET Support = "Cognate siRNA duplex did not affect the expression of other monocyte \
surface markers including CD11b and CD14, thus proving specificity \
(Fig. 2A)."

r(HGNC:CD33) cnc p(HGNC:ITGAM)
r(HGNC:CD33) cnc p(HGNC:CD14)

SET Support = "More importantly, the specific silencing of the CD33 gene was matched \
by enhanced spontaneous production of IL‐1β, IL‐8, and TNF‐α, which \
reached levels 200% to 400% higher than those induced by inverted CD33 \
siRNA used as nonspecific control (Fig. 2B)."

r(HGNC:CD33) -| p(HGNC:IL1B)
r(HGNC:CD33) -| p(HGNC:TNF)
r(HGNC:CD33) -| p(HGNC:CXCL8)

SET Support = "Substantial down‐regulation of CD33 expression was observed when \
monocytes were activated by T cell contact, FcγR cross‐linking, PMA \
and, to a lesser extent, LPS (Fig. 3A), while under similar conditions \
CD11b (used as control) was up‐regulated (Fig. 3B)."

composite(a(MESHA:Monocytes), a(MESHA:T-Lymphocytes)) -| p(HGNC:CD33, loc(MESHA:Monocytes))
act(p(SCOMP:"FcR Gamma Chain Complex")) -| p(HGNC:CD33, loc(MESHA:Monocytes))
a(CHEBI:"phorbol 13-acetate 12-myristate") -| p(HGNC:CD33, loc(MESHA:Monocytes))
composite(a(MESHA:Monocytes), a(MESHA:T-Lymphocytes)) -> p(HGNC:ITGAM, loc(MESHA:Monocytes))
act(p(SCOMP:"FcR Gamma Chain Complex")) -> p(HGNC:ITGAM, loc(MESHA:Monocytes))
a(CHEBI:"phorbol 13-acetate 12-myristate") -> p(HGNC:ITGAM, loc(MESHA:Monocytes))

SET Support = "CD33 is an adhesion molecule that recognizes sialylated motifs 4, 5, \
so we explored whether the interaction of CD33 with its ligand plays a \
role in its repressor activity."

complex(p(HGNC:CD33), a(CHEBI:"sialic acid"))

SET Support = "Indeed, desialilated (neuraminidase‐treated) monocytes showed a marked \
up‐regulation of IL‐1β production (Fig. 4A), which greatly exceeded \
the enhanced production induced by anti‐CD33 mAb (p<0.001) (Fig. 4A)."

a(CHEBI:"sialic acid", loc(MESHA:Monocytes)) -| p(HGNC:IL1B)

SET Support = "Thus, sialic acid residues present on the surface of monocytes act in \
cis as ligands for CD33 and are required for its repressor activity."

complex(p(HGNC:CD33), a(CHEBI:"sialic acid")) -| p(HGNC:IL1B)

SET Support = "Intact RBC, but not desialilated RBC, efficiently reduced the \
production of IL‐1β in a dose‐dependent manner (Fig. 4B)."

a(MESHA:Erythrocytes) -| p(HGNC:IL1B)

SET Support = "The IL‐1β response (protein and steady‐state mRNA levels) to anti‐CD33 \
or to neuraminidase was dramatically enhanced by treatment of \
monocytes with the PI3K‐specific inhibitor LY294002, and it was \
abolished after treatment with the p38 MAPK inhibitor SB203580 \
(p<0.001)."

a(CHEBI:LY294002) =| act(p(SFAM:"PIK3C Class IA Family"), ma(kin))
a(CHEBI:LY294002) -> p(HGNC:IL1B)
composite(p(HGNC:CD33), p(SFAM:"PIK3C Class IA Family")) -| p(HGNC:IL1B)
# MAPK14 == p38/MAPK
a(CHEBI:"SB 203580") =| act(p(HGNC:MAPK14), ma(kin))
a(CHEBI:"SB 203580") -| p(HGNC:IL1B)
act(p(HGNC:MAPK14), ma(kin)) -> p(HGNC:IL1B)

SET Support = "Consistent with these findings, phosphorylation of p38 was observed in \
monocytes activated by anti‐CD33 (Fig. 5E) or neuraminidase (not \
shown), and p38 phosphorylation was abrogated by SB203580."

p(HGNC:CD33) -| p(HGNC:MAPK14, pmod(Ph))
a(CHEBI:"SB 203580") =| p(HGNC:MAPK14, pmod(Ph))
p(HGNC:MAPK14, pmod(Ph)) => act(p(HGNC:MAPK14), ma(kin))

SET Support = "Interestingly, we could not observe an increase in p38 phosphorylation \
when monocytes were preincubated with LY294002, thus indicating that \
IL‐1β enhancement induced by PI3K was not operating directly on p38 \
MAPK."

a(CHEBI:LY294002) cnc p(HGNC:MAPK14, pmod(Ph))


UNSET Section

UNSET PublicationType

##################################################################################

# TODO Determine if "K**-linked ubiquitination" should be substituted for just "Ub"

# Statements Section

SET Citation = {"PubMed", "J Mol Signal. 2013 Jun 13;8(1):7. doi: 10.1186/1750-2187-8-7.", "23758787"}

SET PublicationType = "Review"

SET Section = "TRAFs in signaling by the TNF-R superfamily"

SET Support = "Receptors of the TNF-R superfamily have wide tissue distribution and \
regulate diverse biological functions, including immune responses, \
inflammation, lymphoid organ and brain development, \
osteoclastogenesis, and tissue homeostasis [3,24-26]."

p(SFAM:"TNFRSF Family") reg bp(GOBP:"immune response")
p(SFAM:"TNFRSF Family") reg path(MESHD:Inflammation)
p(SFAM:"TNFRSF Family") reg bp(GOBP:"brain development")
p(SFAM:"TNFRSF Family") reg bp(GOBP:"hematopoietic or lymphoid organ development")
p(SFAM:"TNFRSF Family") reg bp(GOBP:"osteoclast development")
p(SFAM:"TNFRSF Family") reg bp(GOBP:"tissue homeostasis")

SET Support = "Binding of TRAFs to TNF-Rs typically induces signaling cascades \
leading to the activation of NF-κB and MAPKs, including ERK, p38 and \
JNK, and ultimately regulates cell survival or functionality depending \
on the cell type and the context [3,24,26]."

# TNF receptor associated factors == TRAF proteins
complex(p(GFAM:"TNF receptor associated factors"), p(SFAM:"TNFRSF Family")) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(GFAM:"TNF receptor associated factors"), p(SFAM:"TNFRSF Family")) -> act(p(SFAM:"MAPK Erk1/2 Family"), ma(kin))
complex(p(GFAM:"TNF receptor associated factors"), p(SFAM:"TNFRSF Family")) -> act(p(SFAM:"MAPK p38 Family"), ma(kin))
complex(p(GFAM:"TNF receptor associated factors"), p(SFAM:"TNFRSF Family")) -> act(p(SFAM:"MAPK JNK Family"), ma(kin))

SET Support = "Notably, TRAF2 and TRAF5 can also modulate signaling by death \
receptors through association with TRADD, FADD or RIP1 (Table 1)."

composite(p(HGNC:TRAF2), p(HGNC:TRADD)) reg bp(GOBP:signaling)

SET Support = "Most TRAF-dependent receptors of this family trigger the canonical NF- \
κB pathway (RelA/p50, NF-κB1)."

p(SFAM:"TNFRSF Family") -> act(complex(p(HGNC:RELA), p(HGNC:NFKB1)))

SET Support = "In contrast, the alternative NF-κB pathway (RelB/p52, NF-κB2) is \
activated by a subset of TNF-Rs, including CD40, BAFF-R, the \
lymphotoxin-β receptor (LTβR), 4-1BB, and Fn14 [28-32]."

# TNFRSF13C == BAFF-R, TNFRSF12A == Fn14
p(SFAM:"TNFRSF Family") hasMembers list(p(HGNC:CD40), p(HGNC:TNFRSF13C), p(HGNC:LTBR), p(HGNC:TNFRSF12A))
p(HGNC:CD40) -> act(complex(p(HGNC:RELB), p(HGNC:NFKB2)))
p(HGNC:TNFRSF13C) -> act(complex(p(HGNC:RELB), p(HGNC:NFKB2)))
p(HGNC:LTBR) -> act(complex(p(HGNC:RELB), p(HGNC:NFKB2)))
p(HGNC:TNFRSF12A) -> act(complex(p(HGNC:RELB), p(HGNC:NFKB2)))

SET Support = "Interestingly, however, unlike CD40 or BAFF-R, TWEAK-induced Fn14 \
signaling promotes NF-κB2 activation through a distinct mechanism that \
induces lysosomal degradation of cIAP1-TRAF2 in a cIAP1-dependent \
manner [33]."

# TNFSF12 == TWEAK
p(HGNC:TNFSF12) -> act(p(HGNC:TNFRSF12A), ma(GOBP:signaling))
# BIRC2 == cIAP1
deg(complex(p(HGNC:TRAF2), p(HGNC:BIRC2))) -> act(p(HGNC:NFKB2))
act(p(HGNC:TNFRSF12A), ma(GOBP:signaling)) -> deg(complex(p(HGNC:TRAF2), p(HGNC:BIRC2)))

SET Support = "In the absence of stimulation, TRAF3 constitutively binds to NIK (the \
upstream kinase of the NF-κB2 pathway) and TRAF2 (which associates \
with cIAP1/2). In this complex, cIAP1/2 induces K48-linked \
polyubiquitination of NIK, and thus targets NIK for proteasomal \
degradation and inhibits NF-κB2 activation [37,68-71]."

# NIK == MAP3K14
complex(p(HGNC:TRAF3), p(HGNC:MAP3K14), p(HGNC:TRAF2), complex(p(HGNC:BIRC2), p(HGNC:BIRC3))) => p(HGNC:MAP3K14, pmod(GOBP:"protein K48-linked ubiquitination"))
p(HGNC:MAP3K14, pmod(GOBP:"protein K48-linked ubiquitination")) -> deg(p(HGNC:MAP3K14))
deg(p(HGNC:MAP3K14)) -| act(p(HGNC:NFKB2))

SET Support = "Following BAFF or CD154 stimulation, trimerized BAFF-R or CD40 \
recruits TRAF3, TRAF2, cIAP1/2 and MALT1 to membrane signaling rafts, \
releasing NIK from the TRAF3/TRAF2/cIAP1/2 complex [37,72-74]."

act(p(HGNC:TNFSF13B)) => complex(p(HGNC:TNFSF13B), p(HGNC:TNFSF13B), p(HGNC:TNFSF13B))
# CD40LG == CD154
complex(p(HGNC:CD40LG), p(HGNC:CD40)) => act(p(HGNC:CD40))
act(p(HGNC:CD40)) -> p(HGNC:TRAF3, loc(MESHA:"Cell Membrane"))
act(p(HGNC:CD40)) -> p(HGNC:TRAF2, loc(MESHA:"Cell Membrane"))
act(p(HGNC:CD40)) -> complex(p(HGNC:BIRC2, loc(MESHA:"Cell Membrane")), p(HGNC:BIRC3, loc(MESHA:"Cell Membrane")))
act(p(HGNC:CD40)) -> p(HGNC:MALT1, loc(MESHA:"Cell Membrane"))
complex(p(HGNC:TNFSF13B), p(HGNC:TNFSF13B), p(HGNC:TNFSF13B)) -> p(HGNC:TRAF3, loc(MESHA:"Cell Membrane"))
complex(p(HGNC:TNFSF13B), p(HGNC:TNFSF13B), p(HGNC:TNFSF13B)) -> p(HGNC:TRAF2, loc(MESHA:"Cell Membrane"))
complex(p(HGNC:TNFSF13B), p(HGNC:TNFSF13B), p(HGNC:TNFSF13B)) -> complex(p(HGNC:BIRC2, loc(MESHA:"Cell Membrane")), p(HGNC:BIRC3, loc(MESHA:"Cell Membrane")))
complex(p(HGNC:TNFSF13B), p(HGNC:TNFSF13B), p(HGNC:TNFSF13B)) -> p(HGNC:MALT1, loc(MESHA:"Cell Membrane"))
p(HGNC:TRAF3, loc(MESHA:"Cell Membrane")) -| complex(p(HGNC:TRAF3), p(HGNC:MAP3K14), p(HGNC:TRAF2), complex(p(HGNC:BIRC2), p(HGNC:BIRC3)))
p(HGNC:TRAF2, loc(MESHA:"Cell Membrane")) -| complex(p(HGNC:TRAF3), p(HGNC:MAP3K14), p(HGNC:TRAF2), complex(p(HGNC:BIRC2), p(HGNC:BIRC3)))
p(HGNC:MALT1, loc(MESHA:"Cell Membrane")) -| complex(p(HGNC:TRAF3), p(HGNC:MAP3K14), p(HGNC:TRAF2), complex(p(HGNC:BIRC2), p(HGNC:BIRC3)))
complex(p(HGNC:BIRC2, loc(MESHA:"Cell Membrane")), p(HGNC:BIRC3, loc(MESHA:"Cell Membrane"))) -| complex(p(HGNC:TRAF3), p(HGNC:MAP3K14), p(HGNC:TRAF2), complex(p(HGNC:BIRC2), p(HGNC:BIRC3)))
p(HGNC:TRAF3, loc(MESHA:"Cell Membrane")) -> p(HGNC:MAP3K14, loc(MESHA:Cytoplasm))
p(HGNC:TRAF2, loc(MESHA:"Cell Membrane")) -> p(HGNC:MAP3K14, loc(MESHA:Cytoplasm))
p(HGNC:MALT1, loc(MESHA:"Cell Membrane")) -> p(HGNC:MAP3K14, loc(MESHA:Cytoplasm))
complex(p(HGNC:BIRC2, loc(MESHA:"Cell Membrane")), p(HGNC:BIRC3, loc(MESHA:"Cell Membrane"))) -> p(HGNC:MAP3K14, loc(MESHA:Cytoplasm))

SET Support = "NIK protein is accumulated in the cytoplasm, induces the activation of \
IKKα and NF-κB2, and eventually up-regulates the expression of anti- \
apoptotic proteins of the Bcl-2 family (such as Bcl-2, Bcl-xL, and \
Mcl-1) to induce cell survival [28]."

# CHUK == IKKA
p(HGNC:MAP3K14, loc(MESHA:Cytoplasm)) -> act(p(HGNC:CHUK), ma(kin))
p(HGNC:MAP3K14, loc(MESHA:Cytoplasm)) -> act(p(HGNC:NFKB2))
act(p(HGNC:NFKB2)) -> p(HGNC:BCL2)
# BCL2L1 == BCL-xL
act(p(HGNC:NFKB2)) -> p(HGNC:BCL2L1)
act(p(HGNC:NFKB2)) -> p(HGNC:MCL1)
p(HGNC:BCL2) -| bp(MESHPP:Apoptosis)
p(HGNC:BCL2L1) -| bp(MESHPP:Apoptosis)
p(HGNC:MCL1) -| bp(MESHPP:Apoptosis)

SET Support = "In the receptor signaling complex, TRAF2 induces K63-linked \
polyubiquitination of cIAP1/2, which is subsequently activated to \
catalyze K48-linked polyubiquitination and degradation of TRAF3 and \
TRAF2 [37,72,74,75]."

p(HGNC:TRAF2) -> act(complex(p(HGNC:BIRC2), p(HGNC:BIRC3)), ma(GOBP:"protein K63-linked ubiquitination"))
act(complex(p(HGNC:BIRC2), p(HGNC:BIRC3)), ma(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:TRAF3, pmod(GOBP:"protein K48-linked ubiquitination"))
act(complex(p(HGNC:BIRC2), p(HGNC:BIRC3)), ma(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:TRAF2, pmod(GOBP:"protein K48-linked ubiquitination"))
p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination")) -> deg(p(HGNC:TRAF3))
p(HGNC:TRAF2, pmod(GOBP:"protein K63-linked ubiquitination")) -> deg(p(HGNC:TRAF2))

SET Support = "Following CD40 activation, many other signaling proteins (including \
TRAF5, TRAF6, TRAF1, Ubc13, MEKK1, TAK1 and NEMO) are also recruited \
to the cytoplasmic domain of the receptor, and the K63-specific \
ubiquitin ligase activity of TRAF2 and TRAF6 is rapidly stimulated \
[27,72,75]."

act(p(HGNC:CD40)) -> p(HGNC:TRAF5, loc(MESHA:Cytoplasm))
act(p(HGNC:CD40)) -> p(HGNC:TRAF6, loc(MESHA:Cytoplasm))
act(p(HGNC:CD40)) -> p(HGNC:TRAF1, loc(MESHA:Cytoplasm))
# UBE2N == Ubc13
act(p(HGNC:CD40)) -> p(HGNC:UBE2N, loc(MESHA:Cytoplasm))
# MAP3K1 == MEKK1
act(p(HGNC:CD40)) -> p(HGNC:MAP3K1, loc(MESHA:Cytoplasm))
# MAP3K7 == TAK1
act(p(HGNC:CD40)) -> p(HGNC:MAP3K7, loc(MESHA:Cytoplasm))
# IKBKG == NEMO
act(p(HGNC:CD40)) -> p(HGNC:IKBKG, loc(MESHA:Cytoplasm))
act(p(HGNC:CD40)) -> act(p(HGNC:TRAF2), ma(GOBP:"protein K63-linked ubiquitination"))
act(p(HGNC:CD40)) -> act(p(HGNC:TRAF6), ma(GOBP:"protein K63-linked ubiquitination"))

SET Support = "Activated MEKK1 and TAK1 and their associated protein complexes are \
subsequently released from the receptor into the cytoplasm to activate \
MAPKs and NF-κB1, which eventually mediate the effector functions of \
CD40 [35,72]."

act(p(HGNC:MAP3K1)) -> p(HGNC:MAP3K1, loc(MESHA:Cytoplasm))
act(p(HGNC:MAP3K7)) -> p(HGNC:MAP3K7, loc(MESHA:Cytoplasm))
act(p(HGNC:MAP3K1)) -> p(HGNC:NFKB1)
act(p(HGNC:MAP3K7)) -> p(HGNC:NFKB1)
p(HGNC:NFKB1) -> act(p(HGNC:CD40))

SET Support = "Interestingly, the releasing step of MEKK1 and TAK1 is inhibited by \
TRAF3 via a yet unknown mechanism, but promoted by cIAP1/2-catalyzed \
K48-linked polyubiquitination and proteasomal degradation of TRAF3 \
[72,74,75]."

p(HGNC:TRAF3) -| p(HGNC:MAP3K1, loc(MESHA:Cytoplasm))
p(HGNC:TRAF3) -| p(HGNC:MAP3K7, loc(MESHA:Cytoplasm))

SET Support = "In response to BAFF stimulation, a signaling pathway of c-Raf-MEK-ERK- \
dependent phosphorylation and down-regulation of the pro-apoptotic \
protein Bim also contributes to B cell survival [76]."

complex(p(HGNC:TNFSF13B), p(HGNC:TNFRSF13C)) => act(p(HGNC:TNFRSF13C))
# BCL2L11 == Bim
act(p(HGNC:TNFRSF13C)) -| p(HGNC:BCL2L11)
p(HGNC:BCL2L11) -> bp(MESHPP:Apoptosis)

SET Support = "In light of the evidence that TRAF1 mediates 4-1BB-induced ERK- \
dependent phosphorylation and down-regulation of Bim to promote T cell \
survival [77-79], it would be interesting to investigate the role of \
TRAF1 in BAFF-induced Bim down-regulation in B cells."

composite(p(HGNC:TRAF1), p(HGNC:TNFRSF9)) -> act(p(HGNC:MAPK1), ma(kin))
act(p(HGNC:MAPK1), ma(kin)) => p(HGNC:BCL2L11, pmod(Ph))
p(HGNC:BCL2L11, pmod(Ph)) -| p(HGNC:BCL2L11)
p(HGNC:BCL2L11) -| a(MESHA:"T-Lymphocytes")

UNSET Section

SET Section = "TRAFs in TLR signaling"

SET Support = "Upon sensing these molecules, TLR signaling induces the production of \
pro-inflammatory cytokines (such as TNFα, IL-1, IL-6, and IL-12), type \
I interferons (IFNα and IFNβ), chemokines, antimicrobial enzymes, and \
other inflammatory mediators."

act(p(GFAM:"Toll like receptors")) -> p(HGNC:TNF)
act(p(GFAM:"Toll like receptors")) -> p(HGNC:IL1A)
act(p(GFAM:"Toll like receptors")) -> p(HGNC:IL6)
act(p(GFAM:"Toll like receptors")) -> p(HGNC:IL12A)
act(p(GFAM:"Toll like receptors")) -> p(SFAM:"IFNA Family")
act(p(GFAM:"Toll like receptors")) -> p(HGNC:IFNB1)
act(p(GFAM:"Toll like receptors")) -> bp(GOBP:"chemokine production")

SET Support = "TLRs (TLR1, 2, 4–6, 10) that sense lipids or proteins are located on \
the cell membrane, while those (TLR3, 7, 8, 9) that recognize nucleic \
acids are resided in intracellular endosomes [8,87]."

p(HGNC:TLR1, loc(MESHA:"Cell Membrane"))
p(HGNC:TLR2, loc(MESHA:"Cell Membrane"))
p(HGNC:TLR4, loc(MESHA:"Cell Membrane"))
p(HGNC:TLR5, loc(MESHA:"Cell Membrane"))
p(HGNC:TLR6, loc(MESHA:"Cell Membrane"))
p(HGNC:TLR10, loc(MESHA:"Cell Membrane"))
p(HGNC:TLR3, loc(MESHA:Endosomes))
p(HGNC:TLR7, loc(MESHA:Endosomes))
p(HGNC:TLR8, loc(MESHA:Endosomes))
p(HGNC:TLR9, loc(MESHA:Endosomes))
complex(p(HGNC:TLR3), a(CHEBI:"nucleic acid"))
complex(p(HGNC:TLR7), a(CHEBI:"nucleic acid"))
complex(p(HGNC:TLR8), a(CHEBI:"nucleic acid"))
complex(p(HGNC:TLR9), a(CHEBI:"nucleic acid"))

SET Support = "Ligand-induced TLR dimerization or oligomerization recruits TIR \
domain-containing adaptor proteins through TIR-TIR interactions, \
including MyD88, TRIF, Mal and TRAM [83,88,89]. MyD88 is employed by \
all TLRs except TLR3."

act(p(HGNC:TLR1)) => complex(p(HGNC:TLR1), p(HGNC:MYD88))
act(p(HGNC:TLR2)) => complex(p(HGNC:TLR2), p(HGNC:MYD88))
act(p(HGNC:TLR4)) => complex(p(HGNC:TLR4), p(HGNC:MYD88))
act(p(HGNC:TLR5)) => complex(p(HGNC:TLR5), p(HGNC:MYD88))
act(p(HGNC:TLR6)) => complex(p(HGNC:TLR6), p(HGNC:MYD88))
act(p(HGNC:TLR7)) => complex(p(HGNC:TLR7), p(HGNC:MYD88))
act(p(HGNC:TLR8)) => complex(p(HGNC:TLR8), p(HGNC:MYD88))
act(p(HGNC:TLR9)) => complex(p(HGNC:TLR9), p(HGNC:MYD88))
act(p(HGNC:TLR10)) => complex(p(HGNC:TLR10), p(HGNC:MYD88))

SET Support = "TRIF is only used by TLR3 and endocytosed TLR4"

# TICAM1 == TRIF
complex(p(HGNC:TLR3), p(HGNC:TICAM1))
complex(p(HGNC:TLR4, loc(MESHA:"Intracellular Space")), p(HGNC:TICAM1))

SET Support = "Mal (also known as TIRAP) facilitates the recruitment of MyD88 to \
TLR4, while TRAM acts as a bridging adaptor between TRIF and \
endocytosed TLR4."

p(HGNC:TIRAP) => complex(p(HGNC:TIRAP), p(HGNC:TLR4), p(HGNC:MYD88))
p(HGNC:TICAM2) => complex(p(HGNC:TLR4, loc(MESHA:"Intracellular Space")), p(HGNC:TICAM1), p(HGNC:TICAM2))

SET Support = "Collectively, two general pathways are used by TLRs: MyD88-dependent \
(all TLRs except TLR3) and TRIF-dependent (TLR3 and TLR4) pathways. \
Both pathways initiate complex signaling cascades of phosphorylation \
and ubiquitination events, which culminate in the activation of \
transcription factors, including NF-κB, IRFs, and AP-1 family members, \
leading to innate immune responses [83,88,89]."

complex(p(HGNC:TLR1), p(HGNC:MYD88)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TLR2), p(HGNC:MYD88)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TLR4), p(HGNC:MYD88)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TLR5), p(HGNC:MYD88)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TLR6), p(HGNC:MYD88)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TLR7), p(HGNC:MYD88)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TLR8), p(HGNC:MYD88)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TLR9), p(HGNC:MYD88)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TLR10), p(HGNC:MYD88)) -> act(complex(SCOMP:"Nfkb Complex"))

complex(p(HGNC:TLR1), p(HGNC:MYD88)) -> act(p(PFAM:IRF))
complex(p(HGNC:TLR2), p(HGNC:MYD88)) -> act(p(PFAM:IRF))
complex(p(HGNC:TLR4), p(HGNC:MYD88)) -> act(p(PFAM:IRF))
complex(p(HGNC:TLR5), p(HGNC:MYD88)) -> act(p(PFAM:IRF))
complex(p(HGNC:TLR6), p(HGNC:MYD88)) -> act(p(PFAM:IRF))
complex(p(HGNC:TLR7), p(HGNC:MYD88)) -> act(p(PFAM:IRF))
complex(p(HGNC:TLR8), p(HGNC:MYD88)) -> act(p(PFAM:IRF))
complex(p(HGNC:TLR9), p(HGNC:MYD88)) -> act(p(PFAM:IRF))
complex(p(HGNC:TLR10), p(HGNC:MYD88)) -> act(p(PFAM:IRF))

complex(p(HGNC:TLR1), p(HGNC:MYD88)) -> act(complex(SCOMP:"AP-1 Complex"))
complex(p(HGNC:TLR2), p(HGNC:MYD88)) -> act(complex(SCOMP:"AP-1 Complex"))
complex(p(HGNC:TLR4), p(HGNC:MYD88)) -> act(complex(SCOMP:"AP-1 Complex"))
complex(p(HGNC:TLR5), p(HGNC:MYD88)) -> act(complex(SCOMP:"AP-1 Complex"))
complex(p(HGNC:TLR6), p(HGNC:MYD88)) -> act(complex(SCOMP:"AP-1 Complex"))
complex(p(HGNC:TLR7), p(HGNC:MYD88)) -> act(complex(SCOMP:"AP-1 Complex"))
complex(p(HGNC:TLR8), p(HGNC:MYD88)) -> act(complex(SCOMP:"AP-1 Complex"))
complex(p(HGNC:TLR9), p(HGNC:MYD88)) -> act(complex(SCOMP:"AP-1 Complex"))
complex(p(HGNC:TLR10), p(HGNC:MYD88)) -> act(complex(SCOMP:"AP-1 Complex"))

complex(p(HGNC:TLR3), p(HGNC:TICAM1)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TLR3), p(HGNC:TICAM1)) -> act(p(PFAM:IRF))
complex(p(HGNC:TLR3), p(HGNC:TICAM1)) -> act(complex(SCOMP:"AP-1 Complex"))
complex(p(HGNC:TLR4), p(HGNC:TICAM1)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TLR4), p(HGNC:TICAM1)) -> act(p(PFAM:IRF))
complex(p(HGNC:TLR4), p(HGNC:TICAM1)) -> act(complex(SCOMP:"AP-1 Complex"))

act(complex(SCOMP:"Nfkb Complex")) -> bp(GOBP:"innate immune response")
act(p(PFAM:IRF)) -> bp(GOBP:"innate immune response")
act(complex(SCOMP:"AP-1 Complex")) -> bp(GOBP:"innate immune response")

SET Support = "TRAF6 mediates both MyD88-dependent and TRIF-dependent activation of \
NF-κB and AP-1 (Figure 3)."

p(HGNC:MYD88) -- p(HGNC:TRAF6)
p(HGNC:TICAM1) -- p(HGNC:TRAF6)
p(HGNC:TRAF6) reg act(complex(SCOMP:"Nfkb Complex"))
p(HGNC:TRAF6) reg act(complex(SCOMP:"AP-1 Complex"))

SET Support = "In MyD88-dependent TLR signaling, TRAF6 is recruited to \
MyD88-activated IRAK1/2, and oligomerization of TRAF6 stimulates its \
E3 ubiquitin ligase activity."

p(HGNC:MYD88) -> act(complex(p(HGNC:IRAK1), p(HGNC:IRAK2)))
act(complex(p(HGNC:IRAK1), p(HGNC:IRAK2))) => complex(p(HGNC:TRAF6), complex(p(HGNC:IRAK1), p(HGNC:IRAK2)))
complex(p(HGNC:TRAF6), complex(p(HGNC:IRAK1), p(HGNC:IRAK2))) => act(complex(p(HGNC:IRAK1), p(HGNC:IRAK2)), ma(GOBP:"protein ubiquitination"))

SET Support = "In coordination with the E2 complex Uev1A:Ubc13, TRAF6 catalyzes the \
attachment of K63-linked polyubiquitin chains onto its substrates, \
including itself and NEMO [8,89,90], and synthesis of free, unanchored \
K63-polyubiquitin chains [91]."

# How to represent E2 complex?
p(HGNC:TRAF6) => p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF6) => p(HGNC:IKBKG, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination")) => a(MESHC:Polyubiquitin)

SET Support = "Ubiquitinated TRAF6 serves as a signaling scaffold to recruit TAK1 via \
TAB2/3."

p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination")) => complex(p(HGNC:TRAF6), complex(p(HGNC:TAB2), p(HGNC:TAB3)))
complex(p(HGNC:TRAF6), complex(p(HGNC:TAB2), p(HGNC:TAB3))) => act(complex(p(HGNC:TAB2), p(HGNC:TAB3)))
act(complex(p(HGNC:TAB2), p(HGNC:TAB3))) => complex(p(HGNC:MAP3K7), complex(p(HGNC:TAB2), p(HGNC:TAB3)))

SET Support = "TRAF6-generated free K63-polyubiquitin chains also bind to TAB2/3 to \
activate TAK1, and bind to NEMO to activate IKKα/β in the receptor \
complex."

a(MESHC:Polyubiquitin) => complex(a(MESHC:Polyubiquitin), complex(p(HGNC:TAB2), p(HGNC:TAB3)))
complex(a(MESHC:Polyubiquitin), complex(p(HGNC:TAB2), p(HGNC:TAB3))) => act(p(HGNC:MAP3K7))
complex(a(MESHC:Polyubiquitin), p(HGNC:IKBKG)) => act(complex(p(HGNC:CHUK), p(HGNC:IKBKB)))
act(complex(p(HGNC:CHUK), p(HGNC:IKBKB))) => act(complex(SCOMP:"IkappaB Kinase Complex"))

SET Support = "This ultimately results in MyD88-dependent activation of NF-κB \
[8,82,90,91]."

act(complex(SCOMP:"IkappaB Kinase Complex")) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "The TAK1 signaling complex, including TRAF6-TAB2/3-TAB1-TAK1, is \
subsequently dissociated from the receptor and released into the \
cytosol, where TAK1 activates MAPK cascades, leading to activation of \
AP-1."

complex(a(MESHC:Polyubiquitin), complex(p(HGNC:TAB2), p(HGNC:TAB3)), p(HGNC:TRAF6), p(HGNC:MAP3K7)) => p(HGNC:MAP3K7, loc(MESHA:Cytoplasm))
p(HGNC:MAP3K7, loc(MESHA:Cytoplasm)) => bp(GOBP:"MAPK cascade")
bp(GOBP:"MAPK cascade") -> act(complex(SCOMP:"AP-1 Complex"))

SET Support = "Similar to CD40 signaling, the release of the TAK1 signaling complex \
from TLR4 is inhibited by TRAF3, which is recruited to TLR4 by MyD88 \
and IRAK1."

act(p(HGNC:TLR4)) -> complex(complex(p(HGNC:TAB2), p(HGNC:TAB3)), p(HGNC:TRAF6), p(HGNC:MAP3K7))
p(HGNC:TRAF3) -| complex(complex(p(HGNC:TAB2), p(HGNC:TAB3)), p(HGNC:TRAF6), p(HGNC:MAP3K7))
composite(p(HGNC:MYD88), complex(p(HGNC:IRAK1), p(HGNC:IRAK2))) -> p(HGNC:TRAF3, loc(HGNC:TLR4))

SET Support = "However, TRAF6 catalyzes K63-linked polyubiquitination of cIAP1/2, \
which is also recruited by MyD88 and IRAK1."

composite(p(HGNC:MYD88), complex(p(HGNC:IRAK1), p(HGNC:IRAK2))) -> p(HGNC:TRAF6)
act(p(HGNC:TRAF6), ma(cat)) => complex(p(HGNC:BIRC2), p(HGNC:BIRC3, pmod(GOBP:"protein K63-linked ubiquitination")))

SET Support = "Activated cIAP1/2 promotes K48-linked polyubiquitination and \
degradation of TRAF3, allowing activation of MAPKs [92]."

act(complex(p(HGNC:BIRC2), p(HGNC:BIRC3))) -> p(HGNC:TRAF3, pmod(GOBP:"protein K48-linked ubiquitination"))
p(HGNC:TRAF3, pmod(GOBP:"protein K48-linked ubiquitination")) => deg(p(HGNC:TRAF3))
deg(p(HGNC:TRAF3)) -> bp(GOBP:"MAPK cascade")

SET Support = "In TRIF-dependent TLR signaling, TRIF directly recruits TRAF6 and \
RIP1, which work cooperatively to activate TAK1, eventually leading to \
activation of NF-κB and AP-1 [8,82,90]."

p(HGNC:TICAM1) -> p(HGNC:TRAF6)
p(HGNC:TICAM1) -> p(HGNC:RALBP1)
composite(p(HGNC:RALBP1), p(HGNC:TRAF6)) -> act(p(HGNC:MAP3K7), ma(kin))
act(p(HGNC:MAP3K7), ma(kin)) -> act(complex(SCOMP:"AP-1 Complex"))
act(p(HGNC:MAP3K7), ma(kin)) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "Interestingly, in response to engagement of TLR1, 2 or 4, TRAF6 is \
also translocated to mitochondria, where it ubiquitinates \
evolutionarily conserved signaling intermediate in Toll pathways \
(ECSIT), resulting in increased reactive oxygen species (ROS) \
generation and bacteria killing [93]."

act(p(HGNC:TLR1)) -> tloc(p(HGNC:TRAF6), fromLoc(MESHA:Cytoplasm), toLoc(MESHA:Mitochondria))
act(p(HGNC:TLR2)) -> tloc(p(HGNC:TRAF6), fromLoc(MESHA:Cytoplasm), toLoc(MESHA:Mitochondria))
act(p(HGNC:TLR4)) -> tloc(p(HGNC:TRAF6), fromLoc(MESHA:Cytoplasm), toLoc(MESHA:Mitochondria))
p(HGNC:TRAF6, loc(MESHA:Mitochondria)) -> a(CHEBI:"reactive oxygen species")

SET Support = "Notably, TRAF6 is also necessary for IRF7 activation and type I IFN \
production induced by TLR7 and TLR9 in plasmacytoid dendritic cells \
(pDCs) [94]."

SET MeSHAnatomy = "Dendritic Cells"
p(HGNC:TRAF6) -> act(p(HGNC:IRF7))
act(p(HGNC:IRF7)) -> bp(GOBP:"type I interferon production")
act(p(HGNC:TLR7)) -> bp(GOBP:"type I interferon production")
act(p(HGNC:TLR9)) -> bp(GOBP:"type I interferon production")
UNSET MeSHAnatomy

SET Support = "TRAF3 is required for both MyD88-dependent and TRIF-dependent \
activation of IRF3 and IRF7, and thus production of type I IFNs \
[95,96], a class of cytokines with potent antiviral and antibacterial \
activities."

p(HGNC:TRAF3) -> act(p(HGNC:IRF7))
p(HGNC:TRAF3) -> act(p(HGNC:IRF3))
act(p(HGNC:IRF7)) -> bp(GOBP:"type I interferon production")
act(p(HGNC:IRF3)) -> bp(GOBP:"type I interferon production")
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA1)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA2)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA4)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA5)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA6)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA7)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA8)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA10)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA13)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA14)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA16)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA17)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNA21)
bp(GOBP:"type I interferon production") -> p(HGNC:IFNB1)

SET Support = "In MyD88-dependent signaling downstream of TLR7 and TLR9, TRAF3 is \
recruited to MyD88 and IRAK1."

act(p(HGNC:TLR7)) -> complex(p(HGNC:TRAF3), p(HGNC:MYD88), p(HGNC:IRAK1))
act(p(HGNC:TLR9)) -> complex(p(HGNC:TRAF3), p(HGNC:MYD88), p(HGNC:IRAK1))

SET Support = "Activated TRAF3 catalyzes its K63-linked auto-ubiquitination, and \
assembles a signaling complex with MyD88, IRAK4, IRAK1, IKKα and IRF7."

act(p(HGNC:TRAF3), ma(cat)) => p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination")) => complex(p(HGNC:TRAF3), p(HGNC:MYD88), p(HGNC:IRAK4), p(HGNC:IRAK1), p(HGNC:CHUK), p(HGNC:IRF7))

SET Support = "Within this complex, IRF7 is phosphorylated and activated by IRAK1 and \
IKKα to induce the production of type I IFNs [8,82,86]."

complex(p(HGNC:TRAF3), p(HGNC:MYD88), p(HGNC:IRAK4), p(HGNC:IRAK1), p(HGNC:CHUK), p(HGNC:IRF7)) -> act(p(HGNC:IRAK1), ma(kin))
act(p(HGNC:IRAK1), ma(kin)) => p(HGNC:IRF7, pmod(Ph))
act(p(HGNC:CHUK), ma(kin)) => p(HGNC:IRF7, pmod(Ph))
p(HGNC:IRF7, pmod(Ph)) => act(p(HGNC:IRF7))
act(p(HGNC:IRF7)) -> bp(GOBP:"type I interferon production")

SET Support = "In TRIF-dependent signaling downstream of TLR3 and TLR4, TRAF3 \
interacts with oligomerized TRIF, and activated TRAF3 recruits TBK1 \
and IKKϵ through NAP1 and TANK."

act(p(HGNC:TLR3)) -> composite(p(HGNC:TRAF3), p(HGNC:TICAM1))
act(p(HGNC:TLR4)) -> composite(p(HGNC:TRAF3), p(HGNC:TICAM1))
composite(p(HGNC:TRAF3), p(HGNC:TICAM1)) => act(p(HGNC:TRAF3))
# AZI2 == NAP1 (NAK-associated protein 1)
act(p(HGNC:TRAF3)) -> complex(p(HGNC:TRAF3), p(HGNC:TBK1), p(HGNC:IKBKE), p(HGNC:AZI2), p(HGNC:TANK))

SET Support = "In this signaling complex, TRAF3, in cooperation with Ubc13 and/or \
Ubc5, catalyzes K63-polyubiquitination of TRAF3 itself, TBK1 and IKKϵ, \
which facilitates the phosphorylation of IRF3 and IRF7."

# Ubc13 == UBE2N, Ubc5 == UBE2D1
complex(p(HGNC:TRAF3), p(HGNC:TBK1), p(HGNC:IKBKE), p(HGNC:AZI2), p(HGNC:TANK)) -> composite(p(HGNC:TRAF3), p(HGNC:UBE2N), p(HGNC:UBE2D1))
composite(p(HGNC:TRAF3), p(HGNC:UBE2N), p(HGNC:UBE2D1)) => act(p(HGNC:TRAF3), ma(cat))
act(p(HGNC:TRAF3), ma(cat)) => p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination"))
act(p(HGNC:TRAF3), ma(cat)) => p(HGNC:TBK1, pmod(GOBP:"protein K63-linked ubiquitination"))
act(p(HGNC:TRAF3), ma(cat)) => p(HGNC:IKBKE, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:IRF3, pmod(Ph))
p(HGNC:TBK1, pmod(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:IRF3, pmod(Ph))
p(HGNC:IKBKE, pmod(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:IRF3, pmod(Ph))
p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:IRF7, pmod(Ph))
p(HGNC:TBK1, pmod(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:IRF7, pmod(Ph))
p(HGNC:IKBKE, pmod(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:IRF7, pmod(Ph))

SET Support = "The phosphorylated IRF3 and IRF7, in turn, form homodimers or \
heterodimers, translocate into the nucleus and induce the expression \
of type I IFNs as well as IFN-inducible gene [8,82,86] (Figure 3)."

p(HGNC:IRF3, pmod(Ph)) -> tloc(p(HGNC:IRF3), fromLoc(MESHA:Cytoplasm), toLoc(MESHA:"Cell Nucleus"))
p(HGNC:IRF7, pmod(Ph)) -> tloc(p(HGNC:IRF7), fromLoc(MESHA:Cytoplasm), toLoc(MESHA:"Cell Nucleus"))
p(HGNC:IRF3, loc(MESHA:"Cell Nucleus")) => bp(GOBP:"type I interferon production")
p(HGNC:IRF7, loc(MESHA:"Cell Nucleus")) => bp(GOBP:"type I interferon production")

SET Support = "Overexpression of TRAF1 inhibits TRIF- and TLR3-mediated activation of \
NF-κB and expression of IFN-β, suggesting that TRAF1 inhibits TRIF- \
dependent signaling [83,97,98]."

p(HGNC:TRAF1) -| act(complex(SCOMP:"Nfkb Complex"))
p(HGNC:TRAF1) -| r(HGNC:IFNB1)

SET Support = "Similarly, TRAF4 physically interacts with and functionally \
counteracts TRAF6 and TRIF in TLR signaling [99]."

composite(p(HGNC:TRAF4), p(HGNC:TRAF6)) =| act(p(HGNC:TRAF6))

UNSET Section

SET Section = "TRAFs in NLR signaling"

SET Support = "One well-studied pathway of several NLRs, including NLRP3, NLRP1, \
NLRP6, and NLRC4, is the assembly of multi-protein complexes called \
‘inflammasomes’, which contain caspase-1 and apoptosis-associated \
speck-like protein containing a CARD (ASC)."

complex(GOCC:"inflammasome complex") -- p(HGNC:NLRP3)
complex(GOCC:"inflammasome complex") -- p(HGNC:NLRP1)
complex(GOCC:"inflammasome complex") -- p(HGNC:NLRP6)
complex(GOCC:"inflammasome complex") -- p(HGNC:NLRC4)
complex(GOCC:"inflammasome complex") hasComponents list(p(HGNC:CASP1), p(HGNC:PYCARD))

SET Support = "Inflammasomes induce proteolytic processing of pro-IL-1β and pro-IL-18 \
into secretable IL-1β and IL-18, as well as caspase 1-dependent \
apoptosis termed ‘pyroptosis’ [15,100,101]."

# frag 117-269 is the active form of IL1B
# frag 37-193 is the mature form of IL18
complex(GOCC:"inflammasome complex") -> bp(GOBP:proteolysis)
bp(GOBP:proteolysis) -> p(HGNC:IL1B, frag("117_269"))
bp(GOBP:proteolysis) -> p(HGNC:IL18, frag("37_193"))
p(HGNC:IL1B, frag("117_269")) => sec(p(HGNC:IL1B, frag("117_269")))
p(HGNC:IL18, frag("37_193")) => sec(p(HGNC:IL18, frag("37_193")))
complex(GOCC:"inflammasome complex") -> bp(GOBP:pyroptosis)

SET Support = "TRAF2, TRAF5, and TRAF6 are required for NF-κB and MAPK activation \
induced by NOD1 and NOD2 (Figure 4), the founding members of the NLR \
family [15,102,106]."

composite(p(HGNC:TRAF2), p(HGNC:TRAF5), p(HGNC:TRAF6), p(HGNC:NOD1)) -> bp(GOBP:"MAPK cascade")
composite(p(HGNC:TRAF2), p(HGNC:TRAF5), p(HGNC:TRAF6), p(HGNC:NOD1)) -> act(complex(SCOMP:"Nfkb Complex"))
composite(p(HGNC:TRAF2), p(HGNC:TRAF5), p(HGNC:TRAF6), p(HGNC:NOD2)) -> bp(GOBP:"MAPK cascade")
composite(p(HGNC:TRAF2), p(HGNC:TRAF5), p(HGNC:TRAF6), p(HGNC:NOD2)) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "Upon detection of meso-diaminopimelic acid (DAP) by NOD1 or muramyl \
dipeptide (MDP) by NOD2 at the vicinity of plasma membranes, \
oligomerization of NOD1 or NOD2 recruits the dual specificity kinase \
RIP2 (also called RICK) via a homotypic CARD-CARD interactions \
[101-103]."

complex(a(CHEBI:"meso-2,6-diaminopimelic acid"), p(HGNC:NOD1)) => act(p(HGNC:NOD1))
complex(a(CHEBI:"muramyl dipeptide"), p(HGNC:NOD2)) => act(p(HGNC:NOD2))
# RIP2 == RIPK2
act(p(HGNC:NOD1)) -> complex(p(HGNC:RIPK2), p(HGNC:NOD1))
act(p(HGNC:NOD2)) -> complex(p(HGNC:RIPK2), p(HGNC:NOD2))
complex(p(HGNC:RIPK2), p(HGNC:NOD1)) => act(p(HGNC:RIPK2))
complex(p(HGNC:RIPK2), p(HGNC:NOD2)) => act(p(HGNC:RIPK2))

SET Support = "Activated RIP2 induces the formation of the signaling complex \
containing TRAF2, TRAF5, TRAF6, TRAF4, CARD9, cIAP1/2, and \
Ubc13/Uev1A."

act(p(HGNC:RIPK2)) -> complex(complex(p(HGNC:BIRC2), p(HGNC:BIRC3)), p(HGNC:TRAF2), p(HGNC:TRAF5), p(HGNC:TRAF6), p(HGNC:TRAF4), p(HGNC:CARD9), complex(p(HGNC:UBE2N), p(HGNC:UBE2V1)))

SET Support = "In this complex, cIAP1/2, in coordination with Ubc13/Uev1A, catalyze \
K63-linked polyubiquitination of RIP2, which further recruits \
TAB2/3-TAB1-TAK1 and NEMO-IKKα/β, leading to NF-κB activation \
[15,100,107-109]."

complex(complex(p(HGNC:BIRC2), p(HGNC:BIRC3)), p(HGNC:TRAF2), p(HGNC:TRAF5), p(HGNC:TRAF6), p(HGNC:TRAF4), p(HGNC:CARD9), complex(p(HGNC:UBE2N), p(HGNC:UBE2V1))) => composite(complex(p(HGNC:BIRC2), p(HGNC:BIRC3)), complex(p(HGNC:UBE2N), p(HGNC:UBE2V1)))
composite(complex(p(HGNC:BIRC2), p(HGNC:BIRC3)), complex(p(HGNC:UBE2N), p(HGNC:UBE2V1))) => act(complex(p(HGNC:BIRC2), p(HGNC:BIRC3)), ma(GOBP:"protein K63-linked ubiquitination"))
act(complex(p(HGNC:BIRC2), p(HGNC:BIRC3)), ma(GOBP:"protein K63-linked ubiquitination")) => p(HGNC:RIPK2, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:RIPK2, pmod(GOBP:"protein K63-linked ubiquitination")) -> complex(complex(p(HGNC:TAB2), p(HGNC:TAB3)), p(HGNC:TAB1), p(HGNC:MAP3K7))
# IKBKG == NEMO
p(HGNC:RIPK2, pmod(GOBP:"protein K63-linked ubiquitination")) -> complex(p(HGNC:IKBKG), complex(p(HGNC:CHUK), p(HGNC:IKBKB)))
complex(p(HGNC:IKBKG), complex(p(HGNC:CHUK), p(HGNC:IKBKB))) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "Interestingly, a recent study by Damgaard et al. demonstrated that \
XIAP is also recruited to the NOD2 signaling complex, in which XIAP \
primarily conjugates ubiquitin chains on RIP2 that are linked through \
lysine residues other than K63 and K48 [110]."

act(p(HGNC:XIAP), ma(GOBP:"protein ubiquitination")) => p(HGNC:RIPK2, pmod(Ub))

SET Support = "TRAF4 binds directly to NOD2 in an agonist-dependent manner, and \
inhibits NOD2-induced NF-κB activation and bacterial killing [109]. \
This inhibitory effect of TRAF4 requires its phosphorylation at Ser426 \
by IKKα, which is also recruited to the NOD2 signaling complex [113]."

act(p(HGNC:CHUK), ma(kin)) => p(HGNC:TRAF4, pmod(Ph, Ser, 426))
p(HGNC:TRAF4, pmod(Ph, Ser, 426)) =| act(p(HGNC:NOD2))

SET Support = "TRAF3 mediates type I IFN production induced by NOD1 [114], and \
presumably also that induced by NOD2 (Figure 4)."

p(HGNC:TRAF3) reg bp(GOBP:"type I interferon production")

SET Support = "NOD1 and NOD2 induce type I IFN production through distinct \
mechanisms."

p(HGNC:NOD1) -> bp(GOBP:"type I interferon production")
p(HGNC:NOD2) -> bp(GOBP:"type I interferon production")

SET Support = "Upon sensing DAP, oligomerization of NOD1 recruits TRAF3 via RIP2. \
TRAF3 in turn activates TBK1 and IKKϵ, leading to subsequent \
activation of IRF7 and type I IFN production in epithelial cells \
[100,102,114]."

complex(a(CHEBI:"meso-2,6-diaminopimelic acid"), p(HGNC:NOD1)) => act(p(HGNC:RIPK2))
act(p(HGNC:RIPK2)) -> p(HGNC:TRAF3)
p(HGNC:TRAF3) -> act(complex(p(HGNC:TBK1), p(HGNC:IKBKE)))
act(complex(p(HGNC:TBK1), p(HGNC:IKBKE))) -> act(p(HGNC:IRF7))
SET MeSHAnatomy = "Epithelial Cells"
act(p(HGNC:IRF7)) -> bp(GOBP:"type I interferon production")
UNSET MeSHAnatomy

SET Support = "In contrast, NOD2 induces type I IFN production only in response to \
viral ssRNA, but not in response to MDP, via a RIP2-independent \
pathway [102,115]."

a(GOCC:"ssRNA viral genome") => act(p(HGNC:NOD2))
act(p(HGNC:NOD2)) -> bp(GOBP:"type I interferon production")

SET Support = "Following the detection of viral ssRNA, NOD2 engages a signaling \
complex containing MAVS on mitochondria, which induces IRF3 activation \
and type I IFN production [115]."

complex(a(GOCC:"ssRNA viral genome"), p(HGNC:NOD2)) => complex(p(HGNC:NOD2), p(HGNC:MAVS, loc(MESHA:Mitochondria)))
complex(p(HGNC:NOD2), p(HGNC:MAVS, loc(MESHA:Mitochondria))) -> act(p(HGNC:IRF3))
act(p(HGNC:IRF3)) -> bp(GOBP:"type I interferon production")

SET Support = "TRAF3 has been shown to directly interact with MAVS to mediate RLR- \
induced type I IFN production [116]."

complex(p(HGNC:TRAF3), p(HGNC:MAVS)) reg bp(GOBP:"type I interferon production")

SET Support = "TRAF3 regulates NLRP12-mediated suppression of TLR-driven NF-κB \
activation, as NLRP12 interacts with both NIK and TRAF3 [117]."

p(HGNC:TRAF3) reg act(p(HGNC:NLRP12))
composite(p(HGNC:NLRP12), p(HGNC:TRAF3)) -| act(complex(SCOMP:"Nfkb Complex"))
composite(p(HGNC:NLRP12), p(HGNC:MAP3K14)) -| act(complex(SCOMP:"Nfkb Complex"))

SET Support = "TRAF6 interacts with NLRX1, which negatively regulates NF-κB \
activation induced by RIG-I or TLR4 [118,119]."

composite(p(HGNC:TRAF6), p(HGNC:NLRX1)) -| act(p(HGNC:TLR4))
# RIG-1 == DDX58
composite(p(HGNC:TRAF6), p(HGNC:NLRX1)) -| act(p(HGNC:DDX58))
act(p(HGNC:TLR4)) -> act(complex(SCOMP:"Nfkb Complex"))
act(p(HGNC:DDX58)) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "Similarly, NLRC3 also inhibits TLR-induced NF-κB activation by \
interacting with TRAF6 and reducing K63-linked polyubiquitination of \
TRAF6 [120]."

composite(p(HGNC:NLRC3), p(HGNC:TRAF6)) -| p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination"))

UNSET Section

SET Section = "TRAFs in RLR signaling"

SET Support = "RIG-I/MDA5 signaling rapidly elicits the production of type I and type \
III IFNs and proinflammatory cytokines."

# DDX85 == RIG-I, IFIH1 == MDA5
act(p(HGNC:DDX58)) -> bp(GOBP:"type I interferon production")
act(p(HGNC:DDX58)) -> bp(GOBP:"type III interferon production")
act(p(HGNC:IFIH1)) -> bp(GOBP:"type I interferon production")
act(p(HGNC:IFIH1)) -> bp(GOBP:"type III interferon production")

SET Support = "Upon detection of RNA PAMPs, RIG-I/MDA-5 undergoes conformational \
change that leads to dimerization and association with the \
mitochondrial antiviral signaling adaptor (MAVS, also called IPS-1, \
VISA, or Cardif) through homotypic CARD-CARD interactions \
[116,121,122]."

act(p(HGNC:DDX58)) => complex(p(HGNC:DDX58), p(HGNC:DDX58), p(HGNC:MAVS))
act(p(HGNC:IFIH1)) => complex(p(HGNC:IFIH1), p(HGNC:IFIH1), p(HGNC:MAVS))

SET Support = "Dimerization of MAVS directly recruits TRAF2 [125], TRAF3 [126], \
TRAF5, TRAF6 [127], CARD9 and TRADD, which serve as a platform to \
assemble signaling complexes at mitochondrial outer membranes \
[123,124,128,129]."

complex(p(HGNC:DDX58), p(HGNC:DDX58), p(HGNC:MAVS)) => act(p(HGNC:MAVS))
complex(p(HGNC:IFIH1), p(HGNC:IFIH1), p(HGNC:MAVS)) => act(p(HGNC:MAVS))
act(p(HGNC:MAVS)) -> complex(p(HGNC:MAVS), p(HGNC:MAVS))
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> p(HGNC:TRAF2)
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> p(HGNC:TRAF3)
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> p(HGNC:TRAF5)
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> p(HGNC:TRAF6)
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> p(HGNC:CARD9)
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> p(HGNC:TRADD)

SET Support = "These signaling complexes contain players that are further recruited \
by TRAFs or TRADD, including cIAP1/2, TANK-NAP1-SINTBAD, TBK1-IKKϵ, \
NEMO, IKKα/IKKβ, TAB2/3-TAB1-TAK1, MEKK1, Bcl10, and RIP1-FADD- \
Casp8-Casp10."

complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> complex(p(HGNC:BIRC2), p(HGNC:BIRC3))
# TBKBP1 == SINTBAD
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> complex(p(HGNC:TANK), p(HGNC:TBKBP1), p(HGNC:AZI2))
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> complex(p(HGNC:TBK1), p(HGNC:IKBKE))
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> p(HGNC:IKBKG)
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> complex(p(HGNC:CHUK), p(HGNC:IKBKB))
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> complex(complex(p(HGNC:TAB2), p(HGNC:TAB3)), p(HGNC:TAB1), p(HGNC:MAP3K7))
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> p(HGNC:MAP3K1)
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> p(HGNC:BCL10)
complex(p(HGNC:MAVS), p(HGNC:MAVS)) -> complex(p(HGNC:RIPK1), p(HGNC:FADD), p(HGNC:CASP8), p(HGNC:CASP10))

SET Support = "RIG-I/MDA5 signaling cascades culminate in the phosphorylation and \
activation of IRF3, IRF7, NF-κB and AP-1, which work cooperatively to \
induce the expression of IFNs and proinflammatory cytokines (Figure 5) \
[123,124,128,129]."

act(p(HGNC:DDX58)) -> p(HGNC:IRF3, pmod(Ph))
act(p(HGNC:DDX58)) -> p(HGNC:IRF7, pmod(Ph))
act(p(HGNC:DDX58)) -> act(complex(SCOMP:"Nfkb Complex"))
act(p(HGNC:DDX58)) -> act(complex(SCOMP:"AP-1 Complex"))
act(p(HGNC:IFIH1)) -> p(HGNC:IRF3, pmod(Ph))
act(p(HGNC:IFIH1)) -> p(HGNC:IRF7, pmod(Ph))
act(p(HGNC:IFIH1)) -> act(complex(SCOMP:"Nfkb Complex"))
act(p(HGNC:IFIH1)) -> act(complex(SCOMP:"AP-1 Complex"))
p(HGNC:IRF3, pmod(Ph)) => act(p(HGNC:IRF3))
p(HGNC:IRF3, pmod(Ph)) => act(p(HGNC:IRF7))
composite(p(HGNC:IRF3, pmod(Ph)), p(HGNC:IRF7, pmod(Ph)), complex(SCOMP:"Nfkb Complex"), complex(SCOMP:"AP-1 Complex")) -> bp(GOBP:"type I interferon production")
composite(p(HGNC:IRF3, pmod(Ph)), p(HGNC:IRF7, pmod(Ph)), complex(SCOMP:"Nfkb Complex"), complex(SCOMP:"AP-1 Complex")) -> bp(GOBP:"cytokine production involved in inflammatory response")

SET Support = "TRAF3 is essential for RLR-induced IRF3 but not NF-κB activation, and \
TRAF3 deficiency results in impaired type I IFN induction in response \
to RNA virus infection [126]."

composite(p(HGNC:TRAF3), a(GOCC:"ssRNA viral genome")) positiveCorrelation bp(GOBP:"type I interferon production")

SET Support = "MAVS has a TRAF3-interacting motif in the C-terminus that is verified \
by crystallography [130,131], and Tyr9 phosphorylation on MAVS also \
facilitates the recruitment of TRAF3 [132]."

complex(p(HGNC:TRAF3), p(HGNC:MAVS))
p(HGNC:MAVS, pmod(Ph, Tyr, 9)) -> p(HGNC:TRAF3)

SET Support = "Additionally, TRAF3-MAVS interaction requires the assistance of \
another TRAF3-interacting protein, UXT-V1 [133]."

p(HGNC:UXT) -> complex(p(HGNC:TRAF3), p(HGNC:MAVS))

SET Support = "Following its recruitment to MAVS and in conjunction with Ubc5, TRAF3 \
undergoes K63-linked auto-ubiquitination, which enhances its ability \
to bind to NEMO and TANK-NAP1-SINTBAD, thus allowing the recruitment \
and activation of TBK1 and IKKϵ [116,128,134-137]."

complex(p(HGNC:TRAF3), p(HGNC:MAVS), p(HGNC:UBE2D1)) -> p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination")) => complex(p(HGNC:TRAF3), p(HGNC:TANK), p(HGNC:AZI2), p(HGNC:TBKBP1), p(HGNC:IKBKG))
complex(p(HGNC:TRAF3), p(HGNC:TANK), p(HGNC:AZI2), p(HGNC:TBKBP1), p(HGNC:IKBKG)) -> complex(p(HGNC:IKBKE), p(HGNC:TBK1))
complex(p(HGNC:TRAF3), p(HGNC:TANK), p(HGNC:AZI2), p(HGNC:TBKBP1), p(HGNC:IKBKG)) -> act(complex(p(HGNC:IKBKE), p(HGNC:TBK1)))

SET Support = "Interestingly, a recent study shows that linear ubiquitination of NEMO \
switches it from a positive to a negative regulator of RIG-I \
signaling, as linear ubiquitinated NEMO associates with TRAF3 but \
disrupts the MAVS-TRAF3 complex [138]."

p(HGNC:IKBKG, pmod(Ub)) => complex(p(HGNC:TRAF3), p(HGNC:IKBKG, pmod(Ub)))
complex(p(HGNC:TRAF3), p(HGNC:IKBKG, pmod(Ub))) =| complex(p(HGNC:TRAF3), p(HGNC:MAVS))

SET Support = "The NEMO-like adaptor proteins TANK, NAP1, and SINTBAD are \
constitutively bound to both TBK1 and IKKϵ [135]. Autoubiquitinated \
TRAF3 activates TBK1 and IKKϵ to induce the phosphorylation, \
dimerization and nuclear translocation of IRF3, which triggers the \
production of type I IFNs [16,128,129]."

complex(p(HGNC:TBK1), p(HGNC:IKBKE), p(HGNC:AZI2), p(HGNC:TBKBP1), p(HGNC:IKBKG))
p(HGNC:TRAF3, pmod(Ub)) => act(complex(p(HGNC:TBK1), p(HGNC:IKBKE)))
act(complex(p(HGNC:TBK1), p(HGNC:IKBKE))) => p(HGNC:IRF3, pmod(Ph))
p(HGNC:IRF3, pmod(Ph)) => complex(p(HGNC:IRF3), p(HGNC:IRF3))
complex(p(HGNC:IRF3), p(HGNC:IRF3)) -> tloc(complex(p(HGNC:IRF3), p(HGNC:IRF3)), fromLoc(MESHA:Cytoplasm), toLoc(MESHA:"Cell Nucleus"))
tloc(complex(p(HGNC:IRF3), p(HGNC:IRF3)), fromLoc(MESHA:Cytoplasm), toLoc(MESHA:"Cell Nucleus")) -> bp(GOBP:"type I interferon production")

SET Support = "Depletion of either TRAF2 or TRAF5 leads to reduced IRF3 and NF-κB \
activation upon RIG-I stimulation, and TRAF2 and TAK1 are important \
for p38 activation [125,128,139,140]."

composite(p(HGNC:DDX58), p(HGNC:TRAF2)) -> act(p(HGNC:IRF3))
composite(p(HGNC:DDX58), p(HGNC:TRAF2)) -> act(complex(SCOMP:"Nfkb Complex"))
composite(p(HGNC:DDX58), p(HGNC:TRAF5)) -> act(p(HGNC:IRF3))
composite(p(HGNC:DDX58), p(HGNC:TRAF5)) -> act(complex(SCOMP:"Nfkb Complex"))
act(p(HGNC:TRAF2)) -> act(p(HGNC:MAPK14), ma(kin))
act(p(HGNC:MAP3K7)) -> act(p(HGNC:MAPK14), ma(kin))

SET Support = "Upon RIG-I signaling, interaction of TRAF5 with MAVS induces \
K63-linked TRAF5 auto-ubiquitination and subsequent NEMO-dependent \
activation of IRF3 and NF-κB [139]."

act(p(HGNC:DDX58)) -> complex(p(HGNC:TRAF5), p(HGNC:MAVS))
complex(p(HGNC:TRAF5), p(HGNC:MAVS)) => p(HGNC:TRAF5, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF5, pmod(GOBP:"protein K63-linked ubiquitination")) -> act(p(HGNC:IKBKG))
act(p(HGNC:IKBKG)) -> act(p(HGNC:IRF3))
act(p(HGNC:IKBKG)) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "Similarly, activation of p38 by RIG-I proceeds via a \
TRAF2-TAK1-dependent pathway."

act(p(HGNC:DDX58)) -> composite(p(HGNC:TRAF2), p(HGNC:MAP3K7))
composite(p(HGNC:TRAF2), p(HGNC:MAP3K7)) -> act(p(HGNC:MAPK14), ma(kin))

SET Support = "The p38 activation in turn stimulates the production of IFNs and IL-12 \
[125]."

act(p(HGNC:MAPK14), ma(kin)) -> bp(GOBP:"type I interferon production")
act(p(HGNC:MAPK14), ma(kin)) -> r(HGNC:IL12A)

SET Support = "TRAF6 is required for RLR-induced IRF7 activation and also contributes \
to activation of NF-κB, JNK, and p38 by directly interacting with \
MAVS, which has two TRAF6-interacting motifs [127,141]."

p(HGNC:TRAF6) -> act(p(HGNC:IRF7))
complex(p(HGNC:TRAF6), p(HGNC:MAVS)) -> act(complex(SCOMP:"Nfkb Complex"))
# MAPK8 == JNK
complex(p(HGNC:TRAF6), p(HGNC:MAVS)) -> act(p(HGNC:MAPK8), ma(kin))
complex(p(HGNC:TRAF6), p(HGNC:MAVS)) -> act(p(HGNC:MAPK14), ma(kin))

SET Support = "Activation of IRF7 after viral infection resembles IRF3 activation, \
and involves the direct phosphorylation of IRF7 by TBK1 and IKKϵ."

a(GOCC:"ssRNA viral genome") -> act(complex(p(HGNC:TBK1), p(HGNC:IKBKE)), ma(kin))
act(complex(p(HGNC:TBK1), p(HGNC:IKBKE)), ma(kin)) => p(HGNC:IRF7, pmod(Ph))

SET Support = "However, activation of IRF7 but not IRF3 is impaired in TRAF6−/− \
fibroblasts, and TRAF6 mediates IRF7 ubiquitination [141,142]."

p(HGNC:TRAF6) reg p(HGNC:IRF7, pmod(Ub))

SET Support = "Interestingly, RIG-I-MAVS-TRAF6 signaling leads to IKKβ-mediated \
phosphorylation of p65 at ser536, which is under the control of the \
NADPH oxidase NOX2 [143]."

act(complex(p(HGNC:DDX58), p(HGNC:MAVS), p(HGNC:TRAF6))) -> act(p(HGNC:IKBKB), ma(kin))
# p65 == RELA
act(p(HGNC:IKBKB), ma(kin)) => p(HGNC:RELA, pmod(Ph, Ser, 536))
# NOX2 == CYBB
p(HGNC:CYBB) reg p(HGNC:RELA, pmod(Ph, Ser, 536))

SET Support = "Notably, cIAP1 and cIAP2 are also recruited to MAVS, and mediate K48- \
and K63- linked polyubiquitination of TRAF3 and TRAF6 in response to \
viral infections [144]."

complex(p(HGNC:BIRC2), p(HGNC:BIRC3), p(HGNC:MAVS)) reg p(HGNC:TRAF3, pmod(GOBP:"protein K48-linked ubiquitination"))
complex(p(HGNC:BIRC2), p(HGNC:BIRC3), p(HGNC:MAVS)) reg p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination"))

UNSET Section

SET Section = "TRAFs in cytokine receptor signaling"

SET Support = "IL-17 (A/F) signals through a heteromeric receptor complex formed by \
IL-17RA and IL-17RC."

complex(p(HGNC:IL17A), p(HGNC:IL17F)) => act(complex(p(HGNC:IL17RA), p(HGNC:IL17RC)))

SET Support = "Ligand-induced association of IL-17RA and IL-17RC recruits a novel \
adaptor protein Act1 through SEFIR domain-mediated homotypic \
interaction."

# TRAF3IP2 == Act1
act(complex(p(HGNC:IL17RA), p(HGNC:IL17RC))) -> p(HGNC:TRAF3IP2)

SET Support = "Act1 is a U-box E3 ubiquitin ligase that contains both a SEFIR domain \
and TIMs, and further recruits TRAF6, TRAF2 and TRAF5 [12,152,153]."

p(HGNC:TRAF3IP2) -> p(HGNC:TRAF6)
p(HGNC:TRAF3IP2) -> p(HGNC:TRAF2)
p(HGNC:TRAF3IP2) -> p(HGNC:TRAF5)

SET Support = "In cooperation with Ubc13/Uev1A, Act1 catalyzes K63-linked \
polyubiquitination of TRAF6, which in turn mediates the ubiquitination \
of IL-17RA and induces the activation of NF-κB through TAK1 and IKKs."

composite(complex(p(HGNC:UBE2N), p(HGNC:UBE2V1)), p(HGNC:TRAF3IP2)) => act(p(HGNC:TRAF3IP2), ma(cat))
act(p(HGNC:TRAF3IP2), ma(cat)) => p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:IL17RA, pmod(Ub))
p(HGNC:IL17RA, pmod(Ub)) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "Activated NF-κB further induces the expression of IkBζ, C/EBPδ and \
C/EBPβ, transcription factors that work in concert with NF-κB to \
induce the expression of signature target genes of IL-17 \
[12,147,154-158]."

act(complex(SCOMP:"Nfkb Complex")) -> r(HGNC:NFKBIZ)
act(complex(SCOMP:"Nfkb Complex")) -> r(HGNC:CEBPD)
act(complex(SCOMP:"Nfkb Complex")) -> r(HGNC:CEBPB)
p(HGNC:NFKBIZ) -- p(HGNC:IL17A)
p(HGNC:CEBPD) -- p(HGNC:IL17A)
p(HGNC:CEBPB) -- p(HGNC:IL17A)

SET Support = "On the other hand, TRAF6 also induces GSK3β activation likely through \
PI-3K, and ERK1/2 activation likely through Raf1 [12,151,159]."

p(HGNC:TRAF6) -> act(p(HGNC:GSK3B))
p(HGNC:TRAF6) -> act(p(HGNC:MAPK3), ma(kin))
p(HGNC:TRAF6) -> act(p(HGNC:MAPK1), ma(kin))

SET Support = "Activated GSK3β and ERK induce dual phosphorylation of C/EBPβ and \
thereby inhibit its activity [12,151,160]."

composite(p(HGNC:GSK3B), p(HGNC:MAPK3), p(HGNC:MAPK1)) -> p(HGNC:CEBPB, pmod(Ph))
p(HGNC:CEBPB, pmod(Ph)) =| act(p(HGNC:CEBPB))

SET Support = "Interestingly, TRAF2 and TRAF5 transduce the IL-17 signals to \
stabilize mRNA transcripts of chemokines (such as CXCL1) and cytokines \
(such as IL-6) by recruiting the splicing factor SF2 (also known as \
alternative splicing factor, ASF) into the IL-17R-Act1 signaling \
complex [151,153,161]."

p(HGNC:TRAF2) -> r(HGNC:CXCL1)
p(HGNC:TRAF2) -> r(HGNC:IL6)
p(HGNC:TRAF5) -> r(HGNC:CXCL1)
p(HGNC:TRAF5) -> r(HGNC:IL6)
# SRSF1 == Asf
# Members of the complex taken from Figure 6 and caption description
complex(p(HGNC:SRSF1), p(HGNC:IL17RA), p(HGNC:IL17RC), p(HGNC:TRAF3IP2), p(HGNC:TRAF2), p(HGNC:TRAF5), p(HGNC:IKBKE)) -> r(HGNC:CXCL1)
complex(p(HGNC:SRSF1), p(HGNC:IL17RA), p(HGNC:IL17RC), p(HGNC:TRAF3IP2), p(HGNC:TRAF2), p(HGNC:TRAF5), p(HGNC:IKBKE)) -> r(HGNC:IL6)

SET Support = "Notably, formation of this complex requires IKKϵ, an inducible IKK \
that mediates Act1 phosphorylation at Ser311, adjacent to a putative \
TRAF-binding motif."

act(p(HGNC:IKBKE), ma(kin)) => p(HGNC:SRSF1, pmod(Ph, Ser, 311))

SET Support = "Substitution of Ser311 of Act1 with alanine impairs the IL-17-induced \
Act1-TRAF2-TRAF5 interaction and inflammatory gene expression \
[161,162]."

p(HGNC:SRSF1, pmod(Ph, Ser, 311)) => complex(p(HGNC:TRAF2), p(HGNC:TRAF5), p(HGNC:SRSF1))
p(HGNC:SRSF1, pmod(Ph, Ser, 311)) -> bp(GOBP:"cytokine production involved in inflammatory response")

SET Support = "In contrast, TRAF3 and TRAF4 are negative regulators of IL-17R \
signaling [12,163,164]."

p(HGNC:TRAF3) -| act(complex(p(HGNC:IL17RA), p(HGNC:IL17RC)))
p(HGNC:TRAF4) -| act(complex(p(HGNC:IL17RA), p(HGNC:IL17RC)))

SET Support = "Upon IL-17 stimulation, IL-17RA and IL-17RC directly recruit TRAF3 via \
a distal C-terminal TRAF3-binding site."

act(complex(p(HGNC:IL17RA), p(HGNC:IL17RC))) -> p(HGNC:TRAF3)

SET Support = "The binding of TRAF3 to IL-17Rs interferes with the formation of the \
activation signaling complex of IL-17R-Act1-TRAF6, resulting in \
suppression of downstream signaling, including NF-κB and MAPK \
activation, and production of inflammatory cytokines and chemokines \
[12,163]."

complex(p(HGNC:TRAF3), complex(p(HGNC:IL17RA), p(HGNC:IL17RC))) -| complex(p(HGNC:TRAF6), complex(p(HGNC:IL17RA), p(HGNC:IL17RC)), p(HGNC:SRSF1))
complex(p(HGNC:TRAF6), complex(p(HGNC:IL17RA), p(HGNC:IL17RC)), p(HGNC:SRSF1)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TRAF6), complex(p(HGNC:IL17RA), p(HGNC:IL17RC)), p(HGNC:SRSF1)) -> act(p(HGNC:MAPK3), ma(kin))
complex(p(HGNC:TRAF6), complex(p(HGNC:IL17RA), p(HGNC:IL17RC)), p(HGNC:SRSF1)) -> act(p(HGNC:MAPK1), ma(kin))
complex(p(HGNC:TRAF6), complex(p(HGNC:IL17RA), p(HGNC:IL17RC)), p(HGNC:SRSF1)) -> bp(GOBP:"cytokine production involved in inflammatory response")

SET Support = "TRAF4 exerts its negative regulation on IL-17 signaling by competing \
with TRAF6 for the interaction with Act1, as TRAF4 and TRAF6 use the \
same TIMs on Act1."

p(HGNC:TRAF4) -| complex(p(HGNC:SRSF1), p(HGNC:TRAF6))

SET Support = "Upon IFN engagement, TRAF2 directly binds to the membrane proximal \
half of the signal-transducing subunit of the IFN receptor, IFNAR1, \
and is required for IFN-induced NF-κB2 activation and anti-viral \
responses [13,165]."

complex(p(HGNC:TRAF2), p(HGNC:IFNAR1)) -> act(p(HGNC:NFKB2))

SET Support = "Similarly, direct interaction of TRAF6 with the intracellular domain \
of IFNλR1 regulates NF-κB activation and IFNλR1 stability in response \
to type III IFNs (IFNλ1, IFNλ2, and IFNλ3) [166]."

complex(p(HGNC:TRAF6), p(HGNC:IFNLR1)) reg act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:TRAF6), p(HGNC:IFNLR1)) reg p(HGNC:IFNLR1)

SET Support = "The anti-inflammatory cytokine TGFβ binds to type II and type I \
serine/threonine kinase receptors (TβRII and TβRI)."

complex(p(HGNC:TGFB1), p(HGNC:TGFBR1))
complex(p(HGNC:TGFB1), p(HGNC:TGFBR2))

SET Support = "The TβRI-TRAF6 interaction induces auto-ubiquitination of TRAF6."

complex(p(HGNC:TGFBR1), p(HGNC:TRAF6)) -> p(HGNC:TRAF6, pmod(Ub))

SET Support = "TβRI kinase activity is required for activation of the canonical Smad \
pathway, whereas TRAF6 regulates the activation of TAK1 in a receptor \
kinase-independent manner."

act(p(HGNC:TGFBR1), ma(kin)) -> bp(GOBP:"SMAD protein signal transduction")
p(HGNC:TRAF6) reg act(p(HGNC:MAP3K7), ma(kin))

SET Support = "Activated TRAF6 mediates K63-linked polyubiquitylation of TAK1 at \
Lys34 and Lys158, and results in subsequent activation of p38 and JNK, \
leading to cell apoptosis [14,167,168]."

act(p(HGNC:TRAF6)) -> p(HGNC:MAP3K7, pmod(GOBP:"protein K63-linked ubiquitination", Lys, 34), pmod(GOBP:"protein K63-linked ubiquitination", Lys, 158))
p(HGNC:MAP3K7, pmod(GOBP:"protein K63-linked ubiquitination", Lys, 34), pmod(GOBP:"protein K63-linked ubiquitination", Lys, 158)) -> act(p(HGNC:MAPK14), ma(kin))
p(HGNC:MAP3K7, pmod(GOBP:"protein K63-linked ubiquitination", Lys, 34), pmod(GOBP:"protein K63-linked ubiquitination", Lys, 158)) -> act(p(HGNC:MAPK8), ma(kin))
p(HGNC:MAP3K7, pmod(GOBP:"protein K63-linked ubiquitination", Lys, 34), pmod(GOBP:"protein K63-linked ubiquitination", Lys, 158)) -> bp(MESHPP:Apoptosis)

SET Support = "However, in cancer cells, TRAF6-mediated K63-linked polyubiquitination \
of TβRI also promotes cleavage of TβRI by TNFα converting enzyme \
(TACE) in a PKCζ-dependent manner."

SET CellLine = "PC-3 cell"
p(HGNC:TRAF6) -> p(HGNC:TGFBR1, pmod(GOBP:"protein K63-linked ubiquitination"))
# ADAM17 == TACE
p(HGNC:TGFBR1, pmod(GOBP:"protein K63-linked ubiquitination")) -> act(p(HGNC:ADAM17))
act(p(HGNC:ADAM17)) -> p(HGNC:TGFBR1, frag("?"))
UNSET CellLine

SET Support = "The liberated intracellular domain of TβRI associates with the \
transcriptional regulator p300 to activate genes involved in tumor \
invasiveness, such as Snail and MMP2 [169]."

composite(p(HGNC:TGFBR1, frag("?")), p(HGNC:EP300)) -> r(HGNC:SNAI1)
composite(p(HGNC:TGFBR1, frag("?")), p(HGNC:EP300)) -> r(HGNC:MMP2)

SET Support = "Additionally, TRAF6 mediates the suppressive effect of IL-1β or LPS on \
TGFβ-induced signaling through interaction with the type III TGF-β \
receptor (TβRIII), an accessory receptor that presents the TGFβ ligand \
to TβRII."

composite(p(HGNC:TRAF6), p(HGNC:TGFBR3)) reg act(p(HGNC:IL1B))

SET Support = "Co-treatment with TGFβ and IL-1β or LPS promotes the interaction \
between phosphorylated TβRIII and ubiquitinated TRAF6, and thereby \
sequesters TβRIII from the TβRII/TβRI complex, resulting in inhibition \
of Smad2/3 activation [170]."

composite(p(HGNC:TGFB1), p(HGNC:IL1B)) -> composite(p(HGNC:TGFBR3, pmod(Ph)), p(HGNC:TRAF6, pmod(Ub)))
complex(p(HGNC:TGFBR1), p(HGNC:TGFBR2), p(HGNC:TGFBR3))
composite(p(HGNC:TGFBR3, pmod(Ph)), p(HGNC:TRAF6, pmod(Ub))) -> p(HGNC:TGFBR3)
p(HGNC:TGFBR3) -| act(complex(p(HGNC:SMAD2), p(HGNC:SMAD3)))

SET Support = "The binding of TRAF6 to the TIM of the IL-2R β-chain negatively \
regulates IL-2-induced Jak1 activation in CD4 T cells, which is likely \
involved in the proper regulation of T cell activation and development \
[172]."

SET MeSHAnatomy = "CD4-Positive T-Lymphocytes"
complex(p(HGNC:TRAF6), p(HGNC:IL2RB)) =| act(complex(p(HGNC:IL2RB), p(HGNC:IL2)))
act(complex(p(HGNC:IL2RB), p(HGNC:IL2))) -> act(p(HGNC:JAK1), ma(kin))
act(p(HGNC:JAK1), ma(kin)) reg bp(GOBP:"T cell activation")
UNSET MeSHAnatomy

SET Support = "TRAF1, TRAF3, and TRAF6 are able to regulate signaling by the T cell \
receptor (TCR)."

p(HGNC:TRAF1) reg act(complex(SCOMP:"T Cell Receptor Complex"))
p(HGNC:TRAF3) reg act(complex(SCOMP:"T Cell Receptor Complex"))
p(HGNC:TRAF6) reg act(complex(SCOMP:"T Cell Receptor Complex"))

SET Support = "TRAF1 inhibits CD3-induced NF-κB2 activation and proliferation in T \
cells [31,173]."

p(HGNC:TRAF1) -| act(complex(SCOMP:"T Cell Receptor Complex"))
act(complex(SCOMP:"T Cell Receptor Complex")) -> act(p(HGNC:NFKB2))
act(complex(SCOMP:"T Cell Receptor Complex")) -> bp(GOBP:"T cell proliferation")

SET Support = "TRAF3 is recruited to the signaling rafts, and mediates the \
synergistic activation of ERK, LAT, PLCγ1 and ZAP70 as well as \
cytokine production and proliferation in T cells following co- \
stimulation with TCR and CD28 [11]."

p(HGNC:TRAF3) reg composite(p(HGNC:MAPK1), p(HGNC:LAT), p(HGNC:PLCG1), p(HGNC:ZAP70))
p(HGNC:TRAF3) reg composite(complex(SCOMP:"T Cell Receptor Complex"), p(HGNC:CD28))
composite(complex(SCOMP:"T Cell Receptor Complex"), p(HGNC:CD28)) -> bp(GOBP:"T cell proliferation")
composite(complex(SCOMP:"T Cell Receptor Complex"), p(HGNC:CD28)) -> bp(GOBP:"T cell cytokine production")

SET Support = "RAF6 is also recruited to the TCR signaling rafts containing \
CARMA1-MALT1-Bcl10-PKCθ-IKK-Caspase 8 via interaction with the \
paracaspase MALT1, and contributes to the induction of NF-κB \
activation and IL-2 production in T cells [174,175]."

# CARMA1 == CARD11, PKCθ == PRKCQ
SET MeSHAnatomy = "T-Lymphocytes"
complex(p(HGNC:CARD11), p(HGNC:MALT1), p(HGNC:BCL10), p(HGNC:PRKCQ), p(HGNC:TRAF6), complex(SCOMP:"IkappaB Kinase Complex"), p(HGNC:CASP8)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:CARD11), p(HGNC:MALT1), p(HGNC:BCL10), p(HGNC:PRKCQ), p(HGNC:TRAF6), complex(SCOMP:"IkappaB Kinase Complex"), p(HGNC:CASP8)) -> p(HGNC:IL2)
UNSET MeSHAnatomy

SET Support = "Interestingly, a recent study by Xie et al has shown a distinct \
mechanism of TRAF6 in TCR signaling, in which TRAF6 is recruited to \
the TCR/CD28 signaling complex by LAT and promotes the ubiquitination \
and phosphorylation of LAT as well as the activation of NF-AT in T \
cells [176]."

SET MeSHAnatomy = "T-Lymphocytes"
p(HGNC:LAT) -> p(HGNC:TRAF6, loc(SCOMP:"T Cell Receptor Complex"))
p(HGNC:TRAF6, loc(SCOMP:"T Cell Receptor Complex")) -> p(HGNC:LAT, pmod(Ub), pmod(Ph))
p(HGNC:TRAF6, loc(SCOMP:"T Cell Receptor Complex")) -> act(p(SFAM:"NFAT Family"))
UNSET MeSHAnatomy

SET Support = "In this process, TRAF6 is activated by the kinase ataxia \
telangiectasia mutated (ATM), which is a DNA strand break sensor."

p(HGNC:ATM) => act(p(HGNC:TRAF6))

SET Support = "Following DNA damage, ATM translocates in a calcium-dependent manner \
to cytosol and membrane fractions, and interacts with TRAF6 via a TIM, \
resulting in K63-linked polyubiquitination of TRAF6 and recruitment of \
cIAP1 [178]."

bp(GOBP:"DNA damage response, detection of DNA damage") -> tloc(p(HGNC:ATM), fromLoc(MESHA:"Cell Nucleus"), toLoc(MESHA:Cytoplasm))
p(HGNC:ATM, loc(MESHA:Cytoplasm)) => complex(p(HGNC:ATM), p(HGNC:TRAF6))
complex(p(HGNC:ATM), p(HGNC:TRAF6)) => p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:BIRC2)

SET Support = "The ATM-TRAF6-cIAP1 module stimulates TAB2-dependent TAK1 \
phosphorylation, and cIAP1 catalyzes monoubiquitination of NEMO at \
Lys285."

complex(p(HGNC:ATM), p(HGNC:TRAF6), p(HGNC:BIRC2)) -> act(p(HGNC:TAB2), ma(kin))
act(p(HGNC:TAB2), ma(kin)) => p(HGNC:MAP3K7, pmod(Ph))
complex(p(HGNC:ATM), p(HGNC:TRAF6), p(HGNC:BIRC2)) -> act(p(HGNC:BIRC2))
act(p(HGNC:BIRC2)) => p(HGNC:IKBKG, pmod(Ub, Lys, 285))

SET Support = "NEMO monoubiquitination is a prerequisite for genotoxic NF-κB \
activation and DNA damage response [178]."

# NEMO == IKBKG
p(HGNC:IKBKG, pmod(Ub, Lys, 285)) -> act(complex(SCOMP:"Nfkb Complex"))
p(HGNC:IKBKG, pmod(Ub, Lys, 285)) -> bp(GOBP:"DNA repair")

SET Support = "Interestingly, a recent study has shown that TRAF2 becomes a highly \
active K63-specific ubiquitin ligase when bound to \
sphingosine-1-phosphate (S1P), which appears to be a cofactor for \
TRAF2 E3 ligase activity [180]."

# MBTPS1 == S1P
complex(p(HGNC:TRAF2), p(HGNC:MBTPS1)) -> act(p(HGNC:TRAF2), ma(GOBP:"protein K63-linked ubiquitination"))

SET Support = "For example, Act1-mediated K63-linked ubiquitination of TRAF6 recruits \
TAB2/3-TAK1 and NEMO to activate NF-κB in IL-17R signaling [201], \
while cIAP1/2-catalyzed K63-linked ubiquitination of TRAF3 recruits \
TBK1 and IKKϵ to induce type I IFN production in RIG-I signaling \
[144]."

# TRAF3IP2 == Act1
SET SignalingPathway = "RIG-I"
p(HGNC:TRAF3IP2) -> p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination")) -> complex(complex(p(HGNC:TAB2), p(HGNC:TAB3)), p(HGNC:MAP3K7))
p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:IKBKG)
composite(complex(complex(p(HGNC:TAB2), p(HGNC:TAB3)), p(HGNC:MAP3K7)), p(HGNC:IKBKG)) -> act(complex(SCOMP:"Nfkb Complex"))
complex(p(HGNC:BIRC2), p(HGNC:BIRC3)) => p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:TBK1)
p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination")) -> p(HGNC:IKBKE)
p(HGNC:TBK1) -> bp(GOBP:"type I interferon production")
p(HGNC:IKBKE) -> bp(GOBP:"type I interferon production")
UNSET SignalingPathway

SET Support = "In an exceptional case, Pellino3-induced ubiquitination of TRAF6 at \
Lys124 suppresses the ability of TRAF6 to interact with and \
ubiquitinate IRF7, and thus inhibits type I IFN production in TLR3 \
signaling [202]."

SET SignalingPathway = "TLR3"
p(HGNC:PELI3) -> p(HGNC:TRAF6, pmod(Ub, Lys, 124))
p(HGNC:TRAF6, pmod(Ub, Lys, 124)) =| p(HGNC:IRF7, pmod(Ub))
p(HGNC:IRF7, pmod(Ub)) -> bp(GOBP:"type I interferon production")
UNSET SignalingPathway

SET Support = "For example, upon viral infection, Triad3A is up-regulated, and \
induces K48-linked ubiquitination and degradation of TRAF3, thereby \
forming a negative feedback loop to halt RIG-I signaling and type I \
IFN production [203]."

# Triad3A == RNF216
SET SignalingPathway = "RIG-I"
path(MESHD:"RNA Virus Infections") -> p(HGNC:RNF216)
p(HGNC:RNF216) -> p(HGNC:TRAF3, pmod(GOBP:"protein K48-linked ubiquitination"))
p(HGNC:TRAF3, pmod(GOBP:"protein K48-linked ubiquitination")) -> deg(p(HGNC:TRAF3, pmod(GOBP:"protein K48-linked ubiquitination")))
deg(p(HGNC:TRAF3, pmod(GOBP:"protein K48-linked ubiquitination"))) -| bp(GOBP:"type I interferon production")
UNSET SignalingPathway

SET Support = "CYLD, a tumor suppressor and a target gene of NF-κB, negatively \
regulates NF-κB and JNK activation by removing K63-linked \
polyubiquitin chains from TRAF2 and TRAF6 as well as several other \
signaling proteins [214,215]."

act(complex(SCOMP:"Nfkb Complex")) => complex(complex(SCOMP:"Nfkb Complex"), g(HGNC:CYLD))
p(HGNC:CYLD) -| act(complex(SCOMP:"Nfkb Complex"))
p(HGNC:CYLD) -| act(p(HGNC:MAPK8))
act(p(HGNC:CYLD)) =| p(HGNC:TRAF2, pmod(GOBP:"protein K63-linked ubiquitination"))
act(p(HGNC:CYLD)) =| p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination"))

SET Support = "DUBA specifically targets and de-conjugates the K63-linked \
polyubiquitin chains from TRAF3, resulting in TBK1-IKKϵ dissociation \
from TRAF3 and inhibition of type I IFN production induced by TLRs and \
RLRs [128,185,216]."

# OTUD5 == DUBA
SET SignalingPathway = {"TLR", "RLR"}
act(p(HGNC:OTUD5)) =| p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF3, pmod(GOBP:"protein K63-linked ubiquitination")) => complex(p(HGNC:TRAF3), complex(p(HGNC:TBK1), p(HGNC:IKBKE)))
complex(p(HGNC:TRAF3), complex(p(HGNC:TBK1), p(HGNC:IKBKE))) -> bp(GOBP:"type I interferon production")
UNSET SignalingPathway

SET Support = "However, DUBA does not affect NF-κB2 activation, which is entirely \
dependent on K48-linked degradative ubiquitination of TRAF3 \
[128,185,216]."

p(HGNC:OTUD5) cnc act(p(HGNC:NFKB2))
p(HGNC:TRAF3, pmod(GOBP:"protein K48-linked ubiquitination")) -> act(p(HGNC:NFKB2))

SET Support = "Interestingly, A20, an unusual enzyme that contains both \
ubiquitinating and deubiquitinating activities, negatively regulates \
inflammation by inhibiting NF-κB activation in TNF-R and TLR \
signaling."

# A20 == TNFAIP3
SET SignalingPathway = {"TNF-R", "TLR"}
p(HGNC:TNFAIP3) -| act(complex(SCOMP:"Nfkb Complex"))
act(complex(SCOMP:"Nfkb Complex")) -> path(MESHD:Inflammation)
UNSET SignalingPathway

SET Support = "A20 is a target gene of NF-κB, and able to remove K63-linked \
polyubiquitin chains from TRAF6 to turn off NF-κB activation."

complex(g(HGNC:TNFAIP3), complex(SCOMP:"Nfkb Complex"))
act(p(HGNC:TNFAIP3)) =| p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF6, pmod(GOBP:"protein K63-linked ubiquitination")) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "A20 also inhibits the E3 ligase activities of TRAF6, TRAF2, and cIAP1 \
by promoting K48-linked polyubiquitination and degradation of the E2 \
enzymes Ubc13 and UbcH5c [8,128,217]."

# UBE2D3 == UbcH5c, UBE2N == Ubc13
act(p(HGNC:TNFAIP3)) -> p(HGNC:UBE2N, pmod(GOBP:"protein K48-linked ubiquitination"))
act(p(HGNC:TNFAIP3)) -> p(HGNC:UBE2D3, pmod(GOBP:"protein K48-linked ubiquitination"))
p(HGNC:UBE2N, pmod(GOBP:"protein K48-linked ubiquitination")) -> deg(p(HGNC:UBE2N))
p(HGNC:UBE2D3, pmod(GOBP:"protein K48-linked ubiquitination")) -> deg(p(HGNC:UBE2D3))
composite(p(HGNC:UBE2N), p(HGNC:TRAF6)) -> act(p(HGNC:TRAF6))
composite(p(HGNC:UBE2N), p(HGNC:TRAF2)) -> act(p(HGNC:TRAF2))
composite(p(HGNC:UBE2N), p(HGNC:BIRC2)) -> act(p(HGNC:BIRC2))
composite(p(HGNC:UBE2D3), p(HGNC:TRAF6)) -> act(p(HGNC:TRAF6))
composite(p(HGNC:UBE2D3), p(HGNC:TRAF2)) -> act(p(HGNC:TRAF2))
composite(p(HGNC:UBE2D3), p(HGNC:BIRC2)) -> act(p(HGNC:BIRC2))

SET Support = "Furthermore, A20 is capable of targeting an associated signaling \
molecule such as TRAF2 to the lysosomes for degradation, a process \
that does not require A20 ubiquitin modifying activity [218]."

p(HGNC:TNFAIP3) -> deg(p(HGNC:TRAF2))

SET Support = "Phosphorylation of TRAF1 (at Ser 139 in mouse and Ser 146 in human by \
PKN1) inhibits TNF-R2-dependent tonic NF-κB and JNK signaling in HeLa \
cells [233], and also has a negative impact on the recruitment of TBK1 \
to the 4-1BB signaling complex and the subsequent NF-κB activation in \
T cells [234]."

SET CellLine = "HeLa cell"
SET SignalingPathway = "TNF-R2"
act(p(HGNC:PKN1), ma(kin)) => p(HGNC:TRAF1, pmod(Ph, Ser, 146))
p(HGNC:TRAF1, pmod(Ph, Ser, 146)) -| act(complex(SCOMP:"Nfkb Complex"))
p(HGNC:TRAF1, pmod(Ph, Ser, 146)) -| act(p(HGNC:MAPK8), ma(kin))
UNSET SignalingPathway
UNSET CellLine
SET MeSHAnatomy = "T-Lymphocytes"
act(p(HGNC:PKN1), ma(kin)) => p(HGNC:TRAF1, pmod(Ph, Ser, 146))
p(HGNC:TRAF1, pmod(Ph, Ser, 146)) -| p(HGNC:TBK1)
p(HGNC:TRAF1, pmod(Ph, Ser, 146)) -| act(complex(SCOMP:"Nfkb Complex"))
UNSET MeSHAnatomy

SET Support = "Phosphorylation of TRAF2 (at Ser11 and Ser55 by PKCζ or IKKϵ, and at \
Thr117 by PKCδ and PKCϵ), which promotes K63-linked ubiquitination of \
TRAF2 and NF-κB activation, has been demonstrated in TNFα signaling or \
in transformed cells [235-238]."

SET SignalingPathway = "TNFα"
act(p(HGNC:PRKCZ), ma(kin)) => p(HGNC:TRAF2, pmod(Ph, Ser, 11))
act(p(HGNC:PRKCZ), ma(kin)) => p(HGNC:TRAF2, pmod(Ph, Ser, 55))
act(p(HGNC:IKBKE), ma(kin)) => p(HGNC:TRAF2, pmod(Ph, Ser, 11))
act(p(HGNC:IKBKE), ma(kin)) => p(HGNC:TRAF2, pmod(Ph, Ser, 55))
act(p(HGNC:PRKCD), ma(kin)) => p(HGNC:TRAF2, pmod(Ph, Thr, 117))
act(p(HGNC:PRKCE), ma(kin)) => p(HGNC:TRAF2, pmod(Ph, Thr, 117))
p(HGNC:TRAF2, pmod(Ph, Thr, 117), pmod(Ph, Ser, 11), pmod(Ph, Ser, 55)) -> p(HGNC:TRAF2, pmod(GOBP:"protein K63-linked ubiquitination"))
p(HGNC:TRAF2, pmod(GOBP:"protein K63-linked ubiquitination")) -> act(complex(SCOMP:"Nfkb Complex"))
UNSET SignalingPathway

SET Support = "Following NOD2 activation, phosphorylation of TRAF4 (at Ser 426 by \
IKKα) negatively regulates NOD2 signaling in macrophages, including \
NF-κB activation, cytokine production and antibacterial activity \
[113]."

act(p(HGNC:CHUK), ma(kin)) => p(HGNC:TRAF4, pmod(Ph, Ser, 426))
SET MeSHAnatomy = "Macrophages"
p(HGNC:TRAF4, pmod(Ph, Ser, 426)) -| act(p(HGNC:NOD2), ma(GOBP:signaling))
act(p(HGNC:NOD2), ma(GOBP:signaling)) -> act(complex(SCOMP:"Nfkb Complex"))
act(p(HGNC:NOD2), ma(GOBP:signaling)) -> bp(GOBP:"cytokine production")
UNSET MeSHAnatomy

SET Support = "Tyrosine phosphorylation of TRAF6 by Fyn and c-Src has been shown \
following LPS stimulation [191]."

a(CHEBI:lipopolysaccharide) positiveCorrelation act(p(HGNC:FYN), ma(kin))
a(CHEBI:lipopolysaccharide) positiveCorrelation act(p(HGNC:SRC), ma(kin))
act(p(HGNC:FYN), ma(kin)) => p(HGNC:TRAF6, pmod(Ph, Tyr))
act(p(HGNC:SRC), ma(kin)) => p(HGNC:TRAF6, pmod(Ph, Tyr))

SET Support = "Upon IL-1 stimulation, TRAF6 undergoes deglutathionylation catalyzed \
by glutaredoxin-1 (GRX-1), a process that is essential for TRAF6 auto- \
ubiquitination and subsequent NF-κB activation [239]."

p(HGNC:IL1A) -> act(p(HGNC:GLRX), ma(GOBP:"protein deglutathionylation"))
act(p(HGNC:GLRX), ma(GOBP:"protein deglutathionylation")) =| p(HGNC:TRAF6, pmod(CHEBI:glutathione))
p(HGNC:TRAF6, pmod(CHEBI:glutathione)) -| p(HGNC:TRAF6, pmod(Ub))
p(HGNC:TRAF6, pmod(Ub)) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "TRAF2, 3, 5 and 6 are important in the survival, development, and \
activation of B cells."

p(HGNC:TRAF2) -> bp(GOBP:"B cell activation")
p(HGNC:TRAF2) -> bp(GOBP:"B cell differentiation")
p(HGNC:TRAF2) -> a(MESHA:"B-Lymphocytes")
p(HGNC:TRAF3) -> bp(GOBP:"B cell activation")
p(HGNC:TRAF3) -> bp(GOBP:"B cell differentiation")
p(HGNC:TRAF3) -> a(MESHA:"B-Lymphocytes")
p(HGNC:TRAF5) -> bp(GOBP:"B cell activation")
p(HGNC:TRAF5) -> bp(GOBP:"B cell differentiation")
p(HGNC:TRAF5) -> a(MESHA:"B-Lymphocytes")
p(HGNC:TRAF6) -> bp(GOBP:"B cell activation")
p(HGNC:TRAF6) -> bp(GOBP:"B cell differentiation")
p(HGNC:TRAF6) -> a(MESHA:"B-Lymphocytes")

SET Support = "Interestingly, TRAF3−/− B cells also display enhanced activation in \
response to signaling by TLR3, TLR4, TLR7, or TLR9 [272]."

SET Species = "10090"
p(MGI:Traf3) -| act(p(MGI:Tlr3), ma(GOBP:signaling))
p(MGI:Traf3) -| act(p(MGI:Tlr4), ma(GOBP:signaling))
p(MGI:Traf3) -| act(p(MGI:Tlr7), ma(GOBP:signaling))
p(MGI:Traf3) -| act(p(MGI:Tlr9), ma(GOBP:signaling))
act(p(MGI:Tlr3), ma(GOBP:signaling)) -> bp(GOBP:"B cell activation")
act(p(MGI:Tlr4), ma(GOBP:signaling)) -> bp(GOBP:"B cell activation")
act(p(MGI:Tlr7), ma(GOBP:signaling)) -> bp(GOBP:"B cell activation")
act(p(MGI:Tlr9), ma(GOBP:signaling)) -> bp(GOBP:"B cell activation")
UNSET Species

SET Support = "TRAF3−/− B cells exhibit accelerated phosphorylation of JNK, ERK, and \
p38 in response to CD40 signaling."

SET Species = "10090"
SET MeSHAnatomy = "B-Lymphocytes"
act(p(MGI:Cd40), ma(GOBP:signaling)) -> p(MGI:Mapk8, pmod(Ph))
act(p(MGI:Cd40), ma(GOBP:signaling)) -> p(MGI:Mapk1, pmod(Ph))
act(p(MGI:Cd40), ma(GOBP:signaling)) -> p(MGI:Mapk14, pmod(Ph))
p(MGI:Traf3) -| act(p(MGI:Cd40), ma(GOBP:signaling))
UNSET MeSHAnatomy
UNSET Species

SET Support = "In contrast, TRAF2−/− B cells display slower and decreased CD40 \
signaling, while cIAP1−/−cIAP2−/− B cells show impaired CD40 signaling \
[74]."

SET Species = "10090"
SET MeSHAnatomy = "B-Lymphocytes"
p(MGI:Traf2) -> act(p(MGI:Cd40), ma(GOBP:signaling))
composite(p(MGI:Birc2), p(MGI:Birc3)) -> act(p(MGI:Cd40), ma(GOBP:signaling))
UNSET MeSHAnatomy
UNSET Species

SET Support = "Notably, TRAF5−/− B cells show defects in proliferation and up- \
regulation of surface molecules in response to CD40 stimulation, and \
reduced production of IgM and IgG1 in response to stimulation with \
CD40 plus IL-4 [274]."

SET Species = "10090"
SET SignalingPathway = "CD40"
p(MGI:Traf5) -> bp(GOBP:"B cell proliferation")
bp(GOBP:"B cell proliferation") -> a(MESHA:"B-Lymphocytes")
UNSET SignalingPathway
SET MeSHAnatomy = "B-Lymphocytes"
composite(p(MGI:Cd40), p(MGI:Il4), p(MGI:Traf5)) -> p(MGI:Ighm)
composite(p(MGI:Cd40), p(MGI:Il4), p(MGI:Traf5)) -> p(MGI:Ighg1)
UNSET MeSHAnatomy
UNSET Species

SET Support = "TRAF1−/− T cells exhibit hyperproliferation and increased production \
of Th2 cytokines (IL-4, IL-5 and IL-13) in response to TCR signaling, \
but defective 4-1BB-mediated survival responses in effector and memory \
CD8 T cells [77-79,171,266]."

SET Species = "10090"
p(MGI:Traf1) -| bp(GOBP:"T cell proliferation")
bp(GOBP:"T cell proliferation") -> a(MESHA:"T-Lymphocytes")
SET SignalingPathway = "TCR"
SET MeSHAnatomy = "T-Lymphocytes"
p(MGI:Traf1) -| p(MGI:Il4)
p(MGI:Traf1) -| p(MGI:Il5)
p(MGI:Traf1) -| p(MGI:Il13)
UNSET SignalingPathway
UNSET MeSHAnatomy
SET MeSHAnatomy = "CD8-Positive T-Lymphocytes"
# Tnfrsf9 == 4-1BB
p(MGI:Traf1) -> act(p(MGI:Tnfrsf9))
UNSET MeSHAnatomy
UNSET Species

SET Support = "Hyperproliferation of TRAF1−/− T cells is due to constitutive \
activation of the NF-κB2 pathway [31]."

SET Species = "10090"
p(MGI:Traf1) reg act(p(MGI:Nfkb2))
UNSET Species

SET Support = "Similarly, CD27-mediated co-stimulatory signaling was reduced in \
TRAF5−/− T cells [274]."

SET Species = "10090"
p(MGI:Traf5) -> act(p(MGI:Cd27))
act(p(MGI:Cd27)) -> bp(GOBP:signaling)
UNSET Species

SET Support = "In contrast, TRAF6−/− mice show a severe defect in Treg development in \
thymus [276]."

SET Species = "10090"
p(MGI:Traf6) -> a(MESHA:"T-Lymphocytes, Regulatory", loc(MESHA:"Thymus Gland"))
UNSET Species

SET Support = "TRAF6−/− T cells exhibit hyperactivation of the PI3K-Akt pathway, \
resistance to suppression by Treg cells, and also resistance to \
anergizing signals [281,282]."

SET Species = "10090"
SET MeSHAnatomy = "T-Lymphocytes"
p(MGI:Traf6) reg act(p(SFAM:"PIK3C Class IA Family"), ma(kin))
UNSET MeSHAnatomy
UNSET Species

SET Support = "TRAF6−/− CD4 T cells display increased Th17 differentiation, due to \
enhanced sensitivity to TGFβ-induced Smad2/3 activation and IL-2 down- \
regulation [284]."

SET Species = "10090"
SET MeSHAnatomy = "CD4-Positive T-Lymphocytes"
p(MGI:Traf6) -| act(complex(p(MGI:Smad2), p(MGI:Smad3)))
act(complex(p(MGI:Smad2), p(MGI:Smad3))) -> a(MESHA:"Th17 Cells")
p(MGI:Tgfb1) -> act(complex(p(MGI:Smad2), p(MGI:Smad3)))
p(MGI:Traf6) -> p(MGI:Il2)
p(MGI:Il2) -| a(MESHA:"Th17 Cells")
UNSET MeSHAnatomy
UNSET Species

SET Support = "Interestingly, activated TRAF6−/− CD8 T cells exhibit defective AMP- \
activated kinase activation and mitochondrial fatty acid oxidation \
(FAO) in response to growth factor withdrawal, resulting in a profound \
defect in memory CD8 T cell development after infection [283]."

SET Species = "10090"
SET MeSHAnatomy = "CD8-Positive T-Lymphocytes"
p(MGI:Traf6) -> act(complex(SCOMP:"AMP Activated Protein Kinase Complex"))
p(MGI:Traf6) -> bp(GOBP:"fatty acid oxidation")
UNSET MeSHAnatomy
UNSET Species

SET Support = "TRAF1, 2, 3, 4 and 6 regulate the functions of dendritic cells (DCs)."

p(HGNC:TRAF1) reg a(MESHA:"Dendritic Cells")
p(HGNC:TRAF2) reg a(MESHA:"Dendritic Cells")
p(HGNC:TRAF3) reg a(MESHA:"Dendritic Cells")
p(HGNC:TRAF4) reg a(MESHA:"Dendritic Cells")
p(HGNC:TRAF6) reg a(MESHA:"Dendritic Cells")

SET Support = "TRAF1-/- DCs matured in CD154 display impaired NF-κB activation and \
survival but increased TRAF2 degradation in response to CD154 re- \
stimulation [290]."

SET Species = "10090"
composite(p(MGI:Traf1), p(MGI:Cd40lg)) -> a(MESHA:"Dendritic Cells")
SET MeSHAnatomy = "Dendritic Cells"
composite(p(MGI:Traf1), p(MGI:Cd40lg)) -> act(complex(SCOMP:"Nfkb Complex"))
composite(p(MGI:Traf1), p(MGI:Cd40lg)) -| deg(p(MGI:Traf2))
UNSET MeSHAnatomy
UNSET Species

SET Support = "TRAF3−/− DCs produce increased amounts of IL-12 but reduced amounts of \
IL-10 and little type I IFN in response to TLR7 and TLR9 signaling \
[18,95,96]."

SET Species = "10090"
SET MeSHAnatomy = "Dendritic Cells"
p(MGI:Traf3) -| p(MGI:Il12a)
p(MGI:Traf3) -> p(MGI:Il10)
act(p(MGI:Tlr7), ma(GOBP:signaling)) -> bp(GOBP:"type I interferon production")
act(p(MGI:Tlr9), ma(GOBP:signaling)) -> bp(GOBP:"type I interferon production")
p(MGI:Traf3) positiveCorrelation act(p(MGI:Tlr7), ma(GOBP:signaling))
p(MGI:Traf3) positiveCorrelation act(p(MGI:Tlr9), ma(GOBP:signaling))
UNSET MeSHAnatomy
UNSET Species

SET Support = "TRAF3−/− DCs also display constitutive NF-κB2 activation but not \
prolonged survival [18]."

SET Species = "10090"
SET MeSHAnatomy = "Dendritic Cells"
p(MGI:Traf3) reg act(p(MGI:Nfkb2))
UNSET MeSHAnatomy
UNSET Species

SET Support = "TRAF4−/− DCs exhibit reduced migration both in transwell experiments \
and in vivo[273]."

SET Species = "10090"
SET Assay = {"transwell", "in vivo"}
p(MGI:Traf4) -> bp(GOBP:"dendritic cell migration")
UNSET Species
UNSET Assay

SET Support = "Interestingly, TRAF6 is required for DC maturation and activation."

p(HGNC:TRAF6) -> bp(GOBP:"dendritic cell differentiation")
# Checked primary source and they looked at myeloid DCs
p(HGNC:TRAF6) -> bp(GOBP:"myeloid dendritic cell activation")

SET Support = "In response to either microbial components or CD40L, TRAF6−/− DCs fail \
to up-regulate surface expression of MHC class II and CD86, or produce \
inflammatory cytokines [277]."

SET Species = "10090"
SET MeSHAnatomy = "Dendritic Cells"
composite(p(MGI:Cd40lg), p(MGI:Traf6)) -> p(MGI:Cd86, loc(MESHA:"Cell Membrane"))
composite(p(MGI:Cd40lg), p(MGI:Traf6)) -> tloc(complex(SCOMP:"MHC Class II Complex"), fromLoc(MESHA:Cytoplasm), toLoc(MESHA:"Cell Membrane"))
composite(p(MGI:Cd40lg), p(MGI:Traf6)) -> bp(GOBP:"cytokine production involved in inflammatory response")
UNSET Species
UNSET MeSHAnatomy

SET Support = "Similarly, TRAF6−/− mast cells exhibit impaired production of IL-6, \
CCL-9, IL-13, and TNF following FcϵRI aggregation by IgE [278]."

SET Species = "10090"
SET MeSHAnatomy = "Mast Cells"
composite(p(MGI:Traf6), complex(p(MGI:Fcer1a), p(MGI:Ighe))) -> p(MGI:Il6)
composite(p(MGI:Traf6), complex(p(MGI:Fcer1a), p(MGI:Ighe))) -> p(MGI:Ccl9)
composite(p(MGI:Traf6), complex(p(MGI:Fcer1a), p(MGI:Ighe))) -> p(MGI:Il13)
composite(p(MGI:Traf6), complex(p(MGI:Fcer1a), p(MGI:Ighe))) -> p(MGI:Tnf)
UNSET Species
UNSET MeSHAnatomy

UNSET Section

SET Section = "TRAFs in human diseases"

SET Support = "Deletions and mutations of TRAF3 have been reported in multiple \
myeloma [294-296], Waldenström’s macroglobulinemia [300], Hodgkin \
lymphomas (HLs) [301], and a variety of non-Hodgkin lymphomas (NHLs), \
including splenic marginal zone lymphoma, B cell chronic lymphocytic \
leukemia (B-CLL), and mantle cell lymphoma [298,299]."

p(HGNC:TRAF3, var("?")) positiveCorrelation path(MESHD:"Multiple Myeloma")
p(HGNC:TRAF3, var("?")) positiveCorrelation path(MESHD:"Waldenstrom Macroglobulinemia")
p(HGNC:TRAF3, var("?")) positiveCorrelation path(MESHD:"Hodgkin Disease")
p(HGNC:TRAF3, var("?")) positiveCorrelation path(MESHD:"Lymphoma, B-Cell, Marginal Zone")
p(HGNC:TRAF3, var("?")) positiveCorrelation path(MESHD:"Leukemia, B-Cell")
p(HGNC:TRAF3, var("?")) positiveCorrelation path(MESHD:"Lymphoma, Mantle-Cell")

SET Support = "Similarly, inactivating mutations of TRAF2 have been identified in \
multiple myeloma [294-296] and diffuse large B-cell lymphoma (DLBCL) \
[302]."

p(HGNC:TRAF2, var("?")) -| act(p(HGNC:TRAF2))
act(p(HGNC:TRAF2)) negativeCorrelation path(MESHD:"Multiple Myeloma")
act(p(HGNC:TRAF2)) negativeCorrelation path(MESHD:"Lymphoma, Large B-Cell, Diffuse")

SET Support = "Single nucleotide polymorphisms (SNPs) of TRAF3 are also associated \
with altered risk of multiple myeloma [297]."

g(HGNC:TRAF3, var("?")) -- path(MESHD:"Multiple Myeloma")

SET Support = "In contrast, TRAF1 expression is ubiquitously elevated in both HLs \
[314] and NHLs, especially in CLL and mediastinal large B-cell \
lymphoma [315-317]."

p(HGNC:TRAF1) positiveCorrelation path(MESHD:"Lymphoma, Non-Hodgkin")
p(HGNC:TRAF1) positiveCorrelation path(MESHD:"Hodgkin Disease")

SET Support = "In addition, TRAF1 SNPs are associated with NHLs [303]."

g(HGNC:TRAF1, var("?")) positiveCorrelation path(MESHD:"Lymphoma, Non-Hodgkin")

SET Support = "TRAF4 is overexpressed in breast and lung carcinomas [304,318,319]."

p(HGNC:TRAF4) positiveCorrelation path(MESHD:"Small Cell Lung Carcinoma")
p(HGNC:TRAF4) positiveCorrelation path(MESHD:"Breast Carcinoma In Situ")

SET Support = "Increased TRAF4 gene copy number is one major mechanism responsible \
for TRAF4 protein overexpression in human cancers."

r(HGNC:TRAF4) -> p(HGNC:TRAF4)

SET Support = "Intriguingly, TRAF4 is a target gene of the p53 family of \
transcription factors, including p63, p73 and p53, in squamous cell \
carcinoma of the head and neck (SCCHN)."

path(MESHD:"Carcinoma, Squamous Cell") positiveCorrelation complex(p(HGNC:TP63), g(HGNC:TRAF4))
path(MESHD:"Carcinoma, Squamous Cell") positiveCorrelation complex(p(HGNC:TP73), g(HGNC:TRAF4))
path(MESHD:"Carcinoma, Squamous Cell") positiveCorrelation complex(p(HGNC:TP53), g(HGNC:TRAF4))

SET Support = "TRAF6 exhibits overexpression and gene amplification in lung cancer \
and osteosarcoma cells [305,306,323]."

p(HGNC:TRAF6) positiveCorrelation path(MESHD:"Small Cell Lung Carcinoma")
p(HGNC:TRAF6) positiveCorrelation path(MESHD:Osteosarcoma)
r(HGNC:TRAF6) positiveCorrelation path(MESHD:"Small Cell Lung Carcinoma")
r(HGNC:TRAF6) positiveCorrelation path(MESHD:Osteosarcoma)

SET Support = "Downregulation of TRAF6 in human lung cancer and osteosarcoma cells \
suppresses NF-κB activation, cell survival and proliferation, and \
tumor formation and invasion."

SET MeSHDisease = {"Small Cell Lung Carcinoma", "Osteosarcoma"}
p(HGNC:TRAF6) -> act(complex(SCOMP:"Nfkb Complex"))
UNSET MeSHDisease

SET Support = "SNPs of TRAF6 are associated with both SLE and RA [22]."

g(HGNC:TRAF6, var("?")) positiveCorrelation path(MESHD:"Lupus Erythematosus, Systemic")
g(HGNC:TRAF6, var("?")) positiveCorrelation path(MESHD:"Arthritis, Rheumatoid")

SET Support = "Similarly, SNPs at the TRAF1/C5 locus are associated with both SLE and \
RA [307,308,310,311]."

g(HGNC:TRAF1, var("?")) positiveCorrelation path(MESHD:"Lupus Erythematosus, Systemic")
g(HGNC:TRAF1, var("?")) positiveCorrelation path(MESHD:"Arthritis, Rheumatoid")

SET Support = "A single SNP (rs7514863), mapping upstream of the TRAF5 gene and \
affecting a putative transcription factor binding site, demonstrates a \
significant association with RA [309]."

g(dbSNP:rs7514863) positiveCorrelation path(MESHD:"Arthritis, Rheumatoid")

SET Support = "In addition, decreased expression of TRAF2 has been detected in \
peripheral blood mononuclear cells of SLE patients [324]."

p(HGNC:TRAF2, loc(MESHA:"Leukocytes, Mononuclear")) negativeCorrelation path(MESHD:"Lupus Erythematosus, Systemic")

# Not including sections that cite associations found in fewer than 3 patients (e.g. sections: "Immunodeficiencies", "Hypohidrotic ectodermal dysplasia")

SET Support = "Interestingly, recent evidence implicates the E3 ligase activity of \
TRAF6 in the pathogenic aggregation of mutant proteins in \
neurodegenerative diseases such as Parkinson’s disease and Huntington \
disease."

act(p(HGNC:TRAF6)) positiveCorrelation path(MESHD:"Parkinson Disease")
act(p(HGNC:TRAF6)) positiveCorrelation path(MESHD:"Huntington Disease")

SET Support = "It was found that TRAF6 binds to misfolded mutant DJ-1, aSYN and \
N-HTT, proteins involved in the pathogenesis of the Parkinson’s \
disease and Huntington disease. Mutant DJ-1, aSYN and N-HTT proteins \
are all substrates of TRAF6."

complex(p(HGNC:TRAF6), p(HGNC:PARK7, var("?")))
complex(p(HGNC:TRAF6), p(HGNC:SNCA, var("?")))
complex(p(HGNC:TRAF6), p(HGNC:HTT, var("?")))
p(HGNC:PARK7) positiveCorrelation path(MESHD:"Parkinson Disease")
p(HGNC:PARK7) positiveCorrelation path(MESHD:"Huntington Disease")
p(HGNC:SNCA) positiveCorrelation path(MESHD:"Parkinson Disease")
p(HGNC:SNCA) positiveCorrelation path(MESHD:"Huntington Disease")
p(HGNC:HTT) positiveCorrelation path(MESHD:"Parkinson Disease")
p(HGNC:HTT) positiveCorrelation path(MESHD:"Huntington Disease")

SET Support = "Instead of conventional K63-linked polyubiquitination, TRAF6 promotes \
atypical ubiquitination of DJ-1, aSYN and N-HTT with K6, K27, and K29 \
linkage formation, thereby stimulating aggregate formation of mutant \
DJ-1, aSYN and N-HTT in neurodegenerative diseases [326,327]."

p(HGNC:TRAF6) -> p(HGNC:PARK7, var("?"), pmod(GOBP:"protein K6-linked ubiquitination"))
p(HGNC:TRAF6) -> p(HGNC:PARK7, var("?"), pmod(GOBP:"protein K27-linked ubiquitination"))
p(HGNC:TRAF6) -> p(HGNC:PARK7, var("?"), pmod(GOBP:"protein K29-linked ubiquitination"))
p(HGNC:TRAF6) -> p(HGNC:SNCA, var("?"), pmod(GOBP:"protein K6-linked ubiquitination"))
p(HGNC:TRAF6) -> p(HGNC:SNCA, var("?"), pmod(GOBP:"protein K27-linked ubiquitination"))
p(HGNC:TRAF6) -> p(HGNC:SNCA, var("?"), pmod(GOBP:"protein K29-linked ubiquitination"))
p(HGNC:TRAF6) -> p(HGNC:HTT, var("?"), pmod(GOBP:"protein K6-linked ubiquitination"))
p(HGNC:TRAF6) -> p(HGNC:HTT, var("?"), pmod(GOBP:"protein K27-linked ubiquitination"))
p(HGNC:TRAF6) -> p(HGNC:HTT, var("?"), pmod(GOBP:"protein K29-linked ubiquitination"))
p(HGNC:PARK7, var("?"), pmod(GOBP:"protein K6-linked ubiquitination")) -> complex(p(HGNC:PARK7, var("?")), p(HGNC:PARK7, var("?")))
p(HGNC:PARK7, var("?"), pmod(GOBP:"protein K27-linked ubiquitination")) -> complex(p(HGNC:PARK7, var("?")), p(HGNC:PARK7, var("?")))
p(HGNC:PARK7, var("?"), pmod(GOBP:"protein K29-linked ubiquitination")) -> complex(p(HGNC:PARK7, var("?")), p(HGNC:PARK7, var("?")))
p(HGNC:SNCA, var("?"), pmod(GOBP:"protein K6-linked ubiquitination")) -> complex(p(HGNC:SNCA, var("?")), p(HGNC:SNCA, var("?")))
p(HGNC:SNCA, var("?"), pmod(GOBP:"protein K27-linked ubiquitination")) -> complex(p(HGNC:SNCA, var("?")), p(HGNC:SNCA, var("?")))
p(HGNC:SNCA, var("?"), pmod(GOBP:"protein K29-linked ubiquitination")) -> complex(p(HGNC:SNCA, var("?")), p(HGNC:SNCA, var("?")))
p(HGNC:HTT, var("?"), pmod(GOBP:"protein K6-linked ubiquitination")) -> complex(p(HGNC:HTT, var("?")), p(HGNC:HTT, var("?")))
p(HGNC:HTT, var("?"), pmod(GOBP:"protein K27-linked ubiquitination")) -> complex(p(HGNC:HTT, var("?")), p(HGNC:HTT, var("?")))
p(HGNC:HTT, var("?"), pmod(GOBP:"protein K29-linked ubiquitination")) -> complex(p(HGNC:HTT, var("?")), p(HGNC:HTT, var("?")))
complex(p(HGNC:PARK7, var("?")), p(HGNC:PARK7, var("?"))) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")
complex(p(HGNC:SNCA, var("?")), p(HGNC:SNCA, var("?"))) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")
complex(p(HGNC:HTT, var("?")), p(HGNC:HTT, var("?"))) positiveCorrelation path(MESHD:"Neurodegenerative Diseases")

SET Support = "Notably, TRAF2 and TRAF3 are often degraded in response to signaling \
by the TNF-R superfamily [3,27,32,328]."

act(p(SFAM:"TNFRSF Family"), ma(GOBP:signaling)) -> deg(p(HGNC:TRAF2))
act(p(SFAM:"TNFRSF Family"), ma(GOBP:signaling)) -> deg(p(HGNC:TRAF3))

SET Support = "In contrast, TRAF1 expression is up-regulated by NF-κB activation in \
response to signaling by a variety of receptors, including TNF-R \
superfamily and cytokine receptors, etc. [329-331]."

act(p(SFAM:"TNFRSF Family"), ma(GOBP:signaling)) -> act(complex(SCOMP:"Nfkb Complex"))
act(complex(SCOMP:"Nfkb Complex")) -> p(HGNC:TRAF1)

SET Support = "For example, the presence of TRAF1 stabilizes TRAF2, which plays a \
role in promoting proinflammtory responses in HeLa cells [332,333]."

p(HGNC:TRAF1) -> p(HGNC:TRAF2)
SET CellLine = "HeLa cell"
p(HGNC:TRAF2) -> bp(GOBP:"cytokine production involved in inflammatory response")
UNSET CellLine

UNSET Section

UNSET Publication

##################################################################################

SET Citation = {"PubMed", "Ann Transl Med. 2015 Jun;3(10):136. doi: 10.3978/j.issn.2305-5839.2015.03.49.", "26207229"}

SET PublicationType = "Review"
SET Section = "The effects of microglia, detrimental or beneficial?"

SET Support = "As the resident immune cell of the CNS, microglia plays a nuanced and \
complex role in the progression of AD (10)."

a(MESHA:Microglia) -- a(MESHA:"Central Nervous System")
a(MESHA:Microglia) -- path(MESHD:"Alzheimer Disease")

SET Support = "Recently, microglia became more important than ever before in \
demonstrating strong genetic implications for microglia molecules and \
the immune system, especially when genome-wide association study \
(GWAS) have identified variants of several immune genes are risk \
factors for late onset Alzheimer’s disease (LOAD) (11,12). These genes \
include CD33 (11-13), TREM2 (14,15) and the HLA-DRB4-DRB1 region (16), \
etc."

SET StudyType = "GWAS"
g(HGNC:CD33, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:TREM2, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
g(HGNC:HLA-DRB4, var("?")) positiveCorrelation path(ADO:"Late_Onset_Alzheimer_s_Disease")
UNSET StudyType

SET Support = "Under physiological conditions, microglia are active to maintenance of \
homoeostasis, neuroprotection and neuro repair by release growth \
factors such as brain-derived neurotrophic factor (BDNF) and \
transforming growth factor (TGF) β with ramified morphology."

a(MESHA:Microglia) -> bp(GOBP:"homeostatic process")
a(MESHA:Microglia) -> bp(MESHPP:Neuroprotection)
a(MESHA:Microglia) -> p(HGNC:TGFB1)
a(MESHA:Microglia) -> p(HGNC:BDNF)

SET Support = "M1-skewed microglial activation plays a vital role in the defense \
against pathogens and tumor cell by production of proinflammatory \
cytokines, such as IL-1β, TNF-α, STAT3, IL-6, IL-12, IL-23 and free \
radicals such as reactive oxygen species (ROS)."

SET MicroglialActivationState = "M1"
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
a(MESHA:Microglia) -> p(HGNC:IL1B)
a(MESHA:Microglia) -> p(HGNC:TNF)
a(MESHA:Microglia) -> p(HGNC:STAT3)
a(MESHA:Microglia) -> p(HGNC:IL6)
a(MESHA:Microglia) -> p(HGNC:IL12A)
a(MESHA:Microglia) -> p(HGNC:IL23A)
a(MESHA:Microglia) -> p(HGNC:IL1B)
a(MESHA:Microglia) -> a(CHEBI:"reactive oxygen species")
UNSET MicroglialActivationState

SET Support = "Conversely, the alternative M2 anti-inflammatory phenotype promotes \
tissue remodeling/repair and angiogenesis through release high levels \
of anti-inflammatory cytokines such as IL-10, IL-4, IL-13, and TGF-β, \
and low levels of pro-inflammatory cytokines (20) (Figure 1)."

SET MicroglialActivationState = "M2"
a(MESHA:Microglia) positiveCorrelation bp(GOBP:"tissue regeneration")
a(MESHA:Microglia) positiveCorrelation bp(GOBP:angiogenesis)
a(MESHA:Microglia) -> p(HGNC:IL10)
a(MESHA:Microglia) -> p(HGNC:IL4)
a(MESHA:Microglia) -> p(HGNC:IL13)
a(MESHA:Microglia) -> p(HGNC:TGFB1)
UNSET MicroglialActivationState

SET Support = "For AD mouse models, there have been reported increase in both M1 \
(such as IL-1β, TNF-a, iNOS, and IL-6) and M2 (notably YM1, Arg-1, Mrc \
and IL-10) markers compared to age-matched wild-type controls."

SET Species = "10090"
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Il1b)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Tnf)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Nos2)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Il6)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Chil3)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Arg1)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Cd200)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Il10)
UNSET Species

SET Support = "NALP3 inflammasome, which mediates IL-1β production, is one of the \
important players driving the innate immune response towards Aβ and \
determining the cytokine milieu in the brain."

p(HGNC:NLRP3) => complex(GOCC:"NLRP3 inflammasome complex")
complex(GOCC:"NLRP3 inflammasome complex") hasComponents list(p(HGNC:NLRP3), p(HGNC:PYCARD), p(HGNC:CASP1))
# NLRP3 inflammasome includes NALP3 (NLRP3), ASC (PYCARD), and caspase 1
p(HGNC:NLRP3) => complex(p(HGNC:NLRP3), p(HGNC:PYCARD), p(HGNC:CASP1))
complex(GOCC:"NLRP3 inflammasome complex") reg p(HGNC:IL1B)
SET MeSHAnatomy = "Brain"
complex(GOCC:"NLRP3 inflammasome complex") reg bp(GOBP:"cytokine production")
UNSET MeSHAnatomy

SET Support = "Recently, Heneka et al. revealed that inhibition of NLRP3 can induce \
microglial phagocytosis and to an M2 phenotype (21)."

SET MeSHAnatomy = "Microglia"
p(HGNC:NLRP3) negativeCorrelation bp(GOBP:phagocytosis)
UNSET MeSHAnatomy

SET Support = "In AD, the microglia has the capacity to respond to various stimuli, \
including amyloid peptides, their precursor protein (APP), and \
neurofibrillary tangles (22)."

SET MeSHDisease = "Alzheimer Disease"
a(MESHA:Microglia) -- p(HGNC:APP, frag("?"))
a(MESHA:Microglia) -- p(HGNC:APP)
a(MESHA:Microglia) -- a(MESHCS:"Neurofibrillary Tangles")
UNSET MeSHDisease

SET Support = "In the early stages of AD, microglial activation can promote Aβ \
clearance via microglia’s scavenger receptors (SRs) (23), and hinder \
the AD progression."

a(MESHA:Microglia) positiveCorrelation bp(GOBP:"beta-amyloid clearance")
p(GFAM:"Scavenger receptors") positiveCorrelation bp(GOBP:"beta-amyloid clearance")
bp(GOBP:"microglial cell activation involved in immune response") -> p(GFAM:"Scavenger receptors", loc(MESHA:Microglia))
bp(GOBP:"beta-amyloid clearance") negativeCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "The persistent microglial activation stimulated by Aβ via the receptor \
for CD36 (24), Fc receptors, toll-like receptors (TLRs) (25), and \
complement receptors advanced glycation end products (RAGE) (26), can \
increase Aβ production and decrease Aβ clearance, ultimately cause \
neuronal damage."

a(CHEBI:"amyloid-beta") -> bp(GOBP:"microglial cell activation involved in immune response")
a(CHEBI:"amyloid-beta") => complex(a(CHEBI:"amyloid-beta"), p(HGNC:CD36, loc(MESHA:Microglia)))
a(CHEBI:"amyloid-beta") => complex(a(CHEBI:"amyloid-beta"), p(GFAM:"Toll like receptors", loc(MESHA:Microglia)))
complex(a(CHEBI:"amyloid-beta"), p(HGNC:CD36, loc(MESHA:Microglia))) -> a(CHEBI:"amyloid-beta")
complex(a(CHEBI:"amyloid-beta"), p(GFAM:"Toll like receptors", loc(MESHA:Microglia))) -> a(CHEBI:"amyloid-beta")
complex(a(CHEBI:"amyloid-beta"), p(HGNC:CD36, loc(MESHA:Microglia))) -| bp(GOBP:"beta-amyloid clearance")
complex(a(CHEBI:"amyloid-beta"), p(GFAM:"Toll like receptors", loc(MESHA:Microglia))) -| bp(GOBP:"beta-amyloid clearance")
complex(a(CHEBI:"amyloid-beta"), p(HGNC:CD36, loc(MESHA:Microglia))) -| a(MESHA:Neurons)
complex(a(CHEBI:"amyloid-beta"), p(GFAM:"Toll like receptors", loc(MESHA:Microglia))) -| a(MESHA:Neurons)

SET Support = "These evidences suggest that Aβ indirectly contribute to activation of \
inflammatory systems, then lead to progression of AD."

a(CHEBI:"amyloid-beta") -> bp(GOBP:"cytokine production involved in inflammatory response")
a(CHEBI:"amyloid-beta") positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "In 2008, Sastre et al. found that inflammation increase Aβ generation \
via β-secretase (BACE1), the main enzyme responsible for Aβ generation \
(30), therefore creating a feed-forward loop (Figure 2)."

a(MESHA:Microglia) -> p(HGNC:BACE1)
path(MESHD:Inflammation) -> a(CHEBI:"amyloid-beta")
a(CHEBI:"amyloid-beta") -> path(MESHD:Inflammation)
p(HGNC:BACE1) => a(CHEBI:"amyloid-beta")

UNSET Section

SET Section = "Dysregulation of pro-inflammatory cytokine in AD"

SET Support = "Experimental and clinical evidence have demonstrated the increased \
synthesis of pro-inflammatory cytokines such as TNF-α, IFN-γ, IL-1β, \
IL-6, IL-18, and the upregulation of their cognate receptors in the AD \
brain (34-37)."

path(MESHD:Inflammation) positiveCorrelation p(HGNC:TNF)
path(MESHD:Inflammation) positiveCorrelation p(HGNC:IFNG)
path(MESHD:Inflammation) positiveCorrelation p(HGNC:IL1B)
path(MESHD:Inflammation) positiveCorrelation p(HGNC:IL6)
path(MESHD:Inflammation) positiveCorrelation p(HGNC:IL18)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:TNF)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:IFNG)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:IL1B)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:IL6)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:IL18)

SET Support = "Chronic deposition of Aβ in brain drives cerebral neuroinflammation by \
activating microglia, which is reported to be a major source of pro- \
inflammatory cytokines in AD (38)."

SET MeSHAnatomy = "Brain"
a(CHEBI:"amyloid-beta") -> bp(GOBP:"microglial cell activation involved in immune response")
bp(GOBP:"microglial cell activation involved in immune response") -> path(MESHD:Inflammation)
bp(GOBP:"microglial cell activation involved in immune response") positiveCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
UNSET MeSHAnatomy

SET Support = "Aβ binding to the microglial cell surface induces pro-inflammatory \
gene expression and results in the elevation of pro-inflammatory \
cytokine such as TNF-α, IL-1β, IL-6, IL-18, which lead to tau \
hyperphosphorylation and neuronal loss (39)."

complex(a(CHEBI:"amyloid-beta"), a(MESHA:Microglia)) positiveCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
complex(a(CHEBI:"amyloid-beta"), a(MESHA:Microglia)) -> p(HGNC:TNF)
complex(a(CHEBI:"amyloid-beta"), a(MESHA:Microglia)) -> p(HGNC:IL1B)
complex(a(CHEBI:"amyloid-beta"), a(MESHA:Microglia)) -> p(HGNC:IL6)
complex(a(CHEBI:"amyloid-beta"), a(MESHA:Microglia)) -> p(HGNC:IL18)
bp(GOBP:"cytokine production involved in inflammatory response") positiveCorrelation p(HGNC:MAPT, pmod(Ph))
p(HGNC:TNF) -> p(HGNC:MAPT, pmod(Ph))
p(HGNC:IL1B) -> p(HGNC:MAPT, pmod(Ph))
p(HGNC:IL6) -> p(HGNC:MAPT, pmod(Ph))
p(HGNC:IL18) -> p(HGNC:MAPT, pmod(Ph))
bp(GOBP:"cytokine production involved in inflammatory response") negativeCorrelation a(MESHA:Neurons)
p(HGNC:TNF) -| a(MESHA:Neurons)
p(HGNC:IL1B) -| a(MESHA:Neurons)
p(HGNC:IL6) -| a(MESHA:Neurons)
p(HGNC:IL18) -| a(MESHA:Neurons)

SET Support = "Additionally, studies showed that the levels of transcripts for a \
number of pro-inflammatory markers such as TNF and IL-1β were elevated \
in AD, specifically in response to tau (40,41)."

path(MESHD:"Alzheimer Disease") positiveCorrelation r(HGNC:TNF)
path(MESHD:"Alzheimer Disease") positiveCorrelation r(HGNC:IL1B)
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:MAPT) -> r(HGNC:TNF)
p(HGNC:MAPT) -> r(HGNC:IL1B)
UNSET MeSHDisease

SET Support = "In support of this, previous study has revealed that transgenic mice \
inducing neuroinflammation by injecting LPS triggered intracellular Aβ \
deposition (43,44) and tau phosphorylation (45) in the brain."

SET MeSHAnatomy = "Brain"
a(CHEBI:lipopolysaccharide) -> a(CHEBI:"amyloid-beta", loc(MESHA:"Intracellular Space"))
a(CHEBI:lipopolysaccharide) -> p(HGNC:MAPT, pmod(Ph))
UNSET MeSHAnatomy

SET Support = "Yet, intriguingly, previous studies reported that synergistic effects \
may also occur between pro-inflammatory cytokines and Aβ, such as, \
IFN-γ synergize with Aβ causing the release of TNF-α and reactive \
nitrogen species which is toxic to neurons."

composite(p(HGNC:IFNG), a(CHEBI:"amyloid-beta")) -> p(HGNC:TNF)
composite(p(HGNC:IFNG), a(CHEBI:"amyloid-beta")) -> a(CHEBI:"reactive oxygen species")
p(HGNC:TNF) -| a(MESHA:Neurons)
a(CHEBI:"reactive oxygen species") -| a(MESHA:Neurons)

SET Support = "AD brain microvessels release significantly higher levels of pro- \
inflammatory cytokines including TNF-α, IL-1β, IL-6 than microvessels \
in age-matched controls (46)."

SET MeSHAnatomy = "Brain"
SET MeSHDisease = "Alzheimer Disease"
a(MESHA:Microvessels) -> p(HGNC:TNF)
a(MESHA:Microvessels) -> p(HGNC:IL1B)
a(MESHA:Microvessels) -> p(HGNC:IL6)
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Support = "This is supported by the finding that exposure of cultured human brain \
endothelial cells to Aβ1-40 up-regulate expression of inflammatory \
genes IL-1β and IL-6, which is confirmed by quantitative RT-PCR \
analysis (47)."

SET Assay = "RT-PCR"
SET MeSHAnatomy = "Brain"
composite(a(MESHA:"Endothelial Cells"), p(HGNC:APP, frag("1_40"))) -> r(HGNC:IL1B)
composite(a(MESHA:"Endothelial Cells"), p(HGNC:APP, frag("1_40"))) -> r(HGNC:IL6)
UNSET MeSHAnatomy
UNSET Assay

SET Support = "Currently, the only possible biomarker to aid a diagnosis of AD is the \
CSF total tau (t-tau) to Aβ42 and phosphorylated tau181 (p-tau181) \
(53)."

p(HGNC:MAPT, loc(MESHA:"Cerebrospinal Fluid")) biomarkerFor path(MESHD:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, Thr, 181), loc(MESHA:"Cerebrospinal Fluid")) biomarkerFor path(MESHD:"Alzheimer Disease")
p(HGNC:APP, frag("1_42")) biomarkerFor path(MESHD:"Alzheimer Disease")

SET Support = "Pathological altered of IL-1α levels is associated with plaque \
evolution and the rate of cognitive decline among clinically diagnosed \
MCI subjects."

p(HGNC:IL1A) -- path(MESHD:"Plaque, Amyloid")
p(HGNC:IL1A) -- path(ADO:"Mild_cognitive_impairment")
p(HGNC:IL1A) -- path(HP:"Cognitive impairment")

UNSET Section

SET Section = "Pro-inflammatory cytokine signaling in AD"

SET Support = "TNF-α, a pleiotropic pro-inflammatory cytokine, is elevated in both \
the brains and plasma of AD patients and is proximal to amyloid \
plaques on autopsy, which appear to be reflective of disease severity \
and contribute to the inflammatory milieu."

path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:TNF, loc(MESHA:Brain))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:TNF, loc(MESHA:Plasma))

SET Support = "As early as 2001, a GWAS found single-nucleotide polymorphisms in \
TNF-α and/or its receptor is associated with sporadic AD."

SET StudyType = "GWAS"
g(HGNC:TNF, var("?")) positiveCorrelation path(ADO:"sporadic_Alzheimer")
g(HGNC:TNFRSF1A, var("?")) positiveCorrelation path(ADO:"sporadic_Alzheimer")
UNSET StudyType

SET Support = "The study portends that overexpression TNF-α signaling enhances AD- \
associated pathology and is detrimental to neuronal viability (87)."

# Study was in mice
SET Species = "10090"
p(MGI:Tnf) positiveCorrelation path(MESHD:"Alzheimer Disease")
p(MGI:Tnf) -| a(MESHA:Neurons)
UNSET Species

SET Support = "However, TNF-α is also related to a deleterious role induced by Aβ on \
promote learning and memory deficits and synaptic memory mechanisms in \
AD. Inhibition of TNF-α reduced impairment induced by Aβ on \
recognition memory via long-term potentiation (LTP), \
electophysiological experiments correlate with learning and memory \
(88)."

a(CHEBI:"amyloid-beta") negativeCorrelation bp(GOBP:"long-term memory")
p(MGI:Tnf) positiveCorrelation bp(GOBP:"long-term memory")

SET Support = "There are two cognate transmembrane receptors for TNF-α, termed TNF \
receptor 1 (TNFR1) (also known as Tnfrsf1a/p55, CD120a) and TNFR2 \
(Tnfrsf1b/p75, CD120b)."

complex(p(HGNC:TNF), p(HGNC:TNFRSF1A))
complex(p(HGNC:TNF), p(HGNC:TNFRSF1B))

SET Support = "Signaling via the cognate TNF-R receptors elicits distinct cellular \
responses, including cell proliferation, cell migration, and apoptosis \
mediated through the activation of several downstream signal \
transduction cascades involving NF-κB, c-Jun N-terminal kinase (JNK), \
p38, and ceramide-sphingomyelinase pathways (90)."

complex(p(HGNC:TNF), p(HGNC:TNFRSF1A)) -> bp(GOBP:"cell proliferation")
complex(p(HGNC:TNF), p(HGNC:TNFRSF1A)) -> bp(GOBP:"cell migration")
complex(p(HGNC:TNF), p(HGNC:TNFRSF1A)) -> bp(MESHPP:Apoptosis)
complex(p(HGNC:TNF), p(HGNC:TNFRSF1A)) -> bp(GOBP:"NIK/NF-kappaB signaling")
complex(p(HGNC:TNF), p(HGNC:TNFRSF1A)) -> bp(GOBP:"JNK cascade")
complex(p(HGNC:TNF), p(HGNC:TNFRSF1A)) -> bp(GOBP:"p38MAPK cascade")
complex(p(HGNC:TNF), p(HGNC:TNFRSF1B)) -> bp(GOBP:"cell proliferation")
complex(p(HGNC:TNF), p(HGNC:TNFRSF1B)) -> bp(GOBP:"cell migration")
complex(p(HGNC:TNF), p(HGNC:TNFRSF1B)) -> bp(MESHPP:Apoptosis)
complex(p(HGNC:TNF), p(HGNC:TNFRSF1B)) -> bp(GOBP:"NIK/NF-kappaB signaling")
complex(p(HGNC:TNF), p(HGNC:TNFRSF1B)) -> bp(GOBP:"JNK cascade")
complex(p(HGNC:TNF), p(HGNC:TNFRSF1B)) -> bp(GOBP:"p38MAPK cascade")

SET Support = "In the context of AD, deletion of the TNFR1 gene in APP transgenic \
mice reduced plaque deposition and improves the cognitive deficits by \
down-regulating BACE1 promoter activity."

SET MeSHDisease = "Alzheimer Disease"
SET Species = "10090"
SET KnockoutMice = "App transgenic"
g(MGI:Tnfrsf1a) positiveCorrelation path(MESHD:"Plaque, Amyloid")
g(MGI:Tnfrsf1a) positiveCorrelation path(HP:"Cognitive impairment")
g(MGI:Tnfrsf1a) positiveCorrelation r(MGI:Bace1)
UNSET KnockoutMice
UNSET Species
UNSET MeSHDisease

SET Support = "Recently, Montgomery et al. found that knockdown of TNF-RII in the \
long-term in hippocampal neurons via rAAV2 vectore mediated siRNA \
delivery enhances Aβ plaque deposition and paired helical filament \
(PHF) formation (90), which have shown TNF-RII may exert protective \
responses that may be required to counteract TNF-RI driven signal \
transduction."

SET Species = "10090"
SET MeSHAnatomy = "Hippocampus"
p(MGI:Tnfrsf1b, loc(MESHA:Neurons)) -| path(MESHD:"Plaque, Amyloid")
p(MGI:Tnfrsf1b, loc(MESHA:Neurons)) -| complex(p(HGNC:MAPT), a(GOCC:microtubule))
UNSET MeSHAnatomy
UNSET Species

SET Support = "For example, As early as 1995, Barger et al. has found that in the \
presence of Aβ peptide, dissociated neuronal cultures pretreated with \
TNF-α were spares cells from Aβ-induced neuronal death by suppressing \
accumulation of ROS and Ca2+ via NF-κB-dependent signaling (92)."

SET StudyType = "in vitro"
composite(a(MESHA:Neurons), a(CHEBI:"amyloid-beta")) -| a(MESHA:Neurons)
SET ChallengeType = "amyloid-beta peptide"
p(HGNC:TNF) -> bp(GOBP:"NIK/NF-kappaB signaling")
bp(GOBP:"NIK/NF-kappaB signaling") -| a(CHEBI:"reactive oxygen species")
bp(GOBP:"NIK/NF-kappaB signaling") -| a(CHEBI:"calcium(2+)")
a(CHEBI:"reactive oxygen species") -| a(MESHA:Neurons)
a(CHEBI:"calcium(2+)") -| a(MESHA:Neurons)
UNSET StudyType
UNSET ChallengeType

SET Support = "Moreover, Tarkowski et al. proved intrathecal levels of TNF-α were \
significantly inversely correlated to the intracerebral apoptosis and \
neuronal degradation (67)."

# intrathecal = inside cerebrospinal fluid
p(HGNC:TNF, loc(MESHA:"Cerebrospinal Fluid")) positiveCorrelation a(MESHA:Neurons)

SET Support = "Furthermore, incubation of human neuronal cells with TNF-α led to \
production of bcl-2, a molecule known to down-regulate neuronal \
apoptosis."

composite(a(MESHA:Neurons), p(HGNC:TNF)) -> p(HGNC:BCL2)
p(HGNC:BCL2) positiveCorrelation a(MESHA:Neurons)

SET Support = "IL-1β is synthesized and released by both activated microglia and \
astrocytes in pro-forms, pro-IL-1β, in the cytoplasm in response to \
variety of stimuli."

bp(GOBP:"microglial cell activation involved in immune response") -> p(HGNC:IL1B)
a(MESHA:Astrocytes) -> p(HGNC:IL1B, loc(MESHA:Cytoplasm))

SET Support = "In order to generate mature and bioactive form, pro-IL-1β must be \
cleaved by the protease caspase-1, which is activated by cytosolic \
multiprotein complexes called inflammasomes (94,95)."

#frag 117-269 is the active form of IL1B
act(p(HGNC:CASP1), ma(GOBP:proteolysis)) => p(HGNC:IL1B, frag("117_269"))
complex(GOCC:"inflammasome complex") => act(p(HGNC:CASP1))

SET Support = "Recently, Parajuli et al. described that soluble oligomeric amyloidβ \
(oAβ) increased the processing of pro-IL-1β into mature IL-1β in \
microglia via ROS-dependent activation of NLRP3 inflammation (96)."

a(CHEBI:"amyloid-beta", loc(MESHA:"Extracellular Space")) -> p(HGNC:IL1B, frag("117_269"), loc(MESHA:Microglia))
a(CHEBI:"amyloid-beta", loc(MESHA:"Extracellular Space")) -> a(CHEBI:"reactive oxygen species")
a(CHEBI:"reactive oxygen species") -> complex(GOCC:"NLRP3 inflammasome complex")
complex(GOCC:"NLRP3 inflammasome complex") isA complex(GOCC:"inflammasome complex")

SET Support = "Previous studies have identified that over-expression of the immune \
modifying cytokine IL-1β released by microglia and astrocytes \
surrounding Aβ plaques occur in AD brain and in animal models of AD in \
relative to age-matched controls (97,98)."

SET MeSHAnatomy = "Brain"
SET MeSHDisease = "Alzheimer Disease"
a(MESHA:Microglia, loc(MESHD:"Plaque, Amyloid")) -> p(HGNC:IL1B)
a(MESHA:Astrocytes, loc(MESHD:"Plaque, Amyloid")) -> p(HGNC:IL1B)
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Support = "The production of IL-1β depends on the activation of MAP kinases and \
NF-κB signaling pathways."

bp(GOBP:"NIK/NF-kappaB signaling") positiveCorrelation p(HGNC:IL1B)
bp(GOBP:"MAPK cascade") positiveCorrelation p(HGNC:IL1B)

SET Support = "Recently, studies clearly demonstrated IL-1β contributing to APP \
processing in vitro."

SET StudyType = "in vitro"
p(HGNC:IL1B) -- p(HGNC:APP)
UNSET StudyType

SET Support = "Subsequently, many researchers confirmed that overexpression of IL-1β \
exacerbates tau phosphorylation and tangle formation through aberrant \
activation of p38-MAPK and glycogen synthase kinase 3 (GSK3) (76,99), \
which affect synaptic plasticity, inhibiting LTP and subsequently \
learn and memory (100)."

p(HGNC:IL1B) -> p(HGNC:MAPT, pmod(Ph))
p(HGNC:IL1B) -> a(MESHCS:"Neurofibrillary Tangles")
p(HGNC:IL1B) -> act(p(HGNC:MAPK14), ma(kin))
# glycogen synthase kinase 3 has two components, an alpha and beta
p(HGNC:IL1B) -> act(complex(p(HGNC:GSK3A), p(HGNC:GSK3B)))

SET Support = "Blocking or neutralizing IL-1β in an AD mouse model could largely \
protect from cognitive deficits, decrease tau pathology, synthesis of \
S100, and fAβ (76)."

SET Species = "10090"
SET MeSHDisease = "Alzheimer Disease"
p(MGI:Il1b) positiveCorrelation path(HP:"Cognitive impairment")
p(MGI:Il1b) -> p(MGI:Mapt, pmod(Ph))
p(MGI:Il1b) -> p(SFAM:"S100A Family")
p(MGI:Il1b) -> a(CHEBI:"amyloid-beta")
UNSET Species
UNSET MeSHDisease

SET Support = "In addition, fAβ has been reported to activate microglia, leading to \
increased synthesis and release of neurotoxic secretory products, pro- \
inflammatory cytokines such as IL-1β and ROS (101)."

a(CHEBI:"amyloid-beta") -> bp(GOBP:"microglial cell activation involved in immune response")
bp(GOBP:"microglial cell activation involved in immune response") -> p(HGNC:IL1B)
bp(GOBP:"microglial cell activation involved in immune response") -> a(CHEBI:"reactive oxygen species")

SET Support = "In addition, the increased expression of IL-1β, was found to impair \
microglial Aβ clearance functions (102) and increase BBB permeability, \
which can promote the accumulation of Aβ in the brain (103)."

SET MeSHAnatomy = "Microglia"
p(HGNC:IL1B) -| bp(GOBP:"beta-amyloid clearance")
UNSET MeSHAnatomy

SET Support = "It has been shown that sustained overexpression of IL-1β reduces Aβ- \
related pathology by modulating microglia-dependent plaque degradation \
or promoting non-amyloidogenic APP cleavage in a mouse model of AD and \
in a cell culture model (77,104,105)."

SET Species = "10090"
SET StudyType = "in vitro"
p(MGI:Il1b) -> p(MGI:App, frag("?"))
UNSET StudyType
UNSET Species

SET Support = "IL-6 is a pleiotropic inflammatory cytokine mainly produced by \
activated microglia, astrocytes in different brain regions."

p(HGNC:IL6) positiveCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
bp(GOBP:"microglial cell activation") positiveCorrelation p(HGNC:IL6)
a(MESHA:Microglia, loc(MESHA:Brain)) => p(HGNC:IL6)
a(MESHA:Astrocytes, loc(MESHA:Brain)) => p(HGNC:IL6)

SET Support = "In addition, IL-6 could stimulate microglia and astrocytes to release \
a cascade of proinflammatory cytokines and acute-phase proteins, such \
as C-reactive protein (CRP) (106)."

p(HGNC:IL6) -> bp(GOBP:"microglial cell activation")
p(HGNC:IL6) -> p(HGNC:CRP)

SET Support = "The levels of IL-6 have been found significantly elevating in the \
brains, cerebrospinal fluid, and plasma, especially locally around \
amyloid plaques in AD patients and animal models."

path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:IL6, loc(MESHA:Brain))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:IL6, loc(MESHA:"Cerebrospinal Fluid"))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:IL6, loc(MESHA:Plasma))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:IL6, loc(CHEBI:"amyloid-beta"))
SET Species = "10090"
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Il6, loc(MESHA:Brain))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Il6, loc(MESHA:"Cerebrospinal Fluid"))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Il6, loc(MESHA:Plasma))
path(MESHD:"Alzheimer Disease") positiveCorrelation p(MGI:Il6, loc(CHEBI:"amyloid-beta"))
UNSET Species

SET Support = "Production of IL-6 by human neurons is reportedly stimulated by \
glycation end product-modified tau and Aβ."

composite(a(MESHA:Neurons), p(HGNC:MAPT, pmod(Glyco)), a(CHEBI:"amyloid-beta")) => p(HGNC:IL6)

SET Support = "Firstly, studies clearly demonstrated the role of IL-6 that contribute \
to APP processing and production in primary rat cortical neurons (80)."

SET MeSHAnatomy = "Neurons"
p(RGD:Il6) -> p(RGD:App)
UNSET MeSHAnatomy

SET Support = "In turn, fAβ has been reported can activate microglia, which lead to \
increase synthesis and release of IL-6 (47)."

a(CHEBI:"amyloid fibril") positiveCorrelation bp(GOBP:"microglial cell activation")
bp(GOBP:"microglial cell activation") positiveCorrelation p(HGNC:IL6)

SET Support = "Production of IL-6 depends on the activation of Toll-like receptor 2 \
(TLR2)-MyD88 signaling pathways in microglia and the JNK-AP1 pathway \
in human brain endothelial cells (47,107)."

SET MeSHAnatomy = "Microglia"
p(HGNC:TLR2) => complex(p(HGNC:TLR2), p(HGNC:MYD88))
complex(p(HGNC:TLR2), p(HGNC:MYD88)) -> p(HGNC:IL6)
UNSET MeSHAnatomy
SET MeSHAnatomy = "Endothelial Cells"
bp(GOBP:"JNK cascade") positiveCorrelation p(HGNC:IL6)
# MAPK8 == JNK
p(HGNC:MAPK8, pmod(Ph)) -> complex(SCOMP:"Adaptor Protein I Complex")
act(complex(SCOMP:"Adaptor Protein I Complex"), ma(MESHPP:"Transcription, Genetic")) => r(HGNC:IL6)
UNSET MeSHAnatomy

SET Support = "Secondly, hippocampal cells treated with IL-6 could contribute to NFT \
formation by inducing Tau phosphorylation through cd k5/p 35 or \
pathway deregulation."

p(HGNC:IL6, loc(MESHA:Hippocampus)) -> a(MESHCS:"Neurofibrillary Tangles")
p(HGNC:IL6, loc(MESHA:Hippocampus)) -> p(HGNC:MAPT, pmod(Ph))
# p35 == IL12A
p(HGNC:IL6, loc(MESHA:Hippocampus)) -> act(complex(p(HGNC:CDK5), p(HGNC:IL12A)))
act(complex(p(HGNC:CDK5), p(HGNC:IL12A))) => p(HGNC:MAPT, pmod(Ph))

SET Support = "In addition, IL-6 can also activate the JAK/STATs, NMDA receptor and \
the MAPK-p38 protein kinases, both involved in hyperphosphorylation of \
tau (80)."

p(HGNC:IL6) -> bp(GOBP:"JAK-STAT cascade")
p(HGNC:IL6) -> act(p(HGNC:JAK1), ma(kin))
act(p(HGNC:JAK1), ma(kin)) => p(HGNC:STAT3, pmod(Ph))
p(HGNC:STAT3, pmod(Ph)) -> p(HGNC:MAPT, pmod(Ph))
p(HGNC:IL6) -> bp(GOBP:"NMDA selective glutamate receptor signaling pathway")
bp(GOBP:"NMDA selective glutamate receptor signaling pathway") positiveCorrelation p(HGNC:MAPT, pmod(Ph))
p(HGNC:IL6) -> act(p(HGNC:MAPK14), ma(kin))
act(p(HGNC:MAPK14), ma(kin)) => p(HGNC:MAPT, pmod(Ph))

SET Support = "IL-18 (interferon-γ-inducing factor, IL-1γ), a pro-inflammatory \
cytokines belonging to the IL-1 cytokine family, is synthesized as an \
inactive 24-kDa precursor protein (pro-IL-18) and cleaved by the \
intracellular cystein protease caspase-1 and proteinase-3 to generate \
a 18 kDa mature and biologically active form."

p(HGNC:IL18) positiveCorrelation bp(GOBP:"cytokine production involved in inflammatory response")
act(p(HGNC:CASP1), ma(GOBP:proteolysis)) =| p(HGNC:IL18, frag("?", "24kD"))
act(p(HGNC:PRTN3), ma(GOBP:proteolysis)) =| p(HGNC:IL18, frag("?", "24kD"))
act(p(HGNC:CASP1), ma(GOBP:proteolysis)) => p(HGNC:IL18, frag("?", "18kD"))
act(p(HGNC:PRTN3), ma(GOBP:proteolysis)) => p(HGNC:IL18, frag("?", "18kD"))

SET Support = "Activated microglia, astrocytes and ependymal cells and neurons in the \
CNS are all the source of IL-18."

a(MESHA:Microglia) => p(HGNC:IL18)
a(MESHA:Astrocytes) => p(HGNC:IL18)
a(MESHA:"Ependymoglial Cells") => p(HGNC:IL18)
a(MESHA:Neurons, loc(MESHA:"Central Nervous System")) => p(HGNC:IL18)

SET Support = "Up-regulated IL-18 expression can lead to a harmful vicious cycle of \
inflammation: IL-18 drives the local production of IL-1β and IFN-γ, \
the latter cleaves inactive precursor protein of IL-1β and IL-18 to \
their mature and biologically active form via caspase-1, and it also \
increases IL-18 gene expression which may play an important role in \
the pathogenesis of AD (83)."

p(HGNC:IL18) -> p(HGNC:IL1B)
p(HGNC:IL18) -> p(HGNC:IFNG)
p(HGNC:IFNG) -> act(p(HGNC:CASP1), ma(GOBP:proteolysis))
act(p(HGNC:CASP1), ma(GOBP:proteolysis)) => p(HGNC:IL18, frag("?", "18kD"))
act(p(HGNC:CASP1), ma(GOBP:proteolysis)) => p(HGNC:IL1B, frag("?"))
act(p(HGNC:CASP1), ma(GOBP:proteolysis)) -> act(p(HGNC:IL1B))
act(p(HGNC:CASP1), ma(GOBP:proteolysis)) -> act(p(HGNC:IL18))
p(HGNC:IFNG) -> r(HGNC:IL18)
r(HGNC:IL18) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "IL-18 binding to its receptor complex can lead to activation of JNK \
and MAPK p38, which can activate pro-apoptotic signaling pathways both \
in intrinsic and extrinsic."

p(HGNC:IL18) => complex(p(HGNC:IL18), p(HGNC:IL18R1))
complex(p(HGNC:IL18), p(HGNC:IL18R1)) -> act(p(HGNC:MAPK8))
complex(p(HGNC:IL18), p(HGNC:IL18R1)) -> act(p(HGNC:MAPK14), ma(kin))
act(p(HGNC:MAPK8)) -> bp(MESHPP:Apoptosis)
act(p(HGNC:MAPK14), ma(kin)) -> bp(MESHPP:Apoptosis)

SET Support = "In addition, studies have shown that IL-18 is cytotoxic to \
cardiomyocytes, which leads to increased intracellular Ca2+ levels, \
and calcium dysregulation plays an important role in the pathogenesis \
of AD (112)."

p(HGNC:IL18) -| a(MESHA:"Myocytes, Cardiac")
a(MESHA:"Myocytes, Cardiac") -| a(CHEBI:"calcium(2+)", loc(MESHA:Cytosol))
a(CHEBI:"calcium(2+)", loc(MESHA:Cytosol)) -- path(MESHD:"Alzheimer Disease")

SET Support = "More specifically, IL-18 inhibits the induction of LTP in the dentate \
gyrus, a paradigm for the cellular mechanisms underlying learning and \
memory (84)."

SET MeSHAnatomy = "Dentate Gyrus"
p(HGNC:IL18) -| bp(GOBP:"long-term synaptic potentiation")
UNSET MeSHAnatomy

SET Support = "In 2012, Sutinen et al. proved IL-18 can increase production of APP \
and its Thr668 phosphorylation in neuron-like differentiated human SH- \
SY5Y cells."

SET CellLine = "SH-SY5Y cell"
p(HGNC:IL18) -> p(HGNC:APP)
p(HGNC:IL18) -> p(HGNC:APP, pmod(Ph, Thr, 668))
UNSET CellLine

SET Support = "It can also increase amyloidogenic processing to Aβ by inducing \
expression of BACE-1 and N-terminal fragment (NTF) of PS-1, part of \
the functional γ-secretase complex (83)."

p(HGNC:IL18) -> p(HGNC:BACE1)
p(HGNC:IL18) -> p(HGNC:PSEN1, frag("1_?"))
complex(SCOMP:"gamma Secretase Complex") hasComponent p(HGNC:PSEN1, frag("1_?"))
p(HGNC:BACE1) -> a(CHEBI:"amyloid-beta")
complex(SCOMP:"gamma Secretase Complex") -> a(CHEBI:"amyloid-beta")

UNSET Section

# Did not include "Pro-inflammatory cytokine modulation as a therapeutic target for AD" section, did not have any molecular interactions,
# just drug interactions which we currently are not looking for.

UNSET PublicationType

##################################################################################

SET Citation = {"PubMed", "Biomed Res Int. 2016;2016:8090918. doi: 10.1155/2016/8090918. Epub 2016 Jun 27.", "27429982"}

SET PublicationType = "Review"

SET Section = "CX3CL1/CX3CR1 and Microglia"

SET Support = "CX3CL1 is expressed with particularly high levels in hippocampal and \
cortical neurons constitutively but none on microglia [15]."

a(MESHA:Neurons, loc(MESHA:Hippocampus)) -> p(HGNC:CX3CL1)
a(MESHA:Neurons, loc(MESHA:"Cerebral Cortex")) -> p(HGNC:CX3CL1)
a(MESHA:Microglia) cnc p(HGNC:CX3CL1)

SET Support = "It exists in both secreted and membrane-bound form and its membrane- \
tethered mucin stalk acts as a cell adhesion molecule adhering to \
microglia during an inflammatory reaction [16]."

p(HGNC:CX3CL1, loc(MESHA:"Cell Membrane"))
p(HGNC:CX3CL1, loc(MESHA:"Extracellular Space"))
complex(p(HGNC:CX3CL1), a(MESHA:Microglia))
path(MESHD:Inflammation) positiveCorrelation complex(p(HGNC:CX3CL1), a(MESHA:Microglia))

SET Support = "The membrane-bound form can be cleaved in the condition of cathepsin \
S, ADAM-10, and ADAM-17; then the soluble one can serve as a signaling \
molecule mediating neural/microglial interactions via its sole \
receptor CX3CR1 that is mainly expressed on microglia and partly on \
astrocyte as well as on neurons in the CNS [17–19]."

act(p(HGNC:CTSS), ma(GOBP:proteolysis)) => p(HGNC:CX3CL1, loc(MESHA:"Extracellular Space"))
act(p(HGNC:ADAM10), ma(GOBP:proteolysis)) => p(HGNC:CX3CL1, loc(MESHA:"Extracellular Space"))
act(p(HGNC:ADAM17), ma(GOBP:proteolysis)) => p(HGNC:CX3CL1, loc(MESHA:"Extracellular Space"))
p(HGNC:CX3CL1, loc(MESHA:"Extracellular Space")) => complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1))
complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) reg complex(a(MESHA:Neurons), a(MESHA:Microglia))
a(MESHA:Microglia) -> p(HGNC:CX3CR1)
a(MESHA:Astrocytes) -> p(HGNC:CX3CR1)
a(MESHA:Neurons, loc(MESHA:"Central Nervous System")) -> p(HGNC:CX3CR1)

SET Support = "It has been demonstrated that Aβ burdened neurons inducing microglial \
activation may be an early phenomenon in the procession of AD [22]."

a(CHEBI:"amyloid-beta", loc(MESHA:Neurons)) -> bp(GOBP:"microglial cell activation")

SET Support = "This may be associated with microglia activation phenotype which \
includes M1 (iNOS+ microglia) and M2 (Arg+ microglia); iNOS+ microglia \
induce production of neuroinflammation factors while Arg+ microglia \
have enhanced phagocytic activity."

SET MicroglialActivationState = "M1"
a(MESHA:Microglia) -> p(HGNC:NOS2)
UNSET MicroglialActivationState
SET MicroglialActivationState = "M2"
a(MESHA:Microglia) -> p(HGNC:ARG1)
UNSET MicroglialActivationState
composite(p(HGNC:NOS2), a(MESHA:Microglia)) -> path(MESHD:Inflammation)
composite(p(HGNC:ARG1), a(MESHA:Microglia)) -> bp(GOBP:phagocytosis)

SET Support = "In accordance with this, greater numbers of Arg+ microglia containing \
Aβ were found when compared to iNOS+ microglia in the inflamed \
hemisphere [24]."

composite(p(HGNC:ARG1), a(MESHA:Microglia)) =| a(CHEBI:"amyloid-beta", loc(MESHA:Brain))

SET Support = "Neuronal soluble CX3CL1 is likely to alter the microglial state to a \
more neuroprotective one by acting on CX3CR1 in microglia [26]. his \
also has been confirmed that disruption of CX3CL1-CX3CR1 leads to \
dysregulate microglial responses and neuronal damage [12, 18]."

complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) reg a(MESHA:Microglia)

SET Support = "Besides, hAPP-CX3CR1−/− mice as well as hTau-CX3CR1−/− mice showed \
increased expression of inflammatory factors, enhanced tau \
phosphorylation, and exacerbated plaque-independent neuronal \
dysfunction and cognitive deficits [27, 28], while researches also \
demonstrated that both APP-PS1/CX3CR1−/− and CRND8/CX3CR1−/− mice \
showed reduction in Aβ deposition with increased number of microglia \
[29, 30]."

SET Species = "10090"
SET KnockoutMice = {"hTau-CX3CR1−/−", "hAPP-CX3CR1−/−"}
p(MGI:Cx3cr1) negativeCorrelation path(MESHD:Inflammation)
p(MGI:Cx3cr1) -| p(HGNC:MAPT, pmod(Ph))
p(MGI:Cx3cr1) negativeCorrelation path(HP:"Cognitive impairment")
UNSET KnockoutMice
SET KnockoutMice = {"APP-PS1/CX3CR1−/−", "CRND8/CX3CR1−/−"}
p(MGI:Cx3cr1) -> a(CHEBI:"amyloid-beta")
p(MGI:Cx3cr1) -| a(MESHA:Microglia)
UNSET Species
UNSET KnockoutMice

SET Support = "Overexpression of soluble CX3CL1 by adeno-associated viral (AAV) \
vectors plays an active role in reducing tau pathology and neuron \
loss, while it has no effect on Aβ deposition indicating that \
additional CX3CL1 signaling has no additive effect on Aβ deposition \
[26, 33]."

SET ProteinExpressionMethod = "adeno-associated viral vectors"
p(HGNC:CX3CL1) -| p(HGNC:MAPT, pmod(Ph))
p(HGNC:CX3CL1) -> a(MESHA:Neurons)
p(HGNC:CX3CL1) cnc a(CHEBI:"amyloid-beta")
UNSET ProteinExpressionMethod

SET Support = "The expression of CX3CL1 is decreased in cerebral cortex and \
hippocampus of APP transgenic mice while it is increased in tau- \
injured neurons [36, 37]."

SET Species = "10090"
SET KnockoutMice = "App transgenic"
a(MESHA:"Cerebral Cortex") negativeCorrelation p(MGI:Cx3cl1)
a(MESHA:Hippocampus) negativeCorrelation p(MGI:Cx3cl1)
composite(a(MESHA:Neurons), p(MGI:Mapt)) positiveCorrelation p(MGI:Cx3cl1)
UNSET KnockoutMice
UNSET Species

UNSET Section

SET Section = "CX3CL1/CX3CR1 and Neuroinflammation"

SET Support = "Neuroinflammation is classically attributed to Aβ deposition and plays \
a vital role in the pathological progress of AD [5, 39]."

SET MeSHAnatomy = "Brain"
a(CHEBI:"amyloid-beta") -> path(MESHD:Inflammation)
path(MESHD:Inflammation) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET MeSHAnatomy

SET Support = "It is always correlated with increased levels of proinflammatory \
cytokines including tumor necrosis factor-alpha (TNF-α), interleukin-6 \
(IL-6), IL-1β, interferon gamma (IFN-γ), and chemokine (C–C motif) \
ligand 2 (CCL2) and C–X–C motif chemokine 10 (CXCL10/IP-10) [40]."

SET MeSHAnatomy = "Brain"
path(MESHD:Inflammation) positiveCorrelation p(HGNC:TNF)
path(MESHD:Inflammation) positiveCorrelation p(HGNC:IL6)
path(MESHD:Inflammation) positiveCorrelation p(HGNC:IFNG)
path(MESHD:Inflammation) positiveCorrelation p(HGNC:IL1B)
path(MESHD:Inflammation) positiveCorrelation p(HGNC:CCL2)
path(MESHD:Inflammation) positiveCorrelation p(HGNC:CXCL10)
UNSET MeSHAnatomy

SET Support = "CX3CL1, which is identified inhibiting the production of TNF-α, nitric \
oxide (NO), and superoxide in neuron-glial cell cultures [41], has \
been implicated as an endogenous neuronal modulator and may limit \
microgliosis in AD by reducing the inflammatory reaction [37, 42, 43]."

SET StudyType = "in vitro"
SET MeSHAnatomy = {"Neurons", "Neuroglia"}
p(HGNC:CX3CL1) -| p(HGNC:TNF)
p(HGNC:CX3CL1) -| a(CHEBI:"nitric oxide")
p(HGNC:CX3CL1) -| a(CHEBI:superoxide)
UNSET StudyType
UNSET MeSHAnatomy
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:CX3CL1) -| path(MESHD:Inflammation)
path(MESHD:Inflammation) -> bp(GOBP:"microglial cell activation")
UNSET MeSHDisease

SET Support = "TNF-α, a prototypic proinflammatory cytokine, is mainly released by \
activated microglia, colocalized with Aβ deposition, and is elevated \
in the cortex of animal models and human with AD [44–46]."

bp(GOBP:"cytokine production involved in inflammatory response") => p(HGNC:TNF)
a(MESHA:Microglia, loc(CHEBI:"amyloid-beta")) positiveCorrelation bp(GOBP:"microglial cell activation")
a(MESHA:Microglia, loc(CHEBI:"amyloid-beta")) => p(HGNC:TNF)
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TNF) positiveCorrelation a(MESHA:"Cerebral Cortex")
SET Species = "10090"
p(MGI:Tnf) positiveCorrelation a(MESHA:"Cerebral Cortex")
UNSET Species
UNSET MeSHDisease

SET Support = "It has been shown that glial TNF-α enhances Aβ deposition through \
inhibiting BACE1 expression and Aβ clearance and promotes neuronal \
cell cycle events which are toxic for terminally differentiated \
neurons in the pathogenesis of AD [47, 48]."

p(HGNC:TNF, loc(MESHA:Neuroglia)) -> a(CHEBI:"amyloid-beta")
p(HGNC:TNF, loc(MESHA:Neuroglia)) -| p(HGNC:BACE1)
p(HGNC:TNF, loc(MESHA:Neuroglia)) -| bp(GOBP:"beta-amyloid clearance")
p(HGNC:TNF, loc(MESHA:Neuroglia)) -| a(MESHA:Neurons)

SET Support = "TNF-α actives TNFR1 leading to neuron death while TNFR2 which is \
expressed primarily by microglia [50] is beneficial to control \
microglia activity in the progression of AD [51]."

complex(p(HGNC:TNF), p(HGNC:TNFRSF1A)) => act(p(HGNC:TNFRSF1A))
act(p(HGNC:TNFRSF1A)) -| a(MESHA:Neurons)
a(MESHA:Microglia) => p(HGNC:TNFRSF1B)
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TNFRSF1B) reg a(MESHA:Microglia)
UNSET MeSHDisease

SET Support = "Fewer Aβ plaques and Aβ-related lesions developed in APP23/TNFR1−/− \
mice when compared with APP23/TNFR1+/+ littermates [52]."

SET Species = "10090"
SET KnockoutMice = "APP23/TNFR1−/−"
p(MGI:Tnfrsf1a) -> path(MESHD:"Plaque, Amyloid")
UNSET KnockoutMice
UNSET Species

SET Support = "However, Barger et al. suggested that TNF-α protects hippocampal \
neurons against Aβ toxicity [53]. Both 3xTg-AD lacking TNF-R1+R2 and \
3xTg-ADxTNF-R1/R2 knock-out exhibit enhanced Aβ and tau-related \
pathological features by the age of 15 months, in stark contrast to \
age-matched 3xTg-AD counterparts [54]."

SET Species = "10090"
SET MouseStrain = "3xTg-AD"
SET AgeRange = "15 months"
composite(p(MGI:Tnfrsf1a), p(MGI:Tnfrsf1b)) -| a(CHEBI:"amyloid-beta")
composite(p(MGI:Tnfrsf1a), p(MGI:Tnfrsf1b))-| p(MGI:Mapt, pmod(Ph))
UNSET AgeRange
UNSET MouseStrain
UNSET Species

SET Support = "Loss of opposing TNFR2 leads to a stage-independent increase in Iba-1 \
positive microglia, and TNFR1 mediated exacerbation of Aβ and tau \
pathology in aged 3xTg-AD mice [55]."

SET Species = "10090"
SET MouseStrain = "3xTg-AD"
# Iba1 == Aif1
p(MGI:Tnfrsf1b) -| p(MGI:Aif1, loc(MESHA:Microglia))
p(MGI:Tnfrsf1b) -| p(MGI:Mapt, pmod(Ph))
p(MGI:Tnfrsf1b) -| a(CHEBI:"amyloid-beta")
p(MGI:Tnfrsf1a) -> p(MGI:Mapt, pmod(Ph))
p(MGI:Tnfrsf1a) -> a(CHEBI:"amyloid-beta")
UNSET MouseStrain
UNSET Species

SET Support = "Thus suggesting the role of CX3CL1/CX3CR1 which inhibits TNF-α \
secretion [56] may be divaricated dependent on TNFR."

complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) -| p(HGNC:TNF)

SET Support = "But in view of the fact that TNFR1 is increased by 17–28% and TNFR2 is \
significantly decreased by 35–43% in AD brains [57], CX3CL1/CX3CR1 \
inclines to play a beneficial role in the pathogenesis of AD."

path(MESHD:"Alzheimer Disease") positiveCorrelation p(HGNC:TNFRSF1A, loc(MESHA:Brain))
path(MESHD:"Alzheimer Disease") negativeCorrelation p(HGNC:TNFRSF1B, loc(MESHA:Brain))
complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) positiveCorrelation path(MESHD:"Alzheimer Disease")

SET Support = "The expression of another inflammatory cytokine IL-1β is also \
increased in the CX3CR1-deficient APP/PS1 animals [29]."

SET Species = "10090"
SET MouseStrain = "APP/PS1"
p(MGI:Cx3cr1) -| p(MGI:Il1b)
UNSET MouseStrain
UNSET Species

SET Support = "CX3CR1 deficiency promotes impairment of cognitive function, synaptic \
plasticity, and tau hyperphosphorylation via increasing action of \
IL-1β and the impairment could be reversed by infusion with IL-1β \
receptor antagonist significantly [28, 42]."

p(HGNC:CX3CR1) -| act(p(HGNC:IL1B))
act(p(HGNC:IL1B)) -> path(HP:"Cognitive impairment")
act(p(HGNC:IL1B)) -| bp(MESHPP:"Neuronal Plasticity")
act(p(HGNC:IL1B)) -> p(HGNC:MAPT, pmod(Ph))

SET Support = "On the other hand, the upregulated expression of chronic IL-1β \
increases plaque-associated microglia and ameliorates amyloid \
pathology in the APP/PS1 mouse model of AD [60, 61]."

SET Species = "10090"
SET MouseStrain = "APP/PS1"
p(MGI:Il1b) -> a(MESHA:Microglia, loc(MESHD:"Plaque, Amyloid"))
a(MESHA:Microglia, loc(MESHD:"Plaque, Amyloid")) -| a(CHEBI:"amyloid-beta")
UNSET MouseStrain
UNSET Species

SET Support = "In addition, CX3CL1 dose-dependently suppressed the production of \
nitric oxide (NO) [10]."

p(HGNC:CX3CL1) -| a(CHEBI:"nitric oxide")

SET Support = "NO, related to the increased levels of IFN-γ and TNF-α [62], has been \
involved in neuroinflammation with increased expression of inducible \
NO synthase (iNOS) at mild and severe stages of AD [63]."

a(CHEBI:"nitric oxide") positiveCorrelation p(HGNC:IFNG)
a(CHEBI:"nitric oxide") positiveCorrelation p(HGNC:TNF)
SET MeSHAnatomy = "Brain"
a(CHEBI:"nitric oxide") positiveCorrelation path(MESHD:Inflammation)
UNSET MeSHAnatomy
SET MeSHDisease = "Alzheimer Disease"
a(CHEBI:"nitric oxide") positiveCorrelation p(HGNC:NOS2)
UNSET MeSHDisease

SET Support = "In all, CX3CL1-CX3CR1 inhibits microglia activity via controlling the \
overproduction of inflammatory mediators."

complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) reg bp(GOBP:"cytokine production involved in inflammatory response")
bp(GOBP:"cytokine production involved in inflammatory response") -> bp(GOBP:"microglial cell activation")

SET Support = "Apart from AD, CX3CL1/CX3CR1 is also involved in other \
neuroinflammation disorders, including Parkinson's Disease (PD) [71, \
72], multiple sclerosis (MS) [73], tauopathies [33], and age-related \
macular degeneration (ARMD) [74]."

p(HGNC:CX3CL1) -- path(MESHD:"Parkinson Disease")
p(HGNC:CX3CL1) -- path(MESHD:"Multiple Sclerosis")
p(HGNC:CX3CL1) -- path(MESHD:Tauopathies)
p(HGNC:CX3CL1) -- path(MESHD:"Macular Degeneration")
p(HGNC:CX3CR1) -- path(MESHD:"Parkinson Disease")
p(HGNC:CX3CR1) -- path(MESHD:"Multiple Sclerosis")
p(HGNC:CX3CR1) -- path(MESHD:Tauopathies)
p(HGNC:CX3CR1) -- path(MESHD:"Macular Degeneration")

SET Support = "Drugs that attenuate neuronal degeneration and improve learning and \
memory ability are accompanied by reduced TNF-α, IL-1β, TGF-β, and NO \
levels induced by Aβ in CSF in mouse models and patients with AD \
[65–70]."

SET MeSHDisease = "Alzheimer Disease"
a(CHEBI:"amyloid-beta") -> p(HGNC:TNF, loc(MESHA:"Cerebrospinal Fluid"))
a(CHEBI:"amyloid-beta") -> p(HGNC:IL1B, loc(MESHA:"Cerebrospinal Fluid"))
a(CHEBI:"amyloid-beta") -> p(HGNC:TGFB1, loc(MESHA:"Cerebrospinal Fluid"))
a(CHEBI:"amyloid-beta") -> p(CHEBI:"nitric oxide", loc(MESHA:"Cerebrospinal Fluid"))
SET Species = "10090"
a(CHEBI:"amyloid-beta") -> p(MGI:Tnf, loc(MESHA:"Cerebrospinal Fluid"))
a(CHEBI:"amyloid-beta") -> p(MGI:Il1b, loc(MESHA:"Cerebrospinal Fluid"))
a(CHEBI:"amyloid-beta") -> p(MGI:Tgfb1, loc(MESHA:"Cerebrospinal Fluid"))
a(CHEBI:"amyloid-beta") -> p(CHEBI:"nitric oxide", loc(MESHA:"Cerebrospinal Fluid"))
UNSET Species
UNSET MeSHDisease

UNSET Section

SET Section = "CX3CL1/CX3CR1 Regulates Synaptic Plasticity"

SET Support = "Synaptic plasticity plays an important role in learning and memory, \
and Aβ-induced synaptic dysfunction is strongly associated with AD \
[76]."

a(CHEBI:"amyloid-beta") -| bp(GOBP:"synaptic signaling")
bp(GOBP:"synaptic signaling") -- path(MESHD:"Alzheimer Disease")

SET Support = "CX3CL1 is upregulated in the rat hippocampus during memory-associated \
synaptic plasticity [77]."

SET Species = "10116"
p(RGD:Cx3cl1, loc(MESHA:Hippocampus)) positiveCorrelation bp(MESHPP:"Neuronal Plasticity")
UNSET Species

SET Support = "It is considered as a potent neuromodulator of the evoked excitatory \
synaptic transmission and plays a major role in synaptic plasticity \
and neuroprotection [78]."

p(HGNC:CX3CL1) reg bp(GOBP:"synaptic signaling")
p(HGNC:CX3CL1) -- bp(MESHPP:"Neuronal Plasticity")
p(HGNC:CX3CL1) -- bp(MESHPP:Neuroprotection)

SET Support = "Although the underlying mechanisms have been underexplored, \
CX3CL1/CX3CR1 may mediate synaptic plasticity and cognitive function \
mainly by regulating long-term potentiation (LTP) [80], NO signaling, \
and production of brain-derived neurotrophic factor (BDNF) [81]."

complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) reg bp(GOBP:"long-term synaptic potentiation")
complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) reg bp(GOBP:"nitric oxide mediated signal transduction")
complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) reg p(HGNC:BDNF)

SET Support = "LTP is thought to be related to the storage of declarative memory in \
the mammalian brain [82]."

bp(GOBP:"long-term synaptic potentiation") -- bp(GOBP:"long-term memory")

SET Support = "CX3CL1 clearly interferes with LTP mechanisms and its modulation of \
neuronal plasticity appears to be mediated through activation of \
adenosine [80]."

p(HGNC:CX3CL1) -> a(CHEBI:adenosine)
a(CHEBI:adenosine) reg bp(MESHPP:"Neuronal Plasticity")

SET Support = "Adenosine acts as a neuromodulator with four types of G protein- \
coupled receptors, termed A1, A2A, A2B, and A3, and exerts important \
functions in the synaptic plasticity [83]."

# A1 == ADORA1, A2A == ADORA2A, A2B == ADORA2B, A3 == ADORA3
complex(a(CHEBI:adenosine), p(HGNC:ADORA1)) reg bp(MESHPP:"Neuronal Plasticity")
complex(a(CHEBI:adenosine), p(HGNC:ADORA2A)) reg bp(MESHPP:"Neuronal Plasticity")
complex(a(CHEBI:adenosine), p(HGNC:ADORA2B)) reg bp(MESHPP:"Neuronal Plasticity")
complex(a(CHEBI:adenosine), p(HGNC:ADORA3)) reg bp(MESHPP:"Neuronal Plasticity")

SET Support = "Intracerebroventricular injection of Aβ 1–42 inhibited not only NMDA \
receptor-dependent LTP but also voltage-activated Ca2+-dependent LTP \
induced by strong conditioning stimulation during NMDAR blockade [84], \
indicating that there is a non-NMDAR-dependent but Ca2+-dependent \
pathway involved in synaptic dysfunction in AD."

p(HGNC:APP, frag("1_42")) -| act(p(GFAM:"Glutamate ionotropic receptor NMDA type subunits"))
act(p(GFAM:"Glutamate ionotropic receptor NMDA type subunits")) -> bp(GOBP:"long-term synaptic potentiation")
p(HGNC:APP, frag("1_42")) -| a(CHEBI:"calcium(2+)")
a(CHEBI:"calcium(2+)") -> bp(GOBP:"long-term synaptic potentiation")
a(CHEBI:"calcium(2+)") -- path(MESHD:"Alzheimer Disease")

SET Support = "CX3CL1 increases NMDA-fast excitatory postsynaptic potentials by a \
mechanism involving the activity of the adenosine receptor type A2 \
(A2AR) and the release of the NMDAR coagonist D-serine [85]."

p(HGNC:CX3CL1) -> act(p(HGNC:ADORA2A))
p(HGNC:CX3CL1) -> a(CHEBI:D-serine)
complex(a(CHEBI:D-serine), p(GFAM:"Glutamate ionotropic receptor NMDA type subunits"))

SET Support = "An increased density of A2AR on microglia has been detected in human \
cortex from AD patients [87]."

SET MeSHAnatomy = "Brain"
p(HGNC:ADORA2A, loc(MESHA:Microglia)) positiveCorrelation path(MESHD:"Alzheimer Disease")
UNSET MeSHAnatomy

SET Support = "Thus indicating CX3CL1/CX3CR1 may activate A2AR by increasing \
adenosine and promote the release of D-serine; then D-serine enhances \
the function of NMDAR and facilitates LTP."

complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) -> act(p(HGNC:ADORA2A))
complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) -> a(CHEBI:adenosine)
complex(a(CHEBI:adenosine), p(HGNC:ADORA2A)) => act(p(HGNC:ADORA2A))
complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) -> a(CHEBI:D-serine)
a(CHEBI:D-serine) -> act(p(GFAM:"Glutamate ionotropic receptor NMDA type subunits"))

SET Support = "Stimulation of A3R induces an intracellular signaling that increases \
calcium concentrations [89]."

act(p(HGNC:ADORA3)) -> a(CHEBI:"calcium(2+)", loc(MESHA:"Intracellular Space"))
a(CHEBI:"calcium(2+)", loc(MESHA:"Intracellular Space")) -> bp(GOBP:"calcium-mediated signaling")

SET Support = "The phosphorylation of CAMKII and cyclic adenosine monophosphate \
response element-binding protein (CREB) is important to hippocampal \
long-term synaptic plasticity [90]."

SET MeSHAnatomy = "Hippocampus"
p(HGNC:CAMK2A, pmod(Ph)) -> bp(MESHPP:"Neuronal Plasticity")
p(SFAM:"CREB Family", pmod(Ph)) -> bp(MESHPP:"Neuronal Plasticity")
UNSET MeSHAnatomy

SET Support = "Besides, the reduction of CREB activation also leads to memory \
impairment [92]."

act(p(SFAM:"CREB Family")) -> bp(GOBP:"long-term memory")

SET Support = "Based on the information given above, we can hypothesize the way \
CX3CL1 affects LTP; that is, CX3CL1 acts with CX3CR1 on the surface of \
the microglia and stimulates the release of adenosine; adenosine then \
activates A2AR and promotes synaptic facilitation by NMDAR-dependent \
pathway, activates A3R simultaneity, and induces synaptic inhibition \
by a Ca2+-dependent pathway."

p(HGNC:CX3CL1) -- bp(GOBP:"long-term synaptic potentiation")
complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) -> a(CHEBI:adenosine)
a(CHEBI:adenosine) => act(p(HGNC:ADORA2A))
act(p(HGNC:ADORA2A)) -> act(p(GFAM:"Glutamate ionotropic receptor NMDA type subunits"))
a(CHEBI:adenosine) => act(p(HGNC:ADORA3))
act(p(HGNC:ADORA3)) -> a(CHEBI:"calcium(2+)", loc(MESHA:"Intracellular Space"))

SET Support = "Brain-derived neurotrophic factor (BDNF), an important growth factor \
in the CNS, is of great significance for neurons to maintain the \
survival, growth, differentiation, repair, and regeneration after \
nerve injury as well as increasing synaptic plasticity."

p(HGNC:BDNF) -| bp(GOBP:"neuron death")
p(HGNC:BDNF) -> bp(GOBP:"neuron differentiation")
p(HGNC:BDNF) -> bp(GOBP:"long-term synaptic potentiation")

SET Support = "A clinical study involving 535 old participants who underwent annual \
cognitive assessments and brain autopsy at death showed that higher \
brain BDNF expression is associated with slower cognitive decline and \
BDNF may also reduce the deleterious effects of AD pathology on \
cognitive decline [93]."

p(HGNC:BDNF) negativeCorrelation path(HP:"Cognitive impairment")

SET Support = "Studies have shown that Aβ induces decreased anterograde as well as \
retrograde transport of BDNF vesicles in hippocampal neurons of \
various AD models [94]."

SET MeSHDisease = "Alzheimer Disease"
a(CHEBI:"amyloid-beta") -| p(HGNC:BDNF, loc(MESHA:Hippocampus))
UNSET MeSHDisease

SET Support = "Upregulation of BDNF by activating of ERK/CREB pathway can ameliorate \
Aβ-induced neurons loss and dendritic atrophy [95]."

p(SFAM:"MAPK Erk1/2 Family", pmod(Ph)) -> act(p(SFAM:"CREB Family"))
act(p(SFAM:"CREB Family")) -> p(HGNC:BDNF)
a(CHEBI:"amyloid-beta") -| a(MESHA:Neurons)
a(CHEBI:"amyloid-beta") -| bp(GOBP:"dendrite regeneration")
p(HGNC:BDNF) -> a(MESHA:Neurons)
p(HGNC:BDNF) -> bp(GOBP:"dendrite regeneration")

SET Support = "Restoration of normal neuronal BDNF expression levels in the cerebral \
hippocampi and cortices ameliorates the impairment in recognition \
memory and associative learning in mice of AD [96]."

SET MeSHAnatomy = "Hippocampus"
SET Species = "10090"
SET MeSHDisease = "Alzheimer Disease"
p(MGI:Bdnf, loc(MESHA:Neurons)) -> bp(GOBP:"long-term memory")
UNSET Species
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Support = "Importantly, BDNF concentrations are associated with CX3CL1 [97]."

p(HGNC:BDNF) -- p(HGNC:CX3CL1)

SET Support = "In addition, NO is also consistently involved in recognition memory \
[99]."

a(CHEBI:"nitric oxide") -- bp(GOBP:"long-term memory")

SET Support = "As mentioned before, CX3CL1/CX3CR1 inhibits the expression of NO in \
activated microglia cells [10, 100] and may induce synaptic \
inhibition."

complex(p(HGNC:CX3CL1), p(HGNC:CX3CR1)) -| a(CHEBI:"nitric oxide", loc(MESHA:Microglia))
bp(GOBP:"microglial cell activation") -> a(CHEBI:"nitric oxide", loc(MESHA:Microglia))


UNSET Section
UNSET PublicationType

##################################################################################

SET Citation = {"PubMed", "Signal Transduct Target Ther. 2017;2. pii: 17023. doi: 10.1038/sigtrans.2017.23. Epub 2017 Jul 14.", "29158945"}

SET PublicationType = "Review"

# First section was only a couple sentences introducing the topics
SET Section = "Function of NF-κB in innate immune cells"

SET Support = "Innate immune cells, including macrophages, dendritic cells and \
neutrophils, are important players of innate immunity and \
inflammation."

a(MESHA:Macrophages) -- bp(GOBP:"innate immune response")
a(MESHA:Neutrophils) -- bp(GOBP:"innate immune response")
a(MESHA:"Dendritic Cells") -- bp(GOBP:"innate immune response")
a(MESHA:Macrophages) -- path(MESHD:Inflammation)
a(MESHA:Neutrophils) -- path(MESHD:Inflammation)
a(MESHA:"Dendritic Cells") -- path(MESHD:Inflammation)

SET Support = "These cells express PRRs that detect various microbial components, the \
so-called pathogen-associated molecular patterns (PAMPs).18,19"

a(MESHA:Macrophages) -> p(MESHC:"Receptors, Pattern Recognition")
a(MESHA:Neutrophils) -> p(MESHC:"Receptors, Pattern Recognition")
a(MESHA:"Dendritic Cells") -> p(MESHC:"Receptors, Pattern Recognition")
complex(p(MESHC:"Receptors, Pattern Recognition"), a(MESHC:"Pathogen-Associated Molecular Pattern Molecules"))

SET Support = "A common signaling event of the PRRs is activation of the canonical \
NF-κB pathway, which is responsible for transcriptional induction of \
pro-inflammatory cytokines, chemokines and additional inflammatory \
mediators in different types of innate immune cells (Figure 1).2,22,23"

p(MESHC:"Receptors, Pattern Recognition") -> act(complex(SCOMP:"Nfkb Complex"))
act(complex(SCOMP:"Nfkb Complex")) -> bp(GOBP:"cytokine production involved in inflammatory response")
act(complex(SCOMP:"Nfkb Complex")) -> bp(GOBP:"chemokine production")

SET Support = "A signaling component that integrates the different PRR pathways for \
NF-κB activation is transforming growth factor-β-activated kinase 1 \
(TAK1).24,​25,​26"

# MAP3K7 == TAK1
p(HGNC:MAP3K7) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "TAK1 has two regulatory subunits, TAB1 and TAB2, the latter of which \
is capable of binding poly-ubiquitin chains, which is required for \
TAK1 activation."

p(HGNC:TAB1) reg p(HGNC:MAP3K7)
p(HGNC:TAB2) reg p(HGNC:MAP3K7)
complex(p(HGNC:TAB2), a(MESHC:Polyubiquitin)) => act(p(HGNC:MAP3K7), ma(kin))

SET Support = "Upon activation, TAK1 activates the downstream kinase IKK, thereby \
mediating IκBα phosphorylation and NF-κB activation.27"

complex(SCOMP:"Nfkb Complex") hasComponents list(p(HGNC:NFKB1), p(HGNC:NFKB2))
act(p(HGNC:MAP3K7), ma(kin)) => act(complex(SCOMP:"IkappaB Kinase Complex"), ma(kin))
act(complex(SCOMP:"IkappaB Kinase Complex"), ma(kin)) => act(complex(SCOMP:"Nfkb Complex"))

SET Support = "In response to diverse PAMPs and DAMPs, macrophages become rapidly \
activated and secrete a large array of cytokines and chemokines."

a(MESHC:"Pathogen-Associated Molecular Pattern Molecules") -> bp(GOBP:"macrophage activation")
bp(GOBP:"macrophage activation") -> p(MESHC:Cytokines)
bp(GOBP:"macrophage activation") -> p(MESHC:Chemokines)

SET Support = "M1 macrophages are characterized by the production of pro-inflammatory \
cytokines, such as IL-1, IL-6, IL-12, TNF-α and chemokines, involved \
in various inflammatory processes."

SET MacrophageActivationState = "M1"
a(MESHA:Macrophages) => p(HGNC:IL1A)
a(MESHA:Macrophages) => p(HGNC:IL6)
a(MESHA:Macrophages) => p(HGNC:IL12A)
a(MESHA:Macrophages) => p(HGNC:TNF)
UNSET MacrophageActivationState

SET Support = "The M1 macrophages also promote the differentiation of inflammatory T \
cells, including Th1 and Th17 cells, which in turn mediate \
inflammation.30,31"

SET MacrophageActivationState = "M1"
a(MESHA:Macrophages) -> bp(GOBP:"T-helper 1 cell differentiation")
a(MESHA:Macrophages) -> bp(GOBP:"T-helper 17 cell differentiation")
UNSET MacrophageActivationState
bp(GOBP:"T-helper 1 cell differentiation") -> path(MESHD:Inflammation)
bp(GOBP:"T-helper 17 cell differentiation") -> path(MESHD:Inflammation)

SET Support = "In contrast, M2 macrophages produce anti-inflammatory cytokines, such \
as IL-10 and IL-13, and are important for resolution of inflammation \
and mediating wound healing.32"

SET MacrophageActivationState = "M2"
a(MESHA:Macrophages) => p(HGNC:IL10)
a(MESHA:Macrophages) => p(HGNC:IL13)
UNSET MacrophageActivationState
p(HGNC:IL10) -| path(MESHD:Inflammation)
p(HGNC:IL13) -| path(MESHD:Inflammation)

SET Support = "In particular, the TLR4 ligand lipopolysaccharide (LPS) promotes \
macrophage differentiation toward M1 phenotype.24"

complex(a(CHEBI:lipopolysaccharide), p(HGNC:TLR4)) => act(p(HGNC:TLR4))
SET MacrophageActivationState = "M1"
act(p(HGNC:TLR4)) -> bp(GOBP:"macrophage differentiation")
UNSET MacrophageActivationState

SET Support = "LPS stimulates macrophage signaling via two different TLR adapters, \
MyD88 and TRIF.24"

a(CHEBI:lipopolysaccharide) -> act(p(HGNC:MYD88))
# TICAM1 == TRIF
a(CHEBI:lipopolysaccharide) -> act(p(HGNC:TICAM1))
act(p(HGNC:MYD88)) -> bp(GOBP:"macrophage cytokine production")
act(p(HGNC:TICAM1)) -> bp(GOBP:"macrophage cytokine production")

SET Support = "Genetic evidence suggests that the MyD88-dependent TLR pathway is \
crucial for M1 macrophage polarization and inducible expression of \
pro-inflammatory cytokines.33"

SET MacrophageActivationState = "M1"
act(p(HGNC:MYD88)) -> bp(GOBP:"macrophage differentiation")
act(p(HGNC:MYD88)) -> bp(GOBP:"cytokine production involved in inflammatory response")
UNSET MacrophageActivationState

SET Support = "The MyD88-dependent TLR signaling involves activation of IRAK family \
of kinases, which in turn stimulate the E3 ubiquitin ligase activity \
of TRAF6, allowing TRAF6 to undergo self-ubiquitination and to \
conjugate ubiquitin chains onto other signaling molecules that are \
involved in the activation of a ubiquitin-dependent kinase, TAK1.24,25"

act(p(HGNC:TLR4)) -> act(p(HGNC:MYD88))
act(p(HGNC:MYD88)) => act(p(MESHC:"Interleukin-1 Receptor-Associated Kinases"), ma(kin))
act(p(MESHC:"Interleukin-1 Receptor-Associated Kinases"), ma(kin)) => act(p(HGNC:TRAF6))
act(p(HGNC:TRAF6)) => p(HGNC:TRAF6, pmod(Ub))
p(HGNC:TRAF6, pmod(Ub)) => act(p(HGNC:MAP3K7), ma(kin))

SET Support = "Upon activation, TAK1 activates the downstream kinase IKK, which in \
turn phosphorylate the NF-κB inhibitor IκBα, leading to ubiquitin- \
dependent IκBα degradation and NF-κB activation.27"

act(p(HGNC:MAP3K7), ma(kin)) => act(complex(SCOMP:"IkappaB Kinase Complex"), ma(kin))
act(complex(SCOMP:"IkappaB Kinase Complex"), ma(kin)) => p(HGNC:NFKBIA, pmod(Ph))
p(HGNC:NFKBIA, pmod(Ph)) -> deg(p(HGNC:NFKBIA))
deg(p(HGNC:NFKBIA)) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "NF-κB is a key transcription factor of M1 macrophages and is required \
for induction of a large number of inflammatory genes, including those \
encoding TNF-α, IL-1β, IL-6, IL-12p40 and cyclooxygenase-2.30"

SET MacrophageActivationState = "M1"
act(complex(SCOMP:"Nfkb Complex")) => r(HGNC:TNF)
act(complex(SCOMP:"Nfkb Complex")) => r(HGNC:IL1B)
act(complex(SCOMP:"Nfkb Complex")) => r(HGNC:IL6)
act(complex(SCOMP:"Nfkb Complex")) => r(HGNC:IL12B)
# PTGS2 == cyclooxygenase-2
act(complex(SCOMP:"Nfkb Complex")) => r(HGNC:PTGS2)
UNSET MacrophageActivationState

SET Support = "A major function of the TRIF-dependent TLR signaling pathway is to \
mediate the induction of Type I IFNs and IFN-inducible genes.34"

act(p(HGNC:TICAM1)) -> bp(GOBP:"type I interferon production")

SET Support = "This pathway involves recruitment of TRAF proteins, particularly \
TRAF3, to TRIF and subsequent activation of TANK-binding kinase 1 \
(TBK1) and IKKɛ in a mechanism that is thought to involve TRAF3 \
ubiquitination and ubiquitin-dependent recruitment of TBK1 and \
IKKɛ.34,35"

act(p(HGNC:TICAM1)) -> p(HGNC:TRAF3)
p(HGNC:TRAF3) => act(p(HGNC:TBK1), ma(kin))
p(HGNC:TRAF3) => act(p(HGNC:IKBKE), ma(kin))

SET Support = "Activated TBK1 and IKKɛ then phosphorylate the transcription factor \
IRF3 and, thereby, induce IRF3 dimerization, leading to \
transcriptional induction of type I IFNs, IFN-α and IFN-β.30,34"

act(p(HGNC:TBK1), ma(kin)) => p(HGNC:IRF3, pmod(Ph))
act(p(HGNC:IKBKE), ma(kin)) => p(HGNC:IRF3, pmod(Ph))
p(HGNC:IRF3, pmod(Ph)) => complex(p(HGNC:IRF3), p(HGNC:IRF3))
complex(p(HGNC:IRF3), p(HGNC:IRF3)) => r(SFAM:"IFNA Family")
complex(p(HGNC:IRF3), p(HGNC:IRF3)) => r(HGNC:IFNB1)

SET Support = "In addition to IRF3 activation, the TRIF-dependent TLR pathway \
activates NF-κB though stimulation of the adapter kinase receptor- \
interacting protein 1 (RIP1).19,24"

act(p(HGNC:TICAM1)) -> act(p(HGNC:RIPK1), ma(kin))
act(p(HGNC:RIPK1), ma(kin)) -> act(complex(SCOMP:"Nfkb Complex"))

SET Support = "This latter function of TRIF signaling involves activation of an E3 \
ubiquitin ligase, Peli1, which conjugates lysine 63-linked ubiquitin \
chains to RIP1, thereby facilitating the recruitment and activation of \
IKK.36"

act(p(HGNC:TICAM1)) -> act(p(HGNC:PELI1), ma(GOBP:"protein K63-linked ubiquitination"))
act(p(HGNC:PELI1), ma(GOBP:"protein K63-linked ubiquitination")) => p(HGNC:RIPK1, pmod(Ub))
p(HGNC:RIPK1, pmod(Ub)) -> complex(SCOMP:"IkappaB Kinase Complex")
p(HGNC:RIPK1, pmod(Ub)) -> act(complex(SCOMP:"IkappaB Kinase Complex"), ma(kin))

SET Support = "As seen in the MyD88 pathway, TRIF-stimulated NF-κB mediates induction \
of inflammatory cytokine genes."

act(complex(SCOMP:"Nfkb Complex")) -> bp(GOBP:"cytokine production involved in inflammatory response")

UNSET Section

SET Section = "NF-κB function in T cells"

SET Support = "The activation of naive T cells is initiated upon engagement of the \
TCR by a specific antigen presented on antigen-presenting cells, \
mostly dendritic cells."

complex(complex(SCOMP:"T Cell Receptor Complex"), a(MESHA:"Dendritic Cells")) -> bp(GOBP:"T cell activation")

SET Support = "Canonical NF-κB members, RelA and c-Rel, have a central role in \
mediating TCR signaling and naive T-cell activation.38"

complex(SCOMP:"Nfkb Complex") hasComponents list(p(HGNC:RELA), p(HGNC:REL))
p(HGNC:RELA) -> bp(GOBP:"T cell activation")
p(HGNC:RELA) -> bp(GOBP:"T cell receptor signaling pathway")
p(HGNC:REL) -> bp(GOBP:"T cell activation")
p(HGNC:REL) -> bp(GOBP:"T cell receptor signaling pathway")

SET Support = "Deregulated NF-κB activation can cause aberrant T-cell activation, \
which is associated with autoimmune and inflammatory responses.39"

act(complex(SCOMP:"Nfkb Complex")) -> bp(GOBP:"T cell receptor signaling pathway")

SET Support = "NF-κB also plays a role in regulating T-cell differentiation and \
effector function."

complex(SCOMP:"Nfkb Complex") reg bp(GOBP:"T cell differentiation")
complex(SCOMP:"Nfkb Complex") reg bp(GOBP:"adaptive immune effector response")

SET Support = "Upon activation, CD4+ T cells differentiate into different subsets of \
effector T cells, including Th1, Th2, Th17 and T follicular (Tfh) \
cells, which secrete distinct cytokines and mediate different aspects \
of immune responses.37"

bp(GOBP:"CD4-positive, alpha-beta T cell activation") => bp(GOBP:"CD4-positive, alpha-beta T cell differentiation")
bp(GOBP:"CD4-positive, alpha-beta T cell differentiation") => a(MESHA:"Th1 Cells")
bp(GOBP:"CD4-positive, alpha-beta T cell differentiation") => a(MESHA:"Th17 Cells")
bp(GOBP:"CD4-positive, alpha-beta T cell differentiation") => a(MESHA:"Th2 Cells")
a(MESHA:"Th1 Cells") => bp(GOBP:"T-helper 1 cell cytokine production")
a(MESHA:"Th2 Cells") => bp(GOBP:"T-helper 2 cell cytokine production")
a(MESHA:"Th17 Cells") => bp(GOBP:"T-helper 17 type immune response")
bp(GOBP:"T-helper 1 cell cytokine production") => p(MESHC:Cytokines)
bp(GOBP:"T-helper 2 cell cytokine production") => p(MESHC:Cytokines)

SET Support = "Th1 and Th17 cells are generally considered as inflammatory T cells, \
since they mediate inflammatory responses against both infections and \
self-triggers, and are associated with various autoimmune and \
inflammatory conditions.2"

a(MESHA:"Th1 Cells") -> path(MESHD:Inflammation)
a(MESHA:"Th17 Cells") -> path(MESHD:Inflammation)

SET Support = "Th1 cells are characterized by the secretion of IFN-γ, a cytokine that \
both promotes cellular immunity and participate in inflammatory \
processes."

a(MESHA:"Th1 Cells") => p(HGNC:IFNG)
p(HGNC:IFNG) -- path(MESHD:Inflammation)

SET Support = "NF-κB promotes Th1 cell differentiation by regulating TCR signaling as \
well as functioning in innate immune cells to mediate induction of \
cytokines, such as IL-12, which promote Th1 differentiation.38"

complex(SCOMP:"Nfkb Complex") -> a(MESHA:"Th1 Cells")
complex(SCOMP:"Nfkb Complex") reg bp(GOBP:"T cell receptor signaling pathway")
complex(SCOMP:"Nfkb Complex") -> p(MESHC:Cytokines)
complex(SCOMP:"Nfkb Complex") -> p(HGNC:IL12A)
p(HGNC:IL12A) -> a(MESHA:"Th1 Cells")

SET Support = "Th17 cells are characterized by the secretion of IL-17, an \
inflammatory cytokine that recruits monocytes and neutrophils to the \
site of inflammation in response to invasion by pathogens or self- \
antigens."

a(MESHA:"Th17 Cells") => p(HGNC:IL17A)
p(HGNC:IL17A) isA p(MESHC:Cytokines)
p(HGNC:IL17A) -- path(MESHD:Inflammation)
p(HGNC:IL17A) -> a(MESHA:Monocytes)
p(HGNC:IL17A) -> a(MESHA:Neutrophils)

SET Support = "The differentiation of CD4+ T cells is regulated by both cytokines \
secreted by the antigen-presenting cells and other innate immune cells \
and T-cell intrinsic factors."

a(MESHA:"Antigen-Presenting Cells") => p(MESHC:Cytokines)
p(MESHC:Cytokines) reg bp(GOBP:"CD4-positive, alpha-beta T cell differentiation")

SET Support = "Canonical NF-κB regulates CD4+ T-cell differentiation via both \
regulation of cytokine production in innate immune cells and T-cell \
intrinsic mechanisms."

complex(SCOMP:"Nfkb Complex") reg bp(GOBP:"CD4-positive, alpha-beta T cell differentiation")
complex(SCOMP:"Nfkb Complex") reg bp(GOBP:"cytokine production")
bp(GOBP:"cytokine production") => p(MESHC:Cytokines)
p(MESHC:Cytokines) -> bp(GOBP:"CD4-positive, alpha-beta T cell differentiation")

SET Support = "Inhibition of NF-κB in T cells by transgenic expression of a \
degradation-resistant form of IκBα lacking its N-terminal sequence \
impairs Th1 responses.40"

p(HGNC:NFKBIA) =| act(complex(SCOMP:"Nfkb Complex"))
act(complex(SCOMP:"Nfkb Complex")) -> bp(GOBP:"T-helper 1 cell cytokine production")

SET Support = "The Th1 cell generation also requires c-Rel, which mainly functions by \
mediating induction of the Th1-polarizing cytokine in antigen- \
presenting cells.41"

p(HGNC:REL) -> p(MESHC:Cytokines, loc(MESHA:"Antigen-Presenting Cells"))
p(MESHC:Cytokines, loc(MESHA:"Antigen-Presenting Cells")) -> a(MESHA:"Th1 Cells")

SET Support = "NF-κB1 p50, on the other hand, is important for Th2 responses and \
allergic airway inflammation, which appears to involve induction of \
the lineage transcription factor Gata3.42"

# Frag size from Uniprot
p(HGNC:NFKB1, frag("1_433", "50kD")) -> bp(GOBP:"T-helper 2 cell cytokine production")
p(HGNC:NFKB1, frag("1_433", "50kD")) -> p(HGNC:GATA3)

SET Support = "Nfkb1 knockin mice that express p50 but not its precursor, the IκB- \
like molecule p105, display aberrant NF-κB activation and \
spontaneously develop colitis characterized by hyperproduction of Th17 \
cells.43"

SET Species = "10090"
# Frag size from Uniprot
p(MGI:Nfkb1, frag("1_431", "50kD")) -> act(complex(SCOMP:"Nfkb Complex"))
p(MGI:Nfkb1, frag("1_431", "50kD")) positiveCorrelation path(MESHD:Colitis)
p(MGI:Nfkb1, frag("1_431", "50kD")) positiveCorrelation a(MESHA:"Th17 Cells")
UNSET Species

SET Support = "Although p105 deficiency has no T-cell intrinsic effect on Th17 cell \
differentiation, the aberrant activation of NF-κB renders innate \
immune cells hyperresponsive to TLR stimulation for production of \
IL-6, a major cytokine-promoting Th17 differentiation.43"

# Uses same reference as above, so still in mice
SET Species = "10090"
p(MGI:Nfkb1, frag("1_971", "105kD")) cnc a(MESHA:"Th17 Cells")
act(complex(SCOMP:"Nfkb Complex")) -> p(MGI:Il6)
p(MGI:Il6) -> a(MESHA:"Th17 Cells")
UNSET Species

SET Support = "A T-cell intrinsic role of NF-κB in regulating Th17 responses was \
initially indicated by a finding that mice with T-cell-specific IKKβ \
deletion have impaired T-cell activation and are refractory to the \
induction of a Th17-dependent autoimmune disease, experimental \
autoimmune encephalomyelitis (EAE).44"

p(MGI:Ikbkb, loc(MESHA:T-Lymphocytes)) -> bp(GOBP:"T cell differentiation")

SET Support = "Subsequent work has definitively demonstrated a crucial role for c-Rel \
and RelA in mediating induction of the Th17 lineage transcription \
factor RORγt and the generation of Th17 cells.40,41"

p(HGNC:REL) -> p(HGNC:RORC)
p(HGNC:RELA) -> p(HGNC:RORC)
p(HGNC:RORC) -> a(MESHA:"Th17 Cells")

SET Support = "In CD4+ T cells, c-Rel also mediates TCR-stimulated expression of \
IL-21, a γc family cytokine important for the differentiation Th17 and \
Tfh cells.45"

p(HGNC:REL, loc(MESHA:"CD4-Positive T-Lymphocytes")) -> p(HGNC:IL21)
p(HGNC:IL21) -> a(MESHA:"Th17 Cells")

SET Support = "Mutant mice harboring NIK gene deletion or expressing a non- \
processible p100 displays impaired generation of Th1 and Th17 subsets \
of CD4+ effector T cells."

SET Species = "10090"
p(MGI:Map3k14) -> a(MESHA:"Th17 Cells")
p(MGI:Map3k14) -> a(MESHA:"Th1 Cells")
UNSET Species

SET Support = "Moreover, noncanonical NF-κB is required for the pathological effector \
function of Th17 cells in mediating neuroinflammation, which involves \
induction of the inflammatory cytokine GM-CSF.53"

SET MeSHAnatomy = "Brain"
a(MESHA:"Th17 Cells") -- path(MESHD:Inflammation)
UNSET MeSHAnatomy
# CSF2 == GM-CSF
a(MESHA:"Th17 Cells") => p(HGNC:CSF2)
complex(SCOMP:"Nfkb Complex") -> p(HGNC:CSF2, loc(MESHA:"Th17 Cells"))

SET Support = "T-cell activation is associated with inducible expression of several \
TNFRs, including CD27, CD30, OX40 and 4-1BB, which are engaged by \
their ligands on antigen-presenting cells.55"

bp(GOBP:"T cell activation") -> p(HGNC:CD27)
# TNFRSF8 == CD30
bp(GOBP:"T cell activation") -> p(HGNC:TNFRSF8)
# TNFRSF4 == OX40
bp(GOBP:"T cell activation") -> p(HGNC:TNFRSF4)
# TNFSF9 == 4-1BB
bp(GOBP:"T cell activation") -> p(HGNC:TNFSF9)
complex(p(HGNC:CD27), p(HGNC:CD70, loc(MESHA:"Antigen-Presenting Cells")))
complex(p(HGNC:TNFRSF8), p(HGNC:TNFSF8, loc(MESHA:"Antigen-Presenting Cells")))
complex(p(HGNC:TNFRSF4), p(HGNC:TNFSF4, loc(MESHA:"Antigen-Presenting Cells")))
complex(p(HGNC:TNFSF9), p(HGNC:TNFRSF9, loc(MESHA:"Antigen-Presenting Cells")))

SET Support = "Although some of the TNFR ligands are also expressed on activated T \
cells, the T cell–T cell interaction, especially under in vitro \
conditions, only trigger weak activation of noncanonical NF-κB, which \
can be greatly enhanced by cross-linking the TNFR OX40.51,53"

act(p(HGNC:TNFRSF4)) -> act(complex(SCOMP:"Nfkb Complex"))

UNSET Section

SET Section = "NF-κB in inflammasome regulation"

SET Support = "Inflammasomes are a group of intracellular multi-protein complexes \
assembled in response to PAMPs and DAMPs, and characterized by the \
activation of inflammatory caspases.21"

a(MESHC:"Pathogen-Associated Molecular Pattern Molecules") -> complex(GOCC:"inflammasome complex")
act(p(SFAM:"CASP Family")) -- complex(GOCC:"inflammasome complex")

SET Support = "Canonical inflammasomes are composed of a ligand-sensing receptor, \
which includes members of the NLR family as well as AIM2 (absent in \
melanoma 2), the adapter protein ASC (apoptosis-associated speck-like \
protein containing CARD) and pro-caspase 1.56"

# PYCARD == ASC
complex(GOCC:"inflammasome complex") hasComponents list(p(HGNC:AIM2), p(HGNC:PYCARD), p(HGNC:CASP1))

SET Support = "Among the well-characterized inflammasome receptors are NLRP1, NLRP3, \
NLRC4 and AIM2."

complex(p(HGNC:AIM2), p(HGNC:PYCARD), p(HGNC:CASP1)) isA complex(GOCC:"inflammasome complex")
complex(p(HGNC:NLRP1), p(HGNC:PYCARD), p(HGNC:CASP1)) isA complex(GOCC:"inflammasome complex")
complex(p(HGNC:NLRP3), p(HGNC:PYCARD), p(HGNC:CASP1)) isA complex(GOCC:"inflammasome complex")
complex(p(HGNC:NLRC4), p(HGNC:PYCARD), p(HGNC:CASP1)) isA complex(GOCC:"inflammasome complex")

SET Support = "Upon stimulation, the inflammasome receptors oligomerize and recruit \
pro-caspase 1 via ASC, thereby stimulating pro-caspase 1 processing \
and conversion to active caspase 1."

act(p(HGNC:AIM2)) => complex(p(HGNC:AIM2), p(HGNC:PYCARD), p(HGNC:CASP1))
act(p(HGNC:NLRP1)) => complex(p(HGNC:NLRP1), p(HGNC:PYCARD), p(HGNC:CASP1))
act(p(HGNC:NLRP3)) => complex(p(HGNC:NLRP3), p(HGNC:PYCARD), p(HGNC:CASP1))
act(p(HGNC:NLRC4)) => complex(p(HGNC:NLRC4), p(HGNC:PYCARD), p(HGNC:CASP1))
complex(p(HGNC:AIM2), p(HGNC:PYCARD), p(HGNC:CASP1)) => act(p(HGNC:CASP1), ma(GOBP:autolysis))
complex(p(HGNC:NLRP1), p(HGNC:PYCARD), p(HGNC:CASP1)) => act(p(HGNC:CASP1), ma(GOBP:autolysis))
complex(p(HGNC:NLRP3), p(HGNC:PYCARD), p(HGNC:CASP1)) => act(p(HGNC:CASP1), ma(GOBP:autolysis))
complex(p(HGNC:NLRC4), p(HGNC:PYCARD), p(HGNC:CASP1)) => act(p(HGNC:CASP1), ma(GOBP:autolysis))
act(p(HGNC:CASP1), ma(GOBP:autolysis)) => p(HGNC:CASP1, frag("120_297", "20kD"))
act(p(HGNC:CASP1), ma(GOBP:autolysis)) => p(HGNC:CASP1, frag("317_404", "10kD"))

SET Support = "Activated caspase 1 then cleaves pro-IL-1b and pro-IL-18 into their \
mature forms, leading to the secretion of these pro-inflammatory \
cytokines.57"

# active caspase-1 consists of two p10 units and two p20 units
act(complex(p(HGNC:CASP1, frag("120_297", "20kD")), p(HGNC:CASP1, frag("120_297", "20kD")), p(HGNC:CASP1, frag("317_404", "10kD")), p(HGNC:CASP1, frag("317_404", "10kD"))), ma(GOBP:proteolysis)) => p(HGNC:IL1B, frag("117_269", "mature"))
act(complex(p(HGNC:CASP1, frag("120_297", "20kD")), p(HGNC:CASP1, frag("120_297", "20kD")), p(HGNC:CASP1, frag("317_404", "10kD")), p(HGNC:CASP1, frag("317_404", "10kD"))), ma(GOBP:proteolysis)) => p(HGNC:IL18, frag("37_193", "mature"))
p(HGNC:IL1B, frag("117_269", "mature")) => sec(p(HGNC:IL1B, frag("117_269", "mature")))
p(HGNC:IL18, frag("37_193", "mature")) => sec(p(HGNC:IL18, frag("37_193", "mature")))

SET Support = "NLRP3 inflammasome is currently the most extensively studied \
inflammasome, which is composed of NLRP3, ASC and pro-caspase 1, as \
well as an essential regulatory protein, NIMA-related kinase 7 (NEK7)."

complex(p(HGNC:NLRP3), p(HGNC:PYCARD), p(HGNC:CASP1), p(HGNC:NEK7)) isA complex(GOCC:"inflammasome complex")

SET Support = "Activation of the NLRP3 inflammasome usually requires both a priming \
signal (signal 1) and an activation signal (signal 2). A major role of \
the priming signal is to induce the transcriptional expression of \
NLRP3 and pro-IL, since most cell types have insufficient levels of \
NLRP3 for inflammasome activation and do not constitutively express \
pro-IL-1β.60,61 In addition, emerging evidence suggests that signal 1 \
may also prime NLRP3 via post-translational mechanisms, such as NLRP3 \
deubiquitination.62,63 Typical inducers of signal 1 include microbial \
components, such as TLR ligands, and cytokines like TNF-α and IL-1β, \
which are known to activate NF-κB, a transcriptional activator of both \
NLRP3 and pro-IL-1β genes."

act(p(GFAM:"Toll like receptors")) -> act(complex(p(HGNC:NLRP3), p(HGNC:PYCARD), p(HGNC:CASP1), p(HGNC:NEK7)))
act(p(GFAM:"Toll like receptors")) -> r(HGNC:NLRP3)
act(p(GFAM:"Toll like receptors")) -> r(HGNC:IL1B)
p(HGNC:TNF) -> act(complex(p(HGNC:NLRP3), p(HGNC:PYCARD), p(HGNC:CASP1), p(HGNC:NEK7)))
p(HGNC:TNF) -> r(HGNC:NLRP3)
p(HGNC:TNF) -> r(HGNC:IL1B)
p(HGNC:TNF) -> act(complex(SCOMP:"Nfkb Complex"))
p(HGNC:IL1B) -> act(complex(p(HGNC:NLRP3), p(HGNC:PYCARD), p(HGNC:CASP1), p(HGNC:NEK7)))
p(HGNC:IL1B) -> r(HGNC:NLRP3)
p(HGNC:IL1B) -> r(HGNC:IL1B)
p(HGNC:IL1B) -> act(complex(SCOMP:"Nfkb Complex"))
act(complex(SCOMP:"Nfkb Complex")) => r(HGNC:NLRP3)
act(complex(SCOMP:"Nfkb Complex")) => r(HGNC:IL1B)

SET Support = "The second signal of inflammasome activation is triggered by various \
PAMPs and DAMPs, such as pore-forming toxins, viral RNAs, ATP and \
crystalline substances.60,64 These diverse stimuli are thought to \
activate NLRP3 via inducing different cellular events, including K+ \
efflux, Ca2+ signaling, mitochondrial and lysosomal damages that \
release substances such as reactive oxygen species, oxidized \
mitochondrial DNA and lysosomal proteases.61"

a(MESHC:"Pore Forming Cytotoxic Proteins") -> bp(GOBP:"calcium-mediated signaling")
a(CHEBI:ATP) -> bp(GOBP:"calcium-mediated signaling")
a(MESHC:"RNA, Viral") -> bp(GOBP:"calcium-mediated signaling")
a(MESHC:"Pore Forming Cytotoxic Proteins") -> sec(a(CHEBI:"potassium(1+)"))
a(CHEBI:ATP) -> sec(a(CHEBI:"potassium(1+)"))
a(MESHC:"RNA, Viral") -> sec(a(CHEBI:"potassium(1+)"))
a(MESHC:"Pore Forming Cytotoxic Proteins") -> a(CHEBI:"reactive oxygen species")
a(CHEBI:ATP) -> a(CHEBI:"reactive oxygen species")
a(MESHC:"RNA, Viral") -> a(CHEBI:"reactive oxygen species")
a(MESHC:"Pore Forming Cytotoxic Proteins") -> a(MESHC:"DNA, Mitochondrial")
a(CHEBI:ATP) -> a(MESHC:"DNA, Mitochondrial")
a(MESHC:"RNA, Viral") -> a(MESHC:"DNA, Mitochondrial")
a(MESHC:"Pore Forming Cytotoxic Proteins") -> a(MESHC:"lysosomal proteins")
a(CHEBI:ATP) -> a(MESHC:"lysosomal proteins")
a(MESHC:"RNA, Viral") -> a(MESHC:"lysosomal proteins")
bp(GOBP:"calcium-mediated signaling") -> act(p(HGNC:NLRP3))
sec(a(CHEBI:"potassium(1+)")) -> act(p(HGNC:NLRP3))
a(CHEBI:"reactive oxygen species") -> act(p(HGNC:NLRP3))
a(MESHC:"DNA, Mitochondrial") -> act(p(HGNC:NLRP3))
a(MESHC:"lysosomal proteins") -> act(p(HGNC:NLRP3))

SET Support = "However, the role of NF-κB signaling pathway in inflammasome \
regulation appears to be complex, since IKKβ-deficient macrophages \
display hyper-activation of caspase 1 and enhanced secretion of IL-1β \
upon LPS stimulation, and myeloid cell-conditional IKKβ knockout mice \
are more sensitive to endotoxin shock.67"

SET ChallengeType = "lipopolysaccharide"
p(HGNC:IKBKB, loc(MESHA:Macrophages)) -| act(p(HGNC:CASP1))
p(HGNC:IKBKB, loc(MESHA:Macrophages)) -| sec(p(HGNC:IL1B))
UNSET ChallengeType

SET Support = "The negative role of IKKβ in inflammasome activation appears to \
involve induction of autophagy, an intracellular degradation system \
that maintains cellular homeostasis through degradation of abnormal \
proteins and damaged organelles like mitochondria.68"

p(HGNC:IKBKB) -- bp(GOBP:autophagy)
bp(GOBP:autophagy) -- act(complex(GOCC:"inflammasome complex"))

SET Support = "Earlier studies suggest that IKK is important for induction of \
autophagy, which in turn negatively regulates inflammasome activation \
by maintaining healthy mitochondria to prevent release of reactive \
oxygen species mitochondrial DNA and possibly also by degrading major \
components of the inflammasome complex.69–72"

complex(SCOMP:"IkappaB Kinase Complex") -> bp(GOBP:autophagy)
bp(GOBP:autophagy) -| act(complex(GOCC:"inflammasome complex"))

SET Support = "IKK/NF-κB facilitates autophagy induction by inducing the expression \
of an autophagy receptor, p62 (also called SQSTM1), mediating \
recruitment of damaged mitochondria for autophagic clearance via a \
ubiquitin-dependent mechanism.68"

complex(SCOMP:"IkappaB Kinase Complex") -> p(HGNC:SQSTM1)
p(HGNC:SQSTM1) -> bp(GOBP:autophagy)

SET Support = "Myeloid cell-specific p62 ablation results in aberrant accumulation of \
damaged mitochondria and excessive production of IL-1β, associated \
with hyper-sensitivity to endotoxin-induced shock."

p(HGNC:SQSTM1) -| sec(p(HGNC:IL1B))

UNSET Section

SET Section = "NF-κB in inflammatory diseases"

SET Support = "NF-κB has been implicated in the pathogenesis of a number of \
inflammatory diseases, such as rheumatoid arthritis (RA), inflammatory \
bowel disease (IBD), multiple sclerosis, atherosclerosis, systemic \
lupus erythematosus, type I diabetes, chronic obstructive pulmonary \
disease and asthma.73"

complex(SCOMP:"Nfkb Complex") -- path(MESHD:"Arthritis, Rheumatoid")
complex(SCOMP:"Nfkb Complex") -- path(MESHD:"Inflammatory Bowel Diseases")
complex(SCOMP:"Nfkb Complex") -- path(MESHD:"Multiple Sclerosis")
complex(SCOMP:"Nfkb Complex") -- path(MESHD:Atherosclerosis)
complex(SCOMP:"Nfkb Complex") -- path(MESHD:"Lupus Erythematosus, Systemic")
complex(SCOMP:"Nfkb Complex") -- path(MESHD:"Diabetes Mellitus, Type 1")
complex(SCOMP:"Nfkb Complex") -- path(MESHD:"Pulmonary Disease, Chronic Obstructive")
complex(SCOMP:"Nfkb Complex") -- path(MESHD:Asthma)

SET Support = "RA is an autoimmune and inflammatory disease characterized by immune \
cell infiltration into the synovium, associated with chronic \
inflammation and destruction of cartilage and bone.74"

path(MESHD:"Arthritis, Rheumatoid") -- path(MESHD:Inflammation)
path(MESHD:"Arthritis, Rheumatoid") negativeCorrelation a(MESHA:Cartilage)
path(MESHD:"Arthritis, Rheumatoid") negativeCorrelation a(MESHA:"Bone and Bones")


# Rest of review describes loose associations with diseases


UNSET Section

UNSET PublicationType

##################################################################################

UNSET STATEMENT_GROUP
